
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СгенирироватьФормуЗаново = Истина;
	
	пНастройкиКомпоновщика = Неопределено;
		
	ЗагрузитьСохраненныеНастройки(пНастройкиКомпоновщика);
	
	СтруктураТЧДляСнятияФокусаСЗадач = Новый Структура();
	
	ОпределитьОтборНаСервере(пНастройкиКомпоновщика);
	Попытка
		ЗаполнитьЗадачиНаСервере();		
	Исключение
		Сообщить(NStr("ru = 'ОписаниеОшибки '") + 
				 ОписаниеОшибки());
		УстановитьСтандартныеНастройкиНаСервере();
		ЗаполнитьЗадачиНаСервере();
	КонецПопытки;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ТЧНастройкиКолонокПриИзменении(Элемент)
	СгенирироватьФормуЗаново = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ВыполнитьДействиеДляАктивнойСтраницы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "СправочникЗадачаЗаписана"
		ИЛИ ИмяСобытия = "ДобавленУбранНаблюдатель"
		ИЛИ ИмяСобытия = "СправочникВариантВажностиЗаписан"
		ИЛИ ИмяСобытия = "СправочникСпринтЗаписан"
		ИЛИ ИмяСобытия = "Константа.ИспользоватьСвоиЦветаДляЗадач.Записана"
		Тогда
		ЗаполнитьЗадачиНаСервере();		
	Конецесли;
	
	ОбновитьКанбанДоску();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	ОтключитьОбработчикОжидания("ЗаполнитьЗадачиНаКлиенте");
	ПриЗакрытииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ЗаполнитьЗадачиНаКлиенте", 180);
	
	УбратьИзКонтекстногоМенюЛишниеКнопкиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ГенерацияФормыДляВыбранныхСтатусов

&НаСервере
Процедура СгенерироватьФорму()
	УдалитьДинамическиеЭлементы();
	/////   РЕКВИЗИТЫ   //////////
	
	СтруктураИменИспользуемыхТЧ = Новый Структура();

	МассивДобавляемыхРеквизитов = Новый Массив;

	ЭтаФорма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	/////   КОМАНДЫ   //////////

	/////   ЭЛЕМЕНТЫ   //////////
	Для каждого СтрокаТЧНастройкиКолонок из Объект.ТЧНастройкиКолонок цикл
		Если НЕ СтрокаТЧНастройкиКолонок.Видимость Тогда
			Продолжить;
		Конецесли;
		пСтатус = СтрокаТЧНастройкиКолонок.Статус;
		ДобавитьНаКанбанДоскуКолонку(пСтатус);
	Конеццикла;
		
КонецПроцедуры 

&НаСервере
Процедура УдалитьДинамическиеЭлементы()
	пЭлементыФормы = Этаформа.Элементы;
	ПрефиксДляИмениЭлементовКолонок = ПолучитьПрефиксДляИмениЭлементовКолонок();
	ЭлементыКУдалению = Новый Массив();
	Для каждого пЭлементФормы из пЭлементыФормы цикл
		Если СтрНайти(пЭлементФормы.Имя,ПрефиксДляИмениЭлементовКолонок) = 0 Тогда
			Продолжить;
		Конецесли;
		Если ТипЗнч(пЭлементФормы) <> ТИп("ГруппаФормы") Тогда
			Продолжить;
		Конецесли;
		ЭлементыКУдалению.Добавить(пЭлементФормы);
	Конеццикла;
	Для каждого пЭлементФормы из ЭлементыКУдалению цикл
		пЭлементыФормы.Удалить(пЭлементФормы);		
	Конеццикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьПрефиксДляИмениЭлементовКолонок() 
	Возврат "_Колонка_";	
КонецФункции 

&НаСервере
Процедура ДобавитьНаКанбанДоскуКолонку(пСтатус)
	ПредставлениеКолонки = пСтатус.Наименование;
	
	пЭлементыФормы = Этаформа.Элементы;
	
	// ******   Группа формы "Группа для колонки статуса"    ***********
	
	ИменаЭлементов = ПолучитьИменаЭлементовДляКолонки(пСтатус);
	
	пЭлементГруппаКолонка = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяГруппыКолонки,Тип("ГруппаФормы"),пЭлементыФормы.ГруппаКанбанДоска);
	пЭлементГруппаКолонка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	пЭлементГруппаКолонка.Заголовок = ПредставлениеКолонки;
	пЭлементГруппаКолонка.Подсказка = ПредставлениеКолонки;
	пЭлементГруппаКолонка.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;

	// ******   Таблица формы "ТЧЗадачи" для статуса    ***********
	
	СтруктураИменИспользуемыхТЧ.Вставить(ИменаЭлементов.ИмяТЧЗадачи);
	
	пЭлементТЧЗадачи = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяТЧЗадачи,Тип("ТаблицаФормы"),пЭлементГруппаКолонка,Неопределено);
	пЭлементТЧЗадачи.ПутьКДанным = "Объект.ТЧЗадачи";
	пЭлементТЧЗадачи.АвтоВводНовойСтроки = Истина;
	пЭлементТЧЗадачи.ГоризонтальнаяПолосаПрокрутки = ИспользованиеПолосыПрокрутки.НеИспользовать;
	пЭлементТЧЗадачи.ИзменятьПорядокСтрок = Ложь;
	пЭлементТЧЗадачи.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	пЭлементТЧЗадачи.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	пЭлементТЧЗадачи.РежимВыделенияСтроки = РежимВыделенияСтрокиТаблицы.Строка;
	пЭлементТЧЗадачи.Шапка = Ложь;
	пЭлементТЧЗадачи.УстановитьДействие("Выбор","ТЧЗадачиВыбор");
	пЭлементТЧЗадачи.УстановитьДействие("ПриАктивизацииЯчейки","ТЧЗадачиПриАктивизацииЯчейки");
	пЭлементТЧЗадачи.УстановитьДействие("ПередНачаломДобавления","ТЧЗадачиПередНачаломДобавления");
	пЭлементТЧЗадачи.УстановитьДействие("Перетаскивание","ТЧЗадачиПеретаскивание");
	пЭлементТЧЗадачи.КоманднаяПанель.Видимость = Ложь;
	
	// ******    Команды из контекстного меню "ТЧЗадачи"    ***********

	пКомандаДобавитьВСпринт = пЭлементыФормы.Добавить(ИменаЭлементов.ИмяКомандыДобавитьВСпринт, Тип("КнопкаФормы"), пЭлементТЧЗадачи.КонтекстноеМеню);
	пКомандаДобавитьВСпринт.Заголовок="Добавить в спринт";
	пКомандаДобавитьВСпринт.ИмяКоманды="КомандаДобавитьВСпринт";
	пКомандаДобавитьВСпринт.Отображение = ОтображениеКнопки.КартинкаИТекст;	
	
	пКомандаУбратьИзСпринта = пЭлементыФормы.Добавить(ИменаЭлементов.ИмяКомандыУбратьИзСпринта, Тип("КнопкаФормы"), пЭлементТЧЗадачи.КонтекстноеМеню);
	пКомандаУбратьИзСпринта.Заголовок="Убрать из спринта";
	пКомандаУбратьИзСпринта.ИмяКоманды="КомандаУбратьИзСпринта";
	пКомандаУбратьИзСпринта.Отображение = ОтображениеКнопки.КартинкаИТекст;	
	
	пКомандаСледитьЗаЗадачей = пЭлементыФормы.Добавить(ИменаЭлементов.ИмяКомандыСледитьЗаЗадачей, Тип("КнопкаФормы"), пЭлементТЧЗадачи.КонтекстноеМеню);
	пКомандаСледитьЗаЗадачей.Заголовок="Следить за задачей";
	пКомандаСледитьЗаЗадачей.ИмяКоманды="КомандаСледитьЗаЗадачей";
	пКомандаСледитьЗаЗадачей.Отображение = ОтображениеКнопки.КартинкаИТекст;		
		
	пКомандаНеСледитьЗаЗадачей = пЭлементыФормы.Добавить(ИменаЭлементов.ИмяКомандыНеСледитьЗаЗадачей, Тип("КнопкаФормы"), пЭлементТЧЗадачи.КонтекстноеМеню);
	пКомандаНеСледитьЗаЗадачей.Заголовок="Не следить за задачей";
	пКомандаНеСледитьЗаЗадачей.ИмяКоманды="КомандаНеСледитьЗаЗадачей";
	пКомандаНеСледитьЗаЗадачей.Отображение = ОтображениеКнопки.КартинкаИТекст;	
	
	// ******   Группа формы "Карточка задачи"    ***********
	
	пЭлементКарточкаЗадачи = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяКарточкаЗадачи,Тип("ГруппаФормы"),пЭлементТЧЗадачи,Неопределено);
	пЭлементКарточкаЗадачи.Вид = ВидГруппыФормы.ГруппаКолонок;
	пЭлементКарточкаЗадачи.Заголовок = "ТЧЗадачи "+ПредставлениеКолонки;
	пЭлементКарточкаЗадачи.Подсказка = "ТЧЗадачи "+ПредставлениеКолонки;

	// ******   Группа формы "Карточка задачи нижняя часть"    ***********

	пЭлементКарточкаЗадачи_НижняяЧасть = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяКарточкаЗадачи_НижняяЧасть,Тип("ГруппаФормы"),пЭлементКарточкаЗадачи,Неопределено);
	пЭлементКарточкаЗадачи_НижняяЧасть.Вид = ВидГруппыФормы.ГруппаКолонок;
	пЭлементКарточкаЗадачи_НижняяЧасть.Заголовок = "ТЧЗадачи "+ПредставлениеКолонки;
	пЭлементКарточкаЗадачи_НижняяЧасть.Подсказка = "ТЧЗадачи "+ПредставлениеКолонки;
	пЭлементКарточкаЗадачи_НижняяЧасть.Группировка = ГруппировкаКолонок.Горизонтальная;

	// ******   Поле формы "Задача в карточке задачи нижняя часть"    ***********
	
	пЭлементПолеЗадачиВКарточкеЗадачи = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяПоляЗадачиВКарточкеЗадачи,Тип("ПолеФормы"),пЭлементКарточкаЗадачи_НижняяЧасть,Неопределено);
	пЭлементПолеЗадачиВКарточкеЗадачи.Вид = ВидПоляФормы.ПолеВвода;
	пЭлементПолеЗадачиВКарточкеЗадачи.ПутьКДанным = "Объект.ТЧЗадачи.Задача";
	пЭлементПолеЗадачиВКарточкеЗадачи.КнопкаВыпадающегоСписка = Ложь;
	пЭлементПолеЗадачиВКарточкеЗадачи.КнопкаОткрытия = Ложь;
	пЭлементПолеЗадачиВКарточкеЗадачи.Высота = 3;

	// ******   Группа формы "Карточка задачи верхняя часть"    ***********
	
	пЭлементКарточкаЗадачи_ВерхняяЧасть = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяКарточкаЗадачи_ВерхняяЧасть,Тип("ГруппаФормы"),пЭлементКарточкаЗадачи,пЭлементКарточкаЗадачи_НижняяЧасть);
	пЭлементКарточкаЗадачи_ВерхняяЧасть.Вид = ВидГруппыФормы.ГруппаКолонок;
	пЭлементКарточкаЗадачи_ВерхняяЧасть.Заголовок = "ТЧЗадачи_ВерхняяЧасть"+ПредставлениеКолонки;
	пЭлементКарточкаЗадачи_ВерхняяЧасть.Подсказка = "ТЧЗадачи_ВерхняяЧасть"+ПредставлениеКолонки;
	пЭлементКарточкаЗадачи_ВерхняяЧасть.Группировка = ГруппировкаКолонок.ВЯчейке;

	// ******   Поле формы "Короткое имя в карточке задачи верхняя часть"    ***********
	
	пЭлементПолеКороткоеИмяВКарточкеЗадачи = пЭлементыФормы.Вставить(ИменаЭлементов.ИмяПоляКороткоеИмяВКарточкеЗадачи,Тип("ПолеФормы"),пЭлементКарточкаЗадачи_ВерхняяЧасть,Неопределено);
	пЭлементПолеКороткоеИмяВКарточкеЗадачи.Вид = ВидПоляФормы.ПолеВвода;
	пЭлементПолеКороткоеИмяВКарточкеЗадачи.ПутьКДанным = "Объект.ТЧЗадачи.КороткоеИмя";
	пЭлементПолеКороткоеИмяВКарточкеЗадачи.Ширина = 3;
	
	// ******   Поле формы "Основная задача в карточке задачи верхняя часть"    ***********
	
	пЭлементПолеОсновнаяЗадачаПредставлениеВКарточкеЗадачи = пЭлементыФормы.Вставить(
		ИменаЭлементов.ИмяПоляОсновнаяЗадачаПредставлениеВКарточкеЗадачи,
		Тип("ПолеФормы"),
		пЭлементКарточкаЗадачи_ВерхняяЧасть,
		пЭлементПолеКороткоеИмяВКарточкеЗадачи);
					
	пЭлементПолеОсновнаяЗадачаПредставлениеВКарточкеЗадачи.Вид = ВидПоляФормы.ПолеВвода;
	пЭлементПолеОсновнаяЗадачаПредставлениеВКарточкеЗадачи.ПутьКДанным = "Объект.ТЧЗадачи.ОсновнаяЗадачаПредставление";

	// ******   Поле формы "Номер задачи в карточке задачи верхняя часть"    ***********
	
	пЭлементПолеНомерЗадачиПредставлениеВКарточкеЗадачи = пЭлементыФормы.Вставить(
		ИменаЭлементов.ИмяПоляНомерЗадачиПредставлениеВКарточкеЗадачи,
		Тип("ПолеФормы"),
		пЭлементКарточкаЗадачи_ВерхняяЧасть,
		пЭлементПолеОсновнаяЗадачаПредставлениеВКарточкеЗадачи);
		
	пЭлементПолеНомерЗадачиПредставлениеВКарточкеЗадачи.Вид = ВидПоляФормы.ПолеВвода;
	пЭлементПолеНомерЗадачиПредставлениеВКарточкеЗадачи.ПутьКДанным = "Объект.ТЧЗадачи.НомерЗадачиПредставление";	
	пЭлементПолеНомерЗадачиПредставлениеВКарточкеЗадачи.Ширина = 3;
КонецПроцедуры 

&НаСервере
Функция ПолучитьИменаЭлементовДляКолонки(пСтатус)
	
	ИмяКолонки = ПолучитьИмяКолонки(пСтатус);	
	ПрефиксДляИмениЭлементовКолонок = ПолучитьПрефиксДляИмениЭлементовКолонок();	
	
	ИмяГруппыКолонки = ПрефиксДляИмениЭлементовКолонок + ИмяКолонки + "_Группа";
	ИмяТЧЗадачи = ИмяКолонки + "_ТЧЗадачи";
	ИмяКомандыДобавитьВСпринт = ИмяКолонки + "_ТЧЗадачи_КомандаДобавитьВСпринт";
	ИмяКомандыУбратьИзСпринта = ИмяКолонки + "_ТЧЗадачи_КомандаУбратьИзСпринта";
	ИмяКомандыСледитьЗаЗадачей = ИмяКолонки + "_ТЧЗадачи_КомандаСледитьЗаЗадачей";
	ИмяКомандыНеСледитьЗаЗадачей = ИмяКолонки + "_ТЧЗадачи_КомандаНеСледитьЗаЗадачей";
	ИмяКарточкаЗадачи = ИмяКолонки + "_ТЧЗадачиКарточкаЗадачи";
	ИмяКарточкаЗадачи_НижняяЧасть = ИмяКолонки + "_ТЧЗадачиКарточкаЗадачи_НижняяЧасть";
	ИмяПоляЗадачиВКарточкеЗадачи = ИмяКолонки + "_ТЧЗадачи_НижняяЧасть_Задача";
	ИмяКарточкаЗадачи_ВерхняяЧасть = ИмяКолонки +"_ТЧЗадачи_ВерхняяЧасть";
	
	ИмяПоляКороткоеИмяВКарточкеЗадачи = ИмяКолонки + "_ТЧЗадачи_ВерхняяЧасть_КороткоеИмя";
	ИмяПоляОсновнаяЗадачаПредставлениеВКарточкеЗадачи = ИмяКолонки +"_ТЧЗадачи_ВерхняяЧасть_ОсновнаяЗадачаПредставление";
	ИмяПоляНомерЗадачиПредставлениеВКарточкеЗадачи = ИмяКолонки+"_ТЧЗадачи_ВерхняяЧасть_НомерЗадачиПредставление";	
	
	РезультатФункции = Новый Структура();
	РезультатФункции.Вставить("ИмяГруппыКолонки",ИмяГруппыКолонки);
	РезультатФункции.Вставить("ИмяТЧЗадачи",ИмяТЧЗадачи);
	РезультатФункции.Вставить("ИмяКомандыДобавитьВСпринт",ИмяКомандыДобавитьВСпринт);
	РезультатФункции.Вставить("ИмяКомандыУбратьИзСпринта",ИмяКомандыУбратьИзСпринта);
	РезультатФункции.Вставить("ИмяКомандыСледитьЗаЗадачей",ИмяКомандыСледитьЗаЗадачей);
	РезультатФункции.Вставить("ИмяКомандыНеСледитьЗаЗадачей",ИмяКомандыНеСледитьЗаЗадачей);
	РезультатФункции.Вставить("ИмяКарточкаЗадачи",ИмяКарточкаЗадачи);	
	
	РезультатФункции.Вставить("ИмяКарточкаЗадачи_НижняяЧасть",ИмяКарточкаЗадачи_НижняяЧасть);	
	РезультатФункции.Вставить("ИмяПоляЗадачиВКарточкеЗадачи",ИмяПоляЗадачиВКарточкеЗадачи);
	
	РезультатФункции.Вставить("ИмяКарточкаЗадачи_ВерхняяЧасть",ИмяКарточкаЗадачи_ВерхняяЧасть);
	РезультатФункции.Вставить("ИмяПоляКороткоеИмяВКарточкеЗадачи",ИмяПоляКороткоеИмяВКарточкеЗадачи);
	РезультатФункции.Вставить("ИмяПоляОсновнаяЗадачаПредставлениеВКарточкеЗадачи",ИмяПоляОсновнаяЗадачаПредставлениеВКарточкеЗадачи);
	РезультатФункции.Вставить("ИмяПоляНомерЗадачиПредставлениеВКарточкеЗадачи",ИмяПоляНомерЗадачиПредставлениеВКарточкеЗадачи);

	Возврат РезультатФункции;
КонецФункции 

&НаСервере
Функция ПолучитьИмяКолонки(пСтатус)
	пИмяКолонки = "";
	
	НаименованиеСтатуса = пСтатус.Наименование;	
	НомерСимвола = 1;
	ДлинаСтроки = СтрДлина(НаименованиеСтатуса);
	Пока НомерСимвола <= ДлинаСтроки Цикл
		пСимвол = Сред(НаименованиеСтатуса,НомерСимвола,1);
		пКодСимвола = КодСимвола(пСимвол);
		
		//Сообщить("пСимвол " + пСимвол + " пКодСимвола - " + пКодСимвола);
		Если (65 <= пКодСимвола И пКодСимвола <= 90)
			ИЛИ (97 <= пКодСимвола И пКодСимвола <= 122)
			ИЛИ (1072 <= пКодСимвола И пКодСимвола <= 1103)
			ИЛИ (1040 <= пКодСимвола И пКодСимвола <= 1071)
			Тогда
			пИмяКолонки = пИмяКолонки + пСимвол;
		Конецесли;
		Если пКодСимвола = 32 Тогда
			пИмяКолонки = пИмяКолонки + "_";
		Конецесли;
		НомерСимвола = НомерСимвола + 1;	
	Конеццикла;
	Возврат пИмяКолонки;
КонецФункции 

&НаСервере
Процедура УбратьИзКонтекстногоМенюЛишниеКнопкиНаСервере()
	Если НЕ СгенирироватьФормуЗаново Тогда
		Возврат;
	Конецесли;
	
	МассивКоманд = Новый Массив();
	МассивКоманд.Добавить("КонтекстноеМенюИзменить");
	МассивКоманд.Добавить("КонтекстноеМенюКопироватьВБуферОбмена");
	МассивКоманд.Добавить("КонтекстноеМенюСкопировать");
	МассивКоманд.Добавить("КонтекстноеМенюУдалить");
	
	ГруппаКопированиеИВыделение = "КонтекстноеМенюКопированиеИВыделение";
	Команда_КонтекстноеМенюКопироватьВБуферОбмена = "КонтекстноеМенюКопироватьВБуферОбмена";
	
	//Элементы.В_процессе_выполнения_ТЧЗадачи.КонтекстноеМеню.ПодчиненныеЭлементы	
	Для каждого СтрокаТЧНастройкиКолонок из Объект.ТЧНастройкиКолонок цикл
		
		Если НЕ СтрокаТЧНастройкиКолонок.Видимость Тогда
			Продолжить;
		Конецесли;
		
		пСтатус = СтрокаТЧНастройкиКолонок.Статус;
		ИменаЭлементов = ПолучитьИменаЭлементовДляКолонки(пСтатус);
		ИмяТЧЗадачи = ИменаЭлементов.ИмяТЧЗадачи;
		
		пЭлементТЧЗадачи = Элементы[ИмяТЧЗадачи];
		Для каждого пКоманда из пЭлементТЧЗадачи.КонтекстноеМеню.ПодчиненныеЭлементы цикл
			ТипЗнчКоманда = ТипЗнч(пКоманда);
			пИмяКоманды = пКоманда.Имя;
			
			Если ТипЗнчКоманда = Тип("КнопкаФормы") Тогда 		
				Для каждого ЭлМассивКоманд из МассивКоманд цикл
					Если СтрНайти(пИмяКоманды,ЭлМассивКоманд) = 0 Тогда
						Продолжить;
					Конецесли;    
					
					пКоманда.Видимость = Ложь;	
					
					Прервать;
				Конеццикла;				
			Конецесли;
			
			Если ТипЗнчКоманда = Тип("ГруппаФормы")
				И СтрНайти(пИмяКоманды,ГруппаКопированиеИВыделение) > 0 Тогда
				Для каждого пКомандаКопированияИВыделения из пКоманда.ПодчиненныеЭлементы цикл
					Если СтрНайти(пКомандаКопированияИВыделения.Имя,Команда_КонтекстноеМенюКопироватьВБуферОбмена) = 0 Тогда
						Продолжить;
					Конецесли;
					
					пКомандаКопированияИВыделения.Видимость = Ложь;	
					
				Конеццикла;
			Конецесли;
			
		Конеццикла;
	Конеццикла;	
	
    СгенирироватьФормуЗаново = Ложь;
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборТЧ()
	Для каждого СтрокаТЧНастройкиКолонок из Объект.ТЧНастройкиКолонок цикл
		Если НЕ СтрокаТЧНастройкиКолонок.Видимость Тогда
			Продолжить;
		Конецесли;
		пСтатус = СтрокаТЧНастройкиКолонок.Статус;
		ИмяКолонки = ПолучитьИмяКолонки(пСтатус);
		
		ИмяТЧЗадачиДляСтатуса = ИмяКолонки + "_ТЧЗадачи";
		Элементы[ИмяТЧЗадачиДляСтатуса].ОтборСтрок = Новый ФиксированнаяСтруктура("Статус", пСтатус);
	Конеццикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
КонецПроцедуры 

&НаСервере
Процедура УстановитьСтандартныеНастройкиНаСервере()
	СгенирироватьФормуЗаново = Истина;
	КлючНастроек = ПолучитьКлючНастроек();	
	ХранилищеОбщихНастроек.Сохранить(КлючНастроек,КлючНастроек,Неопределено);
	пНастройкиКомпоновщика = Неопределено;
	ЗагрузитьСохраненныеНастройки(пНастройкиКомпоновщика);
	ОпределитьОтборНаСервере(пНастройкиКомпоновщика);
КонецПроцедуры 

&НаСервере
Функция ПолучитьКлючНастроек()
	КлючНастроек = "Обработка.узКанбанДоска";	
	Возврат КлючНастроек;
КонецФункции 

&НаСервере
Процедура ЗаполнитьЗадачиНаСервере()
	Объект.АдресВременногоХранилища = ПоместитьВоВременноеХранилище(КомпоновщикНастроекКомпоновкиДанных.Настройки,Новый УникальныйИдентификатор());
	
	пОбъект = РеквизитФормыВЗначение("Объект"); 
	пОбъект.ЗаполнитьЗадачи();
	ЗначениеВРеквизитФормы(пОбъект,"Объект");
	
	Если СгенирироватьФормуЗаново Тогда
		СгенерироватьФорму();
		УстановитьОтборТЧ();		
	Конецесли;	
	
	ОформлениеЦветаЗадачНаСервере();
КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьСохраненныеНастройки(пНастройкиКомпоновщика)
	КлючНастроек = ПолучитьКлючНастроек();
	СохраненныеНастройки = ХранилищеОбщихНастроек.Загрузить(КлючНастроек,КлючНастроек);
	
	Если СохраненныеНастройки <> Неопределено Тогда
		Если СохраненныеНастройки.Свойство("ТЧНастройкиКолонок") Тогда
			ЗаполнитьТЧНастройкиКолонокНаСервере();
			ТЧНастройкиКолонок = СохраненныеНастройки.ТЧНастройкиКолонок;
			Для каждого СтрокаТЧНастройкиКолонок из Объект.ТЧНастройкиКолонок цикл
				ПараметрыОтбора=Новый Структура();
				ПараметрыОтбора.Вставить("Статус",СтрокаТЧНастройкиКолонок.Статус);
				НайденныеСтрокиСохраненныеНастройки = ТЧНастройкиКолонок.НайтиСтроки(ПараметрыОтбора);
				Для каждого СтрокаНайденныеСтроки из НайденныеСтрокиСохраненныеНастройки цикл
					СтрокаТЧНастройкиКолонок.Видимость = СтрокаНайденныеСтроки.Видимость;		
				Конеццикла;				
			Конеццикла;
		Иначе
			ЗаполнитьТЧНастройкиКолонокНаСервере();	
		Конецесли;
		Если СохраненныеНастройки.Свойство("НастройкиКомпоновщикаХранилище") Тогда
			пНастройкиКомпоновщика = СохраненныеНастройки.НастройкиКомпоновщикаХранилище.Получить();
		Конецесли;		
	Иначе
		ЗаполнитьТЧНастройкиКолонокНаСервере();
	Конецесли;	
КонецПроцедуры 

&НаСервере
Функция ОпределитьОтборНаСервере(пНастройкиКомпоновщика = Неопределено) Экспорт
	пОбъект = РеквизитФормыВЗначение("Объект"); 
	МакетСКД = пОбъект.ПолучитьМакет("СхемаКомпоновкиДанных");
	
	АдресВременногоХранилищаМакетаСКД = ПоместитьВоВременноеХранилище(МакетСКД,Новый УникальныйИдентификатор());
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВременногоХранилищаМакетаСКД);
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(ИсточникНастроек);
	
	Если пНастройкиКомпоновщика = Неопределено Тогда
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(МакетСКД.НастройкиПоУмолчанию);
	Иначе
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(пНастройкиКомпоновщика);
	Конецесли;
КонецФункции

&НаСервере
Процедура ЗаполнитьТЧНастройкиКолонокНаСервере()
	пОбъект = РеквизитФормыВЗначение("Объект"); 
	пОбъект.ЗаполнитьТЧНастройкиКолонок();
	ЗначениеВРеквизитФормы(пОбъект,"Объект");
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьАктивизацииСтрок(ИсключитьИмяТЧ = Неопределено)
	Для каждого ЭлСтруктураТЧДляСнятияФокусаСЗадач из СтруктураТЧДляСнятияФокусаСЗадач цикл		
		ИмяТЧЗадачи = ЭлСтруктураТЧДляСнятияФокусаСЗадач.Ключ;	
		Если ИмяТЧЗадачи = ИсключитьИмяТЧ Тогда
			Продолжить;
		Конецесли;
		
		Элементы[ИмяТЧЗадачи].ВыделенныеСтроки.Очистить();
	Конеццикла;
КонецПроцедуры 

&НаКлиенте
Процедура СменитьСтатусЗадачиНаКлиенте(Элемент,ПараметрыПеретаскивания)
	НовыйСтатус = Элемент.ОтборСтрок.Статус;	
	ДанныеИсточник = ПараметрыПеретаскивания.Значение;
	
	МассивЗадач = Новый Массив();
	Для каждого ДанныеИсточник из ПараметрыПеретаскивания.Значение цикл
		ЗадачаСсылка = ДанныеИсточник.Задача;
		МассивЗадач.Добавить(ЗадачаСсылка);
	Конеццикла;	
	
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("НовыйСтатус",НовыйСтатус);
	ДопПараметры.Вставить("МассивЗадач",МассивЗадач);	
	СменитьСтатусЗадачиНаСервере(ДопПараметры);
КонецПроцедуры 

&НаСервере
Процедура СменитьСтатусЗадачиНаСервере(ДопПараметры)
	пОбъект = РеквизитФормыВЗначение("Объект"); 
	пОбъект.СменитьСтатусЗадачи(ДопПараметры);
	ЗначениеВРеквизитФормы(пОбъект,"Объект");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовуюЗадачу(Элемент)
	пСтатус = Элемент.ОтборСтрок.Статус;
	ПараметрыНовойЗадачи = Новый Структура();
	ПараметрыНовойЗадачи.Вставить("Статус",пСтатус);
	ЭлементыОтбора = КомпоновщикНастроекКомпоновкиДанных.Настройки.Отбор.Элементы;
	Для каждого ЭлементОтбора из ЭлементыОтбора цикл
		Если НЕ ЭлементОтбора.Использование Тогда
			Продолжить;
		Конецесли;
		Если ЭлементОтбора.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно Тогда
			Продолжить;
		Конецесли;	
		Если СтрНайти(ЭлементОтбора.ЛевоеЗначение,".") > 0 Тогда
			Продолжить;
		Конецесли;
		Если ТипЗнч(ЭлементОтбора) =  Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			ПараметрыНовойЗадачи.Вставить("" + ЭлементОтбора.ЛевоеЗначение,ЭлементОтбора.ПравоеЗначение);	
		Конецесли;
	Конеццикла;
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ТребоватьЗаполнитьРодителя",Истина);
	ДопПараметры.Вставить("ПараметрыНовойЗадачи",ПараметрыНовойЗадачи);
	ОткрытьФорму("Справочник.узЗадачи.Форма.ФормаЭлемента",ДопПараметры);	
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьДействиеДляАктивнойСтраницы()
	пТекущаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;
	Если пТекущаяСтраница = Элементы.ГруппаКанбанДоска Тогда
		ОбновитьКанбанДоску();
	Конецесли;	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивЗадачПоВыделеннымСтрокам(МассивВыделенныхСтрок)
	МассивЗадач = Новый Массив();
	Для каждого ЭлМассиваВыделенныхСтрок из МассивВыделенныхСтрок цикл
		ИдентификаторСтроки = ЭлМассиваВыделенныхСтрок;
		СтрокаТЧЗадачи = Объект.ТЧЗадачи.НайтиПоИдентификатору(ИдентификаторСтроки);
		МассивЗадач.Добавить(СтрокаТЧЗадачи.Задача);
	Конеццикла;	
	Возврат МассивЗадач;
КонецФункции

&НаСервере
Процедура СохранитьНастройки()
	НастройкиКомпоновщикаХранилище = Новый ХранилищеЗначения(КомпоновщикНастроекКомпоновкиДанных.Настройки);
	
	КлючНастроек = ПолучитьКлючНастроек();
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ТЧНастройкиКолонок",Объект.ТЧНастройкиКолонок.Выгрузить());
	ДопПараметры.Вставить("НастройкиКомпоновщикаХранилище",НастройкиКомпоновщикаХранилище);
	
	ХранилищеОбщихНастроек.Сохранить(КлючНастроек,КлючНастроек,ДопПараметры);
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура КомандаОбновить(Команда)
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКанбанДоска;
	ЗаполнитьЗадачиНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗадачиНаКлиенте()
	ОбновитьКанбанДоску();
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьКанбанДоску()
	СтруктураТЧДляСнятияФокусаСЗадач.Очистить();
	Объект.АдресВременногоХранилища = ПоместитьВоВременноеХранилище(КомпоновщикНастроекКомпоновкиДанных.Настройки,Новый УникальныйИдентификатор());
	ЗаполнитьЗадачиНаСервере();
	УстановитьВидимостьДоступность();	
	
	ОчиститьАктивизацииСтрок();
	УбратьИзКонтекстногоМенюЛишниеКнопкиНаСервере();
	
КонецПроцедуры 

&НаКлиенте
Процедура КомандаУстановитьСтандартныеНастройки(Команда)
	УстановитьСтандартныеНастройкиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область КомандыИСобытияТЧЗадачи

&НаКлиенте
Процедура ТЧЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Элемент.ТекущиеДанные.Задача);	
КонецПроцедуры

&НаКлиенте
Процедура ТЧЗадачиПриАктивизацииЯчейки(Элемент)
	ИмяТЧЗадачи = Элемент.Имя;
	ИмяАктивнойТЧ = ИмяТЧЗадачи; 
	ОчиститьАктивизацииСтрок(ИмяТЧЗадачи);
	Если НЕ СтруктураТЧДляСнятияФокусаСЗадач.Свойство(ИмяТЧЗадачи) Тогда
		Элемент.ВыделенныеСтроки.Очистить();
		СтруктураТЧДляСнятияФокусаСЗадач.Вставить(ИмяТЧЗадачи,"Истина");
	Конецесли;
КонецПроцедуры

&НаКлиенте
Процедура ТЧЗадачиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)	
	Отказ = Истина;
	ДобавитьНовуюЗадачу(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ТЧЗадачиПередНачаломИзменения(Элемент, Отказ)
	Отказ = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТЧЗадачиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СменитьСтатусЗадачиНаКлиенте(Элемент,ПараметрыПеретаскивания);
	ОформлениеЦветаЗадачНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьВСпринт(Команда)
	ДобавитьУбратьВСпринтНаКлиенте(1);	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУбратьИзСпринта(Команда)
	ДобавитьУбратьВСпринтНаКлиенте(-1);
КонецПроцедуры

&НаСервере
Функция ДобавитьУбратьВСпринтЗадачи(КодОперации,пСпринт,МассивЗадач)
	ЭтоДобавление = КодОперации = 1;
	ЭтоУдаление = КодОперации = -1;
	
	РезультатФункции = Новый Структура();
	МассивСообщений = Новый Массив();
	Для каждого ЭлМассиваЗадач из МассивЗадач цикл
		пЗадачаСсылка = ЭлМассиваЗадач;
		Если ЭтоДобавление Тогда
			Если пЗадачаСсылка.Спринт <> пСпринт Тогда
				ЗадачаОбъект = пЗадачаСсылка.ПолучитьОбъект();
				ЗадачаОбъект.Спринт = пСпринт;
				ЗадачаОбъект.Записать();
				МассивСообщений.Добавить("Добавлена"
					+" в спринт ["+пСпринт+"] "
					+" задача [#"+пЗадачаСсылка.Код+" "+пЗадачаСсылка+"]");
			Иначе
				МассивСообщений.Добавить("Уже указана"
					+" в спринте ["+пСпринт+"] "
					+" задача [#"+пЗадачаСсылка.Код+" "+пЗадачаСсылка+"]");				
			Конецесли;
		ИначеЕсли ЭтоУдаление Тогда
			пСпринт = пЗадачаСсылка.Спринт;
			Если ЗначениеЗаполнено(пСпринт) Тогда
				ЗадачаОбъект = пЗадачаСсылка.ПолучитьОбъект();
				ЗадачаОбъект.Спринт = Неопределено;
				ЗадачаОбъект.Записать();
				МассивСообщений.Добавить("Убрана"
					+" из спринта ["+пСпринт+"] "
					+" задача [#"+пЗадачаСсылка.Код+" "+пЗадачаСсылка+"]");					
			Иначе
				МассивСообщений.Добавить("Не указана в спринте"
					+" задача [#"+пЗадачаСсылка.Код+" "+пЗадачаСсылка+"]");					
			Конецесли;	
		Иначе
			ВызватьИсключение "Неизвестный код операции " + КодОперации;
		КонецЕсли;
	Конеццикла;	
	РезультатФункции.Вставить("МассивСообщений",МассивСообщений);
	РезультатФункции.Вставить("КомандаДобавитьУбратьВыполнена",Истина);
	Возврат РезультатФункции;
КонецФункции

&НаКлиенте
Процедура ДобавитьУбратьВСпринтНаКлиенте(КодОперации)
	СтрокаТЧ = Элементы[ИмяАктивнойТЧ].ТекущиеДанные;	
	Если СтрокаТЧ = Неопределено тогда
		Сообщить("Выберите задачи, которые хотите добавить в спринт");
		Возврат;	
	Конецесли;	
	МассивВыделенныхСтрок = Элементы[ИмяАктивнойТЧ].ВыделенныеСтроки;
	Если МассивВыделенныхСтрок.Количество() = 0 Тогда
		Сообщить("Выберите задачи, которые хотите добавить в спринт");
		Возврат;
	Конецесли;	
	РезультатФункции = ДобавитьУбратьВСпринтНаСервере(КодОперации,МассивВыделенныхСтрок);
	КомандаДобавитьУбратьВыполнена = РезультатФункции.КомандаДобавитьУбратьВыполнена;
	Если НЕ КомандаДобавитьУбратьВыполнена Тогда
		Если РезультатФункции.Свойство("НеобходимоВыбратьСпринт") Тогда
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("МассивВыделенныхСтрок",МассивВыделенныхСтрок);
			ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьВыборСпринта", ЭтаФорма, ДополнительныеПараметры);
			
		    пОтбор = Новый Структура("СпринтЗавершен", Ложь);
			ПараметрыОткрытияФормы = Новый Структура("Отбор", пОтбор);   
			ОткрытьФорму("Справочник.узСпринты.Форма.ФормаВыбора",ПараметрыОткрытияФормы,,,,,ОповещениеОЗакрытии);	
		Конецесли;
	Конецесли;
	ДобавитьУбратьВСпринтНаКлиентеОбработатьРезультатФункции(РезультатФункции);
КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьВыборСпринта(РезультатЗакрытия, ДопПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	Конецесли;
	пСпринт = РезультатЗакрытия;
	МассивВыделенныхСтрок = ДопПараметры.МассивВыделенныхСтрок;
	РезультатФункции = ДобавитьУбратьВСпринтНаСервере(1,МассивВыделенныхСтрок,пСпринт);
	ДобавитьУбратьВСпринтНаКлиентеОбработатьРезультатФункции(РезультатФункции);
КонецПроцедуры 

&НаКлиенте
Процедура ДобавитьУбратьВСпринтНаКлиентеОбработатьРезультатФункции(РезультатФункции)
	Если РезультатФункции.Свойство("МассивСообщений") Тогда
		МассивСообщений = РезультатФункции.МассивСообщений;
		Для каждого ЭлМассиваСообщения из МассивСообщений цикл
			Сообщить(ЭлМассиваСообщения);		 
		Конеццикла;		
	Конецесли;
	ЗаполнитьЗадачиНаСервере();	
КонецПроцедуры 

&НаСервере
Функция ДобавитьУбратьВСпринтНаСервере(КодОперации,МассивВыделенныхСтрок,пСпринт = Неопределено)
	МассивЗадач = ПолучитьМассивЗадачПоВыделеннымСтрокам(МассивВыделенныхСтрок);
	РезультатФункцииТекущийСпринт = Справочники.узСпринты.ПолучитьТекущийСпринт();	
	ТекстОшибки	= РезультатФункцииТекущийСпринт.ТекстОшибки;
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		МассивСообщений = Новый Массив(); 
		МассивСообщений.Добавить(ТекстОшибки);
		
		РезультатФункции = Новый Структура();
		РезультатФункции.Вставить("КомандаДобавитьУбратьВыполнена",Ложь);
		РезультатФункции.Вставить("МассивСообщений",МассивСообщений);
		Возврат РезультатФункции;		
	Конецесли;
	
	Если КодОперации = -1 Тогда
		РезультатФункции = ДобавитьУбратьВСпринтЗадачи(КодОперации,Неопределено,МассивЗадач);
		Возврат РезультатФункции;			
	Конецесли;
	
	Если ЗначениеЗаполнено(пСпринт) Тогда
		РезультатФункции = ДобавитьУбратьВСпринтЗадачи(КодОперации,пСпринт,МассивЗадач);
		Возврат РезультатФункции;
	Конецесли;;		
	
	Только1АктивныйСпринт = РезультатФункцииТекущийСпринт.Только1АктивныйСпринт;	
	Если Только1АктивныйСпринт Тогда		
		пСпринт = РезультатФункцииТекущийСпринт.Спринт;
		РезультатФункции = ДобавитьУбратьВСпринтЗадачи(КодОперации,пСпринт,МассивЗадач);
		Возврат РезультатФункции;
	Конецесли;
	
	РезультатФункции = Новый Структура();
	РезультатФункции.Вставить("КомандаДобавитьУбратьВыполнена",Ложь);
	РезультатФункции.Вставить("НеобходимоВыбратьСпринт",Истина);
	Возврат РезультатФункции;
	
КонецФункции

&НаКлиенте
Процедура КомандаНеСледитьЗаЗадачей(Команда)
	ДобавитьУбратьНаблюдателяЗаЗадачейНаКлиенте(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КомандаСледитьЗаЗадачей(Команда)
	ДобавитьУбратьНаблюдателяЗаЗадачейНаКлиенте(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьУбратьНаблюдателяЗаЗадачейНаКлиенте(ЭтоДобавлениеНаблюдателя)
	СтрокаТЧ = Элементы[ИмяАктивнойТЧ].ТекущиеДанные;	
	Если СтрокаТЧ = Неопределено тогда
		Сообщить("Выберите задачи, которые за которыми хотите следить");
		Возврат;	
	Конецесли;	
	МассивВыделенныхСтрок = Элементы[ИмяАктивнойТЧ].ВыделенныеСтроки;
	Если МассивВыделенныхСтрок.Количество() = 0 Тогда
		Сообщить("Выберите задачи, которые за которыми хотите следить");
		Возврат;
	Конецесли;			
	ДобавитьУбратьНаблюдателяЗаЗадачейНаСервере(МассивВыделенныхСтрок,ЭтоДобавлениеНаблюдателя);	
	Оповестить("ДобавленУбранНаблюдатель");
КонецПроцедуры 

&НаСервере
Процедура ДобавитьУбратьНаблюдателяЗаЗадачейНаСервере(МассивВыделенныхСтрок,ЭтоДобавлениеНаблюдателя)
	МассивЗадач = ПолучитьМассивЗадачПоВыделеннымСтрокам(МассивВыделенныхСтрок);
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("Пользователь",Пользователи.ТекущийПользователь());
	ДопПараметры.Вставить("МассивЗадач",МассивЗадач);
	Если ЭтоДобавлениеНаблюдателя Тогда
		РегистрыСведений.узНаблюдателиЗаЗадачами.СледитьЗаЗадачей(ДопПараметры);	
	Иначе
		РегистрыСведений.узНаблюдателиЗаЗадачами.ПерестатьСледитьЗаЗадачей(ДопПараметры);	
	Конецесли;
КонецПроцедуры 

#КонецОбласти

#Область ОформлениеКанбанДоски

&НаСервере
Процедура ОформлениеЦветаЗадачНаСервере()
	УстановитьБазовоеОформлениеЗадачНаСервере();
	
	УстановитьИндивидуальноеОформлениеЗадачНаСервере(); 
КонецПроцедуры 

&НаСервере
Процедура УстановитьИндивидуальноеОформлениеЗадачНаСервере()
	Если НЕ Константы.узИспользоватьСвоиЦветаДляЗадач.Получить() Тогда
		Возврат;
	Конецесли;
	
	Для Каждого ТекЭлементФормы Из Элементы Цикл
		Если ТипЗнч(ТекЭлементФормы) = Тип("ТаблицаФормы") И ТекЭлементФормы.ПутьКДанным = "Объект.ТЧЗадачи" Тогда
			Для Каждого СтрокаЗадача Из Объект.ТЧЗадачи Цикл
				ТекЗадача = СтрокаЗадача.Задача;
				
				//+ УЗ_Иванов А.Б. 2016-11-26
				//ИмяТЧЗадачи = "ТЧЗадачи" + СтрокаЗадача.Статус.ИмяПредопределенныхДанных;
				ИменаЭлементов = ПолучитьИменаЭлементовДляКолонки(СтрокаЗадача.Статус);
				ИмяТЧЗадачи = ИменаЭлементов.ИмяТЧЗадачи;
				//- УЗ_Иванов А.Б. 2016-11-26
				
				Если ТекЭлементФормы.Имя <> ИмяТЧЗадачи Тогда
					Продолжить;
				КонецЕсли; 
				
				ТекЦвет 	 = ПолучитьПриоритетныйЦветЗадачиНаСервере(СтрокаЗадача);
				ЦветЗаполнен = ПроверитьЗаполнениеЦвета(ТекЦвет);
				
				Если ЦветЗаполнен Тогда
					ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
					Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					Отбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
					Отбор.Использование  = ИСТИНА;
					Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.Задача");
					Отбор.ПравоеЗначение = ТекЗадача;
					
					Поля 				 = ОформлениеЗадачи.Поля.Элементы.Добавить();
					Поля.Использование 	 = ИСТИНА;
					//Поля.Поле 			 = Новый ПолеКомпоновкиДанных(ИмяТЧЗадачи + "НомерЗадачиПредставление");
					Поля.Поле 			 = Новый ПолеКомпоновкиДанных(ИменаЭлементов.ИмяПоляНомерЗадачиПредставлениеВКарточкеЗадачи);
					
					ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", ТекЦвет);
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЦикла;	
КонецПроцедуры 

&НаСервере
Процедура УстановитьБазовоеОформлениеЗадачНаСервере()
	УсловноеОформление.Элементы.Очистить();
	
	ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	ШрифтЖирный = Новый Шрифт(,,Истина);
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтЖирный);
	
	//ВажностьЧисло
	//ЦветДляЗадачиЧисло	
	//Цвет указывается от ЦветДляЗадачиЧисло:
	// 1 - черный цвет; 
	// 2 - красный цвет;
	// 3 - желтый цвет;
	// 4 - зеленый цвет;
	// 10 - салатовый цвет;
	
	//{ ***** Установка цвета от Важности и буфера задачи *****
	
	// 1 - черный цвет;
	ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 	 = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование  = ИСТИНА;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ЦветДляЗадачиЧисло");
	Отбор.ПравоеЗначение = 1;
	
	ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(0,0,0));	
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(255,255,255));	
	
	// 2 - красный цвет;
	ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 	 = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование  = ИСТИНА;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ЦветДляЗадачиЧисло");
	Отбор.ПравоеЗначение = 2;
	
	ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(255,102,102));
	
	// 3 - желтый цвет;
	ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 	 = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование  = ИСТИНА;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ЦветДляЗадачиЧисло");
	Отбор.ПравоеЗначение = 3;
	
	ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(255,255,0));
	
	// 4 - зеленый цвет;
	ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 	 = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование  = ИСТИНА;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ЦветДляЗадачиЧисло");
	Отбор.ПравоеЗначение = 4;
	
	ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(153,204,0));
	
	// 10 - салатовый цвет;	
	ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование  = ИСТИНА;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ЦветДляЗадачиЧисло");
	Отбор.ПравоеЗначение = 10;
	
	ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(207,238,0));
	
	//ОформлениеЗадачи 			 = УсловноеОформление.Элементы.Добавить();
	//ОтборГруппа 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	//ОтборГруппа.ТипГруппы 		 = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	//
	//ОтборЭлементы 				 = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//ОтборЭлементы.ВидСравнения 	 = ВидСравненияКомпоновкиДанных.Равно;
	//ОтборЭлементы.Использование  = ИСТИНА;
	//ОтборЭлементы.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ВажностьЧисло");
	//ОтборЭлементы.ПравоеЗначение = 2;
	//
	//ОтборЭлементы 				= ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//ОтборЭлементы.ВидСравнения 	= ВидСравненияКомпоновкиДанных.НеЗаполнено;
	//ОтборЭлементы.Использование = ИСТИНА;
	//ОтборЭлементы.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ВажностьЧисло");
	//
	//ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	//ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(153,204,0));
	//
	//ОформлениеЗадачи 	 = УсловноеОформление.Элементы.Добавить();
	//Отбор 				 = ОформлениеЗадачи.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	//Отбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	//Отбор.Использование  = ИСТИНА;
	//Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ТЧЗадачи.ВажностьЧисло");
	//Отбор.ПравоеЗначение = 3;
	//
	//ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи);
	//ОформлениеЗадачи.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(207,238,0));
	
	//} ***** Установка цвета от Важности задачи *****
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьПоляОформленияЗадачНаСервере(ОформлениеЗадачи)
	Для Каждого ТекЭлементФормы Из Элементы Цикл
		Если ТипЗнч(ТекЭлементФормы) = Тип("ТаблицаФормы") И ТекЭлементФормы.ПутьКДанным = "Объект.ТЧЗадачи" Тогда
			ИмяТЧЗадачи = ТекЭлементФормы.Имя;
			
			Если ТекЭлементФормы.Имя <> ИмяТЧЗадачи Тогда
				Продолжить;
			КонецЕсли; 
			
			Поля 			   = ОформлениеЗадачи.Поля.Элементы.Добавить();
			Поля.Использование = ИСТИНА;
			Поля.Поле 		   = Новый ПолеКомпоновкиДанных(ИмяТЧЗадачи + "_ВерхняяЧасть_НомерЗадачиПредставление");
			
			//ТЧЗадачи_ВерхняяЧасть_НомерЗадачиПредставление_В_процессе_выполнения
			//Элементы.В_процессе_выполнения_ТЧЗадачи_ВерхняяЧасть_НомерЗадачиПредставление_
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьЗаполнениеЦвета(ТекЦвет)
	Если ТекЦвет = Неопределено ИЛИ ТекЦвет = Новый Цвет(0,0,0) Тогда
		Возврат ЛОЖЬ;
	Иначе
		Возврат ИСТИНА;
	КонецЕсли;
КонецФункции

&НаСервере
Функция ПолучитьПриоритетныйЦветЗадачиНаСервере(СтрокаЗадача)
	Для Каждого ВидПриоритета Из ПолучитьМассивПриоритетовЦветаЗадач() Цикл
		ТекЦвет 	 = ПолучитьЦветЗадачиПоВидуПриоритета(СтрокаЗадача, ВидПриоритета);
		ЦветЗаполнен = ПроверитьЗаполнениеЦвета(ТекЦвет);
		Если ЦветЗаполнен Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла; 
	
	Возврат ТекЦвет;
КонецФункции

&НаСервере
Функция ПолучитьЦветЗадачиПоВидуПриоритета(СтрокаЗадача, ВидПриоритета)
	НужныйЦвет  = Неопределено;
	Задача 		= СтрокаЗадача.Задача;
				 
	пВидыПриоритетаЦвета = ВидыПриоритетаЦвета();
	
	Если ВидПриоритета = пВидыПриоритетаЦвета.Задача Тогда
		Если СтрокаЗадача.ИзмененЦветПоУмолчанию Тогда
			НужныйЦвет = Задача.ЦветЗадачи.Получить();
		Конецесли;
	ИначеЕсли ВидПриоритета = пВидыПриоритетаЦвета.Спринт Тогда 	
		НужныйЦвет = Задача.Спринт.ЦветСпринта.Получить();
	ИначеЕсли ВидПриоритета = пВидыПриоритетаЦвета.ОсновнаяЗадача Тогда 	
		НужныйЦвет = Задача.ОсновнаяЗадача.ЦветЗадачи.Получить();
	ИначеЕсли ВидПриоритета = пВидыПриоритетаЦвета.Важность Тогда 	
		НужныйЦвет = Задача.Важность.ЦветВажности.Получить();
	КонецЕсли; 
	
	Возврат НужныйЦвет;
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьМассивПриоритетовЦветаЗадач()
	Приоритеты = Новый Массив;
	Приоритеты.Добавить(ВидыПриоритетаЦвета().Задача);
	Приоритеты.Добавить(ВидыПриоритетаЦвета().Спринт);
	Приоритеты.Добавить(ВидыПриоритетаЦвета().ОсновнаяЗадача);
	Приоритеты.Добавить(ВидыПриоритетаЦвета().Важность);
	
	Возврат Новый ФиксированныйМассив(Приоритеты);
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция ВидыПриоритетаЦвета()
	Виды = Новый Структура();
	Виды.Вставить("Важность"	  , "Важность");
	Виды.Вставить("ОсновнаяЗадача", "ОсновнаяЗадача");
	Виды.Вставить("Спринт"		  , "Спринт");
	Виды.Вставить("Задача"		  , "Задача");
	
	Возврат Новый ФиксированнаяСтруктура(Виды);
КонецФункции 

#КонецОбласти




