#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Интерактивное удаление помеченных.

// Удаляет помеченные объекты, используется при интерактивном удалении в фоновом задании.
//
// Параметры:
//   ПараметрыВыполнения - Структура - Параметры, необходимые для удаления.
//   АдресХранилища - Строка - Адрес временного хранилища.
//
Процедура УдалитьПомеченныеОбъектыИнтерактивно(ПараметрыВыполнения, АдресХранилища) Экспорт
	УдалитьПомеченныеОбъекты(ПараметрыВыполнения);
	ПоместитьВоВременноеХранилище(ИзвлечьРезультат(ПараметрыВыполнения), АдресХранилища);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Удаление помеченных из регламентного задания.

// Удаляет помеченные объекты из регламентного задания.
Процедура УдалитьПомеченныеОбъектыИзРегламентногоЗадания() Экспорт
	
	ПараметрыВыполнения = Новый Структура;
	УдалитьПомеченныеОбъекты(ПараметрыВыполнения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Инициализация и запуск.

// Основная механика удаления помеченных объектов.
Процедура УдалитьПомеченныеОбъекты(ПараметрыВыполнения)
	
	Если НЕ Пользователи.ЭтоПолноправныйПользователь() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для выполнения операции.'");
	КонецЕсли;
	
	ИнициализироватьПараметры(ПараметрыВыполнения);
	
	Если ПараметрыВыполнения.ИскатьПомеченные Тогда
		ПолучитьПомеченныеНаУдаление(ПараметрыВыполнения);
	КонецЕсли;
	
	Если Не ПараметрыВыполнения.УдалятьПомеченные Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыВыполнения.Интерактивное
		И ПараметрыВыполнения.ВсеПомеченныеНаУдаление.Количество() = 0 Тогда
		Возврат; // Не удалять технологические объекты при интерактивном запуске если нет пользовательских объектов.
	КонецЕсли;
	
	Если ПараметрыВыполнения.Монопольно Тогда
		УдалитьПомеченныеОбъектыМонопольно(ПараметрыВыполнения);
	Иначе // Не монопольно.
		УдалитьПомеченныеОбъектыКонкурентно(ПараметрыВыполнения);
	КонецЕсли;
	
	Обработчики = ОбщегоНазначения.ОбработчикиСлужебногоСобытия("СтандартныеПодсистемы.УдалениеПомеченныхОбъектов\ПослеУдаленияПомеченных");
	Для Каждого Обработчик Из Обработчики Цикл
		Обработчик.Модуль.ПослеУдаленияПомеченных(ПараметрыВыполнения);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Конкурентное удаление помеченных.

// Основная механика.
Процедура УдалитьПомеченныеОбъектыКонкурентно(ПараметрыВыполнения)
	УстановитьПривилегированныйРежим(Истина);
	
	// Удаление технологических объектов (которые создавались и помечались на удаление без участия пользователя).
	Если ПараметрыВыполнения.ТехнологическиеОбъекты <> Неопределено Тогда
		КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ТехнологическиеОбъекты");
		Для Каждого Ссылка Из ПараметрыВыполнения.ТехнологическиеОбъекты Цикл
			УдалитьСсылку(ПараметрыВыполнения, Ссылка); // Для технологических объектов результат не выводится.
			КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, "ТехнологическиеОбъекты");
		КонецЦикла;
	КонецЕсли;
	
	// Удаление помеченных на удаление.
	КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ПользовательскиеОбъекты");
	Для Каждого Ссылка Из ПараметрыВыполнения.ПользовательскиеОбъекты Цикл
		Результат = УдалитьСсылку(ПараметрыВыполнения, Ссылка);
		ЗарегистрироватьРезультатУдаления(ПараметрыВыполнения, Ссылка, Результат, "ПользовательскиеОбъекты");
		КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, "ПользовательскиеОбъекты");
	КонецЦикла;
	
	// Удаление цепочек (линейно связанных объектов).
	КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ПовторноУдаляемые");
	Пока ПараметрыВыполнения.ПовторноУдаляемые.Количество() > 0 Цикл
		Ссылка = ПараметрыВыполнения.ПовторноУдаляемые[0];
		ПараметрыВыполнения.ПовторноУдаляемые.Удалить(0);
		
		Результат = УдалитьСсылку(ПараметрыВыполнения, Ссылка);
		
		ЗарегистрироватьРезультатУдаления(ПараметрыВыполнения, Ссылка, Результат, "ПовторноУдаляемые");
		КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, "ПовторноУдаляемые");
	КонецЦикла;
	
	// Удаление циклов (кольцевых связей объектов).
	УдалитьОставшиесяОбъектыВОднойТранзакции(ПараметрыВыполнения);
	
КонецПроцедуры

// Удаление одиночного объекта с контролем результата и откатом транзакции в случае неудачи.
Функция УдалитьСсылку(ПараметрыВыполнения, Ссылка)
	Результат = Новый Структура; // Результат обрабатывается в ЗарегистрироватьРезультатУдаления().
	Результат.Вставить("Успех", Неопределено);
	Результат.Вставить("ИнформацияОбОшибке", Неопределено);
	Результат.Вставить("ПрепятствующиеУдалению", Неопределено);
	Результат.Вставить("ВложенныеИПодчиненныеОбъекты", Новый Массив);
	Результат.Вставить("Количество", 0);
	Результат.Вставить("Сообщения", Неопределено);
	
	Информация = СформироватьИнформациюОТипах(ПараметрыВыполнения, ТипЗнч(Ссылка));
	
	ИсходныеСообщения = ПолучитьСообщенияПользователю(Истина);
	НачатьТранзакцию();
	Попытка
		ПопробоватьУдалитьСсылку(ПараметрыВыполнения, Ссылка, Информация, Результат);
		Если Результат.Успех Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	Исключение
		Результат.Успех = Ложь;
		Результат.ИнформацияОбОшибке = ИнформацияОбОшибке();
	КонецПопытки;
	Результат.Сообщения = ПолучитьСообщенияПользователю(Истина);
	Для Каждого Сообщение Из ИсходныеСообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	Если Не Результат.Успех Тогда
		ОтменитьТранзакцию();
		ЗаписатьПредупреждение(Ссылка, Результат.ИнформацияОбОшибке);
	КонецЕсли;
	
	Если ТипЗнч(Результат.ПрепятствующиеУдалению) = Тип("ТаблицаЗначений") Тогда
		Результат.ПрепятствующиеУдалению.Колонки[0].Имя = "УдаляемыйСсылка";
		Результат.ПрепятствующиеУдалению.Колонки[1].Имя = "ОбнаруженныйСсылка";
		Результат.ПрепятствующиеУдалению.Колонки[2].Имя = "ОбнаруженныйМетаданные";
		Для Каждого ОбнаруженныйСсылка Из Результат.ВложенныеИПодчиненныеОбъекты Цикл
			СтрокаТаблицы = Результат.ПрепятствующиеУдалению.Добавить();
			СтрокаТаблицы.УдаляемыйСсылка        = Ссылка;
			СтрокаТаблицы.ОбнаруженныйСсылка     = ОбнаруженныйСсылка;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Механика удаления объекта и поиска ссылок.
Процедура ПопробоватьУдалитьСсылку(ПараметрыВыполнения, Ссылка, Информация, Результат)
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить(Информация.ПолноеИмя);
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
	Блокировка.Заблокировать();
	
	Объект = Ссылка.ПолучитьОбъект();
	Если Объект = Неопределено Тогда
		Результат.Успех = Истина; // Объект уже удален.
		Возврат;
	КонецЕсли;
	Если Объект.ПометкаУдаления <> Истина Тогда
		Результат.Успех = Ложь;
		Результат.ИнформацияОбОшибке = НСтр("ru = 'Объект не помечен на удаление.'");
		Возврат;
	КонецЕсли;
	
	НайтиВложенныеИПодчиненныеОбъекты(ПараметрыВыполнения, Ссылка, Информация, Результат);
	
	Объект.Удалить();
	
	НайтиПрепятствующиеУдалению(ПараметрыВыполнения, Ссылка, Информация, Результат);
	
	Если Результат.Количество = 0 Тогда
		Результат.Успех = Истина;
	Иначе
		Результат.Успех = Ложь;
		Результат.ИнформацияОбОшибке = НСтр("ru = 'Объект используется в других объектах программы.'");
	КонецЕсли;
КонецПроцедуры

// Поиск ссылок вложенных и подчиненных (иерархия и связь по владельцу). Выполняется до удаления.
Процедура НайтиВложенныеИПодчиненныеОбъекты(ПараметрыВыполнения, Ссылка, Информация, Результат)
	
	Если Информация.Иерархический Тогда
		Запрос = Новый Запрос(Информация.ТекстЗапросаПоИерархии);
		Запрос.УстановитьПараметр("УдаляемыйСсылка", Ссылка);
		ВложенныеОбъекты = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТаблицы Из ВложенныеОбъекты Цикл
			Результат.ВложенныеИПодчиненныеОбъекты.Добавить(СтрокаТаблицы.Ссылка);
		КонецЦикла;
		Результат.Количество = Результат.Количество + ВложенныеОбъекты.Количество();
	КонецЕсли;
	
	Если Информация.ЕстьПодчиненные Тогда
		Запрос = Новый Запрос(Информация.ТекстЗапросаПоПодчиненным);
		Запрос.УстановитьПараметр("УдаляемыйСсылка", Ссылка);
		ПодчиненныеОбъекты = Запрос.Выполнить().Выгрузить();
		Для Каждого СтрокаТаблицы Из ПодчиненныеОбъекты Цикл
			Результат.ВложенныеИПодчиненныеОбъекты.Добавить(СтрокаТаблицы.Ссылка);
		КонецЦикла;
		Результат.Количество = Результат.Количество + ПодчиненныеОбъекты.Количество();
	КонецЕсли;
	
КонецПроцедуры

// Поиск ссылок путем сканирования всех таблиц. Выполняется после удаления.
Процедура НайтиПрепятствующиеУдалению(ПараметрыВыполнения, Ссылка, Информация, Результат)
	
	ПоискСсылок = Новый Массив;
	ПоискСсылок.Добавить(Ссылка);
	
	ПрепятствующиеУдалению = НайтиПоСсылкам(ПоискСсылок);
	
	// Пропуск ссылок из границ последовательности.
	Количество = ПрепятствующиеУдалению.Количество();
	ИмяКолонки = ПрепятствующиеУдалению.Колонки[1].Имя;
	Для Номер = 1 По Количество Цикл
		ОбратныйИндекс = Количество - Номер;
		СтрокаТаблицы = ПрепятствующиеУдалению[ОбратныйИндекс];
		ПрепятствующийСсылка = СтрокаТаблицы[ИмяКолонки];
		Если ПрепятствующийСсылка = Ссылка
			Или ДокументУжеУдален(ПараметрыВыполнения, ПрепятствующийСсылка) Тогда
			ПрепятствующиеУдалению.Удалить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	// Регистрация результата.
	Результат.ПрепятствующиеУдалению = ПрепятствующиеУдалению;
	Результат.Количество = Результат.Количество + Результат.ПрепятствующиеУдалению.Количество();
	
КонецПроцедуры

// Поиск ссылки документа в базе данных.
Функция ДокументУжеУдален(ПараметрыВыполнения, Ссылка)
	Если Ссылка = Неопределено Тогда
		Возврат Ложь; // Не документ.
	КонецЕсли;
	Информация = СформироватьИнформациюОТипах(ПараметрыВыполнения, ТипЗнч(Ссылка));
	Если Информация.Вид <> "ДОКУМЕНТ" Тогда
		Возврат Ложь; // Не документ.
	КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ "+ Информация.ПолноеИмя +" Где Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Возврат Запрос.Выполнить().Пустой();
КонецФункции

// Удаление циклов (кольцевых связей объектов).
Процедура УдалитьОставшиесяОбъектыВОднойТранзакции(ПараметрыВыполнения)
	Перем Ссылка;
	
	// 1. Объекты, которые невозможно удалить.
	//    Получаются путем определения неразрешимых связей.
	ОбъектыКоторыеНевозможноУдалить = Новый Массив;
	ВложенныеНеразрешимыеСвязи = Новый Массив;
	
	// 1.1. Первичным критерием для определения неразрешимых связей
	//      является тот факт, что объект, препятствующий удалению, не отмечен для удаления.
	Для Каждого СтрокаТаблицы Из ПараметрыВыполнения.ПрепятствующиеУдалению Цикл
		Если ПараметрыВыполнения.НеУдаленные.Найти(СтрокаТаблицы.ОбнаруженныйСсылка) = Неопределено
			И ОбъектыКоторыеНевозможноУдалить.Найти(СтрокаТаблицы.УдаляемыйСсылка) = Неопределено Тогда
			ОбъектыКоторыеНевозможноУдалить.Добавить(СтрокаТаблицы.УдаляемыйСсылка);
			Найденные = ПараметрыВыполнения.ПрепятствующиеУдалению.НайтиСтроки(Новый Структура("ОбнаруженныйСсылка", СтрокаТаблицы.УдаляемыйСсылка));
			ВложенныеНеразрешимыеСвязи.Добавить(Найденные);
		КонецЕсли;
	КонецЦикла;
	
	// 1.2. Далее при помощи массива ВложенныеНеразрешимыеСвязи
	//      получаются неразрешимые подчиненные - "связи связей", "связи связей связей" и т.д...
	Индекс = 0;
	Пока Индекс < ВложенныеНеразрешимыеСвязи.Количество() Цикл
		Найденные = ВложенныеНеразрешимыеСвязи[Индекс];
		Индекс = Индекс + 1;
		Для Каждого СтрокаТаблицы Из Найденные Цикл
			Если ОбъектыКоторыеНевозможноУдалить.Найти(СтрокаТаблицы.УдаляемыйСсылка) = Неопределено Тогда
				ОбъектыКоторыеНевозможноУдалить.Добавить(СтрокаТаблицы.УдаляемыйСсылка);
				Найденные = ПараметрыВыполнения.ПрепятствующиеУдалению.НайтиСтроки(Новый Структура("ОбнаруженныйСсылка", СтрокаТаблицы.УдаляемыйСсылка));
				ВложенныеНеразрешимыеСвязи.Добавить(Найденные);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// 2. Объекты, которые можно попробовать удалить в одной транзакции.
	//    = Массив удаляемых - Массив объектов, которые невозможно удалить.
	МассивСсылок = Новый Массив;
	Для Каждого Ссылка Из ПараметрыВыполнения.НеУдаленные Цикл
		Если ОбъектыКоторыеНевозможноУдалить.Найти(Ссылка) = Неопределено Тогда
			МассивСсылок.Добавить(Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Количество = МассивСсылок.Количество();
	Если Количество = 0 Тогда
		Возврат; // Нет объектов для удаления.
	КонецЕсли;
	
	// 3. Включение всех объектов в одну транзакцию и попытка удалить.
	Успех = Ложь;
	НачатьТранзакцию();
	Попытка
		Для Номер = 1 По Количество Цикл
			ОбратныйИндекс = Количество - Номер;
			Ссылка = МассивСсылок[ОбратныйИндекс];
			
			Информация = СформироватьИнформациюОТипах(ПараметрыВыполнения, ТипЗнч(Ссылка));
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(Информация.ПолноеИмя);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
			Блокировка.Заблокировать();
			
			Объект = Ссылка.ПолучитьОбъект();
			Если Объект = Неопределено Тогда // Объект уже удален.
				Продолжить;
			КонецЕсли;
			Если Объект.ПометкаУдаления <> Истина Тогда
				МассивСсылок.Удалить(ОбратныйИндекс); // Объект уже не помечен на удаление.
				Продолжить;
			КонецЕсли;
			
			Объект.Удалить();
		КонецЦикла;
		Ссылка = Неопределено;
		
		Если МассивСсылок.Количество() > 0 Тогда
			ПрепятствующиеУдалению = НайтиПоСсылкам(МассивСсылок);
			
			ИмяКолонки = ПрепятствующиеУдалению.Колонки[1].Имя;
			Для Каждого Ссылка Из МассивСсылок Цикл
				ПоискНеПрепятствующих = Новый Структура(ИмяКолонки, Ссылка);
				НеПрепятствующие = ПрепятствующиеУдалению.НайтиСтроки(ПоискНеПрепятствующих);
				Для Каждого СтрокаТаблицы Из НеПрепятствующие Цикл
					ПрепятствующиеУдалению.Удалить(СтрокаТаблицы);
				КонецЦикла;
			КонецЦикла;
			
			Если ПрепятствующиеУдалению.Количество() = 0 Тогда
				Успех = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Успех Тогда
			ЗафиксироватьТранзакцию();
		КонецЕсли;
		
	Исключение
		Успех = Ложь;
		ЗаписатьПредупреждение(Ссылка, ИнформацияОбОшибке());
	КонецПопытки;
	
	// 4. Регистрация результата (если успех).
	Если Успех Тогда
		Для Каждого Ссылка Из МассивСсылок Цикл
			// Регистрация ссылки в коллекции удаленных.
			Если ПараметрыВыполнения.Удаленные.Найти(Ссылка) = Неопределено Тогда
				ПараметрыВыполнения.Удаленные.Добавить(Ссылка);
			КонецЕсли;
			
			// Удаление ссылки из коллекции не удаленных.
			Индекс = ПараметрыВыполнения.НеУдаленные.Найти(Ссылка);
			Если Индекс <> Неопределено Тогда
				ПараметрыВыполнения.НеУдаленные.Удалить(Индекс);
			КонецЕсли;
			
			// Очистка информации о связях "от" удаленных объектов.
			Найденные = ПараметрыВыполнения.ПрепятствующиеУдалению.НайтиСтроки(Новый Структура("УдаляемыйСсылка", Ссылка));
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				ПараметрыВыполнения.ПрепятствующиеУдалению.Удалить(СтрокаТаблицы);
			КонецЦикла;
			
			// Очистка информации о связях "к" удаленным объектам.
			Найденные = ПараметрыВыполнения.ПрепятствующиеУдалению.НайтиСтроки(Новый Структура("ОбнаруженныйСсылка", Ссылка));
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				ПараметрыВыполнения.ПрепятствующиеУдалению.Удалить(СтрокаТаблицы);
			КонецЦикла;
		КонецЦикла;
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Регистрация результата удаления и заполнение коллекции ПовторноУдаляемые.
Процедура ЗарегистрироватьРезультатУдаления(ПараметрыВыполнения, Ссылка, Результат, ИмяКоллекции)
	// Результат формируется в УдалитьСсылку().
	Если Результат.Успех Тогда
		// Регистрация ссылки в коллекции удаленных.
		ПараметрыВыполнения.Удаленные.Добавить(Ссылка);
		
		// Исключение удаленного объекта из причин неудаления других объектов. Поиск.
		НеактуальныеПричины = ПараметрыВыполнения.ПрепятствующиеУдалению.НайтиСтроки(Новый Структура("ОбнаруженныйСсылка", Ссылка));
		Для Каждого Причина Из НеактуальныеПричины Цикл
			// Удаление причины неудаления другого объекта.
			УдаляемыйСсылка = Причина.УдаляемыйСсылка;
			ПараметрыВыполнения.ПрепятствующиеУдалению.Удалить(Причина);
			// Поиск других причин неудаления другого объекта.
			Если ПараметрыВыполнения.ПрепятствующиеУдалению.Найти(УдаляемыйСсылка, "УдаляемыйСсылка") = Неопределено Тогда
				// Устранены все причины неудаления другого объекта.
				// Регистрация другого объекта для повторного удаления.
				ПараметрыВыполнения.ПовторноУдаляемые.Добавить(УдаляемыйСсылка);
				Если ИмяКоллекции = "ПовторноУдаляемые" И ПараметрыВыполнения.Интерактивное Тогда
					ПараметрыВыполнения.Всего = ПараметрыВыполнения.Всего + 1;
				КонецЕсли;
				// Очистка записи о другом объекте из коллекции "НеУдаленные".
				Индекс = ПараметрыВыполнения.НеУдаленные.Найти(УдаляемыйСсылка);
				Если Индекс <> Неопределено Тогда
					ПараметрыВыполнения.НеУдаленные.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Иначе // Не успешно.
		
		ПараметрыВыполнения.НеУдаленные.Добавить(Ссылка);
		
		Если ТипЗнч(Результат.ИнформацияОбОшибке) = Тип("ИнформацияОбОшибке")
			Или Результат.ПрепятствующиеУдалению = Неопределено Тогда // Текст ошибки
			Если ТипЗнч(Результат.ИнформацияОбОшибке) = Тип("ИнформацияОбОшибке") Тогда
				ТекстОшибки = СтандартныеПодсистемыКлиентСервер.ИсходнаяПричинаОшибки(Результат.ИнформацияОбОшибке);
				Подробно    = ПодробноеПредставлениеОшибки(Результат.ИнформацияОбОшибке);
			Иначе
				ТекстОшибки = Результат.ИнформацияОбОшибке;
				Подробно    = "";
			КонецЕсли;
			Для Каждого СообщениеОтОбъекта Из Результат.Сообщения Цикл
				ТекстОшибки = СокрП(ТекстОшибки + Символы.ПС + Символы.ПС + СокрЛ(СообщениеОтОбъекта.Текст));
			КонецЦикла;
			Причина = ПараметрыВыполнения.ПрепятствующиеУдалению.Добавить();
			Причина.УдаляемыйСсылка    = Ссылка;
			Причина.УдаляемыйТип       = ТипЗнч(Причина.УдаляемыйСсылка);
			Причина.ОбнаруженныйСсылка = ТекстОшибки;
			Причина.ОбнаруженныйТип    = Тип("Строка");
			Причина.Подробно           = Подробно;
			СформироватьИнформациюОТипах(ПараметрыВыполнения, Причина.УдаляемыйТип);
		Иначе // Регистрация связей (причин не удаления) для вывода пользователю.
			Для Каждого СтрокаТаблицы Из Результат.ПрепятствующиеУдалению Цикл
				ЗаписатьПричинуВРезультат(ПараметрыВыполнения, СтрокаТаблицы);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли; // Результат.Успех.
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Монопольное удаление помеченных.

// Основная механика удаления помеченных объектов.
Процедура УдалитьПомеченныеОбъектыМонопольно(ПараметрыВыполнения)
	
	Если Не ПараметрыВыполнения.Свойство("ИсключенияПоискаСсылок") Тогда
		ПараметрыВыполнения.Вставить("ИсключенияПоискаСсылок", ОбщегоНазначения.ПолучитьОбщийСписокИсключенийПоискаСсылок());
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("ИсключающиеПравила") Тогда
		ПараметрыВыполнения.Вставить("ИсключающиеПравила", Новый Соответствие); // Кэш правил исключений поиска.
	КонецЕсли;
	
	УдаляемыеОбъекты = ПараметрыВыполнения.ВсеПомеченныеНаУдаление;
	
	Пока УдаляемыеОбъекты.Количество() > 0 Цикл
		
		КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "МонопольноеУдаление");
		
		ПрепятствующиеУдалению = Новый ТаблицаЗначений;
		
		// Попытка удалить с контролем ссылочной целостности.
		Попытка
			УстановитьПривилегированныйРежим(Истина);
			УдалитьОбъекты(УдаляемыеОбъекты, Истина, ПрепятствующиеУдалению);
			УстановитьПривилегированныйРежим(Ложь);
		Исключение
			ЗаписатьПредупреждение(Неопределено, ИнформацияОбОшибке());
		КонецПопытки;
		
		// Назначение имен колонок для таблицы конфликтов, возникших при удалении.
		ПрепятствующиеУдалению.Колонки[0].Имя = "УдаляемыйСсылка";
		ПрепятствующиеУдалению.Колонки[1].Имя = "ОбнаруженныйСсылка";
		ПрепятствующиеУдалению.Колонки[2].Имя = "ОбнаруженныйМетаданные";
		
		ВсеСвязиВИсключениях = Истина;
		
		// Анализ причин не удаления (мест использования помеченных на удаление).
		КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ПрепятствующиеУдалению", ПрепятствующиеУдалению);
		Для Каждого СтрокаТаблицы Из ПрепятствующиеУдалению Цикл
			КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, "ПрепятствующиеУдалению");
			
			// Проверка исключающих правил.
			Если СвязьВИсключенияхПоискаСсылок(ПараметрыВыполнения, СтрокаТаблицы) Тогда
				Продолжить; // Связь не препятствует удалению.
			КонецЕсли;
			
			// Невозможно удалить объект (мешает обнаруженная ссылка или запись регистра).
			ВсеСвязиВИсключениях = Ложь;
			
			// Сокращение удаляемых объектов.
			Индекс = УдаляемыеОбъекты.Найти(СтрокаТаблицы.УдаляемыйСсылка);
			Если Индекс <> Неопределено Тогда
				УдаляемыеОбъекты.Удалить(Индекс);
			КонецЕсли;
			
			// Регистрация связи для вывода пользователю.
			ЗаписатьПричинуВРезультат(ПараметрыВыполнения, СтрокаТаблицы);
		КонецЦикла;
		
		// Удаление без контроля, если все связи в исключениях поиска ссылок.
		Если ВсеСвязиВИсключениях Тогда
			Попытка
				УстановитьПривилегированныйРежим(Истина);
				УдалитьОбъекты(УдаляемыеОбъекты, Ложь);
				УстановитьПривилегированныйРежим(Ложь);
			Исключение
				ЗаписатьПредупреждение(Неопределено, ИнформацияОбОшибке());
			КонецПопытки;
			Прервать; // Выход из цикла.
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВыполнения.Вставить("Удаленные", УдаляемыеОбъекты);
	ПараметрыВыполнения.Удалить("ИсключенияПоискаСсылок");
	ПараметрыВыполнения.Удалить("ИсключающиеПравила");
	
КонецПроцедуры

// Проверяет что связь в исключениях.
Функция СвязьВИсключенияхПоискаСсылок(ПараметрыВыполнения, СтрокаТаблицы)
	// Определение исключающего правила для объекта метаданных, препятствующего удалению:
	// Для регистров (т.н. "необъектных таблиц") - массива реквизитов для поиска в записи регистра.
	// Для ссылочных типов (т.н. "объектных таблиц") - готового запроса для поиска в реквизитах.
	Правило = ПараметрыВыполнения.ИсключающиеПравила[СтрокаТаблицы.ОбнаруженныйМетаданные]; // Кэш.
	Если Правило = Неопределено Тогда
		Правило = СформироватьИсключающееПравило(ПараметрыВыполнения, СтрокаТаблицы);
		ПараметрыВыполнения.ИсключающиеПравила.Вставить(СтрокаТаблицы.ОбнаруженныйМетаданные, Правило);
	КонецЕсли;
	
	// Проверка исключающего правила.
	Если Правило = "*" Тогда
		Возврат Истина; // Можно удалять (обнаруженный объект метаданных не мешает).
	ИначеЕсли ТипЗнч(Правило) = Тип("Массив") Тогда // Имена измерений регистра.
		Для Каждого ИмяРеквизита Из Правило Цикл
			Если СтрокаТаблицы.ОбнаруженныйСсылка[ИмяРеквизита] = СтрокаТаблицы.УдаляемыйСсылка Тогда
				Возврат Истина; // Можно удалять (обнаруженная запись регистра не мешает).
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Правило) = Тип("Запрос") Тогда // Запрос к ссылочному объекту.
		Правило.УстановитьПараметр("УдаляемыйСсылка", СтрокаТаблицы.УдаляемыйСсылка);
		Правило.УстановитьПараметр("ОбнаруженныйСсылка", СтрокаТаблицы.ОбнаруженныйСсылка);
		Если НЕ Правило.Выполнить().Пустой() Тогда
			Возврат Истина; // Можно удалять (обнаруженная ссылка не мешает).
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Компонует правило оптимально для проверки.
Функция СформироватьИсключающееПравило(ПараметрыВыполнения, СтрокаТаблицы)
	ИсключениеПоиска = ПараметрыВыполнения.ИсключенияПоискаСсылок[СтрокаТаблицы.ОбнаруженныйМетаданные];
	Если ИсключениеПоиска = "*" Тогда
		Возврат "*"; // Можно удалять (обнаруженный объект метаданных не мешает).
	КонецЕсли;
	
	// Формирование исключающего правила.
	ЭтоРегистрСведений = Метаданные.РегистрыСведений.Содержит(СтрокаТаблицы.ОбнаруженныйМетаданные);
	Если ЭтоРегистрСведений
		Или Метаданные.РегистрыБухгалтерии.Содержит(СтрокаТаблицы.ОбнаруженныйМетаданные) // ЭтоРегистрБухгалтерии
		Или Метаданные.РегистрыНакопления.Содержит(СтрокаТаблицы.ОбнаруженныйМетаданные) Тогда // ЭтоРегистрНакопления
		
		Правило = Новый Массив;
		Если ЭтоРегистрСведений Тогда
			Для Каждого Измерение Из СтрокаТаблицы.ОбнаруженныйМетаданные.Измерения Цикл
				Если Измерение.Ведущее Тогда
					Правило.Добавить(Измерение.Имя);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого Измерение Из СтрокаТаблицы.ОбнаруженныйМетаданные.Измерения Цикл
				Правило.Добавить(Измерение.Имя);
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(ИсключениеПоиска) = Тип("Массив") Тогда
			Для Каждого ИмяРеквизита Из ИсключениеПоиска Цикл
				Если Правило.Найти(ИмяРеквизита) = Неопределено Тогда
					Правило.Добавить(ИмяРеквизита);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ИсключениеПоиска) = Тип("Массив") Тогда
		
		ТекстыЗапросов = Новый Соответствие;
		ИмяКорневойТаблицы = СтрокаТаблицы.ОбнаруженныйМетаданные.ПолноеИмя();
		
		Для Каждого ПутьКРеквизиту Из ИсключениеПоиска Цикл
			ПозицияТочки = СтрНайти(ПутьКРеквизиту, ".");
			Если ПозицияТочки = 0 Тогда
				ПолноеИмяТаблицы = ИмяКорневойТаблицы;
				ИмяРеквизита = ПутьКРеквизиту;
			Иначе
				ПолноеИмяТаблицы = ИмяКорневойТаблицы + "." + Лев(ПутьКРеквизиту, ПозицияТочки - 1);
				ИмяРеквизита = Сред(ПутьКРеквизиту, ПозицияТочки + 1);
			КонецЕсли;
			
			ТекстВложенногоЗапроса = ТекстыЗапросов.Получить(ПолноеИмяТаблицы);
			Если ТекстВложенногоЗапроса = Неопределено Тогда
				ТекстВложенногоЗапроса = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	1
				|ИЗ
				|	"+ ПолноеИмяТаблицы +" КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &ОбнаруженныйСсылка
				|	И (";
			Иначе
				ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + Символы.ПС + Символы.Таб + Символы.Таб + "ИЛИ ";
			КонецЕсли;
			ТекстВложенногоЗапроса = ТекстВложенногоЗапроса + "Таблица." + ИмяРеквизита + " = &УдаляемыйСсылка";
			
			ТекстыЗапросов.Вставить(ПолноеИмяТаблицы, ТекстВложенногоЗапроса);
		КонецЦикла;
		
		ТекстЗапроса = "";
		Для Каждого КлючИЗначение Из ТекстыЗапросов Цикл
			Если ТекстЗапроса <> "" Тогда
				ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
			КонецЕсли;
			ТекстЗапроса = ТекстЗапроса + КлючИЗначение.Значение + ")";
		КонецЦикла;
		
		Правило = Новый Запрос;
		Правило.Текст = ТекстЗапроса;
		
	Иначе
		
		Правило = "";
		
	КонецЕсли;
	
	Возврат Правило;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Общая механика.

// Инициализирует структуру параметров, необходимых для выполнения других служебных методов.
Процедура ИнициализироватьПараметры(ПараметрыВыполнения)
	// Определение параметров работы программы.
	Если Не ПараметрыВыполнения.Свойство("ИскатьПомеченные") Тогда
		ПараметрыВыполнения.Вставить("ИскатьПомеченные", Истина);
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("УдалятьПомеченные") Тогда
		ПараметрыВыполнения.Вставить("УдалятьПомеченные", Истина);
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("Монопольно") Тогда
		ПараметрыВыполнения.Вставить("Монопольно", Ложь);
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("ТехнологическиеОбъекты") Тогда
		ПараметрыВыполнения.Вставить("ТехнологическиеОбъекты", Новый Массив);
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("ПользовательскиеОбъекты") Тогда
		ПараметрыВыполнения.Вставить("ПользовательскиеОбъекты", Новый Массив);
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("ВсеПомеченныеНаУдаление") Тогда
		ПараметрыВыполнения.Вставить("ВсеПомеченныеНаУдаление", Новый Массив);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыВыполнения.ВсеПомеченныеНаУдаление, ПараметрыВыполнения.ТехнологическиеОбъекты);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыВыполнения.ВсеПомеченныеНаУдаление, ПараметрыВыполнения.ПользовательскиеОбъекты);
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("МодельСервиса") Тогда
		ПараметрыВыполнения.Вставить("МодельСервиса", ОбщегоНазначенияПовтИсп.РазделениеВключено());
		Если ПараметрыВыполнения.МодельСервиса Тогда
			ПараметрыВыполнения.Вставить("ВОбластиДанных", ОбщегоНазначения.ИспользованиеРазделителяСеанса());
			ПараметрыВыполнения.Вставить("РазделительОсновныхДанных",        ОбщегоНазначенияПовтИсп.РазделительОсновныхДанных());
			ПараметрыВыполнения.Вставить("РазделительВспомогательныхДанных", ОбщегоНазначенияПовтИсп.РазделительВспомогательныхДанных());
		КонецЕсли;
	КонецЕсли;
	Если Не ПараметрыВыполнения.Свойство("ИнформацияОТипах") Тогда
		ПараметрыВыполнения.Вставить("ИнформацияОТипах", Новый Соответствие);
	КонецЕсли;
	
	ПрепятствующиеУдалению = Новый ТаблицаЗначений;
	ПрепятствующиеУдалению.Колонки.Добавить("УдаляемыйСсылка");
	ПрепятствующиеУдалению.Колонки.Добавить("УдаляемыйТип", Новый ОписаниеТипов("Тип"));
	ПрепятствующиеУдалению.Колонки.Добавить("ОбнаруженныйСсылка");
	ПрепятствующиеУдалению.Колонки.Добавить("ОбнаруженныйТип", Новый ОписаниеТипов("Тип"));
	ПрепятствующиеУдалению.Колонки.Добавить("ОбнаруженныйПометкаУдаления", Новый ОписаниеТипов("Булево"));
	ПрепятствующиеУдалению.Колонки.Добавить("Подробно", Новый ОписаниеТипов("Строка"));
	
	ПараметрыВыполнения.Вставить("Удаленные",              Новый Массив);
	ПараметрыВыполнения.Вставить("НеУдаленные",            Новый Массив);
	ПараметрыВыполнения.Вставить("ПрепятствующиеУдалению", ПрепятствующиеУдалению);
	ПараметрыВыполнения.Вставить("ПовторноУдаляемые",      Новый Массив);
	ПараметрыВыполнения.Вставить("Интерактивное",          ПараметрыВыполнения.Свойство("ПериодЗаписи"));
	
	КлиентИнициализироватьПараметры(ПараметрыВыполнения);
КонецПроцедуры

// Формирует массив помеченных на удаление с учетом разделения.
Процедура ПолучитьПомеченныеНаУдаление(ПараметрыВыполнения)
	
	КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ПередПоискомПомеченныхНаУдаление");
	УдалениеПомеченныхОбъектовПереопределяемый.ПередПоискомПомеченныхНаУдаление(ПараметрыВыполнения);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получение списка помеченных на удаление.
	КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ПоискПомеченныхНаУдаление");
	ПараметрыВыполнения.ВсеПомеченныеНаУдаление = НайтиПомеченныеНаУдаление();
	
	// Распределение помеченных на удаление по коллекциям.
	КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, "ВсеПомеченныеНаУдаление");
	Количество = ПараметрыВыполнения.ВсеПомеченныеНаУдаление.Количество();
	Для Номер = 1 По Количество Цикл
		ОбратныйИндекс = Количество - Номер;
		Ссылка = ПараметрыВыполнения.ВсеПомеченныеНаУдаление[ОбратныйИндекс];
		
		Информация = СформироватьИнформациюОТипах(ПараметрыВыполнения, ТипЗнч(Ссылка));
		КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, "ВсеПомеченныеНаУдаление");
		
		Если ПараметрыВыполнения.МодельСервиса
			И ПараметрыВыполнения.ВОбластиДанных
			И Не Информация.Разделенный Тогда
			ПараметрыВыполнения.ВсеПомеченныеНаУдаление.Удалить(ОбратныйИндекс);
			Продолжить; // Неразделенные объекты запрещено изменять из области данных.
		КонецЕсли;
		
		Если Информация.ЕстьПредопределенные И Информация.Предопределенные.Найти(Ссылка) <> Неопределено Тогда
			ПараметрыВыполнения.ВсеПомеченныеНаУдаление.Удалить(ОбратныйИндекс);
			Продолжить; // Предопределенные элементы создаются и удаляются только автоматически.
		КонецЕсли;
		
		Если Информация.Технический = Истина Тогда
			ПараметрыВыполнения.ТехнологическиеОбъекты.Добавить(Ссылка);
		Иначе
			ПараметрыВыполнения.ПользовательскиеОбъекты.Добавить(Ссылка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Формирует информацию о типе объекта метаданных, как то: полное имя, представления, вид и т.п.
Функция СформироватьИнформациюОТипах(ПараметрыВыполнения, Тип) Экспорт
	Информация = ПараметрыВыполнения.ИнформацияОТипах.Получить(Тип); // Кэш.
	Если Информация <> Неопределено Тогда
		Возврат Информация;
	КонецЕсли;
	
	Информация = Новый Структура("ПолноеИмя, ПредставлениеЭлемента, ПредставлениеСписка,
	|Вид, Ссылочный, Технический, Разделенный,
	|Иерархический, ТекстЗапросаПоИерархии,
	|ЕстьПодчиненные, ТекстЗапросаПоПодчиненным,
	|ЕстьПредопределенные, Предопределенные");
	
	// Поиск объекта метаданных.
	ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
	
	// Заполнение базовой информации.
	Информация.ПолноеИмя = ВРег(ОбъектМетаданных.ПолноеИмя());
	
	// Представления: элемента и списка.
	СтандартныеСвойства = Новый Структура("ПредставлениеОбъекта, РасширенноеПредставлениеОбъекта, ПредставлениеСписка, РасширенноеПредставлениеСписка");
	ЗаполнитьЗначенияСвойств(СтандартныеСвойства, ОбъектМетаданных);
	Если ЗначениеЗаполнено(СтандартныеСвойства.ПредставлениеОбъекта) Тогда
		Информация.ПредставлениеЭлемента = СтандартныеСвойства.ПредставлениеОбъекта;
	ИначеЕсли ЗначениеЗаполнено(СтандартныеСвойства.РасширенноеПредставлениеОбъекта) Тогда
		Информация.ПредставлениеЭлемента = СтандартныеСвойства.РасширенноеПредставлениеОбъекта;
	Иначе
		Информация.ПредставлениеЭлемента = ОбъектМетаданных.Представление();
	КонецЕсли;
	Если ЗначениеЗаполнено(СтандартныеСвойства.ПредставлениеСписка) Тогда
		Информация.ПредставлениеСписка = СтандартныеСвойства.ПредставлениеСписка;
	ИначеЕсли ЗначениеЗаполнено(СтандартныеСвойства.РасширенноеПредставлениеСписка) Тогда
		Информация.ПредставлениеСписка = СтандартныеСвойства.РасширенноеПредставлениеСписка;
	Иначе
		Информация.ПредставлениеСписка = ОбъектМетаданных.Представление();
	КонецЕсли;
	
	// Вид и его свойства.
	Информация.Вид = Лев(Информация.ПолноеИмя, СтрНайти(Информация.ПолноеИмя, ".")-1);
	Если Информация.Вид = "СПРАВОЧНИК"
		Или Информация.Вид = "ДОКУМЕНТ"
		Или Информация.Вид = "ПЕРЕЧИСЛЕНИЕ"
		Или Информация.Вид = "ПЛАНВИДОВХАРАКТЕРИСТИК"
		Или Информация.Вид = "ПЛАНСЧЕТОВ"
		Или Информация.Вид = "ПЛАНВИДОВРАСЧЕТА"
		Или Информация.Вид = "БИЗНЕСПРОЦЕСС"
		Или Информация.Вид = "ЗАДАЧА"
		Или Информация.Вид = "ПЛАНОБМЕНА" Тогда
		Информация.Ссылочный = Истина;
	Иначе
		Информация.Ссылочный = Ложь;
	КонецЕсли;
	
	Если Информация.Вид = "СПРАВОЧНИК"
		Или Информация.Вид = "ПЛАНВИДОВХАРАКТЕРИСТИК" Тогда
		Информация.Иерархический = ОбъектМетаданных.Иерархический;
	ИначеЕсли Информация.Вид = "ПЛАНСЧЕТОВ" Тогда
		Информация.Иерархический = Истина;
	Иначе
		Информация.Иерархический = Ложь;
	КонецЕсли;
	Если Информация.Иерархический Тогда
		ШаблонЗапроса = "ВЫБРАТЬ Ссылка ИЗ &ПолноеИмя ГДЕ Родитель = &УдаляемыйСсылка";
		Информация.ТекстЗапросаПоИерархии = СтрЗаменить(ШаблонЗапроса, "&ПолноеИмя", Информация.ПолноеИмя);
	КонецЕсли;
	
	Информация.ЕстьПодчиненные = Ложь;
	Информация.ТекстЗапросаПоПодчиненным = "";
	Если Информация.Вид = "СПРАВОЧНИК"
		Или Информация.Вид = "ПЛАНВИДОВХАРАКТЕРИСТИК"
		Или Информация.Вид = "ПЛАНОБМЕНА"
		Или Информация.Вид = "ПЛАНСЧЕТОВ"
		Или Информация.Вид = "ПЛАНВИДОВРАСЧЕТА" Тогда
		
		ШаблонЗапроса = "ВЫБРАТЬ Ссылка ИЗ Справочник.&Имя ГДЕ Владелец = &УдаляемыйСсылка";
		ТекстЗапроса = "";
		
		Для Каждого Справочник Из Метаданные.Справочники Цикл
			Если Справочник.Владельцы.Содержит(ОбъектМетаданных) Тогда
				Если Информация.ЕстьПодчиненные = Ложь Тогда
					Информация.ЕстьПодчиненные = Истина;
				Иначе
					ТекстЗапроса = ТекстЗапроса + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
				КонецЕсли;
				ТекстЗапроса = ТекстЗапроса + СтрЗаменить(ШаблонЗапроса, "&Имя", Справочник.Имя);
			КонецЕсли;
		КонецЦикла;
		
		Информация.ТекстЗапросаПоПодчиненным = ТекстЗапроса;
	КонецЕсли;
	
	Если Информация.ПолноеИмя = "СПРАВОЧНИК.ИДЕНТИФИКАТОРЫОБЪЕКТОВМЕТАДАННЫХ"
		Или Информация.ПолноеИмя = "СПРАВОЧНИК.ПРЕДОПРЕДЕЛЕННЫЕВАРИАНТЫОТЧЕТОВ" Тогда
		Информация.Технический = Истина;
		Информация.Разделенный = Ложь;
	ИначеЕсли Информация.ПолноеИмя = "СПРАВОЧНИК.ИДЕНТИФИКАТОРЫОБЪЕКТОВРАСШИРЕНИЙ"
		Или Информация.ПолноеИмя = "СПРАВОЧНИК.ПРЕДОПРЕДЕЛЕННЫЕВАРИАНТЫОТЧЕТОВРАСШИРЕНИЙ" Тогда
		Информация.Технический = Истина;
		Информация.Разделенный = Истина;
	Иначе
		Информация.Технический = Ложь;
		Если ПараметрыВыполнения.МодельСервиса Тогда
			Информация.Разделенный = ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(ОбъектМетаданных, ПараметрыВыполнения.РазделительОсновныхДанных)
				Или ОбщегоНазначения.ЭтоРазделенныйОбъектМетаданных(ОбъектМетаданных, ПараметрыВыполнения.РазделительВспомогательныхДанных);
		КонецЕсли;
	КонецЕсли;
	
	Если Информация.Вид = "СПРАВОЧНИК"
		Или Информация.Вид = "ПЛАНВИДОВХАРАКТЕРИСТИК"
		Или Информация.Вид = "ПЛАНСЧЕТОВ"
		Или Информация.Вид = "ПЛАНВИДОВРАСЧЕТА" Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ "+ Информация.ПолноеИмя +" ГДЕ Предопределенный И ПометкаУдаления");
		Информация.Предопределенные = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		Информация.ЕстьПредопределенные = Информация.Предопределенные.Количество() > 0;
	Иначе
		Информация.ЕстьПредопределенные = Ложь;
	КонецЕсли;
	
	ПараметрыВыполнения.ИнформацияОТипах.Вставить(Тип, Информация);
	
	Возврат Информация;
КонецФункции

// Регистрирует предупреждение в журнале регистрации.
Процедура ЗаписатьПредупреждение(Ссылка, ИнформацияОбОшибке)
	Если ТипЗнч(ИнформацияОбОшибке) = Тип("ИнформацияОбОшибке") Тогда
		ТекстДляЖурнала = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	Иначе
		ТекстДляЖурнала = ИнформацияОбОшибке;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Удаление помеченных'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Предупреждение,
		,
		Ссылка,
		ТекстДляЖурнала);
КонецПроцедуры

// Регистрация причины неудаления.
Процедура ЗаписатьПричинуВРезультат(ПараметрыВыполнения, СтрокаТаблицы)
	УдаляемыйТип        = ТипЗнч(СтрокаТаблицы.УдаляемыйСсылка);
	УдаляемыйИнформация = СформироватьИнформациюОТипах(ПараметрыВыполнения, УдаляемыйТип);
	Если УдаляемыйИнформация.Технический Тогда
		Возврат;
	КонецЕсли;
	
	// Добавление не удаленных объектов.
	Если ПараметрыВыполнения.НеУдаленные.Найти(СтрокаТаблицы.УдаляемыйСсылка) = Неопределено Тогда
		ПараметрыВыполнения.НеУдаленные.Добавить(СтрокаТаблицы.УдаляемыйСсылка);
	КонецЕсли;
	
	Причина = ПараметрыВыполнения.ПрепятствующиеУдалению.Добавить();
	ЗаполнитьЗначенияСвойств(Причина, СтрокаТаблицы);
	Причина.УдаляемыйТип    = УдаляемыйТип;
	Причина.ОбнаруженныйТип = ТипЗнч(Причина.ОбнаруженныйСсылка);
	
	Если СтрокаТаблицы.ОбнаруженныйСсылка = Неопределено Тогда
		Если Метаданные.Константы.Содержит(СтрокаТаблицы.ОбнаруженныйМетаданные) Тогда
			Причина.ОбнаруженныйТип = Тип("КонстантаМенеджерЗначения." + СтрокаТаблицы.ОбнаруженныйМетаданные.Имя);
		Иначе
			Причина.ОбнаруженныйСсылка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Обнаружены неразрешимые ссылки (%1)'"),
				СтрокаТаблицы.ОбнаруженныйМетаданные.Представление());
			Причина.ОбнаруженныйТип = Тип("Строка");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Регистрация информации об объектах метаданных (если требуется).
	ОбнаруженныйИнформация = СформироватьИнформациюОТипах(ПараметрыВыполнения, Причина.ОбнаруженныйТип);
	
	// Заполнение подчиненных полей.
	Если ОбнаруженныйИнформация.Ссылочный Тогда
		Причина.ОбнаруженныйПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Причина.ОбнаруженныйСсылка, "ПометкаУдаления");
	Иначе
		Причина.ОбнаруженныйПометкаУдаления = Ложь;
	КонецЕсли;
КонецПроцедуры

// Основная механика удаления помеченных объектов.
Функция ИзвлечьРезультат(ПараметрыВыполнения)
	РезультатУдаления = ПараметрыВыполнения;
	РезультатУдаления.Удалить("ВсеПомеченныеНаУдаление");
	РезультатУдаления.Удалить("ВсеПомеченныеНаУдаление");
	Возврат РезультатУдаления;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Передача информации на клиент.

// Инициализирует структуру параметров, необходимых для передачи на клиент.
Процедура КлиентИнициализироватьПараметры(ПараметрыВыполнения)
	Если Не ПараметрыВыполнения.Интерактивное Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения.Вставить("ДостигнутыйПроцент", 0);
	ПараметрыВыполнения.Вставить("Диапазон", 0);
	ПараметрыВыполнения.Вставить("Всего", 0);
	ПараметрыВыполнения.Вставить("Номер", 0);
	ПараметрыВыполнения.Вставить("Время", ТекущаяДатаСеанса() - 0.1);
	
	ПараметрыВыполнения.Вставить("Диапазоны", Новый Соответствие);
	
	ОбщийВес = 0;
	Если ПараметрыВыполнения.ИскатьПомеченные Тогда
		ПараметрыВыполнения.Диапазоны.Вставить("ПередПоискомПомеченныхНаУдаление", 5);
		ПараметрыВыполнения.Диапазоны.Вставить("ПоискПомеченныхНаУдаление", 4);
		ПараметрыВыполнения.Диапазоны.Вставить("ВсеПомеченныеНаУдаление", 1);
		ОбщийВес = ОбщийВес + 10;
	КонецЕсли;
	Если ПараметрыВыполнения.УдалятьПомеченные Тогда
		Если ПараметрыВыполнения.Монопольно Тогда
			ПараметрыВыполнения.Диапазоны.Вставить("МонопольноеУдаление", 80);
			ПараметрыВыполнения.Диапазоны.Вставить("ПрепятствующиеУдалению", 10);
		Иначе // Не монопольно.
			ПараметрыВыполнения.Диапазоны.Вставить("ТехнологическиеОбъекты", 10);
			ПараметрыВыполнения.Диапазоны.Вставить("ПользовательскиеОбъекты", 70);
			ПараметрыВыполнения.Диапазоны.Вставить("ПовторноУдаляемые", 10);
		КонецЕсли;
		ОбщийВес = ОбщийВес + 90;
	КонецЕсли;
	Если ОбщийВес <> 0 И ОбщийВес <> 100 Тогда
		Коэффициент = 100/ОбщийВес;
		Для Каждого КлючИЗначение Из ПараметрыВыполнения.Диапазоны Цикл
			ПараметрыВыполнения.Диапазоны.Вставить(КлючИЗначение.Ключ, Окр(КлючИЗначение.Значение*Коэффициент, 0));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Регистрирует начало процесса.
Процедура КлиентОтметитьНачалоОбхода(ПараметрыВыполнения, ИмяКоллекции, Коллекция = Неопределено)
	Если Не ПараметрыВыполнения.Интерактивное Тогда
		Возврат;
	КонецЕсли;
	ПараметрыВыполнения.ДостигнутыйПроцент = ПараметрыВыполнения.ДостигнутыйПроцент + ПараметрыВыполнения.Диапазон;
	ПараметрыВыполнения.Диапазон = ПараметрыВыполнения.Диапазоны[ИмяКоллекции];
	Если Коллекция <> Неопределено Или ПараметрыВыполнения.Свойство(ИмяКоллекции, Коллекция) Тогда
		ПараметрыВыполнения.Всего = Коллекция.Количество();
		ПараметрыВыполнения.Номер = 0;
	Иначе
		ПараметрыВыполнения.Всего = 1;
		ПараметрыВыполнения.Номер = 0;
		КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, ИмяКоллекции);
	КонецЕсли;
КонецПроцедуры

// Регистрирует прогресс.
Процедура КлиентОтметитьПрогрессОбходаКоллекции(ПараметрыВыполнения, ИмяКоллекции)
	Если Не ПараметрыВыполнения.Интерактивное Тогда
		Возврат;
	КонецЕсли;
	
	// Регистрация прогресса.
	ПараметрыВыполнения.Номер = ПараметрыВыполнения.Номер + 1;
	
	// Проверка что подошло время передачи информации на клиент.
	Если ТекущаяДатаСеанса() >= ПараметрыВыполнения.Время Тогда
		// Установка следующего времени передачи информации на клиент.
		ПараметрыВыполнения.Время = ПараметрыВыполнения.Время + ПараметрыВыполнения.ПериодЗаписи;
	ИначеЕсли ПараметрыВыполнения.Номер = ПараметрыВыполнения.Всего Тогда
		// Вывод последнего.
	Иначе
		Возврат;
	КонецЕсли;
	
	Процент = ПараметрыВыполнения.ДостигнутыйПроцент
		+ ПараметрыВыполнения.Диапазон*ПараметрыВыполнения.Номер/ПараметрыВыполнения.Всего;
	
	// Подготовка передаваемых параметров.
	Если ИмяКоллекции = "ПередПоискомПомеченныхНаУдаление" Тогда
		
		Текст = НСтр("ru = 'Подготовка к поиску объектов, помеченных на удаление.'");
		
	ИначеЕсли ИмяКоллекции = "НайтиПомеченныеНаУдаление" Тогда
		
		Текст = НСтр("ru = 'Поиск объектов, помеченных на удаление.'");
		
	ИначеЕсли ИмяКоллекции = "ВсеПомеченныеНаУдаление" Тогда
		
		Текст = НСтр("ru = 'Анализ помеченных на удаление.'");
		
	ИначеЕсли ИмяКоллекции = "ТехнологическиеОбъекты" Тогда
		
		Текст = НСтр("ru = 'Подготовка к удалению.'");
		
	ИначеЕсли ИмяКоллекции = "МонопольноеУдаление" Тогда
		
		Текст = НСтр("ru = 'Выполняется удаление объектов.'");
		
	ИначеЕсли ИмяКоллекции = "ПользовательскиеОбъекты" Тогда
		
		НеУдалено = ПараметрыВыполнения.НеУдаленные.Количество();
		ПредставлениеНомер     = Формат(ПараметрыВыполнения.Номер, "ЧН=0; ЧГ=");
		ПредставлениеВсего     = Формат(ПараметрыВыполнения.Всего, "ЧН=0; ЧГ=");
		ПредставлениеНеУдалено = Формат(НеУдалено, "ЧН=0; ЧГ=");
		Если НеУдалено = 0 Тогда // Переход на СтрШаблон невозможен.
			Текст = НСтр("ru = 'Удалено: %1 из %2 объектов.'");
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ПредставлениеНомер, ПредставлениеВсего);
		Иначе
			Текст = НСтр("ru = 'Обработано: %1 из %2 объектов, из них не удалено: %3.'");
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ПредставлениеНомер, ПредставлениеВсего, ПредставлениеНеУдалено);
		КонецЕсли;
		
	ИначеЕсли ИмяКоллекции = "ПовторноУдаляемые" Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Повторная проверка не удаленных объектов: %1 из %2.'"),
			Формат(ПараметрыВыполнения.Номер, "ЧН=0; ЧГ="),
			Формат(ПараметрыВыполнения.Всего, "ЧН=0; ЧГ="));
		
	ИначеЕсли ИмяКоллекции = "ПрепятствующиеУдалению" Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Анализ объектов, препятствующих удалению: %1 из %2.'"),
			Формат(ПараметрыВыполнения.Номер, "ЧН=0; ЧГ="),
			Формат(ПараметрыВыполнения.Всего, "ЧН=0; ЧГ="));
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	// Регистрация сообщения для чтения из клиентского сеанса.
	ДлительныеОперации.СообщитьПрогресс(Процент, Текст);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
