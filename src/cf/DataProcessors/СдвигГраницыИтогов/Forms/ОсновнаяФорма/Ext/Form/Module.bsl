#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	Если Не УправлениеИтогамиИАгрегатамиСлужебный.НадоСдвинутьГраницуИтогов() Тогда
		Отказ = Истина; // Период уже был установлен в сеансе другого пользователя.
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДлительнаяОперация = ДлительнаяОперацияЗапускСервер(УникальныйИдентификатор);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ДлительнаяОперацияЗавершениеКлиент", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДлительнаяОперацияЗапускСервер(УникальныйИдентификатор)
	ИмяМетода = "Обработки.СдвигГраницыИтогов.ВыполнитьКоманду";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Итоги и агрегаты: Ускорение проведения документов и формирования отчетов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, Неопределено, НастройкиЗапуска);
КонецФункции

&НаКлиенте
Процедура ДлительнаяОперацияЗавершениеКлиент(Операция, ДополнительныеПараметры) Экспорт
	Результат = ДлительнаяОперацияЗавершениеСервер(Операция);
	Обработчик = Новый ОписаниеОповещения("ДлительнаяОперацияПослеВыводаРезультата", ЭтотОбъект, Результат);
	СтандартныеПодсистемыКлиент.ВывестиПредупреждение(ЭтотОбъект, Результат, Обработчик);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДлительнаяОперацияЗавершениеСервер(Операция)
	Результат = Новый Структура("ЗакрытьФорму, ВывестиПредупреждение", Истина, Ложь);
	Если Операция = Неопределено Тогда
		Результат.Вставить("Текст", НСтр("ru = 'Задание отменено'"));
	ИначеЕсли Операция.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Операция.АдресРезультата);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, РезультатОперации, Истина);
	ИначеЕсли Операция.Статус = "Ошибка" Тогда
		Результат.Вставить("Текст", Операция.КраткоеПредставлениеОшибки);
		Результат.Вставить("Подробно", Операция.ПодробноеПредставлениеОшибки);
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ДлительнаяОперацияПослеВыводаРезультата(ПустойПараметр, Результат) Экспорт
	Если ОписаниеОповещенияОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, Истина); // Обход особенности вызова из ПриОткрытии.
	КонецЕсли;
	Если Результат.ЗакрытьФорму И Открыта() Тогда
		Закрыть(Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти