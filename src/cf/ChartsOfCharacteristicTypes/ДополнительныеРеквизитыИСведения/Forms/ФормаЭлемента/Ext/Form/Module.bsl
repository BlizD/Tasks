&НаКлиенте
Перем ОбработкаПродолженияПриОшибкеЗаписи;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПереданныеПараметрыФормы = Новый Структура;
	ПереданныеПараметрыФормы.Вставить("ВладелецДополнительныхЗначений");
	ПереданныеПараметрыФормы.Вставить("ПоказатьУточнениеНабора");
	ПереданныеПараметрыФормы.Вставить("ТекущийНаборСвойств");
	ПереданныеПараметрыФормы.Вставить("ЭтоДополнительноеСведение");
	ПереданныеПараметрыФормы.Вставить("ВыборОбщегоСвойства");
	ПереданныеПараметрыФормы.Вставить("ВыбранныеЗначения");
	ПереданныеПараметрыФормы.Вставить("ВыборВладельцаДополнительныхЗначений");
	ПереданныеПараметрыФормы.Вставить("ЗначениеКопирования");
	ПереданныеПараметрыФормы.Вставить("КопированиеСВопросом");
	ПереданныеПараметрыФормы.Вставить("Перетаскивание", Ложь);
	ЗаполнитьЗначенияСвойств(ПереданныеПараметрыФормы, Параметры);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НаборыСвойств, "Наборы", Новый Массив, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НаборыСвойств, "ЭтоДополнительноеСведение", ЭтоДополнительноеСведение = 1, Истина);
	
	Если ПереданныеПараметрыФормы.КопированиеСВопросом
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеРеквизитыИСведения")
		И (Не РеквизитСоСпискомДополнительныхЗначений()
			Или Не ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеЗначения")) Тогда
		ПереданныеПараметрыФормы.КопированиеСВопросом = Ложь;
		ПереданныеПараметрыФормы.ЗначениеКопирования = ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений;
	КонецЕсли;
	
	Если ПереданныеПараметрыФормы.ВыборОбщегоСвойства
		Или ПереданныеПараметрыФормы.ВыборВладельцаДополнительныхЗначений
		Или ПереданныеПараметрыФормы.КопированиеСВопросом Тогда
		ЭтотОбъект.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		РежимПомощника               = Истина;
		Если ПереданныеПараметрыФормы.КопированиеСВопросом Тогда
			Элементы.СтраницыПомощникКарточка.ТекущаяСтраница = Элементы.ВыборДействия;
			ЗаполнитьСписокДействийПриДобавленииРеквизита();
		Иначе
			ЗаполнитьСтраницуВыбора();
		КонецЕсли;
		ОбновитьСоставЭлементовФормы();
		
		Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
			Элементы.КарточкаРеквизита.Видимость = Ложь;
		КонецЕсли;
	Иначе
		ЗаполнитьКарточкуРеквизитаИлиСведения();
		// Обработчик подсистемы запрета редактирования реквизитов объектов.
		ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект, Элементы.ОсновнаяКоманднаяПанель);
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПоискИУдалениеДублей") Тогда
		Элементы.ФормаПоискИУдалениеДублей.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.РазблокированиеРеквизитов") Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтотОбъект, ВыбранноеЗначение);
		Возврат;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
		Закрыть();
		
		// Открытие формы свойства.
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ВыбранноеЗначение);
		ПараметрыФормы.Вставить("ТекущийНаборСвойств", ТекущийНаборСвойств);
		
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
			ПараметрыФормы, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ПараметрыЗаписи.Свойство("КогдаНаименованиеУжеИспользуется") Тогда
	
		// Заполнение наименования по набору свойств
		// и проверка есть ли свойство с тем же наименованием.
		ТекстВопроса = НаименованиеУжеИспользуется(
			Объект.Заголовок, Объект.Ссылка, Объект.НаборСвойств, Объект.Наименование);
		
		Если ЗначениеЗаполнено(ТекстВопроса) Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить("ПродолжитьЗапись",            НСтр("ru = 'Продолжить запись'"));
			Кнопки.Добавить("ВернутьсяКВводуНаименования", НСтр("ru = 'Вернуться к вводу наименования'"));
			
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПослеОтветаНаВопросКогдаНаименованиеУжеИспользуется", ЭтотОбъект, ПараметрыЗаписи),
				ТекстВопроса, Кнопки, , "ВернутьсяКВводуНаименования");
			
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ОбработкаПродолжения") Тогда
		ОбработкаПродолженияПриОшибкеЗаписи = ПараметрыЗаписи.ОбработкаПродолжения;
		ПодключитьОбработчикОжидания("ПослеОшибкиЗаписи", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если УправлениеСвойствамиСлужебный.ТипЗначенияСодержитЗначенияСвойств(Объект.ТипЗначения) Тогда
		ТекущийОбъект.ДополнительныеЗначенияИспользуются = Истина;
	Иначе
		ТекущийОбъект.ДополнительныеЗначенияИспользуются = Ложь;
		ТекущийОбъект.ЗаголовокФормыЗначения = "";
		ТекущийОбъект.ЗаголовокФормыВыбораЗначения = "";
	КонецЕсли;
	
	Если Объект.ЭтоДополнительноеСведение
	 ИЛИ НЕ (    Объект.ТипЗначения.СодержитТип(Тип("Число" ))
	         ИЛИ Объект.ТипЗначения.СодержитТип(Тип("Дата"  ))
	         ИЛИ Объект.ТипЗначения.СодержитТип(Тип("Булево")) )Тогда
		
		ТекущийОбъект.ФорматСвойства = "";
	КонецЕсли;
	
	ТекущийОбъект.МногострочноеПолеВвода = 0;
	
	Если НЕ Объект.ЭтоДополнительноеСведение
	   И Объект.ТипЗначения.Типы().Количество() = 1
	   И Объект.ТипЗначения.СодержитТип(Тип("Строка")) Тогда
		
		Если МногострочноеПолеВвода Тогда
			ТекущийОбъект.МногострочноеПолеВвода = МногострочноеПолеВводаЧисло;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.НаборСвойств) Тогда
		ДобавитьВНабор = ТекущийОбъект.НаборСвойств;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НаборыДополнительныхРеквизитовИСведений");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДобавитьВНабор);
		Блокировка.Заблокировать();
		ЗаблокироватьДанныеДляРедактирования(ДобавитьВНабор);
		
		ОбъектНаборСвойств = ДобавитьВНабор.ПолучитьОбъект();
		Если ТекущийОбъект.ЭтоДополнительноеСведение Тогда
			ТабличнаяЧасть = ОбъектНаборСвойств.ДополнительныеСведения;
		Иначе
			ТабличнаяЧасть = ОбъектНаборСвойств.ДополнительныеРеквизиты;
		КонецЕсли;
		НайденнаяСтрока = ТабличнаяЧасть.Найти(ТекущийОбъект.Ссылка, "Свойство");
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ТабличнаяЧасть.Добавить();
			НоваяСтрока.Свойство = ТекущийОбъект.Ссылка;
			ОбъектНаборСвойств.Записать();
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ИзмененныйНабор", ДобавитьВНабор);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ОчиститьВведенныеВесовыеКоэффициенты") Тогда
		ОчиститьВведенныеВесовыеКоэффициенты();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если РежимДобавленияРеквизита = "СоздатьКопированием" Тогда
		ЗаписатьЗначенияДополнительногоРеквизитаПриКопировании(ТекущийОбъект);
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ОбновитьСоставЭлементовФормы();
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененныйНабор") Тогда
		ПараметрыЗаписи.Вставить("ИзмененныйНабор", ТекущийОбъект.ДополнительныеСвойства.ИзмененныйНабор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ДополнительныеРеквизитыИСведения",
		Новый Структура("Ссылка", Объект.Ссылка), Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ИзмененныйНабор") Тогда
		
		Оповестить("Запись_НаборыДополнительныхРеквизитовИСведений",
			Новый Структура("Ссылка", ПараметрыЗаписи.ИзмененныйНабор), ПараметрыЗаписи.ИзмененныйНабор);
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ОбработкаПродолжения") Тогда
		ОбработкаПродолженияПриОшибкеЗаписи = Неопределено;
		ОтключитьОбработчикОжидания("ПослеОшибкиЗаписи");
		ВыполнитьОбработкуОповещения(
			Новый ОписаниеОповещения(ПараметрыЗаписи.ОбработкаПродолжения.ИмяПроцедуры,
				ЭтотОбъект, ПараметрыЗаписи.ОбработкаПродолжения.Параметры),
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если РежимПомощника Тогда
		УстановитьНастройкиПомощника();
	Иначе
		УстановитьКартинкуКомментария();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭтоДополнительноеСведениеПриИзменении(Элемент)
	
	Объект.ЭтоДополнительноеСведение = ЭтоДополнительноеСведение;
	
	ОбновитьСоставЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеСпискаЗначенийКомментарийНажатие(Элемент)
	
	ЗаписатьОбъект("ПереходКСпискуЗначений",
		"УточнениеСпискаЗначенийКомментарийНажатиеЗавершение");
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеНаборовКомментарийНажатие(Элемент)
	
	ЗаписатьОбъект("ПереходКСпискуЗначений",
		"УточнениеНаборовКомментарийНажатиеПродолжение");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	
	ТекстПредупреждения = "";
	ОбновитьСоставЭлементовФормы(ТекстПредупреждения);
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЗначенияСВесомПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Ссылка)
	   И НЕ Объект.ДополнительныеЗначенияСВесом Тогда
		
		ТекстВопроса =
			НСтр("ru = 'Очистить введенные весовые коэффициенты?
			           |
			           |Данные будут записаны.'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("ОчиститьИЗаписать", НСтр("ru = 'Очистить и записать'"));
		Кнопки.Добавить("Отмена", НСтр("ru = 'Отмена'"));
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеПодтвержденияОчисткиВесовыхКоэффициентов", ЭтотОбъект),
			ТекстВопроса, Кнопки, , "ОчиститьИЗаписать");
	Иначе
		ЗаписатьОбъект("ИзменениеИспользованияВеса",
			"ДополнительныеЗначенияСВесомПриИзмененииЗавершение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МногострочноеПолеВводаЧислоПриИзменении(Элемент)
	
	МногострочноеПолеВвода = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьОбязательноПриИзменении(Элемент)
	Элементы.ЗадатьУсловиеЗаполнения.Доступность = Объект.ЗаполнятьОбязательно;
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьУсловиеДоступностиНажатие(Элемент)
	ОткрытьФормуНастройкиЗависимости("Доступен");
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьУсловиеНажатие(Элемент)
	ОткрытьФормуНастройкиЗависимости("ЗаполнятьОбязательно");
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьУсловиеВидимостиНажатие(Элемент)
	ОткрытьФормуНастройкиЗависимости("Виден");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Свойства_УстановленаЗависимостьРеквизита" Тогда
		Модифицированность = Истина;
		ЗначениеДобавлено = Ложь;
		Для Каждого УсловиеЗависимости Из УсловияЗависимостиРеквизитов Цикл
			Значение = Неопределено;
			Если Параметр.Свойство(УсловиеЗависимости.Представление, Значение) Тогда
				ЗначениеВХранилище = ПоместитьВоВременноеХранилище(Значение, УникальныйИдентификатор);
				УсловиеЗависимости.Значение = ЗначениеВХранилище;
				ЗначениеДобавлено = Истина;
			КонецЕсли;
		КонецЦикла;
		Если Не ЗначениеДобавлено Тогда
			Для Каждого ПереданныйПараметр Из Параметр Цикл
				ЗначениеВХранилище = ПоместитьВоВременноеХранилище(ПереданныйПараметр.Значение, УникальныйИдентификатор);
				УсловияЗависимостиРеквизитов.Добавить(ЗначениеВХранилище, ПереданныйПараметр.Ключ);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьЗависимостиДополнительныхРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	УстановитьКартинкуКомментария();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборыСвойств

&НаКлиенте
Процедура НаборыСвойствПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ПриИзмененииТекущегоНабора", 0.1, Истина)
КонецПроцедуры

&НаКлиенте
Процедура НаборыСвойствПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвойства

&НаКлиенте
Процедура СвойстваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	КомандаДалее(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗначения

&НаКлиенте
Процедура ЗначенияПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		ИмяСобытия = "Запись_ЗначенияСвойствОбъектов";
	Иначе
		ИмяСобытия = "Запись_ЗначенияСвойствОбъектовИерархия";
	КонецЕсли;
	
	Оповестить(ИмяСобытия,
		Новый Структура("Ссылка", Элемент.ТекущиеДанные.Ссылка),
		Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Копирование", Копирование);
	ДополнительныеПараметры.Вставить("Родитель", Родитель);
	ДополнительныеПараметры.Вставить("Группа", Группа);
	
	ЗаписатьОбъект("ПереходКСпискуЗначений",
		"ЗначенияПередНачаломДобавленияЗавершение", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элементы.ДополнительныеЗначения.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьОбъект("ПереходКСпискуЗначений",
		"ЗначенияПередНачаломИзмененияЗавершение");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		Если Не Элементы.КарточкаРеквизита.Видимость Тогда
			Элементы.КарточкаРеквизита.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимДобавленияРеквизита = "СделатьОбщим" Тогда
		КонвертироватьДополнительныйРеквизитВОбщий();
	КонецЕсли;
	
	Если РежимДобавленияРеквизита = "ДобавитьОбщийРеквизитВНабор"
		Или РежимДобавленияРеквизита = "СделатьОбщим" Тогда
		Результат = Новый Структура;
		Результат.Вставить("ОбщееСвойство", ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений);
		Если ПереданныеПараметрыФормы.Перетаскивание Тогда
			Результат.Вставить("Перетаскивание", Истина);
		КонецЕсли;
		ОповеститьОВыборе(Результат);
		Возврат;
	КонецЕсли;
	
	ОсновнаяСтраница = Элементы.СтраницыПомощникКарточка;
	ИндексСтраницы = ОсновнаяСтраница.ПодчиненныеЭлементы.Индекс(ОсновнаяСтраница.ТекущаяСтраница);
	Если ИндексСтраницы = 0
		И Элементы.Свойства.ТекущиеДанные = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Элемент не выбран!'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	Если ИндексСтраницы = 2 Тогда
		Если РежимДобавленияРеквизита = "СоздатьКопированием" Тогда
			Элементы.СтраницыЗначенияРеквизита.ТекущаяСтраница = Элементы.ДополнительныеЗначения;
		КонецЕсли;
		Записать();
		Закрыть();
		Возврат;
	КонецЕсли;
	ТекущаяСтраница = ОсновнаяСтраница.ПодчиненныеЭлементы.Получить(ИндексСтраницы + 1);
	УстановитьНастройкиПомощника(ТекущаяСтраница);
	
	ПриСменеСтраницы("Вперед", ОсновнаяСтраница, ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	ОсновнаяСтраница = Элементы.СтраницыПомощникКарточка;
	ИндексСтраницы = ОсновнаяСтраница.ПодчиненныеЭлементы.Индекс(ОсновнаяСтраница.ТекущаяСтраница);
	Если ИндексСтраницы = 1 Тогда
		РежимДобавленияРеквизита = "";
	КонецЕсли;
	ТекущаяСтраница = ОсновнаяСтраница.ПодчиненныеЭлементы.Получить(ИндексСтраницы - 1);
	УстановитьНастройкиПомощника(ТекущаяСтраница);
	
	ПриСменеСтраницы("Назад", ОсновнаяСтраница, ТекущаяСтраница);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьФорматЗначения(Команда)
	
	Конструктор = Новый КонструкторФорматнойСтроки(Объект.ФорматСвойства);
	
	Конструктор.ДоступныеТипы = Объект.ТипЗначения;
	
	Конструктор.Показать(
		Новый ОписаниеОповещения("РедактироватьФорматЗначенияЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеСпискаЗначенийИзменить(Команда)
	
	ЗаписатьОбъект("ИзменениеВидаРеквизита",
		"УточнениеСпискаЗначенийИзменитьЗавершение");
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеНаборовИзменить(Команда)
	
	ЗаписатьОбъект("ИзменениеВидаРеквизита",
		"УточнениеНаборовИзменитьЗавершение");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗаблокированныеРеквизиты = ЗапретРедактированияРеквизитовОбъектовКлиент.Реквизиты(ЭтотОбъект);
	
	Если ЗаблокированныеРеквизиты.Количество() > 0 Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", Объект.Ссылка);
		ПараметрыФормы.Вставить("ЭтоДополнительныйРеквизит", Объект.ЭтоДополнительноеСведение);
		
		Оповещение = Новый ОписаниеОповещения("ОбновитьФормуДляРедактирования", ЭтотОбъект);
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.РазблокированиеРеквизитов",
			ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
	Иначе
		ЗапретРедактированияРеквизитовОбъектовКлиент.ПоказатьПредупреждениеВсеВидимыеРеквизитыРазблокированы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискИУдалениеДублей(Команда)
	МодульПоискИУдалениеДублейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПоискИУдалениеДублейКлиент");
	ИмяФормыПоискИУдалениеДублей = МодульПоискИУдалениеДублейКлиент.ИмяФормыОбработкиПоискИУдалениеДублей();
	ОткрытьФорму(ИмяФормыПоискИУдалениеДублей);
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	Если Элементы.Свойства.ТекущиеДанные <> Неопределено Тогда
		// Открытие формы свойства.
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", Элементы.Свойства.ТекущиеДанные.Свойство);
		ПараметрыФормы.Вставить("ТекущийНаборСвойств", ВыбранныйНаборСвойств);
		
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
			ПараметрыФормы, Элементы.Свойства,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьКартинкуКомментария()
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗависимостиДополнительныхРеквизитов()
	
	Если УсловияЗависимостиРеквизитов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	
	ЗависимостиДополнительныхРеквизитов = ТекущийОбъект.ЗависимостиДополнительныхРеквизитов;
	
	Для Каждого УсловиеЗависимости Из УсловияЗависимостиРеквизитов Цикл
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("ЗависимоеСвойство", УсловиеЗависимости.Представление);
		МассивСтрок = ЗависимостиДополнительныхРеквизитов.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаТабличнойЧасти Из МассивСтрок Цикл
			ЗависимостиДополнительныхРеквизитов.Удалить(СтрокаТабличнойЧасти);
		КонецЦикла;
		
		ЗначениеИзХранилища = ПолучитьИзВременногоХранилища(УсловиеЗависимости.Значение);
		Если ЗначениеИзХранилища = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого НоваяЗависимость Из ЗначениеИзХранилища.Получить() Цикл
			ЗаполнитьЗначенияСвойств(ТекущийОбъект.ЗависимостиДополнительныхРеквизитов.Добавить(), НоваяЗависимость);
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	УстановитьЗаголовкиГиперссылок();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраницуВыбора()
	
	ИспользоватьОбщиеДополнительныеЗначения = ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеЗначения");
	ИспользоватьОбщиеДополнительныеРеквизитыИСведения = ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеРеквизитыИСведения");
	
	Если ПереданныеПараметрыФормы.ЭтоДополнительноеСведение <> Неопределено Тогда
		ЭтоДополнительноеСведение = ПереданныеПараметрыФормы.ЭтоДополнительноеСведение;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Наборы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК Наборы
	|ГДЕ
	|	Наборы.Родитель = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка)";
	
	Наборы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ДоступныеНаборы = Новый Массив;
	Для Каждого Ссылка Из Наборы Цикл
		ВидыСвойствНабора = УправлениеСвойствамиСлужебный.ВидыСвойствНабора(Ссылка, Ложь);
		
		Если ЭтоДополнительноеСведение = 1
		   И ВидыСвойствНабора.ДополнительныеСведения
		 ИЛИ ЭтоДополнительноеСведение = 0
		   И ВидыСвойствНабора.ДополнительныеРеквизиты Тогда
			
			ДоступныеНаборы.Добавить(Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НаборыСвойств, "Наборы", ДоступныеНаборы, Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НаборыСвойств, "ЭтоДополнительноеСведение", (ЭтоДополнительноеСведение = 1), Истина);
	
КонецПроцедуры

&НаСервере
Процедура КонвертироватьДополнительныйРеквизитВОбщий()
	НачатьТранзакцию();
	Попытка
		ВыбранныйРеквизит = ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений;
		ВыбранныйРеквизитОбъект = ВыбранныйРеквизит.ПолучитьОбъект();
		ВыбранныйРеквизитОбъект.НаборСвойств = Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка();
		ВыбранныйРеквизитОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКарточкуРеквизитаИлиСведения()
	
	Если ЗначениеЗаполнено(ПереданныеПараметрыФормы.ЗначениеКопирования) Тогда
		РежимДобавленияРеквизита = "СоздатьКопированием";
	КонецЕсли;
	
	СозданиеРеквизитаКопированием = (РежимДобавленияРеквизита = "СоздатьКопированием");
	
	ТекущийНаборСвойств = ПереданныеПараметрыФормы.ТекущийНаборСвойств;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ЭтоДополнительноеСведение.Доступность = Ложь;
		ПоказатьУточнениеНабора = ПереданныеПараметрыФормы.ПоказатьУточнениеНабора;
	Иначе
		Объект.Доступен = Истина;
		Объект.Виден  = Истина;
		
		Объект.ЗависимостиДополнительныхРеквизитов.Очистить();
		Если ЗначениеЗаполнено(ТекущийНаборСвойств) Тогда
			Объект.НаборСвойств = ТекущийНаборСвойств;
		КонецЕсли;
		
		Если СозданиеРеквизитаКопированием Тогда
			Объект.ВладелецДополнительныхЗначений = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
		ИначеЕсли ЗначениеЗаполнено(ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений) Тогда
			Объект.ВладелецДополнительныхЗначений = ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений;
		КонецЕсли;
		
		Если ПереданныеПараметрыФормы.ЭтоДополнительноеСведение <> Неопределено Тогда
			Объект.ЭтоДополнительноеСведение = ПереданныеПараметрыФормы.ЭтоДополнительноеСведение;
			
		ИначеЕсли НЕ ЗначениеЗаполнено(ПереданныеПараметрыФормы.ЗначениеКопирования) Тогда
			Элементы.ЭтоДополнительноеСведение.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Предопределенный И НЕ ЗначениеЗаполнено(Объект.Заголовок) Тогда
		Объект.Заголовок = Объект.Наименование;
	КонецЕсли;
	
	ЭтоДополнительноеСведение = ?(Объект.ЭтоДополнительноеСведение, 1, 0);
	
	Если СозданиеРеквизитаКопированием Тогда
		// Для случаев, когда копирование выполняется из карточки реквизита по команде "Скопировать".
		Если Не ЗначениеЗаполнено(ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений) Тогда
			ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений = ПереданныеПараметрыФормы.ЗначениеКопирования;
		КонецЕсли;
		
		СвойстваВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений, "ТипЗначения, ДополнительныеЗначенияСВесом");
		
		Объект.ТипЗначения = СвойстваВладельца.ТипЗначения;
		
		ЗначенияВладельцаСВесом                                = СвойстваВладельца.ДополнительныеЗначенияСВесом;
		Объект.ДополнительныеЗначенияСВесом                    = ЗначенияВладельцаСВесом;
		Элементы.ЗначенияДополнительногоРеквизита.Шапка        = ЗначенияВладельцаСВесом;
		Элементы.ЗначенияДополнительногоРеквизитаВес.Видимость = ЗначенияВладельцаСВесом;
		Элементы.СтраницыЗначенияРеквизита.ТекущаяСтраница     = Элементы.СтраницаДеревоЗначений;
		
		ЗаполнитьЗначенияДополнительныхРеквизитов(ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений);
	КонецЕсли;
	
	ОбновитьСоставЭлементовФормы();
	
	Если Объект.МногострочноеПолеВвода > 0 Тогда
		МногострочноеПолеВвода = Истина;
		МногострочноеПолеВводаЧисло = Объект.МногострочноеПолеВвода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуДляРедактирования(Результат, Параметр) Экспорт
	#Если ВебКлиент Тогда
		ОбновитьОтображениеДанных();
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросКогдаНаименованиеУжеИспользуется(Ответ, ПараметрыЗаписи) Экспорт
	
	Если Ответ <> "ПродолжитьЗапись" Тогда
		ТекущийЭлемент = Элементы.Заголовок;
		Если ПараметрыЗаписи.Свойство("ОбработкаПродолжения") Тогда
			ВыполнитьОбработкуОповещения(
				Новый ОписаниеОповещения(ПараметрыЗаписи.ОбработкаПродолжения.ИмяПроцедуры,
					ЭтотОбъект, ПараметрыЗаписи.ОбработкаПродолжения.Параметры),
				Истина);
		КонецЕсли;
	Иначе
		ПараметрыЗаписи.Вставить("КогдаНаименованиеУжеИспользуется");
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияОчисткиВесовыхКоэффициентов(Ответ, Неопределен) Экспорт
	
	Если Ответ <> "ОчиститьИЗаписать" Тогда
		Объект.ДополнительныеЗначенияСВесом = НЕ Объект.ДополнительныеЗначенияСВесом;
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ОчиститьВведенныеВесовыеКоэффициенты");
	
	ЗаписатьОбъект("ИзменениеИспользованияВеса",
		"ДополнительныеЗначенияСВесомПриИзмененииЗавершение",
		,
		ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЗначенияСВесомПриИзмененииЗавершение(Отказ, Неопределен) Экспорт
	
	Если Отказ Тогда
		Объект.ДополнительныеЗначенияСВесом = НЕ Объект.ДополнительныеЗначенияСВесом;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Оповестить(
			"Изменение_ЗначениеХарактеризуетсяВесовымКоэффициентом",
			Объект.ДополнительныеЗначенияСВесом,
			Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеСпискаЗначенийКомментарийНажатиеЗавершение(Отказ, Неопределен) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказатьУточнениеНабора", Истина);
	ПараметрыФормы.Вставить("Ключ", Объект.ВладелецДополнительныхЗначений);
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ТекущийНаборСвойств);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
		ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеНаборовКомментарийНажатиеПродолжение(Отказ, Неопределен) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйНабор = Неопределено;
	
	Если СписокНаборов.Количество() > 1 Тогда
		ПоказатьВыборИзСписка(
			Новый ОписаниеОповещения("УточнениеНаборовКомментарийНажатиеЗавершение", ЭтотОбъект),
			СписокНаборов, Элементы.УточнениеНаборовКомментарий);
	Иначе
		УточнениеНаборовКомментарийНажатиеЗавершение(, СписокНаборов[0].Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеНаборовКомментарийНажатиеЗавершение(ВыбранныйЭлемент, ВыбранныйНабор) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ВыбранныйНабор = ВыбранныйЭлемент.Значение;
	КонецЕсли;
	
	Если ВыбранныйНабор <> Неопределено Тогда
		Закрыть();
		
		ЗначениеВыбора = Новый Структура;
		ЗначениеВыбора.Вставить("Набор", ВыбранныйНабор);
		ЗначениеВыбора.Вставить("Свойство", Объект.Ссылка);
		ЗначениеВыбора.Вставить("ЭтоДополнительноеСведение", Объект.ЭтоДополнительноеСведение);
		ОповеститьОВыборе(ЗначениеВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПередНачаломДобавленияЗавершение(Отказ, ПараметрыОбработки) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимДобавленияРеквизита = "СоздатьКопированием" Тогда
		Элементы.СтраницыЗначенияРеквизита.ТекущаяСтраница = Элементы.ДополнительныеЗначения;
	КонецЕсли;
	
	Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		ИмяТаблицыЗначений = "Справочник.ЗначенияСвойствОбъектов";
	Иначе
		ИмяТаблицыЗначений = "Справочник.ЗначенияСвойствОбъектовИерархия";
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Родитель", ПараметрыОбработки.Родитель);
	ЗначенияЗаполнения.Вставить("Владелец", Объект.Ссылка);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СкрытьВладельца", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Если ПараметрыОбработки.Группа Тогда
		ПараметрыФормы.Вставить("ЭтоГруппа", Истина);
		
		ОткрытьФорму(ИмяТаблицыЗначений + ".ФормаГруппы", ПараметрыФормы, Элементы.Значения);
	Иначе
		ПараметрыФормы.Вставить("ПоказатьВес", Объект.ДополнительныеЗначенияСВесом);
		
		Если ПараметрыОбработки.Копирование Тогда
			ПараметрыФормы.Вставить("ЗначениеКопирования", Элементы.Значения.ТекущаяСтрока);
		КонецЕсли;
		
		ОткрытьФорму(ИмяТаблицыЗначений + ".ФормаОбъекта", ПараметрыФормы, Элементы.Значения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияПередНачаломИзмененияЗавершение(Отказ, Неопределен) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		ИмяТаблицыЗначений = "Справочник.ЗначенияСвойствОбъектов";
	Иначе
		ИмяТаблицыЗначений = "Справочник.ЗначенияСвойствОбъектовИерархия";
	КонецЕсли;
	
	Если Элементы.Значения.ТекущаяСтрока <> Неопределено Тогда
		// Открытие формы значения или группы значений.
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СкрытьВладельца", Истина);
		ПараметрыФормы.Вставить("ПоказатьВес", Объект.ДополнительныеЗначенияСВесом);
		ПараметрыФормы.Вставить("Ключ", Элементы.Значения.ТекущаяСтрока);
		
		ОткрытьФорму(ИмяТаблицыЗначений + ".ФормаОбъекта", ПараметрыФормы, Элементы.Значения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеСпискаЗначенийИзменитьЗавершение(Отказ, Неопределен) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ТекущийНаборСвойств);
	ПараметрыФормы.Вставить("НаборСвойств", Объект.НаборСвойств);
	ПараметрыФормы.Вставить("Свойство", Объект.Ссылка);
	ПараметрыФормы.Вставить("ВладелецДополнительныхЗначений", Объект.ВладелецДополнительныхЗначений);
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Объект.ЭтоДополнительноеСведение);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ИзменениеНастройкиСвойства",
		ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УточнениеНаборовИзменитьЗавершение(Отказ, Неопределен) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущийНаборСвойств", ТекущийНаборСвойств);
	ПараметрыФормы.Вставить("Свойство", Объект.Ссылка);
	ПараметрыФормы.Вставить("НаборСвойств", Объект.НаборСвойств);
	ПараметрыФормы.Вставить("ВладелецДополнительныхЗначений", Объект.ВладелецДополнительныхЗначений);
	ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", Объект.ЭтоДополнительноеСведение);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ИзменениеНастройкиСвойства",
		ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьОбъект(ВариантТекстаВопроса, ИмяПроцедурыПродолжения, ДополнительныеПараметры = Неопределено, ПараметрыЗаписи = Неопределено)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И НЕ Модифицированность Тогда
		
		ВыполнитьОбработкуОповещения(Новый ОписаниеОповещения(
			ИмяПроцедурыПродолжения, ЭтотОбъект, ДополнительныеПараметры), Ложь);
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаписи = Неопределено Тогда
		ПараметрыЗаписи = Новый Структура;
	КонецЕсли;
	
	ОбработкаПродолжения = Новый Структура;
	ОбработкаПродолжения.Вставить("ИмяПроцедуры", ИмяПроцедурыПродолжения);
	ОбработкаПродолжения.Вставить("Параметры", ДополнительныеПараметры);
	
	ПараметрыЗаписи.Вставить("ОбработкаПродолжения", ОбработкаПродолжения);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаписатьОбъектПродолжение("Записать", ПараметрыЗаписи);
		Возврат;
	КонецЕсли;
	
	Если ВариантТекстаВопроса = "ПереходКСпискуЗначений" Тогда
		ТекстВопроса =
			НСтр("ru = 'Переход к работе со списком значений
			           |возможен только после записи данных.
			           |
			           |Данные будут записаны.'");
	Иначе
		ТекстВопроса =
			НСтр("ru = 'Данные будут записаны.'")
	КонецЕсли;
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Записать", НСтр("ru = 'Записать'"));
	Кнопки.Добавить("Отмена", НСтр("ru = 'Отмена'"));
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения(
			"ЗаписатьОбъектПродолжение", ЭтотОбъект, ПараметрыЗаписи),
		ТекстВопроса, Кнопки, , "Записать");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьОбъектПродолжение(Ответ, ПараметрыЗаписи) Экспорт
	
	Если Ответ <> "Записать" Тогда
		Возврат;
	КонецЕсли;
	
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОшибкиЗаписи()
	
	Если ОбработкаПродолженияПриОшибкеЗаписи <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(
			Новый ОписаниеОповещения(ОбработкаПродолженияПриОшибкеЗаписи.ИмяПроцедуры,
				ЭтотОбъект, ОбработкаПродолженияПриОшибкеЗаписи.Параметры),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьФорматЗначенияЗавершение(Текст, Неопределен) Экспорт
	
	Если Текст <> Неопределено Тогда
		Объект.ФорматСвойства = Текст;
		УстановитьЗаголовокКнопкиФормата(ЭтотОбъект);
		
		ТекстПредупреждения = "";
		Массив = СтрРазделить(Текст, ";", Ложь);
		
		Для каждого Подстрока Из Массив Цикл
			Если СтрНайти(Подстрока, "ДП=") > 0 ИЛИ СтрНайти(Подстрока, "DE=") > 0 Тогда
				ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
					+ НСтр("ru = 'Представление пустой даты не поддерживается в полях ввода.'");
				Продолжить;
			КонецЕсли;
			Если СтрНайти(Подстрока, "ЧН=") > 0 ИЛИ СтрНайти(Подстрока, "NZ=") > 0 Тогда
				ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
					+ НСтр("ru = 'Представление пустого числа не поддерживается в полях ввода.'");
				Продолжить;
			КонецЕсли;
			Если СтрНайти(Подстрока, "ДФ=") > 0 ИЛИ СтрНайти(Подстрока, "DF=") > 0 Тогда
				Если СтрНайти(Подстрока, "ддд") > 0 ИЛИ СтрНайти(Подстрока, "ddd") > 0 Тогда
					ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
						+ НСтр("ru = 'Кратное название дня недели не поддерживается в полях ввода.'");
				КонецЕсли;
				Если СтрНайти(Подстрока, "дддд") > 0 ИЛИ СтрНайти(Подстрока, "dddd") > 0 Тогда
					ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
						+ НСтр("ru = 'Полное название дня недели не поддерживается в полях ввода.'");
				КонецЕсли;
				Если СтрНайти(Подстрока, "МММ") > 0 ИЛИ СтрНайти(Подстрока, "MMM") > 0 Тогда
					ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
						+ НСтр("ru = 'Кратное название месяца не поддерживается в полях ввода.'");
				КонецЕсли;
				Если СтрНайти(Подстрока, "ММММ") > 0 ИЛИ СтрНайти(Подстрока, "MMMM") > 0 Тогда
					ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
						+ НСтр("ru = 'Полное название месяца не поддерживается в полях ввода.'");
				КонецЕсли;
			КонецЕсли;
			Если СтрНайти(Подстрока, "ДЛФ=") > 0 ИЛИ СтрНайти(Подстрока, "DLF=") > 0 Тогда
				Если СтрНайти(Подстрока, "ДД") > 0 ИЛИ СтрНайти(Подстрока, "DD") > 0 Тогда
					ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
						+ НСтр("ru = 'Длинная дата (месяц прописью) не поддерживается в полях ввода.'");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
			ТекстПредупреждения = ТекстПредупреждения + Символы.ПС
				+ НСтр("ru = 'В местах использования полей надписей ограничений нет.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиПомощника(ТекущаяСтраница = Неопределено)
	
	Если ТекущаяСтраница = Неопределено Тогда
		ТекущаяСтраница = Элементы.СтраницыПомощникКарточка.ТекущаяСтраница;
	КонецЕсли;
	
	ШаблонЗаголовкаСписка        = НСтр("ru = 'Выберите %1 для включения в набор ""%2""'");
	ШаблонЗаголовкаПереключателя = НСтр("ru = 'Выберите вариант добавления дополнительного %1 ""%2"" в набор ""%3""'");
	
	Если ТекущаяСтраница = Элементы.ВыборРеквизита Тогда
		Если ПереданныеПараметрыФормы.ЭтоДополнительноеСведение Тогда
			Заголовок = НСтр("ru = 'Добавление дополнительного сведения'");
		Иначе
			Заголовок = НСтр("ru = 'Добавление дополнительного реквизита'");
		КонецЕсли;
		
		Элементы.КоманднаяПанельЛево.Доступность = Ложь;
		Элементы.КомандаДалее.Заголовок = НСтр("ru = 'Далее >'");
		
		Элементы.ДекорацияЗаголовок.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗаголовкаСписка,
			?(ПереданныеПараметрыФормы.ЭтоДополнительноеСведение, НСтр("ru = 'дополнительное сведение'"), НСтр("ru = 'дополнительный реквизит'")),
			Строка(ПереданныеПараметрыФормы.ТекущийНаборСвойств));
		
	ИначеЕсли ТекущаяСтраница = Элементы.ВыборДействия Тогда
		Если ПереданныеПараметрыФормы.КопированиеСВопросом Тогда
			Элементы.КоманднаяПанельЛево.Доступность = Ложь;
			ВладелецДополнительныхЗначений = ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений;
		Иначе
			Элементы.КоманднаяПанельЛево.Доступность = Истина;
			ВыбранныйЭлемент = Элементы.Свойства.ТекущиеДанные;
			Если ВыбранныйЭлемент = Неопределено Тогда
				ВладелецДополнительныхЗначений = ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений;
			Иначе
				ВладелецДополнительныхЗначений = Элементы.Свойства.ТекущиеДанные.Свойство;
			КонецЕсли;
		КонецЕсли;
		Элементы.КомандаДалее.Заголовок = НСтр("ru = 'Далее >'");
		
		Элементы.РежимДобавленияРеквизита.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗаголовкаПереключателя,
			?(ПереданныеПараметрыФормы.ЭтоДополнительноеСведение, НСтр("ru = 'сведения'"), НСтр("ru = 'реквизита'")),
			Строка(ВладелецДополнительныхЗначений),
			Строка(ПереданныеПараметрыФормы.ТекущийНаборСвойств));
		
		Если ПереданныеПараметрыФормы.ЭтоДополнительноеСведение Тогда
			Заголовок = НСтр("ru = 'Добавление дополнительного сведения'");
		Иначе
			Заголовок = НСтр("ru = 'Добавление дополнительного реквизита'");
		КонецЕсли;
		
	Иначе
		Элементы.КомандаДалее.Заголовок = НСтр("ru = 'Готово'");
		Элементы.КоманднаяПанельЛево.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСоставЭлементовФормы(ТекстПредупреждения = "")
	
	Если РежимПомощника Тогда
		Элементы.ОсновнаяКоманднаяПанель.Видимость = Ложь;
		Элементы.КомандаДалее.КнопкаПоУмолчанию    = Истина;
	Иначе
		Элементы.КоманднаяПанельПомощника.Видимость = Ложь;
		Элементы.СтраницыПомощникКарточка.ТекущаяСтраница = Элементы.КарточкаРеквизита;
	КонецЕсли;
	
	УстановитьЗаголовокФормы();
	
	Если НЕ Объект.ТипЗначения.СодержитТип(Тип("Число"))
	   И НЕ Объект.ТипЗначения.СодержитТип(Тип("Дата"))
	   И НЕ Объект.ТипЗначения.СодержитТип(Тип("Булево")) Тогда
		
		Объект.ФорматСвойства = "";
	КонецЕсли;
	
	УстановитьЗаголовокКнопкиФормата(ЭтотОбъект);
	
	Если Объект.ЭтоДополнительноеСведение
	 ИЛИ НЕ (    Объект.ТипЗначения.СодержитТип(Тип("Число" ))
	         ИЛИ Объект.ТипЗначения.СодержитТип(Тип("Дата"  ))
	         ИЛИ Объект.ТипЗначения.СодержитТип(Тип("Булево")) )Тогда
		
		Элементы.РедактироватьФорматЗначения.Видимость = Ложь;
	Иначе
		Элементы.РедактироватьФорматЗначения.Видимость = Истина;
	КонецЕсли;
	
	Если НЕ Объект.ЭтоДополнительноеСведение
	   И Объект.ТипЗначения.Типы().Количество() = 1
	   И Объект.ТипЗначения.СодержитТип(Тип("Строка")) Тогда
		
		Элементы.ГруппаМногострочность.Видимость = Истина;
	Иначе
		Элементы.ГруппаМногострочность.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.ЭтоДополнительноеСведение Тогда
		Объект.ЗаполнятьОбязательно = Ложь;
		Элементы.ГруппаСвойстваИЗависимости.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСвойстваИЗависимости.Видимость = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СтарыйТипЗначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ТипЗначения");
		Если ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
			Элементы.ЗадатьУсловиеЗаполнения.Доступность   = Объект.ЗаполнятьОбязательно;
			Элементы.ЗадатьУсловиеДоступности.Доступность  = Истина;
			Элементы.ЗадатьУсловиеВидимости.Доступность    = Истина;
		Иначе
			Элементы.ГруппаНастройкаЗависимостей.Видимость = Ложь;
			Элементы.Виден.Видимость  = Ложь;
			Элементы.Доступен.Видимость = Ложь;
		КонецЕсли;
		УстановитьЗаголовкиГиперссылок();
	Иначе
		СтарыйТипЗначения = Новый ОписаниеТипов;
		Элементы.ЗадатьУсловиеЗаполнения.Доступность   = Объект.ЗаполнятьОбязательно;
		Элементы.ЗадатьУсловиеДоступности.Доступность  = Истина;
		Элементы.ЗадатьУсловиеВидимости.Доступность    = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВладелецДополнительныхЗначений) Тогда
		
		СвойстваВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ВладелецДополнительныхЗначений, "ТипЗначения, ДополнительныеЗначенияСВесом");
		
		Если СвойстваВладельца.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
			Объект.ТипЗначения = Новый ОписаниеТипов(
				Объект.ТипЗначения,
				"СправочникСсылка.ЗначенияСвойствОбъектовИерархия",
				"СправочникСсылка.ЗначенияСвойствОбъектов");
		Иначе
			Объект.ТипЗначения = Новый ОписаниеТипов(
				Объект.ТипЗначения,
				"СправочникСсылка.ЗначенияСвойствОбъектов",
				"СправочникСсылка.ЗначенияСвойствОбъектовИерархия");
		КонецЕсли;
		
		ВладелецЗначений = Объект.ВладелецДополнительныхЗначений;
		ЗначенияСВесом   = СвойстваВладельца.ДополнительныеЗначенияСВесом;
	Иначе
		// Проверка возможности удаления типа дополнительных значений.
		Если УправлениеСвойствамиСлужебный.ТипЗначенияСодержитЗначенияСвойств(СтарыйТипЗначения) Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
			
			Если СтарыйТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
				Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ИСТИНА КАК ЗначениеИстина
				|ИЗ
				|	Справочник.ЗначенияСвойствОбъектовИерархия КАК ЗначенияСвойствОбъектовИерархия
				|ГДЕ
				|	ЗначенияСвойствОбъектовИерархия.Владелец = &Владелец";
			Иначе
				Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ИСТИНА КАК ЗначениеИстина
				|ИЗ
				|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
				|ГДЕ
				|	ЗначенияСвойствОбъектов.Владелец = &Владелец";
			КонецЕсли;
			
			Если НЕ Запрос.Выполнить().Пустой() Тогда
				
				Если СтарыйТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия"))
				   И НЕ Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
					
					ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Недопустимо удалять тип ""%1"",
						           |так как дополнительные значения уже введены.
						           |Сначала нужно удалить введенные дополнительные значения.
						           |
						           |Удаленный тип восстановлен.'"),
						Строка(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) );
					
					Объект.ТипЗначения = Новый ОписаниеТипов(
						Объект.ТипЗначения,
						"СправочникСсылка.ЗначенияСвойствОбъектовИерархия",
						"СправочникСсылка.ЗначенияСвойствОбъектов");
				
				ИначеЕсли СтарыйТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"))
				        И НЕ Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
					
					ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Недопустимо удалять тип ""%1"",
						           |так как дополнительные значения уже введены.
						           |Сначала нужно удалить введенные дополнительные значения.
						           |
						           |Удаленный тип восстановлен.'"),
						Строка(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) );
					
					Объект.ТипЗначения = Новый ОписаниеТипов(
						Объект.ТипЗначения,
						"СправочникСсылка.ЗначенияСвойствОбъектов",
						"СправочникСсылка.ЗначенияСвойствОбъектовИерархия");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Проверка, что установлено не более одного типа дополнительных значений.
		Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия"))
		   И Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
			
			Если НЕ СтарыйТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
				
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Недопустимо одновременно использовать типы значения
					           |""%1"" и
					           |""%2"".
					           |
					           |Второй тип удален.'"),
					Строка(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")),
					Строка(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) );
				
				// Удаление второго типа.
				Объект.ТипЗначения = Новый ОписаниеТипов(
					Объект.ТипЗначения,
					,
					"СправочникСсылка.ЗначенияСвойствОбъектовИерархия");
			Иначе
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Недопустимо одновременно использовать типы значения
					           |""%1"" и
					           |""%2"".
					           |
					           |Первый тип удален.'"),
					Строка(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")),
					Строка(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) );
				
				// Удаление первого типа.
				Объект.ТипЗначения = Новый ОписаниеТипов(
					Объект.ТипЗначения,
					,
					"СправочникСсылка.ЗначенияСвойствОбъектов");
			КонецЕсли;
		КонецЕсли;
		
		ВладелецЗначений = Объект.Ссылка;
		ЗначенияСВесом   = Объект.ДополнительныеЗначенияСВесом;
	КонецЕсли;
	
	Если УправлениеСвойствамиСлужебный.ТипЗначенияСодержитЗначенияСвойств(Объект.ТипЗначения) Тогда
		Элементы.ГруппаЗаголовкиФормЗначений.Видимость = Истина;
		Элементы.ДополнительныеЗначенияСВесом.Видимость = Истина;
		Элементы.СтраницаЗначения.Видимость = Истина;
	Иначе
		Элементы.ГруппаЗаголовкиФормЗначений.Видимость = Ложь;
		Элементы.ДополнительныеЗначенияСВесом.Видимость = Ложь;
		Элементы.СтраницаЗначения.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Значения.Шапка        = ЗначенияСВесом;
	Элементы.ЗначенияВес.Видимость = ЗначенияСВесом;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Значения, "Владелец", ВладелецЗначений, , , Истина);
	
	Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		Значения.ОсновнаяТаблица = "Справочник.ЗначенияСвойствОбъектов";
	Иначе
		Значения.ОсновнаяТаблица = "Справочник.ЗначенияСвойствОбъектовИерархия";
	КонецЕсли;
	
	// Отображение уточнений.
	
	Если НЕ ЗначениеЗаполнено(Объект.ВладелецДополнительныхЗначений) Тогда
		Элементы.УточнениеСпискаЗначений.Видимость = Ложь;
		Элементы.ДополнительныеЗначения.ТолькоПросмотр = Ложь;
		Элементы.ЗначенияКоманднаяПанельРедактирования.Видимость = Истина;
		Элементы.ЗначенияКонтекстноеМенюРедактирования.Видимость = Истина;
		Элементы.ДополнительныеЗначенияСВесом.Видимость = Истина;
	Иначе
		Элементы.УточнениеСпискаЗначений.Видимость = Истина;
		Элементы.ДополнительныеЗначения.ТолькоПросмотр = Истина;
		Элементы.ЗначенияКоманднаяПанельРедактирования.Видимость = Ложь;
		Элементы.ЗначенияКонтекстноеМенюРедактирования.Видимость = Ложь;
		Элементы.ДополнительныеЗначенияСВесом.Видимость = Ложь;
		
		Элементы.УточнениеСпискаЗначенийКомментарий.Гиперссылка = ЗначениеЗаполнено(Объект.Ссылка);
		Элементы.УточнениеСпискаЗначенийИзменить.Доступность    = ЗначениеЗаполнено(Объект.Ссылка);
		
		СвойстваВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ВладелецДополнительныхЗначений, "НаборСвойств, Заголовок, ЭтоДополнительноеСведение");
		
		Если СвойстваВладельца.ЭтоДополнительноеСведение <> Истина Тогда
			ШаблонУточнения = НСтр("ru = 'Список значений общий с реквизитом ""%1"" набора ""%2""'");
		Иначе
			ШаблонУточнения = НСтр("ru = 'Список значений общий со сведением ""%1"" набора ""%2""'");
		КонецЕсли;
		
		Элементы.УточнениеСпискаЗначенийКомментарий.Заголовок =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУточнения, СвойстваВладельца.Заголовок, Строка(СвойстваВладельца.НаборСвойств)) + "  ";
	КонецЕсли;
	
	ОбновитьСписокНаборов();
	
	Если НЕ ПоказатьУточнениеНабора
	   И ЗначениеЗаполнено(Объект.НаборСвойств)
	   И СписокНаборов.Количество() < 2 Тогда
		
		Элементы.УточнениеНаборов.Видимость = Ложь;
	Иначе
		Элементы.УточнениеНаборов.Видимость = Истина;
		Элементы.УточнениеНаборовКомментарий.Гиперссылка =
			ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(ТекущийНаборСвойств);
		
		Элементы.УточнениеНаборовИзменить.Доступность = ЗначениеЗаполнено(Объект.Ссылка);
		
		Если ЗначениеЗаполнено(Объект.НаборСвойств)
		   И СписокНаборов.Количество() < 2 Тогда
			
			Элементы.УточнениеНаборовИзменить.Видимость = Ложь;
		
		ИначеЕсли ЗначениеЗаполнено(ТекущийНаборСвойств) Тогда
			Элементы.УточнениеНаборовИзменить.Видимость = Истина;
		Иначе
			Элементы.УточнениеНаборовИзменить.Видимость = Ложь;
		КонецЕсли;
		
		Если СписокНаборов.Количество() > 0 Тогда
		
			Если ЗначениеЗаполнено(Объект.НаборСвойств)
			   И СписокНаборов.Количество() < 2 Тогда
				
				Если Объект.ЭтоДополнительноеСведение Тогда
					ШаблонУточнения = НСтр("ru = 'Сведение входит в набор: %1'");
				Иначе
					ШаблонУточнения = НСтр("ru = 'Реквизит входит в набор: %1'");
				КонецЕсли;
				ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУточнения, СокрЛП(СписокНаборов[0].Представление));
			Иначе
				Если СписокНаборов.Количество() > 1 Тогда
					Если Объект.ЭтоДополнительноеСведение Тогда
						ШаблонУточнения = НСтр("ru = 'Общее сведение входит в %1 %2'");
					Иначе
						ШаблонУточнения = НСтр("ru = 'Общий реквизит входит в %1 %2'");
					КонецЕсли;
					
					СтрокаНаборы = ПользователиСлужебныйКлиентСервер.ПредметЦелогоЧисла(СписокНаборов.Количество(),
						"Л = ru_RU", НСтр("ru = 'набор,набора,наборов,,,,,,0'"));
					
					ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУточнения, Формат(СписокНаборов.Количество(), "ЧГ="), СтрокаНаборы);
				Иначе
					Если Объект.ЭтоДополнительноеСведение Тогда
						ШаблонУточнения = НСтр("ru = 'Общее сведение входит в набор: %1'");
					Иначе
						ШаблонУточнения = НСтр("ru = 'Общий реквизит входит в набор: %1'");
					КонецЕсли;
					
					ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУточнения, СокрЛП(СписокНаборов[0].Представление));
				КонецЕсли;
			КонецЕсли;
		Иначе
			Элементы.УточнениеНаборовКомментарий.Гиперссылка = Ложь;
			Элементы.УточнениеНаборовИзменить.Видимость = Ложь;
			
			Если ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
				Если Объект.ЭтоДополнительноеСведение Тогда
					ТекстКомментария = НСтр("ru = 'Сведение не входит в набор'");
				Иначе
					ТекстКомментария = НСтр("ru = 'Реквизит не входит в набор'");
				КонецЕсли;
			Иначе
				Если Объект.ЭтоДополнительноеСведение Тогда
					ТекстКомментария = НСтр("ru = 'Общее сведение не входит в наборы'");
				Иначе
					ТекстКомментария = НСтр("ru = 'Общий реквизит не входит в наборы'");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.УточнениеНаборовКомментарий.Заголовок = ТекстКомментария + " ";
		
		Если Элементы.УточнениеНаборовКомментарий.Гиперссылка Тогда
			Элементы.УточнениеНаборовКомментарий.Подсказка = НСтр("ru = 'Переход к набору'");
		Иначе
			Элементы.УточнениеНаборовКомментарий.Подсказка = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьВведенныеВесовыеКоэффициенты()
	
	Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		ИмяТаблицыЗначений = "Справочник.ЗначенияСвойствОбъектов";
	Иначе
		ИмяТаблицыЗначений = "Справочник.ЗначенияСвойствОбъектовИерархия";
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицыЗначений);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТекущаяТаблица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЗначенияСвойствОбъектов КАК ТекущаяТаблица
		|ГДЕ
		|	ТекущаяТаблица.Вес <> 0";
		Запрос.Текст = СтрЗаменить(Запрос.Текст , "Справочник.ЗначенияСвойствОбъектов", ИмяТаблицыЗначений);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗначениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ЗначениеОбъект.Вес = 0;
			ЗначениеОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаборов()
	
	СписокНаборов.Очистить();
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДополнительныеРеквизиты.Ссылка КАК Набор,
		|	ДополнительныеРеквизиты.Ссылка.Наименование
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
		|ГДЕ
		|	ДополнительныеРеквизиты.Свойство = &Свойство
		|	И НЕ ДополнительныеРеквизиты.Ссылка.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДополнительныеСведения.Ссылка,
		|	ДополнительныеСведения.Ссылка.Наименование
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Свойство = &Свойство
		|	И НЕ ДополнительныеСведения.Ссылка.ЭтоГруппа");
		
		Запрос.УстановитьПараметр("Свойство", Объект.Ссылка);
		
		НачатьТранзакцию();
		Попытка
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				СписокНаборов.Добавить(Выборка.Набор, Выборка.Наименование + "         ");
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриСменеСтраницы(Направление, ОсновнаяСтраница, ТекущаяСтраница)
	
	ОсновнаяСтраница.ТекущаяСтраница = ТекущаяСтраница;
	Если ТекущаяСтраница = Элементы.ВыборДействия Тогда
		Если Направление = "Вперед" Тогда
			ВыбранныйЭлемент = Элементы.Свойства.ТекущиеДанные;
			ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений = ВыбранныйЭлемент.Свойство;
			ЗаполнитьСписокДействийПриДобавленииРеквизита();
		КонецЕсли;
	ИначеЕсли ТекущаяСтраница = Элементы.КарточкаРеквизита Тогда
		ЗаполнитьКарточкуРеквизитаИлиСведения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоОбщийДополнительныйРеквизит(ВыбранныйЭлемент)
	НаборСвойствРеквизита = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыбранныйЭлемент, "НаборСвойств");
	Возврат Не ЗначениеЗаполнено(НаборСвойствРеквизита.НаборСвойств);
КонецФункции

&НаСервере
Функция РеквизитСоСпискомДополнительныхЗначений()
	
	РеквизитСоСпискомДополнительныхЗначений = Истина;
	СвойстваВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений, "ТипЗначения");
	Если Не СвойстваВладельца.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"))
		И Не СвойстваВладельца.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
		РеквизитСоСпискомДополнительныхЗначений = Ложь;
	КонецЕсли;
	
	Возврат РеквизитСоСпискомДополнительныхЗначений;
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокДействийПриДобавленииРеквизита()
	
	ЭтоОбщийДополнительныйРеквизит = ЭтоОбщийДополнительныйРеквизит(ПереданныеПараметрыФормы.ВладелецДополнительныхЗначений);
	ИспользоватьОбщиеДополнительныеЗначения = ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеЗначения");
	ИспользоватьОбщиеДополнительныеРеквизитыИСведения = ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеРеквизитыИСведения");
	
	РеквизитСоСпискомДополнительныхЗначений = РеквизитСоСпискомДополнительныхЗначений();
	
	Если ПереданныеПараметрыФормы.ЭтоДополнительноеСведение Тогда
		ДобавитьОбщий = НСтр("ru = 'Добавить общее сведение в набор (рекомендуется)
			|
			|Выбранное сведение уже входит в несколько наборов, поэтому рекомендуется также включить его в этот набор ""как есть"".
			|В этом случае будет возможно отбирать по нему данные разных типов в списках и отчетах.'");
		СделатьОбщим = НСтр("ru = 'Сделать дополнительное сведение общим и добавить в набор
			|
			|Этот вариант подходит для тех случаев, когда сведение должно быть одинаково для обоих наборов.
			|В этом случае будет возможно отбирать по нему данные разных типов в списках и отчетах.'");
		СделатьПоОбразцу = НСтр("ru = 'Сделать копию сведения по образцу (с общим списком значений)
			|
			|Список значений этого сведения будет одинаков для обоих наборов.
			|С помощью этого варианта удобно выполнять централизованную настройку списка значений сразу для нескольких однотипных сведений.
			|При этом можно отредактировать наименование и ряд других свойств сведения.'");
		СоздатьКопированием = НСтр("ru = 'Сделать копию сведения
			|
			|Будет создана копия сведения%1'");
	Иначе
		ДобавитьОбщий = НСтр("ru = 'Добавить общий реквизит в набор (рекомендуется)
			|
			|Выбранный реквизит уже входит в несколько наборов, поэтому рекомендуется также включить его в этот набор ""как есть"".
			|В этом случае будет возможно отбирать по нему данные разных типов в списках и отчетах.'");
		СделатьОбщим = НСтр("ru = 'Сделать реквизит общим и добавить в набор
			|
			|Этот вариант подходит для тех случаев, когда реквизит должен быть одинаков для обоих наборов.
			|В этом случае будет возможно отбирать по нему данные разных типов в списках и отчетах.'");
		СделатьПоОбразцу = НСтр("ru = 'Сделать копию реквизита по образцу (с общим списком значений)
			|
			|Список значений этого реквизита будет одинаков для обоих наборов.
			|С помощью этого варианта удобно выполнять централизованную настройку списка значений сразу для нескольких однотипных реквизитов.
			|При этом можно отредактировать наименование и ряд других свойств реквизита.'");
		СоздатьКопированием = НСтр("ru = 'Сделать копию реквизита
			|
			|Будет создана копия реквизита%1'");
	КонецЕсли;
	
	СписокВыбора = Элементы.РежимДобавленияРеквизита.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если РеквизитСоСпискомДополнительныхЗначений Тогда
		ШаблонВставки = " " + НСтр("ru = 'и всех его значений.'");
	Иначе
		ШаблонВставки = ".";
	КонецЕсли;
	СоздатьКопированием = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СоздатьКопированием, ШаблонВставки);
	
	СписокВыбора.Добавить("СоздатьКопированием", СоздатьКопированием);
	
	Если ИспользоватьОбщиеДополнительныеЗначения И РеквизитСоСпискомДополнительныхЗначений Тогда
		СписокВыбора.Добавить("СоздатьПоОбразцу", СделатьПоОбразцу);
	КонецЕсли;
	
	Если ИспользоватьОбщиеДополнительныеРеквизитыИСведения И ЭтоОбщийДополнительныйРеквизит Тогда
		СписокВыбора.Добавить("ДобавитьОбщийРеквизитВНабор", ДобавитьОбщий);
	ИначеЕсли ИспользоватьОбщиеДополнительныеРеквизитыИСведения Тогда
		СписокВыбора.Добавить("СделатьОбщим", СделатьОбщим);
	КонецЕсли;
	
	РежимДобавленияРеквизита = "СоздатьКопированием";
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначенияДополнительногоРеквизитаПриКопировании(ТекущийОбъект)
	
	Если ТекущийОбъект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия")) Тогда
		Родитель = Справочники.ЗначенияСвойствОбъектовИерархия.ПустаяСсылка();
	Иначе
		Родитель = Справочники.ЗначенияСвойствОбъектов.ПустаяСсылка();
	КонецЕсли;
	
	Владелец = ТекущийОбъект.Ссылка;
	СтрокаДерева = ЗначенияДополнительныхРеквизитов.ПолучитьЭлементы();
	ЗаписатьЗначенияДополнительногоРеквизитаПриКопированииРекурсивно(Владелец, СтрокаДерева, Родитель);
	СтрокаДерева.Очистить();
	Элементы.СтраницыЗначенияРеквизита.ТекущаяСтраница = Элементы.ДополнительныеЗначения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗначенияДополнительногоРеквизитаПриКопированииРекурсивно(Владелец, СтрокаДерева, Родитель)
	
	Для Каждого ЭлементДерева Из СтрокаДерева Цикл
		КопияОбъекта = ЭлементДерева.Ссылка.ПолучитьОбъект().Скопировать();
		КопияОбъекта.Владелец = Владелец;
		КопияОбъекта.Родитель = Родитель;
		КопияОбъекта.Записать();
		
		ДочерниеЭлементы = ЭлементДерева.ПолучитьЭлементы();
		ЗаписатьЗначенияДополнительногоРеквизитаПриКопированииРекурсивно(Владелец, ДочерниеЭлементы, КопияОбъекта.Ссылка)
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиГиперссылок()
	
	ЗаданаЗависимостьДоступности              = Ложь;
	ЗаданаЗависимостьОбязательностиЗаполнения = Ложь;
	ЗаданаЗависимостьВидимости                = Ложь;
	ЗависимостиСвойств = Объект.ЗависимостиДополнительныхРеквизитов;
	
	Для Каждого ЗависимостьСвойства Из ЗависимостиСвойств Цикл
		Если ЗависимостьСвойства.ЗависимоеСвойство = "Доступен" Тогда
			ЗаданаЗависимостьДоступности = Истина;
		ИначеЕсли ЗависимостьСвойства.ЗависимоеСвойство = "ЗаполнятьОбязательно" Тогда
			ЗаданаЗависимостьОбязательностиЗаполнения = Истина;
		ИначеЕсли ЗависимостьСвойства.ЗависимоеСвойство = "Виден" Тогда
			ЗаданаЗависимостьВидимости = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ШаблонЗависимостьЗадана = НСтр("ru = 'при заданном условии'");
	ШаблонЗависимостьНеЗадана = НСтр("ru = 'всегда'");
	
	Элементы.ЗадатьУсловиеДоступности.Заголовок = ?(ЗаданаЗависимостьДоступности,
		ШаблонЗависимостьЗадана,
		ШаблонЗависимостьНеЗадана);
	
	Элементы.ЗадатьУсловиеЗаполнения.Заголовок = ?(ЗаданаЗависимостьОбязательностиЗаполнения,
		ШаблонЗависимостьЗадана,
		ШаблонЗависимостьНеЗадана);
	
	Элементы.ЗадатьУсловиеВидимости.Заголовок = ?(ЗаданаЗависимостьВидимости,
		ШаблонЗависимостьЗадана,
		ШаблонЗависимостьНеЗадана);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиЗависимости(НастраиваемоеСвойство)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДополнительныйРеквизит", Объект.Ссылка);
	ПараметрыФормы.Вставить("ЗависимостиРеквизитов", Объект.ЗависимостиДополнительныхРеквизитов);
	ПараметрыФормы.Вставить("Набор", Объект.НаборСвойств);
	ПараметрыФормы.Вставить("НастраиваемоеСвойство", НастраиваемоеСвойство);
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ЗависимостьРеквизитов", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
			Если Объект.ЭтоДополнительноеСведение Тогда
				Заголовок = Строка(Объект.Заголовок) + " " + НСтр("ru = '(Дополнительное сведение)'");
			Иначе
				Заголовок = Строка(Объект.Заголовок) + " " + НСтр("ru = '(Дополнительный реквизит)'");
			КонецЕсли;
		Иначе
			Если Объект.ЭтоДополнительноеСведение Тогда
				Заголовок = Строка(Объект.Заголовок) + " " + НСтр("ru = '(Общее дополнительное сведение)'");
			Иначе
				Заголовок = Строка(Объект.Заголовок) + " " + НСтр("ru = '(Общий дополнительный реквизит)'");
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Объект.НаборСвойств) Тогда
			Если Объект.ЭтоДополнительноеСведение Тогда
				Заголовок = НСтр("ru = 'Дополнительное сведение (создание)'");
			Иначе
				Заголовок = НСтр("ru = 'Дополнительный реквизит (создание)'");
			КонецЕсли;
		Иначе
			Если Объект.ЭтоДополнительноеСведение Тогда
				Заголовок = НСтр("ru = 'Общее дополнительное сведение (создание)'");
			Иначе
				Заголовок = НСтр("ru = 'Общий дополнительный реквизит (создание)'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТекущегоНабора()
	
	Если Элементы.НаборыСвойств.ТекущиеДанные = Неопределено Тогда
		Если ЗначениеЗаполнено(ВыбранныйНаборСвойств) Тогда
			ВыбранныйНаборСвойств = Неопределено;
			ПриИзмененииТекущегоНабораНаСервере();
		КонецЕсли;
		
	ИначеЕсли Элементы.НаборыСвойств.ТекущиеДанные.Ссылка <> ВыбранныйНаборСвойств Тогда
		ВыбранныйНаборСвойств = Элементы.НаборыСвойств.ТекущиеДанные.Ссылка;
		ТекущийНаборЭтоГруппа = Элементы.НаборыСвойств.ТекущиеДанные.ЭтоГруппа;
		ПриИзмененииТекущегоНабораНаСервере(ТекущийНаборЭтоГруппа);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииТекущегоНабораНаСервере(ТекущийНаборЭтоГруппа = Неопределено)
	
	Если ЗначениеЗаполнено(ВыбранныйНаборСвойств)
		И НЕ ТекущийНаборЭтоГруппа Тогда
		ОбновитьСписокСвойствТекущегоНабора();
	Иначе
		Свойства.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСвойствТекущегоНабора()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Набор", ВыбранныйНаборСвойств);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СвойстваНаборов.НомерСтроки,
	|	СвойстваНаборов.Свойство,
	|	СвойстваНаборов.ПометкаУдаления,
	|	ЕСТЬNULL(Свойства.Заголовок, ПРЕДСТАВЛЕНИЕ(СвойстваНаборов.Свойство)) КАК Заголовок,
	|	Свойства.ВладелецДополнительныхЗначений,
	|	Свойства.ТипЗначения КАК ТипЗначения,
	|	ВЫБОР
	|		КОГДА Свойства.Ссылка ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		КОГДА Свойства.НаборСвойств = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Общее,
	|	ВЫБОР
	|		КОГДА СвойстваНаборов.ПометкаУдаления = ИСТИНА
	|			ТОГДА 4
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК НомерКартинки,
	|	Свойства.Подсказка,
	|	Свойства.ЗаголовокФормыЗначения,
	|	Свойства.ЗаголовокФормыВыбораЗначения
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК СвойстваНаборов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
	|		ПО СвойстваНаборов.Свойство = Свойства.Ссылка
	|ГДЕ
	|	СвойстваНаборов.Ссылка = &Набор
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвойстваНаборов.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Наборы.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК Наборы
	|ГДЕ
	|	Наборы.Ссылка = &Набор";
	
	Если ЭтоДополнительноеСведение Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты",
			"Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения");
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если Элементы.Свойства.ТекущаяСтрока = Неопределено Тогда
		Строка = Неопределено;
	Иначе
		Строка = Свойства.НайтиПоИдентификатору(Элементы.Свойства.ТекущаяСтрока);
	КонецЕсли;
	ТекущееСвойство = ?(Строка = Неопределено, Неопределено, Строка.Свойство);
	
	Свойства.Очистить();
	
	Выборка = РезультатыЗапроса[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Свойства.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		НоваяСтрока.ОбщиеЗначения = ЗначениеЗаполнено(Выборка.ВладелецДополнительныхЗначений);
		
		Если Выборка.ТипЗначения <> NULL
		   И УправлениеСвойствамиСлужебный.ТипЗначенияСодержитЗначенияСвойств(Выборка.ТипЗначения) Тогда
			
			НоваяСтрока.ТипЗначения = Строка(Новый ОписаниеТипов(
				Выборка.ТипЗначения,
				,
				"СправочникСсылка.ЗначенияСвойствОбъектовИерархия,
				|СправочникСсылка.ЗначенияСвойствОбъектов"));
			
			Запрос = Новый Запрос;
			Если ЗначениеЗаполнено(Выборка.ВладелецДополнительныхЗначений) Тогда
				Запрос.УстановитьПараметр("Владелец", Выборка.ВладелецДополнительныхЗначений);
			Иначе
				Запрос.УстановитьПараметр("Владелец", Выборка.Свойство);
			КонецЕсли;
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 4
			|	ЗначенияСвойствОбъектов.Наименование КАК Наименование
			|ИЗ
			|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
			|ГДЕ
			|	ЗначенияСвойствОбъектов.Владелец = &Владелец
			|	И НЕ ЗначенияСвойствОбъектов.ЭтоГруппа
			|	И НЕ ЗначенияСвойствОбъектов.ПометкаУдаления
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 4
			|	ЗначенияСвойствОбъектовИерархия.Наименование
			|ИЗ
			|	Справочник.ЗначенияСвойствОбъектовИерархия КАК ЗначенияСвойствОбъектовИерархия
			|ГДЕ
			|	ЗначенияСвойствОбъектовИерархия.Владелец = &Владелец
			|	И НЕ ЗначенияСвойствОбъектовИерархия.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА КАК ЗначениеИстина
			|ИЗ
			|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
			|ГДЕ
			|	ЗначенияСвойствОбъектов.Владелец = &Владелец
			|	И НЕ ЗначенияСвойствОбъектов.ЭтоГруппа
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Справочник.ЗначенияСвойствОбъектовИерархия КАК ЗначенияСвойствОбъектовИерархия
			|ГДЕ
			|	ЗначенияСвойствОбъектовИерархия.Владелец = &Владелец";
			РезультатыЗапроса = Запрос.ВыполнитьПакет();
			
			ПервыеЗначения = РезультатыЗапроса[0].Выгрузить().ВыгрузитьКолонку("Наименование");
			
			Если ПервыеЗначения.Количество() = 0 Тогда
				Если РезультатыЗапроса[1].Пустой() Тогда
					ПредставлениеЗначений = НСтр("ru = 'Значения еще не введены'");
				Иначе
					ПредставлениеЗначений = НСтр("ru = 'Значения помечены на удаление'");
				КонецЕсли;
			Иначе
				ПредставлениеЗначений = "";
				Номер = 0;
				Для каждого Значение Из ПервыеЗначения Цикл
					Номер = Номер + 1;
					Если Номер = 4 Тогда
						ПредставлениеЗначений = ПредставлениеЗначений + ",...";
						Прервать;
					КонецЕсли;
					ПредставлениеЗначений = ПредставлениеЗначений + ?(Номер > 1, ", ", "") + Значение;
				КонецЦикла;
			КонецЕсли;
			ПредставлениеЗначений = "<" + ПредставлениеЗначений + ">";
			Если ЗначениеЗаполнено(НоваяСтрока.ТипЗначения) Тогда
				ПредставлениеЗначений = ПредставлениеЗначений + ", ";
			КонецЕсли;
			НоваяСтрока.ТипЗначения = ПредставлениеЗначений + НоваяСтрока.ТипЗначения;
		КонецЕсли;
		
		Если Выборка.Свойство = ТекущееСвойство Тогда
			Элементы.Свойства.ТекущаяСтрока =
				Свойства[Свойства.Количество()-1].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияДополнительныхРеквизитов(ВладелецЗначений)
	
	ДеревоЗначений = РеквизитФормыВЗначение("ЗначенияДополнительныхРеквизитов");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Ссылка КАК Ссылка,
		|	ЗначенияСвойствОбъектов.Владелец КАК Владелец,
		|	0 КАК КодКартинки,
		|	ЗначенияСвойствОбъектов.Вес,
		|	ЗначенияСвойствОбъектов.Наименование
		|ИЗ
		|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|ГДЕ
		|	ЗначенияСвойствОбъектов.ПометкаУдаления = ЛОЖЬ
		|	И ЗначенияСвойствОбъектов.Владелец = &Владелец
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗначенияСвойствОбъектовИерархия.Ссылка,
		|	ЗначенияСвойствОбъектовИерархия.Владелец,
		|	0,
		|	ЗначенияСвойствОбъектовИерархия.Вес,
		|	ЗначенияСвойствОбъектовИерархия.Наименование
		|ИЗ
		|	Справочник.ЗначенияСвойствОбъектовИерархия КАК ЗначенияСвойствОбъектовИерархия
		|ГДЕ
		|	ЗначенияСвойствОбъектовИерархия.ПометкаУдаления = ЛОЖЬ
		|	И ЗначенияСвойствОбъектовИерархия.Владелец = &Владелец
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка ИЕРАРХИЯ";
	Запрос.УстановитьПараметр("Владелец", ВладелецЗначений);
	Результат = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДеревоЗначений = Результат.Скопировать();
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ЗначенияДополнительныхРеквизитов");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаименованиеУжеИспользуется(Знач Заголовок, Знач ТекущееСвойство, Знач НаборСвойств, НовоеНаименование)
	
	Если ЗначениеЗаполнено(НаборСвойств) Тогда
		НаименованиеНабора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НаборСвойств, "Наименование");
		НовоеНаименование = Заголовок + " (" + НаименованиеНабора + ")";
	Иначе
		НовоеНаименование = Заголовок;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Свойства.ЭтоДополнительноеСведение,
	|	Свойства.НаборСвойств
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
	|ГДЕ
	|	Свойства.Наименование = &Наименование
	|	И Свойства.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",       ТекущееСвойство);
	Запрос.УстановитьПараметр("Наименование", НовоеНаименование);
	
	НачатьТранзакцию();
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Если НЕ Выборка.Следующий() Тогда
		Возврат "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.НаборСвойств) Тогда
		Если Выборка.ЭтоДополнительноеСведение Тогда
			ТекстВопроса = НСтр("ru = 'Существует дополнительное сведение с наименованием
			                          |""%1"".'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Существует дополнительный реквизит с наименованием
			                          |""%1"".'");
		КонецЕсли;
	Иначе
		Если Выборка.ЭтоДополнительноеСведение Тогда
			ТекстВопроса = НСтр("ru = 'Существует общее дополнительное сведение с наименованием
			                          |""%1"".'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Существует общий дополнительный реквизит с наименованием
			                          |""%1"".'");
		КонецЕсли;
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстВопроса + Символы.ПС + Символы.ПС
		                         + НСтр("ru ='Рекомендуется использовать другое наименование,
		                         |иначе программа может работать некорректно.'"),
		НовоеНаименование);
	
	Возврат ТекстВопроса;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокКнопкиФормата(Форма)
	
	Если ПустаяСтрока(Форма.Объект.ФорматСвойства) Тогда
		ТекстЗаголовка = НСтр("ru = 'Формат по умолчанию'");
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Формат установлен'");
	КонецЕсли;
	
	Форма.Элементы.РедактироватьФорматЗначения.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

#КонецОбласти
