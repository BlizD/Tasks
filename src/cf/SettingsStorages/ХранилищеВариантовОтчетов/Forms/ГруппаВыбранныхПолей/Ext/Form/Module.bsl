#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ЗаголовокГруппы", ЗаголовокГруппы);
	
	Расположение = Неопределено;
	Если Не Параметры.Свойство("Расположение", Расположение) Тогда
		ВызватьИсключение НСтр("ru = 'Не передан служебный параметр ""Расположение"".'");
	КонецЕсли;
	Если Расположение = РасположениеПоляКомпоновкиДанных.Авто Тогда
		РасположениеГруппы = "Авто";
	ИначеЕсли Расположение = РасположениеПоляКомпоновкиДанных.Вертикально Тогда
		РасположениеГруппы = "Вертикально";
	ИначеЕсли Расположение = РасположениеПоляКомпоновкиДанных.Вместе Тогда
		РасположениеГруппы = "Вместе";
	ИначеЕсли Расположение = РасположениеПоляКомпоновкиДанных.Горизонтально Тогда
		РасположениеГруппы = "Горизонтально";
	ИначеЕсли Расположение = РасположениеПоляКомпоновкиДанных.ОтдельнаяКолонка Тогда
		РасположениеГруппы = "ОтдельнаяКолонка";
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Некорректное значение параметра ""Расположение"": ""%1"".'"), Строка(Расположение));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ЗаголовокГруппы", ЗаголовокГруппы);
	РезультатВыбора.Вставить("Расположение", РасположениеПоляКомпоновкиДанных[РасположениеГруппы]);
	ОповеститьОВыборе(РезультатВыбора);
	Если Открыта() Тогда
		Закрыть(РезультатВыбора);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти