
#Область ОбработчикиСобытийФормы
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Инициализируем внутренние флаги.
	МожноДобавлятьВСправочник = Справочники.СтраныМира.ЕстьПравоДобавления();
	
	Если Параметры.РазрешитьДанныеКлассификатора=Неопределено Тогда
		РазрешитьДанныеКлассификатора = Истина;
	Иначе
		ТипБулево = Новый ОписаниеТипов("Булево");
		РазрешитьДанныеКлассификатора = ТипБулево.ПривестиЗначение(Параметры.РазрешитьДанныеКлассификатора);
	КонецЕсли;
	
	ТолькоДанныеКлассификатора = Параметры.ТолькоДанныеКлассификатора;
	
	Параметры.Свойство("РежимВыбора", РежимВыбора);
	
	// Разрешаем элементы
	Элементы.Список.РежимВыбора = РежимВыбора;
	Элементы.СписокВыбрать.Видимость         = РежимВыбора;
	Элементы.СписокВыбрать.КнопкаПоУмолчанию = РежимВыбора;
	
	РежимВыбораКлассификатораОКСМ = РежимВыбора И Не ТолькоДанныеКлассификатора;
	Если МожноДобавлятьВСправочник Тогда
		Элементы.СписокВыбратьИзКлассификатора.Видимость = РежимВыбораКлассификатораОКСМ;
	КонецЕсли;
	Элементы.СписокКлассификатор.Видимость           = НЕ РежимВыбораКлассификатораОКСМ;
	
	// По флагам определяем режим
	Если РежимВыбора Тогда
		Если РазрешитьДанныеКлассификатора Тогда
			Если ТолькоДанныеКлассификатора Тогда
				Если МожноДобавлятьВСправочник Тогда 
					// Выбор только стран классификатора.
					ОткрытьФормуКлассификатора = Истина
					
				Иначе
					// Показываем только пересечение справочника и классификатора.
					УстановитьОтборПересеченияСКлассификатором();
					// Кнопки классификатора скрываем.
					Элементы.СписокВыбратьИзКлассификатора.Видимость = Ложь;
					Элементы.СписокКлассификатор.Видимость           = Ложь;
				КонецЕсли;
				
			Иначе
				Если МожноДобавлятьВСправочник Тогда 
					// Показываем справочник и кнопку выбора из классификатора (установки по умолчанию).
				Иначе
					// Кнопки классификатора скрываем.
					Элементы.СписокВыбратьИзКлассификатора.Видимость = Ложь;
					Элементы.СписокКлассификатор.Видимость           = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		 Иначе
			// Показываем только элементы справочника.
			Элементы.СписокКлассификатор.Видимость = Ложь;
			// Кнопки классификатора скрываем.
			Элементы.СписокВыбратьИзКлассификатора.Видимость = Ложь;
			Элементы.СписокКлассификатор.Видимость           = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОткрытьФормуКлассификатора Тогда
		// Выбор только стран классификатора, открываем его форму для выбора.
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("РежимВыбора",        Истина);
		ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", ЗакрыватьПриВыборе);
		ПараметрыОткрытия.Вставить("ТекущаяСтрока",      Элементы.Список.ТекущаяСтрока);
		ПараметрыОткрытия.Вставить("РежимОткрытияОкна",  РежимОткрытияОкна);
		ПараметрыОткрытия.Вставить("ТекущаяСтрока",      Элементы.Список.ТекущаяСтрока);
		
		ОткрытьФорму("Справочник.СтраныМира.Форма.Классификатор", ПараметрыОткрытия, ВладелецФормы);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия="Справочник.СтраныМира.Изменение" Тогда
		ОбновитьОтображениеСпискаСтран();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
//

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Не Копирование Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru='Есть возможность подобрать страну мира из классификатора.
		                        |Подобрать?'");
								
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросОПодбореИзКлассификатора", ЭтотОбъект);
		КнопкиВыбора = Новый СписокЗначений();
		КнопкиВыбора.Добавить(КодВозвратаДиалога.Да, "Подобрать");
		КнопкиВыбора.Добавить(КодВозвратаДиалога.Нет, "Создать");
		КнопкиВыбора.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, КнопкиВыбора, , КодВозвратаДиалога.Отмена);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если РежимВыбора Тогда
		// Выбор из классификатора
		ОповеститьОВыборе(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	ОбновитьОтображениеСпискаСтран();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
//

&НаКлиенте
Процедура ОткрытьКлассификатор(Команда)
	// Открываем на просмотр
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТекущаяСтрока", Элементы.Список.ТекущаяСтрока);
	ОткрытьФорму("Справочник.СтраныМира.Форма.Классификатор", ПараметрыОткрытия, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзКлассификатора(Команда)
	// Открываем для выбора
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", ЗакрыватьПриВыборе);
	ПараметрыОткрытия.Вставить("ТекущаяСтрока", Элементы.Список.ТекущаяСтрока);
	ПараметрыОткрытия.Вставить("РежимОткрытияОкна", РежимОткрытияОкна);
	ПараметрыОткрытия.Вставить("ТекущаяСтрока", Элементы.Список.ТекущаяСтрока);
	ОткрытьФорму("Справочник.СтраныМира.Форма.Классификатор", ПараметрыОткрытия, Элементы.Список);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//

&НаКлиенте
Процедура ОбновитьОтображениеСпискаСтран()
	
	Если ИдентификаторЭлементаОтбораСсылки<>Неопределено Тогда
		// Был наложен дополнительный отбор, который надо обновить.
		УстановитьОтборПересеченияСКлассификатором();
	КонецЕсли;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПересеченияСКлассификатором()
	ОтборСписка = Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор;
	
	Если ИдентификаторЭлементаОтбораСсылки=Неопределено Тогда
		ЭлементОтбора = ОтборСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.Использование    = Истина;
		
		ИдентификаторЭлементаОтбораСсылки = ОтборСписка.ПолучитьИдентификаторПоОбъекту(ЭлементОтбора);
	Иначе
		ЭлементОтбора = ОтборСписка.ПолучитьОбъектПоИдентификатору(ИдентификаторЭлементаОтбораСсылки);
	КонецЕсли;
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Код, Наименование
		|ПОМЕСТИТЬ
		|	Классификатор
		|ИЗ
		|	&Классификатор КАК Классификатор
		|ИНДЕКСИРОВАТЬ ПО
		|	Код, Наименование
		|;////////////////////////////////////////////////////////////
		|ВЫБРАТЬ 
		|	Ссылка
		|ИЗ
		|	Справочник.СтраныМира КАК СтраныМира
		|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|	Классификатор КАК Классификатор
		|ПО
		|	СтраныМира.Код = Классификатор.Код
		|	И СтраныМира.Наименование = Классификатор.Наименование
		|");
	Запрос.УстановитьПараметр("Классификатор", Справочники.СтраныМира.ТаблицаКлассификатора());
	ЭлементОтбора.ПравоеЗначение = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросОПодбореИзКлассификатора(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса=КодВозвратаДиалога.Да Тогда
		// Подбор из классификатора
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ОткрытьФорму("Справочник.СтраныМира.Форма.Классификатор", ПараметрыОткрытия, Элементы.Список);
		
	ИначеЕсли РезультатВопроса=КодВозвратаДиалога.Нет Тогда
		// Добавление нового элемента
		ОткрытьФорму("Справочник.СтраныМира.ФормаОбъекта");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
