
Процедура ПриЗаписи(Отказ)
	
	ОчиститьЗаписи_узСпринтыЗадач_ПоЭтомуСпринту(Отказ);
	
	СформироватьЗапись_узСпринтыЗадач(Отказ);
	
КонецПроцедуры

Процедура СформироватьЗапись_узСпринтыЗадач(Отказ)
	
	Если Отказ Тогда
		Возврат;
	Конецесли;
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		Возврат;
	Конецесли;		
	
	Для Каждого СтрокаТЧЗадачи Из ТЧЗадачи Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧЗадачи.Задача) Тогда
			Продолжить;
		Конецесли;
		
		МенеджерЗаписи = РегистрыСведений.узСпринтыЗадач.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Период = ДатаНачала;
		МенеджерЗаписи.Задача = СтрокаТЧЗадачи.Задача;
		МенеджерЗаписи.Спринт = Ссылка;
				
		МенеджерЗаписи.Записать();
	КонецЦикла;	
	
КонецПроцедуры 

Процедура ОчиститьЗаписи_узСпринтыЗадач_ПоЭтомуСпринту(Отказ)
	
	Если Отказ Тогда
		Возврат;
	Конецесли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	узСпринтыЗадач.Период,
	|	узСпринтыЗадач.Задача,
	|	узСпринтыЗадач.Спринт
	|ИЗ
	|	РегистрСведений.узСпринтыЗадач КАК узСпринтыЗадач
	|ГДЕ
	|	узСпринтыЗадач.Спринт = &Спринт";
	
	Запрос.УстановитьПараметр("Спринт",Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	Конецесли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.узСпринтыЗадач.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Период = Выборка.Период;
		МенеджерЗаписи.Задача = Выборка.Задача;
		МенеджерЗаписи.Спринт = Выборка.Спринт;
		
		МенеджерЗаписи.Удалить();
	КонецЦикла;	
	
КонецПроцедуры 

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = ТекущаяДата();
	Конецесли;
	
КонецПроцедуры
