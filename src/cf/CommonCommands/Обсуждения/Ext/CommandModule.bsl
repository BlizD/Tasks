///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	НовоеОбсуждение = НовоеОбсуждение(ПараметрКоманды);
	
	Если НовоеОбсуждение = "Недоступно" Тогда
		Возврат;
	ИначеЕсли НовоеОбсуждение = "НеПодключено" Тогда
		ПредлагатьОбсужденияТекст = 
			НСтр("ru = 'Включить обсуждения?
				|
				|С их помощью пользователи смогут отправлять друг другу текстовые сообщения и совершать видеозвонки, создавать тематические обсуждения и вести переписку по документам.'");
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПредлагатьОбсужденияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеОЗавершении, ПредлагатьОбсужденияТекст, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НовоеОбсуждение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПредлагатьОбсужденияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОбсужденияСлужебныйКлиент.ПоказатьПодключение();
	
КонецПроцедуры

&НаСервере
Функция НовоеОбсуждение(ПользовательСсылка)
	
	Если Не ОбсужденияСлужебныйВызовСервера.Подключены() Тогда
		Возврат "НеПодключено";
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторПользователяИнформационнойБазы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПользовательСсылка, "ИдентификаторПользователяИБ");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(ИдентификаторПользователяИнформационнойБазы) Тогда
		Возврат "Недоступно";
	КонецЕсли;
	
	Попытка
		ИдентификаторПользователяСистемыВзаимодействия = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(
			ИдентификаторПользователяИнформационнойБазы);
	Исключение
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для начала обсуждения необходимо, чтобы пользователь %1
			           |хотя бы один раз запустил программу.'"),
			ПользовательСсылка);
	КонецПопытки;
	
	Если ИдентификаторПользователяСистемыВзаимодействия = СистемаВзаимодействия.ИдентификаторТекущегоПользователя() Тогда 
		ВызватьИсключение НСтр("ru = 'Для начала обсуждения выберите другого пользователя.'");
	КонецЕсли;
	
	Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	Если ПоддерживаетсяГрупповоеОбсуждение() Тогда 
		Обсуждение.Групповое = Ложь;
	Иначе 
		Обсуждение.Заголовок = НСтр("ru='Обсуждение'");
	КонецЕсли;
	Обсуждение.Участники.Добавить(СистемаВзаимодействия.ИдентификаторТекущегоПользователя());
	Обсуждение.Участники.Добавить(ИдентификаторПользователяСистемыВзаимодействия);
	Обсуждение.Записать();
	
	Возврат ПолучитьНавигационнуюСсылку(Обсуждение.Идентификатор);
	
КонецФункции

&НаСервере
Функция ПоддерживаетсяГрупповоеОбсуждение()
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ТекущаяВерсияПлатформы = СистемнаяИнформация.ВерсияПриложения;
	ТребуемаяВерсияПлатформы = "8.3.13.0";
	
	Возврат ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ТребуемаяВерсияПлатформы, ТекущаяВерсияПлатформы) <= 0;
	
КонецФункции

#КонецОбласти
