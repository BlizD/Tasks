///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Параметры.ТипРеспондента)) Тогда
		
		СтруктураСвойствСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		СтруктураСвойствСписка.ОсновнаяТаблица = Параметры.ТипРеспондента.Метаданные().ПолноеИмя();
		
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Респонденты, СтруктураСвойствСписка);
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РеспондентыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	МассивДляПередачи = Новый Массив;
	
	Для каждого ЭлементМассива Из ВыбраннаяСтрока Цикл
		Если НЕ Элементы.Респонденты.ДанныеСтроки(ЭлементМассива).Свойство("ЭтоГруппа") 
			ИЛИ НЕ Элементы.Респонденты.ДанныеСтроки(ЭлементМассива).ЭтоГруппа Тогда
			МассивДляПередачи.Добавить(ЭлементМассива);
		КонецЕсли;
	КонецЦикла;
	
	ОбработатьВыборРеспондента(МассивДляПередачи);
	
КонецПроцедуры

&НаКлиенте
Процедура РеспондентыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ОбработатьВыборРеспондента(Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборРеспондента(МассивВыбора)
	
	Оповестить("ВыборРеспондентов",Новый Структура("ОтобранныеРеспонденты",МассивВыбора));
	
КонецПроцедуры

#КонецОбласти



