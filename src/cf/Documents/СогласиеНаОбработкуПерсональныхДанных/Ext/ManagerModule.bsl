#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СогласиеНаОбработкуПерсональныхДанных";
	КомандаПечати.Представление = НСтр("ru = 'Согласие на обработку ПДн (табличный документ 1С:Предприятия)'");
	
	// В формате Microsoft Word.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СогласиеНаОбработкуПерсональныхДанных(MSWord)";
	КомандаПечати.Представление = НСтр("ru = 'Согласие на обработку ПДн (документ Microsoft Word)'");
	КомандаПечати.Картинка = БиблиотекаКартинок.ФорматWord2007;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Обработчик = "ЗащитаПерсональныхДанныхКлиент.ПечатьСогласияНаОбработкуПерсональныхДанных";
	КомандаПечати.ТребуетсяРасширениеРаботыСФайлами = Истина;
	
	// В формате OpenOffice.org Writer.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СогласиеНаОбработкуПерсональныхДанных(ODT)";
	КомандаПечати.Представление = НСтр("ru = 'Согласие на обработку ПДн (документ OpenOffice.org Writer)'");
	КомандаПечати.Картинка = БиблиотекаКартинок.ФорматOpenOfficeWriter;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Обработчик = "ЗащитаПерсональныхДанныхКлиент.ПечатьСогласияНаОбработкуПерсональныхДанных";
	КомандаПечати.ТребуетсяРасширениеРаботыСФайлами = Истина;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Печать согласия
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "СогласиеНаОбработкуПерсональныхДанных");
	Если ПечатнаяФорма <> Неопределено Тогда
		// имена файлов
		ИменаФайлов = Новый Соответствие;
		Шаблон = НСтр("ru = 'Согласие на обработку ПДн [Субъект] - [Организация] (№[Номер] получено [ДатаПолучения])'");
		ЗначенияРеквизитовДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивОбъектов, "Организация,Субъект,Номер,ДатаПолучения,Ссылка");
		Для Каждого Ссылка Из МассивОбъектов Цикл
			ЗначенияРеквизитовДокумента = ЗначенияРеквизитовДокументов[Ссылка];
			ЗначенияРеквизитовДокумента.ДатаПолучения = Формат(ЗначенияРеквизитовДокумента.ДатаПолучения, "ДЛФ=D");
			ЗначенияРеквизитовДокумента.Номер = ЗначенияРеквизитовДокумента.Номер;
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрефиксацияОбъектов") Тогда
				Модуль = ОбщегоНазначения.ОбщийМодуль("ПрефиксацияОбъектовКлиентСервер");
				ЗначенияРеквизитовДокумента.Номер = Модуль.ПолучитьНомерНаПечать(ЗначенияРеквизитовДокумента.Номер);
			КонецЕсли;
			ИмяФайла = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ЗначенияРеквизитовДокументов[Ссылка]);
			ИменаФайлов.Вставить(Ссылка, ИмяФайла);
		КонецЦикла;
		
		// описание печатной формы
		ПечатнаяФорма.ТабличныйДокумент = ПечатьСогласияНаОбработкуПДн(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Согласие на обработку ПДн'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.СогласиеНаОбработкуПерсональныхДанных.ПФ_MXL_СогласиеНаОбработкуПерсональныхДанных";
		ПечатнаяФорма.ИмяФайлаПечатнойФормы = ИменаФайлов;
	КонецЕсли;
		
	// Печать в формате Word
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СогласиеНаОбработкуПерсональныхДанных(MSWord)");
	Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СогласиеНаОбработкуПерсональныхДанных(MSWord)",
			НСтр("ru = 'Согласие на обработку ПДн (документ Microsoft Word)'"),
			ПечатьСогласияНаОбработкуПДн(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.СогласиеНаОбработкуПерсональныхДанных.ПФ_DOC_СогласиеНаОбработкуПерсональныхДанных");
	КонецЕсли;
	
	// Печать в формате OpenOffice
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СогласиеНаОбработкуПерсональныхДанных(ODT)");
	Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СогласиеНаОбработкуПерсональныхДанных(ODT)",
			НСтр("ru = 'Согласие на обработку ПДн (документ OpenOffice.org Writer)'"),
			ПечатьСогласияНаОбработкуПДн(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.СогласиеНаОбработкуПерсональныхДанных.ПФ_ODT_СогласиеНаОбработкуПерсональныхДанных");
	КонецЕсли;
	
КонецПроцедуры

// Процедура печати согласия на обработку персональных данных.
//
Функция ПечатьСогласияНаОбработкуПДн(Согласия, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_СогласиеНаОбработкуПерсональныхДанных";
	
	ИмяМакета = "СогласиеНаОбработкуПерсональныхДанных";
	
	ОбластиМакета = Новый Структура;
	ОбластиМакета.Вставить("Заголовок");
	ОбластиМакета.Вставить("НомерДата");
	ОбластиМакета.Вставить("Преамбула");
	ОбластиМакета.Вставить("П1_ЦелиОбработкиПДн");
	ОбластиМакета.Вставить("П2_СоставПДн");
	ОбластиМакета.Вставить("П3_ДействияСПДн");
	ОбластиМакета.Вставить("П4_ПравоПолученияИнформации");
	ОбластиМакета.Вставить("П5_СрокДействия");
	ОбластиМакета.Вставить("П6_ПравоОтзыва");
	ОбластиМакета.Вставить("РеквизитыОператора");
	ОбластиМакета.Вставить("РеквизитыСубъекта");
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СогласиеНаОбработкуПерсональныхДанных.ПФ_MXL_СогласиеНаОбработкуПерсональныхДанных");
	
	ДанныеДляПечати = ДанныеДляПечатиСогласий(Согласия);
	
	// Вывод форм для субъектов.
	ПервыйДокумент = Истина;
	Для Каждого ОписаниеСогласия Из ДанныеДляПечати Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();	
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		// Формируем печатную форму согласия.
		СогласиеФорма = Новый ТабличныйДокумент;
		Для Каждого КлючИЗначение Из ОбластиМакета Цикл
			ИмяОбласти = КлючИЗначение.Ключ;
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			ОбластьМакета.Параметры.Заполнить(ОписаниеСогласия);
			СогласиеФорма.Вывести(ОбластьМакета);
		КонецЦикла;
		ТабличныйДокумент.Вывести(СогласиеФорма);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ОписаниеСогласия.ДокументОснование);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Структура данных для подготовки печатных форм.
//
// Параметры:
//	Согласия - массив документов типа ДокументСсылка.СогласиеНаОбработкуПерсональныхДанных.
//
// Возвращаемое значение - массив структур, см. ОписаниеСогласия().
//
Функция ДанныеДляПечатиСогласий(Согласия)
	
	ДанныеДляПечати = Новый Массив;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Согласия.Ссылка КАК ДокументОснование,
		|	Согласия.Номер,
		|	Согласия.Организация,
		|	Согласия.ЮридическийАдресОрганизации КАК АдресОрганизации,
		|	Согласия.ОтветственныйЗаОбработкуПерсональныхДанных,
		|	Согласия.Субъект,
		|	Согласия.ФИОСубъекта КАК ФИО,
		|	Согласия.АдресСубъекта КАК Адрес,
		|	Согласия.ПаспортныеДанные,
		|	Согласия.ДатаПолучения КАК ДатаСогласия,
		|	Согласия.СрокДействия,
		|	Согласия.Ответственный,
		|	Согласия.Комментарий
		|ИЗ
		|	Документ.СогласиеНаОбработкуПерсональныхДанных КАК Согласия
		|ГДЕ
		|	Согласия.Ссылка В(&Согласия)");
		
	Запрос.УстановитьПараметр("Согласия", Согласия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Описание = ОписаниеСогласия();
		ЗаполнитьЗначенияСвойств(Описание, Выборка);
		Описание.ДатаСогласия = Формат(Выборка.ДатаСогласия, "ДЛФ=D");
		Если ЗначениеЗаполнено(Выборка.СрокДействия) Тогда
			Описание.ПериодДействия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'с %1 по %2.'"), Формат(Выборка.ДатаСогласия, "ДЛФ=D"), Формат(Выборка.СрокДействия, "ДЛФ=D"));
		Иначе
			Описание.ПериодДействия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'с %1 бессрочно.'"), Формат(Выборка.ДатаСогласия, "ДЛФ=D"));
		КонецЕсли;
		ДанныеДляПечати.Добавить(Описание);
	КонецЦикла;
	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Структура данных для подготовки печатных форм.
//
// Параметры:
//	Согласия - массив документов типа ДокументСсылка.СогласиеНаОбработкуПерсональныхДанных.
//
// Возвращаемое значение - соответствие, в котором ключом является ссылкой, а значением описание, см. ОписаниеСогласия().
//
Функция СоответствиеДанныхДляПечатиСогласий(Согласия)
	
	Соответствие = Новый Соответствие;
	
	ДанныеДляПечати = ДанныеДляПечатиСогласий(Согласия);
	
	Для Каждого ОписаниеСогласия Из ДанныеДляПечати Цикл
		Соответствие.Вставить(ОписаниеСогласия.ДокументОснование, ОписаниеСогласия);
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

Функция ОписаниеСогласия()
	
	ОписаниеСогласия = Новый Структура(
		"ДатаСогласия,
		|ДокументОснование, 
		|Организация, 
		|АдресОрганизации, 
		|ОтветственныйЗаОбработкуПерсональныхДанных, 
		|ПериодДействия, 
		|ФИО, 
		|Адрес, 
		|ПаспортныеДанные");
		
	ОписаниеСогласия.ДатаСогласия = НСтр("ru = '«____»_______________20___г.'");
	ОписаниеСогласия.Организация = НСтр("ru = '<Организация>'");
	ОписаниеСогласия.АдресОрганизации = НСтр("ru = '<Адрес организации>'");
	ОписаниеСогласия.ОтветственныйЗаОбработкуПерсональныхДанных = НСтр("ru = '<ФИО ответственного лица>'");

	Возврат ОписаниеСогласия;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Работа с макетами офисных документов.

Функция ПолучитьДанныеПечати(Знач Согласия, Знач МассивИменМакетов) Экспорт
	
	ДанныеПоВсемОбъектам = Новый Соответствие;
	
	СоответствиеПоСогласиям = СоответствиеДанныхДляПечатиСогласий(Согласия);
	
	Для Каждого ОбъектСсылка Из Согласия Цикл
		ДанныеОбъектаПоМакетам = Новый Соответствие;
		Для Каждого ИмяМакета Из МассивИменМакетов Цикл
			ДанныеОбъектаПоМакетам.Вставить(ИмяМакета, СоответствиеПоСогласиям[ОбъектСсылка]);
		КонецЦикла;
		ДанныеПоВсемОбъектам.Вставить(ОбъектСсылка, ДанныеОбъектаПоМакетам);
	КонецЦикла;
	
	ОписаниеОбластей = Новый Соответствие;
	ДвоичныеДанныеМакетов = Новый Соответствие;
	ТипыМакетов = Новый Соответствие;
	
	Для Каждого ИмяМакета Из МассивИменМакетов Цикл
		Если ИмяМакета = "СогласиеНаОбработкуПерсональныхДанных(MSWord)" Тогда
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, УправлениеПечатью.МакетПечатнойФормы("Документ.СогласиеНаОбработкуПерсональныхДанных.ПФ_DOC_СогласиеНаОбработкуПерсональныхДанных"));
			ТипыМакетов.Вставить(ИмяМакета, "DOC");
		ИначеЕсли ИмяМакета = "СогласиеНаОбработкуПерсональныхДанных(ODT)" Тогда
			ДвоичныеДанныеМакетов.Вставить(ИмяМакета, УправлениеПечатью.МакетПечатнойФормы("Документ.СогласиеНаОбработкуПерсональныхДанных.ПФ_ODT_СогласиеНаОбработкуПерсональныхДанных"));
			ТипыМакетов.Вставить(ИмяМакета, "ODT");
		КонецЕсли;
		ОписаниеОбластей.Вставить(ИмяМакета, ОписаниеОбластейМакетаОфисногоДокумента());
	КонецЦикла;
	
	Макеты = Новый Структура("ОписаниеОбластей, ТипыМакетов, ДвоичныеДанныеМакетов");
	Макеты.ОписаниеОбластей = ОписаниеОбластей;
	Макеты.ТипыМакетов = ТипыМакетов;
	Макеты.ДвоичныеДанныеМакетов = ДвоичныеДанныеМакетов;
	
	ДанныеПечати = Новый Структура("Данные, Макеты");
	ДанныеПечати.Данные = ДанныеПоВсемОбъектам;
	ДанныеПечати.Макеты = Макеты;
	
	Возврат ДанныеПечати;
	
КонецФункции

Функция ОписаниеОбластейМакетаОфисногоДокумента()
	
	ОписаниеОбластей = Новый Структура;
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Заголовок",			"Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "НомерДата",			"Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Преамбула",			"Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ОсновнойТекст",		"Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "РеквизитыОператора",	"Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "РеквизитыСубъекта",	"Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подпись",				"Общая");
	
	Возврат ОписаниеОбластей;
	
КонецФункции

#КонецОбласти

#КонецЕсли