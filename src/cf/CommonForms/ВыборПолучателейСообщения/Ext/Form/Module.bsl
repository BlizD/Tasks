
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПолучателейСообщения = ПолучитьИзВременногоХранилища(Параметры.ПолучателиСообщения);
	Если ТипЗнч(ПараметрыПолучателейСообщения) = Тип("Строка") Тогда
		ЗаполнитьТаблицуПолучателейИзСтроки(ПараметрыПолучателейСообщения);
	ИначеЕсли ТипЗнч(ПараметрыПолучателейСообщения) = Тип("СписокЗначений") Тогда
		ЗаполнитьТаблицуПолучателейИзСпискаЗначений(ПараметрыПолучателейСообщения);
	ИначеЕсли ТипЗнч(ПараметрыПолучателейСообщения) = Тип("Массив") Тогда
		ЗаполнитьТаблицуПолучателейИзМассиваСтруктур(ПараметрыПолучателейСообщения);
	КонецЕсли;
	
	ОтметитьВыбранныхПолучателей(Параметры.ПочтовыйАдресПолучателя);
	
	УстановитьВидимостьРеквизитовТабличнойЧасти();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПолучатели

&НаКлиенте
Процедура ПолучателиСообщенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиСообщенияПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучателиСообщенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элемент.ТекущиеДанные.Выбран = НЕ Элемент.ТекущиеДанные.Выбран;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Результат = СформироватьПочтовыйАдресПолучателя();
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсехПолучателей(Команда)
	
	УстановитьВыборДляВсехПолучателей(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыборДляВсех(Команда)
	
	УстановитьВыборДляВсехПолучателей(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СформироватьПочтовыйАдресПолучателя()
	
	Для Каждого Получатель Из ПолучателиСообщения Цикл
		Если Получатель.Выбран Тогда
			ПочтовыйАдресПолучателя = ПочтовыйАдресПолучателя + Получатель.Представление + " <" + СокрЛП(Получатель.Адрес) + ">; ";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("ПочтовыйАдресПолучателя", СокрЛП(ПочтовыйАдресПолучателя));
	
КонецФункции

&НаКлиенте
Функция УстановитьВыборДляВсехПолучателей(Выбор)
	
	Для Каждого Получатель Из ПолучателиСообщения Цикл
		Получатель.Выбран = Выбор;
	КонецЦикла;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуПолучателейИзМассиваСтруктур(ПараметрыПолучателейСообщения)
	
	Для Каждого ПараметрыПолучателя Из ПараметрыПолучателейСообщения Цикл
		Получатель = ПолучателиСообщения.Добавить();
		ЗаполнитьЗначенияСвойств(Получатель, ПараметрыПолучателя);
		Получатель.Выбран = Ложь;
		Получатель.ПредставлениеАдреса = Получатель.Адрес;
		Если ПараметрыПолучателя.Свойство("ВидПочтовогоАдреса") И НЕ ПустаяСтрока(ПараметрыПолучателя.ВидПочтовогоАдреса) Тогда
			Получатель.ПредставлениеАдреса = Получатель.ПредставлениеАдреса + " (" + ПараметрыПолучателя.ВидПочтовогоАдреса + ")";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПолучателейИзСпискаЗначений(ПараметрыПолучателейСообщения)
	
	Для Каждого ПараметрыПолучателя Из ПараметрыПолучателейСообщения Цикл
		Получатель = ПолучателиСообщения.Добавить();
		Получатель.Адрес               = ПараметрыПолучателя.Значение;
		Получатель.Представление       = ПараметрыПолучателя.Представление;
		Получатель.ПредставлениеАдреса = Получатель.Адрес;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуПолучателейИзСтроки(Знач ПараметрыПолучателейСообщения)
	
	ПараметрыПолучателейСообщения = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(ПараметрыПолучателейСообщения);
	
	Для Каждого ПараметрыПолучателя Из ПараметрыПолучателейСообщения Цикл
		Получатель = ПолучателиСообщения.Добавить();
		Получатель.Адрес               = ПараметрыПолучателя.Адрес;
		Получатель.Представление       = ПараметрыПолучателя.Псевдоним;
		Получатель.ПредставлениеАдреса = Получатель.Адрес;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьВыбранныхПолучателей(ПочтовыеАдресаПолучателей)
	
	ПараметрыПолучателей = ОбщегоНазначенияКлиентСервер.АдресаЭлектроннойПочтыИзСтроки(ПочтовыеАдресаПолучателей);
	ПочтовыйАдресПолучателя = "";
	Отбор = Новый Структура("Адрес", "");
	Для Каждого ПараметрыПолучателя Из ПараметрыПолучателей Цикл
		Если НЕ ЗначениеЗаполнено(ПараметрыПолучателя.Адрес) Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор.Адрес = ПараметрыПолучателя.Адрес;
		НайденныеСтроки = ПолучателиСообщения.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].Выбран = Истина;
			
		Иначе
			ПочтовыйАдресПолучателя = ПочтовыйАдресПолучателя + ПараметрыПолучателя.Адрес + "; ";
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРеквизитовТабличнойЧасти()
	
	ВидимостьПояснения = Ложь;
	
	Для Каждого СтрокаТаблицы Из ПолучателиСообщения Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.Пояснение) Тогда
			ВидимостьПояснения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ВидимостьПояснения Тогда
		Элементы.ГруппаПояснение.Видимость = Истина;
		Элементы.ПолучателиСообщения.Шапка = Истина;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти