///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПустаяСтрока(Параметры.ТекстПредложения) Тогда
		Элементы.ДекорацияПояснение.Заголовок = Параметры.ТекстПредложения
			+ Символы.ПС
			+ НСтр("ru = 'Установить?'");
		
	ИначеЕсли Не Параметры.ВозможноПродолжениеБезУстановки Тогда
		Элементы.ДекорацияПояснение.Заголовок =
			НСтр("ru = 'Для выполнения действия требуется установить расширение для работы с 1С:Предприятием.
			           |Установить?'");
	КонецЕсли;
	
	Если Не Параметры.ВозможноПродолжениеБезУстановки Тогда
		Элементы.ПродолжитьБезУстановки.Заголовок = НСтр("ru = 'Отмена'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьИПродолжить(Команда)
	
	Оповещение = Новый ОписаниеОповещения("УстановитьИПродолжитьЗавершение", ЭтотОбъект);
	НачатьУстановкуРасширенияРаботыСФайлами(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьБезУстановки(Команда)
	
	Закрыть("БольшеНеПредлагать");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьИПродолжитьЗавершение(Контекст) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("УстановитьИПродолжитьПослеПодключенияРасширения", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИПродолжитьПослеПодключенияРасширения(Подключено, Контекст) Экспорт
	
	Если Подключено Тогда
		Закрыть("РасширениеПодключено");
	Иначе
		Закрыть("ПродолжитьБезУстановки");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
