///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для использования в обработчиках обновления.
//

// Записывает изменения в переданном объекте.
// Для использования в обработчиках обновления.
//
// Параметры:
//   Данные                            - Произвольный - объект, набор записей или менеджер константы, который
//                                                      необходимо записать.
//   РегистрироватьНаУзлахПлановОбмена - Булево       - включает регистрацию на узлах планов обмена при записи объекта.
//   ВключитьБизнесЛогику              - Булево       - включает бизнес-логику при записи объекта.
//
Процедура ЗаписатьДанные(Знач Данные, Знач РегистрироватьНаУзлахПлановОбмена = Неопределено, 
	Знач ВключитьБизнесЛогику = Ложь) Экспорт
	
	Данные.ОбменДанными.Загрузка = Не ВключитьБизнесЛогику;
	Данные.ДополнительныеСвойства.Вставить("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ", РегистрироватьНаУзлахПлановОбмена);
	
	Если РегистрироватьНаУзлахПлановОбмена = Неопределено
		Или Не РегистрироватьНаУзлахПлановОбмена Тогда
		Данные.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
	КонецЕсли;
	
	Данные.Записать();
	
	ОтметитьВыполнениеОбработки(Данные);
	
КонецПроцедуры

// Записывает изменения в переданном объекте ссылочного типа.
// Для использования в обработчиках обновления.
//
// Параметры:
//   Объект                            - Произвольный - записываемый объект ссылочного типа. Например, СправочникОбъект.
//   РегистрироватьНаУзлахПлановОбмена - Булево       - включает регистрацию на узлах планов обмена при записи объекта.
//   ВключитьБизнесЛогику              - Булево       - включает бизнес-логику при записи объекта.
//   ДокументРежимЗаписи              - ДокументРежимЗаписи - имеет смысл только для данных типа ДокументОбъект - режим
//                                                            записи документа.
//											Если параметр не передан, то документ записывается в режиме "Запись".
//
Процедура ЗаписатьОбъект(Знач Объект, Знач РегистрироватьНаУзлахПлановОбмена = Неопределено, 
	Знач ВключитьБизнесЛогику = Ложь, ДокументРежимЗаписи = Неопределено) Экспорт
	
	Объект.ДополнительныеСвойства.Вставить("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ", РегистрироватьНаУзлахПлановОбмена);
	Объект.ОбменДанными.Загрузка = Не ВключитьБизнесЛогику;
	
	Если РегистрироватьНаУзлахПлановОбмена = Неопределено
		Или Не РегистрироватьНаУзлахПлановОбмена
		И Не Объект.ЭтоНовый() Тогда
		Объект.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
	КонецЕсли;
	
	Если ДокументРежимЗаписи <> Неопределено Тогда
		Если ТипЗнч(ДокументРежимЗаписи) <> Тип("РежимЗаписиДокумента") Тогда
			ТекстИсключения = НСтр("ru = 'Неправильный тип параметра ДокументРежимЗаписи'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		Объект.ОбменДанными.Загрузка = Объект.ОбменДанными.Загрузка
			И Не ДокументРежимЗаписи = РежимЗаписиДокумента.Проведение
			И Не ДокументРежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		Объект.Записать(ДокументРежимЗаписи);
	Иначе
		Объект.Записать();
	КонецЕсли;
	
	ОтметитьВыполнениеОбработки(Объект);
	
КонецПроцедуры

// Записывает изменения в переданном наборе записей.
// Для использования в обработчиках обновления.
//
// Параметры:
//   НаборЗаписей                      - РегистрСведенийНаборЗаписей,
//                                       РегистрНакопленияНаборЗаписей,
//                                       РегистрБухгалтерииНаборЗаписей,
//                                       РегистрРасчетаНаборЗаписей - набор записей, который необходимо записать.
//   Замещать                          - Булево       - определяет режим замещения существующей записи в соответствии с
//       текущими установками отбора. Истина - перед записью существующие записи будут удалены. Ложь - записи будут
//       дописаны к уже существующим в информационной базе записям.
//   РегистрироватьНаУзлахПлановОбмена - Булево       - включает регистрацию на узлах планов обмена при записи объекта.
//   ВключитьБизнесЛогику              - Булево       - включает бизнес-логику при записи объекта.
//
Процедура ЗаписатьНаборЗаписей(Знач НаборЗаписей, Замещать = Истина, Знач РегистрироватьНаУзлахПлановОбмена = Неопределено,
	Знач ВключитьБизнесЛогику = Ложь) Экспорт
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ", РегистрироватьНаУзлахПлановОбмена);
	НаборЗаписей.ОбменДанными.Загрузка = Не ВключитьБизнесЛогику;
	
	Если РегистрироватьНаУзлахПлановОбмена = Неопределено 
		Или Не РегистрироватьНаУзлахПлановОбмена Тогда
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов");
		НаборЗаписей.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
	КонецЕсли;
	
	НаборЗаписей.Записать(Замещать);
	
	ОтметитьВыполнениеОбработки(НаборЗаписей);
	
КонецПроцедуры

// Удаляет переданный объект.
// Для использования в обработчиках обновления.
//
// Параметры:
//  Данные                            - Произвольный - объект, который необходимо удалить.
//  РегистрироватьНаУзлахПлановОбмена - Булево       - включает регистрацию на узлах планов обмена при записи объекта.
//  ВключитьБизнесЛогику              - Булево       - включает бизнес-логику при записи объекта.
//
Процедура УдалитьДанные(Знач Данные, Знач РегистрироватьНаУзлахПлановОбмена = Неопределено, 
	Знач ВключитьБизнесЛогику = Ложь) Экспорт
	
	Данные.ДополнительныеСвойства.Вставить("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ", РегистрироватьНаУзлахПлановОбмена);
	
	Данные.ОбменДанными.Загрузка = Не ВключитьБизнесЛогику;
	Если РегистрироватьНаУзлахПлановОбмена = Неопределено 
		Или Не РегистрироватьНаУзлахПлановОбмена Тогда
		Данные.ОбменДанными.Получатели.АвтоЗаполнение = Ложь;
	КонецЕсли;
	
	Данные.Удалить();
	
КонецПроцедуры

// Возвращает строковую константу для формирования сообщений журнала регистрации.
//
// Возвращаемое значение:
//   Строка - текст события журнала регистрации.
//
Функция СобытиеЖурналаРегистрации() Экспорт
	
	Возврат ОбновлениеИнформационнойБазыСлужебный.СобытиеЖурналаРегистрации();
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для проверки доступности объекта при выполнении отложенного обновления.
//

// Вызывает исключение или блокирует форму от редактирования, если
// имеются незавершенные отложенные обработчики обновления,
// которые в данный момент обрабатывают переданный объект Данные.
//
// При вызове из отложенного обработчика обновления (случай проверки в программном интерфейсе)
// проверка не выполняется, если не указан параметр ИмяОтложенногоОбработчика, так как
// предполагается, что порядок обновления уже учтен при построении очередей.
//
// Параметры:
//  Данные - ЛюбаяСсылка, НаборЗаписей, Объект, ДанныеФормыСтруктура, Строка - ссылка на объект, сам объект,
//           набор записей или полное имя объекта метаданных, обработку которого необходимо проверить.
//  Форма  - УправляемаяФорма - если объект не обработан, то у переданной формы
//           будет установлено свойство ТолькоПросмотр. Если форма не была
//           передана, то будет вызвано исключение.
//
//  ИмяОтложенногоОбработчика - Строка - если заполнено, тогда при вызове из другого отложенного обработчика
//           проверяется, что указанный отложенный обработчик имеет номер очереди меньше, чем текущий.
//           Если это не так, тогда вызывается исключение о недопустимости использования
//           программного интерфейса, указанного в параметре ИмяПроцедурыПрограммногоИнтерфейса.
//
//  ИмяПроцедурыПрограммногоИнтерфейса - Строка - имя процедуры программного интерфейса,
//           которое выводится в тексте исключения, вызываемого при проверке номера очереди
//           отложенного обработчика обновления, указанного в параметре ИмяОтложенногоОбработчика.
//
//  Пример:
//   Блокировка формы объекта в обработчике ПриСозданииНаСервере модуля формы:
//   ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
//
//   Блокировка записи объекта в обработчике ПередЗаписью модуля объекта (набора записей):
//   ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
//
//   Проверить, что обновлен конкретный объект и вызвать исключение о недопустимости вызова
//   процедуры ЭлектроннаяПодпись.ОбновитьПодпись, если он еще не обработан указанным обработчиком
//   Справочник.ЭлектронныеПодписи.ОбработатьДанныеДляПереходаНаНовуюВерсию:
//
//   ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ПодписанныйОбъект,,
//      "Справочник.ЭлектронныеПодписи.ОбработатьДанныеДляПереходаНаНовуюВерсию",
//      "ЭлектроннаяПодпись.ОбновитьПодпись");
//
//   Проверить и вызвать исключение, если обновлены не все объекты требуемого типа:
//   ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан("Документ.ЗаказПокупателя"); 
//
Процедура ПроверитьОбъектОбработан(Данные, Форма = Неопределено, ИмяОтложенногоОбработчика = "", ИмяПроцедурыПрограммногоИнтерфейса = "") Экспорт
	
	Если Не ЭтоВызовИзОбработчикаОбновления() Тогда
		Результат = ОбъектОбработан(Данные);
		Если Результат.Обработан Тогда
			Возврат;
		КонецЕсли;
			
		Если Форма = Неопределено Тогда
			ВызватьИсключение Результат.ТекстИсключения;
		КонецЕсли;
		
		Форма.ТолькоПросмотр = Истина;
		ОбщегоНазначения.СообщитьПользователю(Результат.ТекстИсключения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяОтложенногоОбработчика) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяОтложенногоОбработчика = ПараметрыСеанса.ПараметрыОбработчикаОбновления.ИмяОбработчика Тогда
		Возврат;
	КонецЕсли;
	
	ОчередьТребуемогоОбработчика = ОчередьОтложенногоОбработчикаОбновления(ИмяОтложенногоОбработчика);
	ОчередьТекущегоОбработчика = ПараметрыСеанса.ПараметрыОбработчикаОбновления.ОчередьОтложеннойОбработки;
	Если ОчередьТекущегоОбработчика > ОчередьТребуемогоОбработчика Тогда
		Возврат;
	КонецЕсли;
	
	ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Недопустимо вызывать %1
		           |из обработчика обновления
		           |%2
		           |так как его номер очереди меньше или равен номеру очереди обработчика обновления
		           |%3'"),
		ИмяПроцедурыПрограммногоИнтерфейса,
		ПараметрыСеанса.ПараметрыОбработчикаОбновления.ИмяОбработчика,
		ИмяОтложенногоОбработчика);
	
КонецПроцедуры

// Проверяет, имеются ли отложенные обработчики обновления,
// которые в данный момент обрабатывают переданный объект Данные.
//
// Параметры:
//  Данные - ЛюбаяСсылка, НаборЗаписей, Объект, ДанныеФормыСтруктура, Строка - ссылка на объект, сам объект, 
//           набор записей или полное имя объекта метаданных, блокировку которого необходимо проверить.
//
// Возвращаемое значение:
//   Структура - с полями:
//     * Обработан       - Булево - признак того, что переданный объект обработан.
//     * ТекстИсключения - Строка - текст исключения, если объект еще не обработан,
//                         содержит список незавершенных обработчиков.
//
// Пример:
//   Проверить, что обновлены все объекты требуемого типа:
//   ВсеЗаказыОбработаны = ОбновлениеИнформационнойБазы.ОбъектОбработан("Документ.ЗаказПокупателя"); 
//
Функция ОбъектОбработан(Данные) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Обработан", Истина);
	Результат.Вставить("ТекстИсключения", "");
	Результат.Вставить("НевыполненныеОбработчикиСтрокой", "");
	
	Если Данные = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		
		ЭтоПодчиненныйУзелРИБ = ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ();
		Если Не ЭтоПодчиненныйУзелРИБ Тогда
			Возврат Результат;
		ИначеЕсли ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеВГлавномУзлеЗавершеноУспешно") Тогда
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	СведенияОБлокируемыхОбъектах = ОбновлениеИнформационнойБазыСлужебный.СведенияОБлокируемыхОбъектах();
	
	Если ТипЗнч(Данные) = Тип("Строка") Тогда
		ПолноеИмя = Данные;
	Иначе
		МетаданныеИОтбор = МетаданныеИОтборПоДанным(Данные);
		ПолноеИмя = МетаданныеИОтбор.Метаданные.ПолноеИмя();
	КонецЕсли;
	
	ПроверяемыйОбъект = СтрЗаменить(ПолноеИмя, ".", "");
	
	ОбработчикиОбъекта = СведенияОБлокируемыхОбъектах.БлокируемыеОбъекты[ПроверяемыйОбъект];
	Если ОбработчикиОбъекта = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Обработан = Истина;
	НезавершенныеОбработчики = Новый Массив;
	Для Каждого Обработчик Из ОбработчикиОбъекта Цикл
		СвойстваОбработчика = СведенияОБлокируемыхОбъектах.Обработчики[Обработчик];
		Если СвойстваОбработчика.Выполнен Тогда
			Обработан = Истина;
		ИначеЕсли ТипЗнч(Данные) = Тип("Строка") Тогда
			Обработан = Ложь;
		Иначе
			Обработан = ОбщегоНазначения.ВычислитьВБезопасномРежиме(
				СвойстваОбработчика.ПроцедураПроверки + "(Параметры)", МетаданныеИОтбор);
		КонецЕсли;
		
		Результат.Обработан = Обработан И Результат.Обработан;
		
		Если Не Обработан Тогда
			НезавершенныеОбработчики.Добавить(Обработчик);
		КонецЕсли;
	КонецЦикла;
	
	Если НезавершенныеОбработчики.Количество() > 0 Тогда
		ТекстИсключения = НСтр("ru = 'Действия с объектом временно запрещены, так как не завершен переход на новую версию программы.
			|Это плановый процесс, который скоро завершится.
			|Остались следующие процедуры обработки данных:'");
		
		НевыполненныеОбработчикиСтрокой = "";
		Для Каждого НезавершенныйОбработчик Из НезавершенныеОбработчики Цикл
			НевыполненныеОбработчикиСтрокой = НевыполненныеОбработчикиСтрокой + Символы.ПС + НезавершенныйОбработчик;
		КонецЦикла;
		Результат.ТекстИсключения = ТекстИсключения + НевыполненныеОбработчикиСтрокой;
		Результат.НевыполненныеОбработчикиСтрокой = НевыполненныеОбработчикиСтрокой;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции для использования в отложенных обработчиках обновления
// с режимом выполнения "Параллельно".
//

// Отмечает, что переданные данные обновлены.
//
// Параметры:
//  Данные					 - Ссылка, Массив, НаборДанных - данные, по которым нужно зарегистрировать изменения.
//							 - ТаблицаЗначений - значения измерений независимого регистра сведений. Требования:
//													- все измерения регистра должны входить в основной отбор;
//													- в таблице должны быть только колонки, соответствующие по именам измерениям регистра,
//														по которым ранее регистрировалась необходимость обработки;
//													- запись наборов в процессе обновления должна проходить с тем же отбором,
//														что и регистрация необходимости обработки;
//													- в ДополнительныеПараметры нужно передать соответствующий признак и полное имя регистра.
//  ДополнительныеПараметры	 - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
//  Очередь					 - Число, Неопределено - очередь обработки, в которой выполняется текущий обработчик. По умолчанию очередь передавать
//													не нужно, т.к. она будет взята из параметров сеанса, в котором запущен обработчик обновления.
//
Процедура ОтметитьВыполнениеОбработки(Данные, ДополнительныеПараметры = Неопределено, Очередь = Неопределено) Экспорт
	Если Очередь = Неопределено Тогда
		Если ПараметрыСеанса.ПараметрыОбработчикаОбновления.РежимВыполнения <> "Отложенно"
			Или ПараметрыСеанса.ПараметрыОбработчикаОбновления.РежимВыполненияОтложенныхОбработчиков <> "Параллельно" Тогда
			Возврат;
		КонецЕсли;
		Очередь = ПараметрыСеанса.ПараметрыОбработчикаОбновления.ОчередьОтложеннойОбработки;
	КонецЕсли;
	
	Если Не ПараметрыСеанса.ПараметрыОбработчикаОбновления.ЕстьОбработанныеОбъекты Тогда
		НовыеПараметрыСеанса = ОбновлениеИнформационнойБазыСлужебный.НовыеПараметрыОбработчикаОбновления();
		
		ЗаполнитьЗначенияСвойств(НовыеПараметрыСеанса, ПараметрыСеанса.ПараметрыОбработчикаОбновления);
		НовыеПараметрыСеанса.ЕстьОбработанныеОбъекты = Истина;
		
		ПараметрыСеанса.ПараметрыОбработчикаОбновления = Новый ФиксированнаяСтруктура(НовыеПараметрыСеанса);
	КонецЕсли;
	
	КопияДанных = Данные;
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки();
	КонецЕсли;
	
	Если (ТипЗнч(Данные) = Тип("Массив")
		Или ТипЗнч(Данные) = Тип("ТаблицаЗначений"))
		И Данные.Количество() = 0 Тогда
		
		ТекстИсключения = НСтр("ru = 'В процедуру ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки передан пустой массив. Не возможно отметить выполнение обработки.'");
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	Узел = ОчередьСсылкой(Очередь);
	
	Если ДополнительныеПараметры.ЭтоДвижения Тогда
		
		Набор = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра).СоздатьНаборЗаписей();
		
		Если ТипЗнч(Данные) = Тип("Массив") Тогда
			Для Каждого СтрокаМассива Из Данные Цикл
				Набор.Отбор.Регистратор.Установить(СтрокаМассива);
				ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Набор);
			КонецЦикла;
		Иначе
			Набор.Отбор.Регистратор.Установить(Данные);
			ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Набор);
		КонецЕсли;
		
	ИначеЕсли ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений Тогда
		
		Набор = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра).СоздатьНаборЗаписей();
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра);
		
		УстановитьНедостающиеОтборыВНаборе(Набор, МетаданныеОбъекта, Данные);	
		
		Для каждого СтрокаТаблицы Из Данные Цикл
			Для Каждого Колонка Из Данные.Колонки Цикл
				Набор.Отбор[Колонка.Имя].Значение = СтрокаТаблицы[Колонка.Имя];
				Набор.Отбор[Колонка.Имя].Использование = Истина;
			КонецЦикла;
			
			ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Набор);
		КонецЦикла;
		
	Иначе
		Если ТипЗнч(Данные) = Тип("ОбъектМетаданных") Тогда
			ТекстИсключения = НСтр("ru = 'Не поддерживается отметка выполнения обработки обновления целиком объекта метаданных. Нужно отмечать обработку конкретных данных.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		Если ТипЗнч(Данные) <> Тип("Массив") Тогда
			
			ТипЗначенияОбъекта = ТипЗнч(Данные);
			МетаданныеОбъекта  = Метаданные.НайтиПоТипу(ТипЗначенияОбъекта);
			
			Если ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
				И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
				Набор = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеОбъекта.ПолноеИмя()).СоздатьНаборЗаписей();
				Для Каждого ЭлементОтбора Из Данные.Отбор Цикл
					Набор.Отбор[ЭлементОтбора.Имя].Значение = ЭлементОтбора.Значение;
					Набор.Отбор[ЭлементОтбора.Имя].Использование = ЭлементОтбора.Использование;
				КонецЦикла;
				УстановитьНедостающиеОтборыВНаборе(Набор, МетаданныеОбъекта, Данные.Отбор);
			ИначеЕсли ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта)
				И Не ОбщегоНазначения.ЭтоСсылка(ТипЗначенияОбъекта)
				И Данные.ЭтоНовый() Тогда
				
				Возврат;
			Иначе
				Набор = Данные;
			КонецЕсли;
			
			ПланыОбмена.УдалитьРегистрациюИзменений(Узел, Набор);
			КопияДанных = Набор;
		Иначе
			Для Каждого ЭлементМассива Из Данные Цикл
				ПланыОбмена.УдалитьРегистрациюИзменений(Узел, ЭлементМассива);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ() Тогда
		РегистрыСведений.ДанныеОбработанныеВЦентральномУзлеРИБ.ОтметитьВыполнениеОбработки(Очередь, КопияДанных, ДополнительныеПараметры); 
	КонецЕсли;
	
КонецПроцедуры

// Дополнительные параметры функций ОтметитьКОбработке и ОтметитьВыполнениеОбработки.
// 
// Возвращаемое значение:
//  Структура - структура со свойствами:
//     * ЭтоДвижения - Булево - в параметре Данные функции переданы ссылки на регистраторы, по которым нужно обновить движения.
//                              Значение по умолчанию - Ложь.
//      * ПолноеИмяРегистра - Строка - полное имя регистра, по которому нужно обновить данные. Например, РегистрНакопления.ТоварыНаСкладах.
//      * ОтметитьВсеРегистраторы - Булево - необходимо отметить к обработке все проведенные документы переданного во
//                                           втором параметре типа.
//                                           В этом случае в параметре Данные процедуры можно передавать
//                                           ОбъектМетаданных:Документ или ДокументСсылка.
//      * ЭтоНезависимыйРегистрСведений - Булево - в параметре Данные функции передана таблица со значениями измерений,
//                                                 по которым нужно обновлять данные, значение по умолчанию - Ложь.
//
Функция ДополнительныеПараметрыОтметкиОбработки() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭтоДвижения", Ложь);
	ДополнительныеПараметры.Вставить("ОтметитьВсеРегистраторы", Ложь);
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Ложь);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "");
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

// Основные параметры процедуры ОбновлениеИнформационнойБазы.ОтметитьКОбработке,
// которые инициализируются механизмом регистрации изменений
// и не должны переопределяться в коде процедур отметки к обработке обработчиков обновления.
//
// Возвращаемое значение:
//  Структура - со свойствами:
//     * Очередь - Число - очередь обработки, в которой выполняется текущий обработчик.
//     * ЗаписьИзмененийДляПодчиненногоУзлаРИБСФильтрами - ЗаписьFastInfoset - параметр
//          существует, только если внедрена подсистема ОбменДанными.
//     * ПараметрыВыборки - Структура - см. ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки().
//
Функция ОсновныеПараметрыОтметкиКОбработке() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Очередь", 0);
	Параметры.Вставить("ПовторнаяРегистрация", Ложь);
	Параметры.Вставить("ПараметрыВыборки");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбменДанными") Тогда
		
		Параметры.Вставить("ИмяФайлаСИзменениями", Неопределено);
		Параметры.Вставить("ЗаписьИзмененийДляПодчиненногоУзлаРИБСФильтрами", Неопределено);
		
	КонецЕсли;
	
	Возврат Параметры; 
	
КонецФункции

// Возвращает информацию о переданных данных в нормализованном виде. 
// Для использования в процедурах проверки блокировки данных отложенных обработчиков обновления.
//
// Параметры:
//  Данные				     - ЛюбаяСсылка, НаборЗаписей, Объект, ДанныеФормыСтруктура - данные, которые нужно проанализировать.
//  ДополнительныеПараметры	 - Структура, Неопределено - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
// 
// Возвращаемое значение:
//  Структура - со свойствами:
//      * Данные			  - ЛюбаяСсылка, НаборЗаписей, Объект, ДанныеФормыСтруктура - значение входящего параметра Данные.
//  	* МетаданныеОбъекта   - ОбъектМетаданных - объект метаданных, соответствующий параметру Данные.
//  	* ПолноеИмя           - Строка      - полное имя объекта метаданных (см. метод ОбъектМетаданных.ПолноеИмя).
//		* Отбор               - ЛюбаяСсылка - если Данные - это ссылочный объект, то значение ссылки, 
//                                            если регистр подчиненный регистратору, - значение отбора по регистратору.
//			   	              - Структура   - если Данные - это независимый регистр сведений, то структура, соответствующая 
//                                            установленным отборам по измерениям.
//		* ЭтоНовый            - Булево      - если Данные - это ссылочный объект, то признак нового объекта. 
//                                            Для других типов - всегда Ложь.
//	
Функция МетаданныеИОтборПоДанным(Данные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки();
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЭтоДвижения Тогда
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра);		
	Иначе
		МетаданныеОбъекта = Неопределено;
	КонецЕсли;
	
	Отбор = Неопределено;
	ТипДанных = ТипЗнч(Данные);
	ЭтоНовый = Ложь;
	
	Если ТипЗнч(Данные) = Тип("Строка") Тогда
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(Данные);
	ИначеЕсли ТипДанных = Тип("ДанныеФормыСтруктура") Тогда
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Данные, "Ссылка") Тогда
			
			Если МетаданныеОбъекта = Неопределено Тогда
				МетаданныеОбъекта = Данные.Ссылка.Метаданные();
			КонецЕсли;
			
			Отбор = Данные.Ссылка;
			
			Если Не ЗначениеЗаполнено(Отбор) Тогда
				ЭтоНовый = Истина;
			КонецЕсли;
			
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Данные, "ИсходныйКлючЗаписи") Тогда	

			Если МетаданныеОбъекта = Неопределено Тогда
				МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(Данные.ИсходныйКлючЗаписи));	
			КонецЕсли;
			Отбор = Новый Структура;
			Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
				Отбор.Вставить(Измерение.Имя, Данные[Измерение.Имя]);
			КонецЦикла;
			
		Иначе
			ТекстИсключения = НСтр("ru = 'Процедура ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным не может быть использована для этой формы.'");
		КонецЕсли;
		
	Иначе
		
		Если МетаданныеОбъекта = Неопределено Тогда
			МетаданныеОбъекта = Данные.Метаданные();
		КонецЕсли;
		
		Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта) Тогда
			
			Если ОбщегоНазначения.ЭтоСсылка(ТипДанных) Тогда
				Отбор = Данные;
			Иначе
				Отбор = Данные.Ссылка;
				
				Если Данные.ЭтоНовый() Тогда
					ЭтоНовый = Истина;
				КонецЕсли;
			
			КонецЕсли;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
			И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			
			Отбор = Новый Структура;
			Для Каждого ЭлементОтбора Из Данные.Отбор Цикл
				Если ЭлементОтбора.Использование Тогда 
					Отбор.Вставить(ЭлементОтбора.Имя, ЭлементОтбора.Значение);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
			Если ДополнительныеПараметры.ЭтоДвижения Тогда
				Отбор = Данные;
			Иначе
				Отбор = Данные.Отбор.Регистратор.Значение;
			КонецЕсли;
		Иначе
			ТекстИсключения = НСтр("ru = 'Для этого типа метаданных не поддерживается анализ в функции ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Данные", Данные);
	Результат.Вставить("Метаданные", МетаданныеОбъекта);
	Результат.Вставить("ПолноеИмя", МетаданныеОбъекта.ПолноеИмя());
	Результат.Вставить("Отбор", Отбор);
	Результат.Вставить("ЭтоНовый", ЭтоНовый);
	
	Возврат Результат;
КонецФункции

// Отмечает, что переданные данные необходимо обновить.
// Важно: не рекомендуется передавать в параметр Данные сразу все данные, которые
// необходимо зарегистрировать к обработке, т.к. большие коллекции типа Массив
// или ТаблицаЗначений могут занять существенный объем памяти сервера и привести
// к сильному снижению производительности системы. Рекомендуется получать и передавать
// данные небольшими порциями, например по 1000 объектов.
//
// Параметры:
//  ОсновныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//  Данные            - Ссылка, Массив, НаборЗаписей - данные, по которым нужно зарегистрировать изменения.
//                    - ТаблицаЗначений - значения измерений независимого регистра сведений. Требования:
//                        - нет измерений с именем "Узел";
//                        - все измерения регистра должны входить в основной отбор;
//                        - в таблице должны быть только колонки, соответствующие по именам измерениям регистра,
//                          по которым нужно регистрировать необходимость обработки;
//                        - запись наборов в процессе обновления должна проходить с тем же отбором,
//                          что и регистрация необходимости обработки;
//                        - в ДополнительныеПараметры нужно передать соответствующий признак и полное имя регистра.
//  ДополнительныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
// 
Процедура ОтметитьКОбработке(ОсновныеПараметры, Данные, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки();
	КонецЕсли;
	
	Если (ТипЗнч(Данные) = Тип("Массив")
		Или ТипЗнч(Данные) = Тип("ТаблицаЗначений"))
		И Данные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Узел = ОчередьСсылкой(ОсновныеПараметры.Очередь);
	
	Если ДополнительныеПараметры.ЭтоДвижения Тогда
		
		Набор = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра).СоздатьНаборЗаписей();
		
		Если ДополнительныеПараметры.ОтметитьВсеРегистраторы Тогда
			
			Если ТипЗнч(Данные) = Тип("ОбъектМетаданных") Тогда
				МетаданныеДокумента = Данные;
			ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Данные)) Тогда
				МетаданныеДокумента = Данные.Метаданные();
			Иначе
				ТекстИсключения = НСтр("ru = 'Для регистрации всех регистраторов регистра необходимо в параметре ""Данные"" передать ОбъектМетаданных:Документ или ДокументСсылка.'");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			ПолноеИмяДокумента = МетаданныеДокумента.ПолноеИмя();
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаДокумента.Ссылка КАК Ссылка
			|ИЗ
			|	#ТаблицаДокумента КАК ТаблицаДокумента
			|ГДЕ
			|	ТаблицаДокумента.Проведен";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаДокумента", ПолноеИмяДокумента);
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапроса;
			
			МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
			Для Каждого ЭлементМассива Из МассивСсылок Цикл
				Набор.Отбор.Регистратор.Установить(ЭлементМассива);
				ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, Набор, "ПодчиненныйРегистр", ДополнительныеПараметры.ПолноеИмяРегистра);
			КонецЦикла;
			
		Иначе
			
			Если ТипЗнч(Данные) = Тип("Массив") Тогда
				Итератор = 0;
				Попытка
					Для Каждого ЭлементМассива Из Данные Цикл
						Если Итератор = 0 Тогда
							НачатьТранзакцию();
						КонецЕсли;
						Набор.Отбор.Регистратор.Установить(ЭлементМассива);
						ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, Набор, "ПодчиненныйРегистр", ДополнительныеПараметры.ПолноеИмяРегистра);
						Итератор = Итератор + 1;
						Если Итератор = 1000 Тогда
							Итератор = 0;
							ЗафиксироватьТранзакцию();
						КонецЕсли;
					КонецЦикла;
					
					Если Итератор <> 0 Тогда
						ЗафиксироватьТранзакцию();
					КонецЕсли;
				Исключение
					ОтменитьТранзакцию();
					ВызватьИсключение;
				КонецПопытки
			Иначе
				
				Набор.Отбор.Регистратор.Установить(Данные);
				ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, Набор, "ПодчиненныйРегистр", ДополнительныеПараметры.ПолноеИмяРегистра);
				
			КонецЕсли;
			
		КонецЕсли;
	ИначеЕсли ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений Тогда
		
		Набор = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра).СоздатьНаборЗаписей();
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ДополнительныеПараметры.ПолноеИмяРегистра);
		УстановитьНедостающиеОтборыВНаборе(Набор, МетаданныеОбъекта, Данные);
		
		Для Каждого СтрокаТаблицы Из Данные Цикл
			
			Для Каждого Колонка Из Данные.Колонки Цикл
				Набор.Отбор[Колонка.Имя].Значение = СтрокаТаблицы[Колонка.Имя];
				Набор.Отбор[Колонка.Имя].Использование = Истина;
			КонецЦикла;
			
			ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, Набор, "НезависимыйРегистр", ДополнительныеПараметры.ПолноеИмяРегистра);
			
		КонецЦикла;
	Иначе
		Если ТипЗнч(Данные) = Тип("Массив") Тогда
			Итератор = 0;
			Попытка
				Для Каждого ЭлементМассива Из Данные Цикл
					Если Итератор = 0 Тогда
						НачатьТранзакцию();
					КонецЕсли;
					ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, ЭлементМассива, "Ссылка");
					Итератор = Итератор + 1;
					Если Итератор = 1000 Тогда
						Итератор = 0;
						ЗафиксироватьТранзакцию();
					КонецЕсли;
				КонецЦикла;
				
				Если Итератор <> 0 Тогда
					ЗафиксироватьТранзакцию();
				КонецЕсли;
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение;
			КонецПопытки
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Данные)) Тогда
			ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, Данные, "Ссылка");
		Иначе
			Если ТипЗнч(Данные) = Тип("ОбъектМетаданных") Тогда
				ТекстИсключения = НСтр("ru = 'Не поддерживается регистрация к обновлению целиком объекта метаданных. Нужно обновлять конкретные данные.'");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			
			МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(Данные));
			
			Если ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
				И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
				
				УстановитьНедостающиеОтборыВНаборе(Данные, МетаданныеОбъекта, Данные.Отбор);
				
			КонецЕсли;
			ЗарегистрироватьИзменения(ОсновныеПараметры, Узел, Данные, "НезависимыйРегистр", МетаданныеОбъекта.ПолноеИмя());
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Отмечает, что по переданным регистраторам нужно переформировать движения.
// 
// Параметры:
//  Параметры         - Структура - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//  Регистраторы      - Массив - массив ссылок регистраторов.
//  ПолноеИмяРегистра - Строка - полное имя регистра, для которого необходимо обновить движения.
//
Процедура ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра) Экспорт
	
	ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
КонецПроцедуры

// Дополнительные параметры выборки данных для обработки.
// 
// Возвращаемое значение:
//  Структура - поля структуры:
//   * ВыбиратьПорциями - Булево - выбирать данные для обработки порциями.
//                        Если выбираются документы, то порция определяется с учетом упорядочивания по убыванию
//                        по дате документа. Если выбираются регистраторы регистра, то порция определяется с
//                        учетом упорядочивания по убыванию по дате регистратора, если передано полное имя документа.
//                        Если полное имя документа не передано - упорядочивание происходит по периоду регистра:
//                        - берется максимальная дата по каждому регистратору;
//                        - если по регистратору нет записей - он в топе.
//   * ИмяВременнойТаблицы - Строка - параметр актуален для методов, создающих временные таблицы. Если имя не задано
//                           (поведение по умолчанию), то временная таблица будет создана с именем, указанным
//                           в описании каждого метода.
//   * ДополнительныеИсточникиДанных - Соответствие - параметр актуален для методов, выбирающих регистраторы и ссылки
//                                     для обработки. В ключах соответствия может быть только один из следующих
//                                     видов данных:
//                                     1. Пути к реквизитам шапки документа или реквизитам табличных частей, которые
//                                        участвуют в соединениях с другими таблицами (в т.ч. неявных соединениях при
//                                        обращении "через точку").
//                                     2. Имена ссылочных объектов метаданных (Строка), в значениях которых находится
//                                        соответствие, в котором ключ - это имя регистра (Строка), а в значении
//                                        соответствие в ключах которого то же, что и в п. 1, т.е.
//                                        иерархия соответствий "Объект" -> "Регистр" -> "Источники".
//                                     Процедуры проверяют блокировку данных этих таблиц обработчиками меньших
//                                     очередей. Формат имен источников: <ИмяРеквизита> или
//                                     <ИмяТабличной>.<ИмяРеквизитаТабличнойЧасти>. Для удобства заполнения
//                                     см. УстановитьИсточникДанных() и см. ПолучитьИсточникДанных().
//   * ПоляУпорядочивания  - Массив - имена полей независимого регистра сведений, используется для упорядочивания
//                                    результата запроса.
//   * МаксимумВыборки - Число - максимальное количество выбираемых записей.
//
Функция ДополнительныеПараметрыВыборкиДанныхДляОбработки() Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыбиратьПорциями", Истина);
	ДополнительныеПараметры.Вставить("ИмяВременнойТаблицы", "");
	ДополнительныеПараметры.Вставить("ДополнительныеИсточникиДанных", Новый Соответствие);
	ДополнительныеПараметры.Вставить("ПоляУпорядочивания", Новый Массив);
	ДополнительныеПараметры.Вставить("МаксимумВыборки", МаксимальноеКоличествоЗаписейВВыборке());
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

// Дополнительные параметры выборки данных для многопоточной обработки.
//
// Возвращаемое значение:
//  Структура - поля из ДополнительныеПараметрыВыборкиДанныхДляОбработки(), дополненные следующими полями:
//   * ПолныеИменаОбъектов - Строка - полные имена обновляемых объектов (например, документов), разделенные запятыми.
//   * ПолныеИменаРегистров - Строка - полные имена регистров, разделенные запятыми.
//   * ПоляУпорядочиванияПриРаботеПользователей - Массив - поля упорядочивания, используемые при обновлении
//                                                с приоритетом работы пользователей.
//   * ПоляУпорядочиванияПриОбработкеДанных - Массив - поля упорядочивания, используемые при обновлении
//                                            с приоритетом обработки данных.
//   * СпособВыборки - Строка - один из способов выборки:
//                              ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений(),
//                              ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра(),
//                              ОбновлениеИнформационнойБазы.СпособВыборкиСсылки().
//   * ПоследняяВыбраннаяЗапись - СписокЗначений - конец предыдущей выборки (служебное поле).
//   * ПерваяЗапись - СписокЗначений - начало выборки (служебное поле).
//   * ПоследняяЗапись - СписокЗначений - конец выборки (служебное поле).
//   * ОптимизироватьВыборкуПоСтраницам - Булево - если Истина, то выборка выполняется без ИЛИ, значение Ложь может
//                                        быть полезно, если исходный запрос не оптимален, тогда с ИЛИ будет быстрее.
//
Функция ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки() Экспорт
	
	ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.Вставить("ПолныеИменаОбъектов");
	ДополнительныеПараметры.Вставить("ПолныеИменаРегистров");
	ДополнительныеПараметры.Вставить("ПоляУпорядочиванияПриРаботеПользователей", Новый Массив);
	ДополнительныеПараметры.Вставить("ПоляУпорядочиванияПриОбработкеДанных", Новый Массив);
	ДополнительныеПараметры.Вставить("СпособВыборки");
	ДополнительныеПараметры.Вставить("ПоследняяВыбраннаяЗапись");
	ДополнительныеПараметры.Вставить("ПерваяЗапись");
	ДополнительныеПараметры.Вставить("ПоследняяЗапись");
	ДополнительныеПараметры.Вставить("ОптимизироватьВыборкуПоСтраницам", Истина);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

// Установить параметр ДополнительныеИсточникиДанных в структуре, возвращаемой функцией
// ДополнительныеПараметрыВыборкиДанныхДляОбработки().
//
// Используется, когда источники данных нужно установить в разрезе документов и регистров.
// Применяется при многопоточном обновлении.
//
// Параметры:
//  ДополнительныеИсточникиДанных - Соответствие - то же, что и ДополнительныеИсточникиДанных
//                                  (см. ДополнительныеПараметрыВыборкиДанныхДляОбработки()).
//  Источник - Строка - источники данных (см. ДополнительныеПараметрыВыборкиДанныхДляОбработки()).
//  Объект - Строка - имя документа (полное или короткое).
//  Регистр - Строка - имя регистра (полное или короткое).
//
Процедура УстановитьИсточникДанных(ДополнительныеИсточникиДанных, Источник, Объект = "", Регистр = "") Экспорт
	
	ИмяОбъекта = ИмяОбъектаМетаданных(Объект);
	ИмяРегистра = ИмяОбъектаМетаданных(Регистр);
	
	Если ПустаяСтрока(ИмяОбъекта) И ПустаяСтрока(ИмяРегистра) Тогда
		ДополнительныеИсточникиДанных.Вставить(Источник);
	Иначе
		РегистрыОбъекта = ДополнительныеИсточникиДанных[ИмяОбъекта];
		
		Если РегистрыОбъекта = Неопределено Тогда
			РегистрыОбъекта = Новый Соответствие;
			ДополнительныеИсточникиДанных[ИмяОбъекта] = РегистрыОбъекта;
		КонецЕсли;
		
		ИсточникиДанных = РегистрыОбъекта[ИмяРегистра];
		
		Если ИсточникиДанных = Неопределено Тогда
			ИсточникиДанных = Новый Соответствие;
			РегистрыОбъекта[ИмяРегистра] = ИсточникиДанных;
		КонецЕсли;
		
		ИсточникиДанных.Вставить(Источник);
	КонецЕсли;
	
КонецПроцедуры

// Получить значение параметра ДополнительныеИсточникиДанных из структуры, возвращаемой
// функцией ДополнительныеПараметрыВыборкиДанныхДляОбработки().
//
// Можно использовать, когда источники данных нужно получить в разрезе документов и регистров.
// Применяется при многопоточном обновлении.
//
// Параметры:
//  ДополнительныеИсточникиДанных - Соответствие - то же, что и ДополнительныеИсточникиДанных
//                                  (см. ДополнительныеПараметрыВыборкиДанныхДляОбработки()).
//  Объект - Строка - имя документа (полное или короткое).
//  Регистр - Строка - имя регистра (полное или короткое).
//
// Возвращаемое значение:
//  Соответствие - источники данных для указанного документа и регистра.
//
Функция ИсточникиДанных(ДополнительныеИсточникиДанных, Объект = "", Регистр = "") Экспорт
	
	Если ЭтоПростойИсточникДанных(ДополнительныеИсточникиДанных) Тогда
		Возврат ДополнительныеИсточникиДанных;
	Иначе
		ИмяОбъекта = ИмяОбъектаМетаданных(Объект);
		ИмяРегистра = ИмяОбъектаМетаданных(Регистр);
		РегистрыОбъекта = ДополнительныеИсточникиДанных[ИмяОбъекта];
		ТипСоответствие = Тип("Соответствие");
		
		Если ТипЗнч(РегистрыОбъекта) = ТипСоответствие Тогда
			ИсточникиДанных = РегистрыОбъекта[ИмяРегистра];
			
			Если ТипЗнч(ИсточникиДанных) = ТипСоответствие Тогда
				Возврат ИсточникиДанных;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Новый Соответствие;
	КонецЕсли;
	
КонецФункции

// Создает временную таблицу ссылок, которые не обработаны в текущей очереди
//  и не заблокированы меньшими очередями.
//  Имя таблицы: ВТДляОбработки<ИмяРегистра>, например ВТДляОбработкиТоварыНаСкладах.
//  Колонки таблицы:
//  * Регистратор - ДокументСсылка.
//
// Параметры:
//  Очередь					 - Число					 - очередь обработки, в которой выполняется текущий обработчик.
//  ПолноеИмяДокумента		 - Строка - имя документа, движения по которому нужно переформировать. Если движения формируются не по данным
//									документа, то нужно передать Неопределено - тогда не будет проверяться блокировка таблицы документа.
//									Например, Документ.ПриходныйОрдерНаТовары.
//  ПолноеИмяРегистра	 - Строка	 - имя регистра, движения по которому нужно переформировать.
//  	Например, РегистрНакопления.ТоварыНаСкладах.
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц	 - менеджер, в котором будет создана временная таблица.
//  ДополнительныеПараметры	 - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
// 
// Возвращаемое значение:
//  Структура - результат формирования временной таблицы:
//  * ЕстьЗаписиВоВременнойТаблице - Булево - в создаваемой таблице есть хотя бы одна запись. Записей может не быть по
//                                            двум причинам
//												все обработано или все, что нужно обработать, еще заблокировано обработчиками с меньшей очередью.
//  * ЕстьДанныеДляОбработки - Булево - в очереди есть ссылки для обработки, т.е. еще не все обработано.
//  * ИмяВременнойТаблицы - Строка - имя созданной временной таблицы.
//
Функция СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(Очередь, ПолноеИмяДокумента, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	ИмяРегистра = СтрРазделить(ПолноеИмяРегистра,".",Ложь)[1];
	
	ТекущаяОчередь = ОчередьСсылкой(Очередь);
	
	Если ПолноеИмяДокумента = Неопределено Тогда 
		Если ДополнительныеПараметры.ВыбиратьПорциями Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаРегистраИзменения.Регистратор КАК Регистратор,
			|	МАКСИМУМ(ЕСТЬNULL(ТаблицаРегистра.Период, ДАТАВРЕМЯ(3000, 1, 1))) КАК Период
			|ПОМЕСТИТЬ ВТДляОбработкиРегистраторПолная
			|ИЗ
			|	#ТаблицаРегистраИзменения КАК ТаблицаРегистраИзменения
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоРегистратор КАК ВТЗаблокированоРегистратор
			|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоРегистратор.Регистратор
			|		ЛЕВОЕ СОЕДИНЕНИЕ #ТаблицаДвиженийРегистра КАК ТаблицаРегистра
			|		ПО ТаблицаРегистраИзменения.Регистратор = ТаблицаРегистра.Регистратор
			|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
			|ГДЕ
			|	ТаблицаРегистраИзменения.Узел = &ТекущаяОчередь
			|	И ВТЗаблокированоРегистратор.Регистратор ЕСТЬ NULL
			|	И &УсловиеПоДопИсточникамРегистрам
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаРегистраИзменения.Регистратор
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТДляОбработкиРегистраторПолная.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ #ВТДляОбработкиРегистратор
			|ИЗ
			|	ВТДляОбработкиРегистраторПолная КАК ВТДляОбработкиРегистраторПолная
			|ГДЕ
			|	ВТДляОбработкиРегистраторПолная.Регистратор В
			|			(ВЫБРАТЬ ПЕРВЫЕ 10000
			|				ВТДляОбработкиРегистраторПолная.Регистратор КАК Регистратор
			|			ИЗ
			|				ВТДляОбработкиРегистраторПолная КАК ВТДляОбработкиРегистраторПолная
			|			УПОРЯДОЧИТЬ ПО
			|				ВТДляОбработкиРегистраторПолная.Период УБЫВ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТЗаблокированоРегистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТДляОбработкиРегистраторПолная";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТаблицаДвиженийРегистра", ПолноеИмяРегистра);	
		Иначе
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаРегистраИзменения.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ #ВТДляОбработкиРегистратор
			|ИЗ
			|	#ТаблицаРегистраИзменения КАК ТаблицаРегистраИзменения
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоРегистратор КАК ВТЗаблокированоРегистратор
			|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоРегистратор.Регистратор
			|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
			|ГДЕ
			|	ТаблицаРегистраИзменения.Узел = &ТекущаяОчередь
			|	И ВТЗаблокированоРегистратор.Регистратор ЕСТЬ NULL
			|	И &УсловиеПоДопИсточникамРегистрам
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТЗаблокированоРегистратор";
		КонецЕсли;
	Иначе
		Если ДополнительныеПараметры.ВыбиратьПорциями Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаРегистраИзменения.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ ВТДляОбработкиРегистраторПолная
			|ИЗ
			|	#ТаблицаРегистраИзменения КАК ТаблицаРегистраИзменения
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоРегистратор КАК ВТЗаблокированоРегистратор
			|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоРегистратор.Регистратор
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоСсылка КАК ВТЗаблокированоСсылка
			|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоСсылка.Ссылка
			|		#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
			|		#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
			|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
			|ГДЕ
			|	ТаблицаРегистраИзменения.Узел = &ТекущаяОчередь
			|	И ТаблицаРегистраИзменения.Регистратор ССЫЛКА #ПолноеИмяДокумента 
			|	И ВТЗаблокированоРегистратор.Регистратор ЕСТЬ NULL 
			|	И ВТЗаблокированоСсылка.Ссылка ЕСТЬ NULL 
			|	И &УсловиеПоДопИсточникамСсылкам
			|	И &УсловиеПоДопИсточникамРегистрам
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Регистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТДляОбработкиРегистраторПолная.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ #ВТДляОбработкиРегистратор
			|ИЗ
			|	ВТДляОбработкиРегистраторПолная КАК ВТДляОбработкиРегистраторПолная
			|ГДЕ
			|	ВТДляОбработкиРегистраторПолная.Регистратор В
			|			(ВЫБРАТЬ ПЕРВЫЕ 10000
			|				ВТДляОбработкиРегистраторПолная.Регистратор КАК Регистратор
			|			ИЗ
			|				ВТДляОбработкиРегистраторПолная КАК ВТДляОбработкиРегистраторПолная
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяДокумента КАК ТаблицаДокумента
			|					ПО
			|						ВТДляОбработкиРегистраторПолная.Регистратор = ТаблицаДокумента.Ссылка
			|			УПОРЯДОЧИТЬ ПО
			|				ТаблицаДокумента.Дата УБЫВ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТЗаблокированоРегистратор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТЗаблокированоСсылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТДляОбработкиРегистраторПолная";

		Иначе	
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаРегистраИзменения.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ #ВТДляОбработкиРегистратор
			|ИЗ
			|	#ТаблицаРегистраИзменения КАК ТаблицаРегистраИзменения
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоРегистратор КАК ВТЗаблокированоРегистратор
			|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоРегистратор.Регистратор
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоСсылка КАК ВТЗаблокированоСсылка
			|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоСсылка.Ссылка
			|		#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
			|		#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
			|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
			|ГДЕ
			|	ТаблицаРегистраИзменения.Узел = &ТекущаяОчередь
			|	И ТаблицаРегистраИзменения.Регистратор ССЫЛКА #ПолноеИмяДокумента 
			|	И ВТЗаблокированоРегистратор.Регистратор ЕСТЬ NULL 
			|	И ВТЗаблокированоСсылка.Ссылка ЕСТЬ NULL 
			|	И &УсловиеПоДопИсточникамСсылкам
			|	И &УсловиеПоДопИсточникамРегистрам
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Регистратор 
			|;
			|УНИЧТОЖИТЬ
			|	ВТЗаблокированоРегистратор 
			|;
			|УНИЧТОЖИТЬ
			|	ВТЗаблокированоСсылка";
		КонецЕсли;
		
		ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
		ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = "ВТЗаблокированоСсылка";
		СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяДокумента, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
	КонецЕсли;
	
	Если ПустаяСтрока(ДополнительныеПараметры.ИмяВременнойТаблицы) Тогда
		ИмяВременнойТаблицы = "ВТДляОбработки" + ИмяРегистра;
	Иначе
		ИмяВременнойТаблицы = ДополнительныеПараметры.ИмяВременнойТаблицы;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаРегистраИзменения", ПолноеИмяРегистра + ".Изменения");	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТДляОбработкиРегистратор", ИмяВременнойТаблицы);
	
	ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = "ВТЗаблокированоРегистратор";
	СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
	
	ДобавитьПроверкуБлокировкиДополнительныхИсточников(Очередь, ТекстЗапроса, ПолноеИмяДокумента, ПолноеИмяРегистра, МенеджерВременныхТаблиц, Истина, ДополнительныеПараметры);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяДокумента", ПолноеИмяДокумента);
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяОчередь", ТекущаяОчередь);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура("ЕстьЗаписиВоВременнойТаблице,ЕстьДанныеДляОбработки,ИмяВременнойТаблицы", Ложь, Ложь, "");
	Результат.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
	Результат.ЕстьЗаписиВоВременнойТаблице = РезультатЗапроса[0].Выгрузить()[0].Количество <> 0;
	
	Если Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Результат.ЕстьДанныеДляОбработки = Истина;
	Иначе
		Результат.ЕстьДанныеДляОбработки = ЕстьДанныеДляОбработки(Очередь, ПолноеИмяРегистра);
	КонецЕсли;	
	
	Возврат Результат; 
	
КонецФункции

// Возвращает порцию регистраторов, по которым нужно переформировать движения.
//  Данные берутся из зарегистрированных в очереди, учитываются заблокированные более приоритетными очередями данные.
//  Блокировка по другим очередям делается по документу и по регистру.
//  Регистраторы в выборке упорядочены по дате регистратора по убыванию, если передано полное имя документа.
//  Если полное имя документа не передано - упорядочивание происходит по периоду регистра:
//				- берется максимальная дата по каждому регистратору;
//				- если по регистратору нет записей - он в топе.
// Параметры:
//  Очередь					 - Число - очередь, к которой отнесен обработчик и в которой зарегистрированы данные, которые он будет обрабатывать.
//  ПолноеИмяДокумента		 - Строка - имя документа, движения по которому нужно переформировать. Если движения формируются не по данным
//									документа, то нужно передать Неопределено - тогда не будет проверяться блокировка таблицы документа.
//									Например, Документ.ПриходныйОрдерНаТовары.
//  ПолноеИмяРегистра		 - Строка	 - имя регистра, движения по которому нужно переформировать.
//  	Например, РегистрНакопления.ТоварыНаСкладах.
//  ДополнительныеПараметры	 - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
// 
// Возвращаемое значение:
//  * ВыборкаИзРезультатаЗапроса - выборка регистраторов, которые нужно обработать, поля выборки:
//    ** Регистратор - ДокументСсылка.
//    ** Период - Дата - дата документа, если передано полное имя документа, максимальный период по регистратору,
//                       если полное имя документа не передано.
//    ** Проведен - Булево, Неопределено - значение реквизита Проведен документа, если передано полное имя документа,
//                                         Неопределено - если имя документа не передано.
//  * ТаблицаЗначений - данные, которые нужно обработать, имена колонок соответствуют именам измерений регистра.
//
Функция ВыбратьРегистраторыРегистраДляОбработки(Очередь, ПолноеИмяДокумента, ПолноеИмяРегистра, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	ПроверитьПараметрыВыборки(ДополнительныеПараметры);
	ПараметрыПостроения = ПараметрыПостроенияВыборки(ДополнительныеПараметры);
	
	Если ПолноеИмяДокумента = Неопределено Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 10000
		|	&ВыбираемыеПоля
		|ИЗ
		|	#ТаблицаРегистраИзменения КАК ТаблицаРегистраИзменения
		|		ЛЕВОЕ СОЕДИНЕНИЕ #ТаблицаДвиженийРегистра КАК ТаблицаРегистра
		|		ПО ТаблицаРегистраИзменения.Регистратор = ТаблицаРегистра.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоРегистратор КАК ВТЗаблокированоРегистратор
		|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоРегистратор.Регистратор
		|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
		|ГДЕ
		|	ТаблицаРегистраИзменения.Узел = &ТекущаяОчередь
		|	И ВТЗаблокированоРегистратор.Регистратор ЕСТЬ NULL 
		|	И &УсловиеПоДопИсточникамРегистрам
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаРегистраИзменения.Регистратор";
		
		Если ПараметрыПостроения.ПостраничнаяВыборка Тогда
			ТекстЗапроса = ТекстЗапроса + "
				|
				|ИМЕЮЩИЕ
				|	&УсловиеПоСтраницам"
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
			|
			|УПОРЯДОЧИТЬ ПО
			|	&ПорядокВыборки";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаДвиженийРегистра", ПолноеИмяРегистра);
		УстановитьПоляУпорядочиванияРегистра(ПараметрыПостроения);
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 10000
		|	&ВыбираемыеПоля
		|ИЗ
		|	#ТаблицаРегистраИзменения КАК ТаблицаРегистраИзменения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоРегистратор КАК ВТЗаблокированоРегистратор
		|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоРегистратор.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокированоСсылка КАК ВТЗаблокированоСсылка
		|		ПО ТаблицаРегистраИзменения.Регистратор = ВТЗаблокированоСсылка.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяДокумента КАК ТаблицаДокумента
		|			#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
		|		ПО ТаблицаРегистраИзменения.Регистратор = ТаблицаДокумента.Ссылка
		|		#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
		|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
		|
		|ГДЕ
		|	ТаблицаРегистраИзменения.Узел = &ТекущаяОчередь
		|	И ТаблицаРегистраИзменения.Регистратор ССЫЛКА #ПолноеИмяДокумента 
		|	И ВТЗаблокированоРегистратор.Регистратор ЕСТЬ NULL 
		|	И ВТЗаблокированоСсылка.Ссылка ЕСТЬ NULL 
		|	И &УсловиеПоДопИсточникамСсылкам
		|	И &УсловиеПоДопИсточникамРегистрам
		|	И &УсловиеПоСтраницам
		|
		|УПОРЯДОЧИТЬ ПО
		|	&ПорядокВыборки";
		ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
		ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = "ВТЗаблокированоСсылка";
		СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяДокумента, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
		УстановитьПоляУпорядочиванияРегистраПоДокументу(ПараметрыПостроения);
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаРегистраИзменения", ПолноеИмяРегистра + ".Изменения");	
	
	ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = "ВТЗаблокированоРегистратор";
	СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
	УстановитьРазмерВыборки(ТекстЗапроса, ДополнительныеПараметры);
	ДобавитьПроверкуБлокировкиДополнительныхИсточников(Очередь, ТекстЗапроса, ПолноеИмяДокумента, ПолноеИмяРегистра, МенеджерВременныхТаблиц, Ложь, ДополнительныеПараметры);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяДокумента", ПолноеИмяДокумента);
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяОчередь", ОчередьСсылкой(Очередь));
	
	УстановитьПоляПоСтраницам(Запрос, ПараметрыПостроения);
	УстановитьПорядокПоСтраницам(Запрос, ПараметрыПостроения);
	
	Возврат ВыбратьДанныеДляОбработки(Запрос, ПараметрыПостроения);
	
КонецФункции

// Возвращает порцию ссылок, по которым нужно произвести обработку.
//  Данные берутся из зарегистрированных в очереди, учитываются заблокированные более приоритетными очередями данные.
//	Ссылки на документы возвращаются упорядоченными по убыванию по дате.
//
// Параметры:
//  Очередь				 - Число - очередь, к которой отнесен обработчик и в которой зарегистрированы данные, которые он будет
//									обрабатывать.
//  ПолноеИмяОбъекта	 - Строка	 - имя объекта, который нужно обработать. Например, Документ.ПриходныйОрдерНаТовары.
//  ДополнительныеПараметры	 - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
// 
// Возвращаемое значение:
//  * ВыборкаИзРезультатаЗапроса - выборка ссылок, которые нужно обработать, поля выборки:
//    ** Ссылка - ЛюбаяСсылка.
//  * ТаблицаЗначений - данные, которые нужно обработать, имена колонок соответствуют именам измерений регистра.
//
Функция ВыбратьСсылкиДляОбработки(Очередь, ПолноеИмяОбъекта, ДополнительныеПараметры = Неопределено) Экспорт
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	ИмяОбъекта = СтрРазделить(ПолноеИмяОбъекта,".",Ложь)[1];
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	ЭтоДокумент = ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта)
				Или ОбщегоНазначения.ЭтоЗадача(МетаданныеОбъекта);
	
	ПроверитьПараметрыВыборки(ДополнительныеПараметры);
	ПараметрыПостроения = ПараметрыПостроенияВыборки(ДополнительныеПараметры);
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 10000
	|	&ВыбираемыеПоля
	|ИЗ
	|	#ТаблицаОбъектаИзменения КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ #ВТЗаблокированоСсылка КАК ВТЗаблокированоСсылка
	|		ПО ТаблицаИзменений.Ссылка = ВТЗаблокированоСсылка.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОбъекта КАК ТаблицаОбъекта
	|			#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
	|		ПО ТаблицаИзменений.Ссылка = ТаблицаОбъекта.Ссылка
	|		#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
	|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
	|ГДЕ
	|	ТаблицаИзменений.Узел = &ТекущаяОчередь
	|	И ВТЗаблокированоСсылка.Ссылка ЕСТЬ NULL 
	|	И &УсловиеПоДопИсточникамСсылкам
	|	И &УсловиеПоДопИсточникамРегистрам
	|	И &УсловиеПоСтраницам";
	Если ЭтоДокумент Или ПараметрыПостроения.ПостраничнаяВыборка Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	&ПорядокВыборки";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|УНИЧТОЖИТЬ
	|	#ВТЗаблокированоСсылка"; 
	УстановитьПоляУпорядочиванияСсылок(ПараметрыПостроения, ЭтоДокумент);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТЗаблокированоСсылка","ВТЗаблокировано" + ИмяОбъекта);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТаблицаОбъектаИзменения", ПолноеИмяОбъекта + ".Изменения");	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТаблицаОбъекта", ПолноеИмяОбъекта);	
	УстановитьРазмерВыборки(ТекстЗапроса, ДополнительныеПараметры);
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	ДобавитьПроверкуБлокировкиДополнительныхИсточников(Очередь, ТекстЗапроса, ПолноеИмяОбъекта, Неопределено, МенеджерВременныхТаблиц, Ложь, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяОчередь", ОчередьСсылкой(Очередь));
	
	УстановитьПоляПоСтраницам(Запрос, ПараметрыПостроения);
	Если ЭтоДокумент Или ПараметрыПостроения.ПостраничнаяВыборка Тогда
		УстановитьПорядокПоСтраницам(Запрос, ПараметрыПостроения);
	КонецЕсли;
	
	Возврат ВыбратьДанныеДляОбработки(Запрос, ПараметрыПостроения);
	
КонецФункции

// Создает временную таблицу ссылок, которые не обработаны в текущей очереди
//  и не заблокированы меньшими очередями.
//  Имя таблицы: ВТДляОбработки<ИмяОбъекта>, например ВТДляОбработкиНоменклатура.
//  Колонки таблицы:
//  * Ссылка - ЛюбаяСсылка.
//
// Параметры:
//  Очередь           - Число  - очередь обработки, в которой выполняется текущий обработчик.
//  ПолноеИмяОбъекта  - Строка - полное имя объекта, для которого выполняется проверка, например Справочник.Номенклатура.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором будет создана временная таблица.
//  ДополнительныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
// 
// Возвращаемое значение:
//  Структура - результат формирования временной таблицы:
//  * ЕстьЗаписиВоВременнойТаблице - Булево - в создаваемой таблице есть хотя бы одна запись. Записей может не быть по
//                                            двум причинам:
//                                             все обработано или все, что нужно обработать, еще заблокировано
//                                             обработчиками с меньшей очередью.
//  * ЕстьДанныеДляОбработки - Булево - в очереди есть ссылки для обработки, т.е. еще не все обработано.
//  * ИмяВременнойТаблицы - Строка - имя созданной временной таблицы.
//
Функция СоздатьВременнуюТаблицуСсылокДляОбработки(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	ИмяОбъекта = СтрРазделить(ПолноеИмяОбъекта,".",Ложь)[1];
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	
	Если ДополнительныеПараметры.ВыбиратьПорциями Тогда
		
		ЭтоДокумент = ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта)
					Или ОбщегоНазначения.ЭтоЗадача(МетаданныеОбъекта);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИзменений.Ссылка КАК Ссылка";
		Если ЭтоДокумент Тогда
			ТекстЗапроса = ТекстЗапроса + ",
			|	ТаблицаОбъекта.Дата КАК Дата";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + "
		|ПОМЕСТИТЬ ВТДляОбработкиСсылкаПолная
		|ИЗ
		|	#ТаблицаОбъектаИзменения КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ #ВТЗаблокированоСсылка КАК ВТЗаблокированоСсылка
		|		ПО ТаблицаИзменений.Ссылка = ВТЗаблокированоСсылка.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОбъекта КАК ТаблицаОбъекта
		|			#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
		|		ПО ТаблицаИзменений.Ссылка = ТаблицаОбъекта.Ссылка
		|			#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
		|			#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
		|ГДЕ
		|	ТаблицаИзменений.Узел = &ТекущаяОчередь
		|	И ВТЗаблокированоСсылка.Ссылка ЕСТЬ NULL 
		|	И &УсловиеПоДопИсточникамСсылкам
		|	И &УсловиеПоДопИсточникамРегистрам
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка 
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТДляОбработкиСсылкаПолная.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ #ВТДляОбработкиСсылка
		|ИЗ
		|	ВТДляОбработкиСсылкаПолная КАК ВТДляОбработкиСсылкаПолная
		|ГДЕ
		|	ВТДляОбработкиСсылкаПолная.Ссылка В
		|			(ВЫБРАТЬ ПЕРВЫЕ 10000
		|				ВТДляОбработкиСсылкаПолная.Ссылка КАК Ссылка
		|			ИЗ
		|				ВТДляОбработкиСсылкаПолная КАК ВТДляОбработкиСсылкаПолная";
		Если ЭтоДокумент Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|			УПОРЯДОЧИТЬ ПО
			|				ВТДляОбработкиСсылкаПолная.Дата УБЫВ";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
		|)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ #ВТЗаблокированоСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДляОбработкиСсылкаПолная"; 
		
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИзменений.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ #ВТДляОбработкиСсылка
		|ИЗ
		|	#ТаблицаОбъектаИзменения КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ #ВТЗаблокированоСсылка КАК ВТЗаблокированоСсылка
		|		ПО ТаблицаИзменений.Ссылка = ВТЗаблокированоСсылка.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОбъекта КАК ТаблицаОбъекта
		|			#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
		|		ПО ТаблицаИзменений.Ссылка = ТаблицаОбъекта.Ссылка
		|		#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
		|		#ТекстЗапросаСоединениеСДопИсточникамиРегистрами
		|ГДЕ
		|	ТаблицаИзменений.Узел = &ТекущаяОчередь
		|	И ВТЗаблокированоСсылка.Ссылка ЕСТЬ NULL
		|	И &УсловиеПоДопИсточникамСсылкам
		|	И &УсловиеПоДопИсточникамРегистрам
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ #ВТЗаблокированоСсылка"; 
	КонецЕсли;
	
	Если ПустаяСтрока(ДополнительныеПараметры.ИмяВременнойТаблицы) Тогда
		ИмяВременнойТаблицы = "ВТДляОбработки" + ИмяОбъекта;
	Иначе
		ИмяВременнойТаблицы = ДополнительныеПараметры.ИмяВременнойТаблицы;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТЗаблокированоСсылка","ВТЗаблокировано" + ИмяОбъекта);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТДляОбработкиСсылка",ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТаблицаОбъектаИзменения", ПолноеИмяОбъекта + ".Изменения");	
	
	СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	ДобавитьПроверкуБлокировкиДополнительныхИсточников(Очередь, ТекстЗапроса, ПолноеИмяОбъекта, Неопределено, МенеджерВременныхТаблиц, Истина, ДополнительныеПараметры);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТаблицаОбъекта", ПолноеИмяОбъекта);	
	
	ТекущаяОчередь = ОчередьСсылкой(Очередь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяОчередь", ТекущаяОчередь);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура("ЕстьЗаписиВоВременнойТаблице,ЕстьДанныеДляОбработки,ИмяВременнойТаблицы", Ложь, Ложь,"");
	Результат.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
	Результат.ЕстьЗаписиВоВременнойТаблице = РезультатЗапроса[0].Выгрузить()[0].Количество <> 0;
	
	Если Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Результат.ЕстьДанныеДляОбработки = Истина;
	Иначе
		Результат.ЕстьДанныеДляОбработки = ЕстьДанныеДляОбработки(Очередь, ПолноеИмяОбъекта);
	КонецЕсли;	
		
	Возврат Результат;
	
КонецФункции

// Возвращает значения измерений независимого регистра сведений для обработки.
// Данные берутся из зарегистрированных в очереди, учитываются заблокированные более приоритетными очередями данные.
//
// Параметры:
//  Очередь           - Число - очередь, к которой отнесен обработчик и в которой зарегистрированы данные, которые он будет
//                              обрабатывать.
//  ПолноеИмяОбъекта  - Строка - имя объекта, который нужно обработать. Например, РегистрСведений.ШтрихкодыНоменклатуры.
//  ДополнительныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
// 
// Возвращаемое значение:
//  * ВыборкаИзРезультатаЗапроса - выборка значений измерений, которые нужно обработать, имена полей соответствует именам
//                                 измерений регистра. Если по измерению не регистрировалась необходимость обработки,
//                                 то в выборке по этому измерению будет пустое значение.
//  * ТаблицаЗначений - данные, которые нужно обработать, имена колонок соответствуют именам измерений регистра.
//
Функция ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки(Очередь, ПолноеИмяОбъекта, ДополнительныеПараметры = Неопределено) Экспорт
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	ИмяОбъекта = СтрРазделить(ПолноеИмяОбъекта,".",Ложь)[1];
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	ПараметрыПостроения = ПараметрыПостроенияВыборки(ДополнительныеПараметры, "ТаблицаИзменений");
	ЗаданыПоляУпорядочивания = ЗаданыПоляУпорядочивания(ДополнительныеПараметры);
	НеобходимоУпорядочивание = ЗаданыПоляУпорядочивания Или ПараметрыПостроения.ПостраничнаяВыборка;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 10000
	|	&ВыбираемыеПоля
	|ИЗ
	|	#ТаблицаОбъектаИзменения КАК ТаблицаИзменений
	|	ЛЕВОЕ СОЕДИНЕНИЕ #ВТЗаблокированоИзмерения КАК ВТЗаблокированоИзмерения
	|	ПО &ТекстУсловияСоединенияИзмерений
	|   #ТекстЗапросаСоединениеСДопИсточниками
	|ГДЕ
	|	ТаблицаИзменений.Узел = &ТекущаяОчередь
	|	И &ТекстУсловияПоОтборуНезаблокированных
	|	И &УсловиеПоДопИсточникамСсылкам
	|	И &УсловиеПоСтраницам";
	
	Если НеобходимоУпорядочивание Тогда
		УстановитьПоляУпорядочиванияНезависимогоРегистраСведений(ПараметрыПостроения);
		ТекстЗапроса = ТекстЗапроса + "
			|УПОРЯДОЧИТЬ ПО
			|	&ПорядокВыборки
			|";
	КонецЕсли;
	
	ТекстУсловияСоединенияИзмерений = "ИСТИНА";
	ТекстУсловияПоОтборуНезаблокированных = "ИСТИНА";
	ПервоеИзмерение = Истина;
	Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
		
		Если Не Измерение.ОсновнойОтбор Тогда
			Продолжить;
		КонецЕсли;
		
		УстановитьИзмерение(ПараметрыПостроения, Измерение.Имя);
		ТекстУсловияСоединенияИзмерений = ТекстУсловияСоединенияИзмерений + "
		|	И (ТаблицаИзменений." + Измерение.Имя + " = ВТЗаблокированоИзмерения." + Измерение.Имя + "
		|		ИЛИ ТаблицаИзменений." + Измерение.Имя + " = &ПустоеЗначениеИзмерения"+ Измерение.Имя + "
		|		ИЛИ ВТЗаблокированоИзмерения." + Измерение.Имя + " = &ПустоеЗначениеИзмерения"+ Измерение.Имя + ")";
		
		Запрос.УстановитьПараметр("ПустоеЗначениеИзмерения"+ Измерение.Имя, Измерение.Тип.ПривестиЗначение()); 
		Если ПервоеИзмерение Тогда
			ТекстУсловияПоОтборуНезаблокированных =  "ВТЗаблокированоИзмерения." + Измерение.Имя + " ЕСТЬ NULL ";
			ПервоеИзмерение = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ПризнакНепериодический = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический;
	Если МетаданныеОбъекта.ПериодичностьРегистраСведений <> ПризнакНепериодический
		И МетаданныеОбъекта.ОсновнойОтборПоПериоду Тогда
		УстановитьПериод(ПараметрыПостроения);
	КонецЕсли;
	
	УстановитьРесурсы(ПараметрыПостроения, МетаданныеОбъекта.Ресурсы);
	УстановитьРеквизиты(ПараметрыПостроения, МетаданныеОбъекта.Реквизиты);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияСоединенияИзмерений", ТекстУсловияСоединенияИзмерений);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияПоОтборуНезаблокированных", ТекстУсловияПоОтборуНезаблокированных);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОбъектаИзменения", ПолноеИмяОбъекта + ".Изменения");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТЗаблокированоИзмерения","ВТЗаблокировано" + ИмяОбъекта);
	УстановитьРазмерВыборки(ТекстЗапроса, ДополнительныеПараметры);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	ДобавитьПроверкуБлокировкиДополнительныхИсточниковДляНезависимогоРегистра(Очередь,
																				ТекстЗапроса,
																				ПолноеИмяОбъекта,
																				МенеджерВременныхТаблиц,
																				ДополнительныеПараметры);	
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяОчередь", ОчередьСсылкой(Очередь));
	
	УстановитьПоляПоСтраницам(Запрос, ПараметрыПостроения);
	Если НеобходимоУпорядочивание Тогда
		УстановитьПорядокПоСтраницам(Запрос, ПараметрыПостроения);
	КонецЕсли;
	
	Возврат ВыбратьДанныеДляОбработки(Запрос, ПараметрыПостроения);
	
КонецФункции

// Создает временную таблицу значения измерений независимого регистра сведений для обработки.
//  Имя таблицы: ВТДляОбработки<ИмяОбъекта>, например ВТДляОбработкиШтрихкодыНоменклатуры.
//  Колонки таблицы соответствуют измерениям регистра. Если по измерению не регистрировалась 
//	необходимость обработки, то в выборке по этому измерению будет пустое значение.
//
// Параметры:
//  Очередь					 - Число					 - очередь обработки, в которой выполняется текущий обработчик.
//  ПолноеИмяОбъекта		 - Строка					 - полное имя объекта, для которого выполняется проверка, например Справочник.Номенклатура.
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц	 - менеджер, в котором будет создана временная таблица.
//  ДополнительныеПараметры	 - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
// 
// Возвращаемое значение:
//  Структура - результат формирования временной таблицы:
//  * ЕстьЗаписиВоВременнойТаблице - Булево - в создаваемой таблице есть хотя бы одна запись. Записей может не быть по
//                                            двум причинам:
//                                              все обработано или все, что нужно обработать, еще заблокировано
//                                              обработчиками с меньшей очередью.
//  * ЕстьДанныеДляОбработки - Булево - в очереди есть данные для обработки, т.е. еще не все обработано.
//  * ИмяВременнойТаблицы - Строка - имя созданной временной таблицы.
//
Функция СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	ИмяОбъекта = СтрРазделить(ПолноеИмяОбъекта,".",Ложь)[1];
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
		                      
	Запрос = Новый Запрос;
	Если ДополнительныеПараметры.ВыбиратьПорциями Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&ТекстВыборкиИзмерений
		|ПОМЕСТИТЬ ВТДляОбработкиИзмеренияПолная
		|ИЗ
		|	#ТаблицаОбъектаИзменения КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ #ВТЗаблокированоИзмерения КАК ВТЗаблокированоИзмерения
		|		ПО (&ТекстУсловияСоединенияИзмерений)
		|   #ТекстЗапросаСоединениеСДопИсточниками
		|ГДЕ
		|	ТаблицаИзменений.Узел = &ТекущаяОчередь
		|	И &ТекстУсловияПоОтборуНезаблокированных
		|	И &УсловиеПоДопИсточникамСсылкам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 10000
		|	&ТекстВыборкиИзмерений
		|ПОМЕСТИТЬ #ВТДляОбработкиИзмерения
		|ИЗ
		|	ВТДляОбработкиИзмеренияПолная КАК ТаблицаИзменений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ #ВТЗаблокированоИзмерения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДляОбработкиИзмеренияПолная";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&ТекстВыборкиИзмерений
		|ПОМЕСТИТЬ #ВТДляОбработкиИзмерения
		|ИЗ
		|	#ТаблицаОбъектаИзменения КАК ТаблицаИзменений
		|	ЛЕВОЕ СОЕДИНЕНИЕ #ВТЗаблокированоИзмерения КАК ВТЗаблокированоИзмерения
		|	ПО &ТекстУсловияСоединенияИзмерений
		|   #ТекстЗапросаСоединениеСДопИсточниками
		|ГДЕ
		|	ТаблицаИзменений.Узел = &ТекущаяОчередь
		|	И &ТекстУсловияПоОтборуНезаблокированных
		|	И &УсловиеПоДопИсточникамСсылкам
		|;
		|УНИЧТОЖИТЬ
		|	#ВТЗаблокированоИзмерения";
	КонецЕсли;
	ТекстВыборкиИзмерений = "";
	ТекстУсловияСоединенияИзмерений = "ИСТИНА";
	
	ПервоеИзмерение = Истина;
	РегистрПериодический = 
		(МетаданныеОбъекта.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический)
		И МетаданныеОбъекта.ОсновнойОтборПоПериоду;
	Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
		Если Не Измерение.ОсновнойОтбор Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
		|	ТаблицаИзменений." + Измерение.Имя + " КАК " + Измерение.Имя + ",";
		
		ТекстУсловияСоединенияИзмерений = ТекстУсловияСоединенияИзмерений + "
		|	И ТаблицаИзменений." + Измерение.Имя + " = ВТЗаблокированоИзмерения." + Измерение.Имя + "
		|		ИЛИ ТаблицаИзменений." + Измерение.Имя + " = &ПустоеЗначениеИзмерения"+ Измерение.Имя + "
		|		ИЛИ ВТЗаблокированоИзмерения." + Измерение.Имя + " = &ПустоеЗначениеИзмерения"+ Измерение.Имя;
		Запрос.УстановитьПараметр("ПустоеЗначениеИзмерения"+ Измерение.Имя, Измерение.Тип.ПривестиЗначение()); 
		
		Если ПервоеИзмерение Тогда
			ТекстУсловияПоОтборуНезаблокированных =  "ВТЗаблокированоИзмерения." + Измерение.Имя + " ЕСТЬ NULL ";
			
			Если РегистрПериодический Тогда
				ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
					|	ТаблицаИзменений.Период КАК Период,";
			КонецЕсли;
			
			ПервоеИзмерение = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ТекстВыборкиИзмерений = Лев(ТекстВыборкиИзмерений, СтрДлина(ТекстВыборкиИзмерений) - 1);
	
	Если ПустаяСтрока(ДополнительныеПараметры.ИмяВременнойТаблицы) Тогда
		ИмяВременнойТаблицы = "ВТДляОбработки" + ИмяОбъекта;
	Иначе
		ИмяВременнойТаблицы = ДополнительныеПараметры.ИмяВременнойТаблицы;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкиИзмерений", ТекстВыборкиИзмерений);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияСоединенияИзмерений", ТекстУсловияСоединенияИзмерений);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияПоОтборуНезаблокированных", ТекстУсловияПоОтборуНезаблокированных);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"#ТаблицаОбъектаИзменения", ПолноеИмяОбъекта + ".Изменения");	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТЗаблокированоИзмерения","ВТЗаблокировано" + ИмяОбъекта);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ВТДляОбработкиИзмерения",ИмяВременнойТаблицы);
	
	СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	ДобавитьПроверкуБлокировкиДополнительныхИсточниковДляНезависимогоРегистра(Очередь,
																				ТекстЗапроса,
																				ПолноеИмяОбъекта,
																				МенеджерВременныхТаблиц,
																				ДополнительныеПараметры);	
	
	ТекущаяОчередь = ОчередьСсылкой(Очередь);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущаяОчередь", ТекущаяОчередь);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура("ЕстьЗаписиВоВременнойТаблице,ЕстьДанныеДляОбработки,ИмяВременнойТаблицы", Ложь, Ложь,"");
	Результат.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
	Результат.ЕстьЗаписиВоВременнойТаблице = РезультатЗапроса[0].Выгрузить()[0].Количество <> 0;
	
	Если Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Результат.ЕстьДанныеДляОбработки = Истина;
	Иначе
		Результат.ЕстьДанныеДляОбработки = ЕстьДанныеДляОбработки(Очередь, ПолноеИмяОбъекта);
	КонецЕсли;	
		
	Возврат Результат;
	
КонецФункции

// Проверяет, есть ли еще необработанные данные.
//
// Параметры:
//  Очередь    - Число        - очередь, к которой отнесен обработчик и в которой зарегистрированы данные,
//                              которые он будет обрабатывать.
//             - Неопределено - проверяется, завершена ли обработка в целом;
//             - Массив       - проверяется, есть данные для обработки в списке очередей.
//  ПолноеИмяМетаданныеОбъекта- Строка, ОбъектМетаданных - полное имя обрабатываемого объекта или 
//                              его метаданные. Например, "Документ.ПриходныйОрдерНаТовары"
//                            - Массив - массив полных имен объектов или объектов метаданных,
//                              в массиве не должно быть независимых регистров сведений.
//  Отбор - ЛюбаяСсылка, Структура, Неопределено, Массив - отбор данных для проверки.
//                              Если передано Неопределено - проверяется по всему типу объекта.
//                              Если объект - регистр, подчиненный регистратору, то в отборе - ссылка
//                                 на регистратор или массив ссылок.
//                              Если объект ссылочного типа, то в отборе - или ссылка, или массив ссылок.
//                              Если объект - независимый регистр сведений, то в отборе - структура со значениями измерений.
//                              Ключ структуры - имя измерения, значение - значение отбора (можно передать массив значений).
//
// Возвращаемое значение:
//  Булево - Истина, если еще не все данные обработаны.
//
Функция ЕстьДанныеДляОбработки(Очередь, ПолноеИмяМетаданныеОбъекта, Отбор = Неопределено) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		ЭтоПодчиненныйУзелРИБ = ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ();
		Если Не ЭтоПодчиненныйУзелРИБ Тогда
			Возврат Ложь;
		ИначеЕсли ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеВГлавномУзлеЗавершеноУспешно") Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ПолноеИмяМетаданныеОбъекта) = Тип("Строка") Тогда
		ПолныеИменаОбрабатываемыхОбъектов = СтрРазделить(ПолноеИмяМетаданныеОбъекта, ",");
	ИначеЕсли ТипЗнч(ПолноеИмяМетаданныеОбъекта) = Тип("Массив") Тогда
		ПолныеИменаОбрабатываемыхОбъектов = ПолноеИмяМетаданныеОбъекта;
	ИначеЕсли ТипЗнч(ПолноеИмяМетаданныеОбъекта) = Тип("ОбъектМетаданных") Тогда
		ПолныеИменаОбрабатываемыхОбъектов = Новый Массив;
		ПолныеИменаОбрабатываемыхОбъектов.Добавить(ПолноеИмяМетаданныеОбъекта.ПолноеИмя());
	Иначе
		ТекстИсключения = НСтр("ru = 'Передан неправильный тип параметра ""ПолноеИмяМетаданныеОбъекта"" в функцию ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	
	ТекстыЗапроса = Новый Массив;
	ОтборУстановлен = Ложь;
	
	Для каждого ОбрабатываемыйТип Из ПолныеИменаОбрабатываемыхОбъектов Цикл 
		
		Если ТипЗнч(ОбрабатываемыйТип) = Тип("ОбъектМетаданных") Тогда
			МетаданныеОбъекта = ОбрабатываемыйТип;
			ПолноеИмяОбъекта  = ОбрабатываемыйТип.ПолноеИмя();
		Иначе
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ОбрабатываемыйТип);
			ПолноеИмяОбъекта  = ОбрабатываемыйТип;
		КонецЕсли;
		
		ИмяОбъекта = СтрРазделить(ПолноеИмяОбъекта,".",Ложь)[1];
		
		УсловиеОтбораДанных = "ИСТИНА";
		
		Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта) Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТаблицаИзменений.Ссылка КАК Ссылка
			|ИЗ
			|	#ТаблицаИзменений КАК ТаблицаИзменений
			|	ЛЕВОЕ СОЕДИНЕНИЕ #ТаблицаОбъекта КАК #ИмяОбъекта
			|		ПО #ИмяОбъекта.Ссылка = ТаблицаИзменений.Ссылка
			|ГДЕ
			|	&УсловиеОтбораУзла
			|	И &УсловиеОтбораДанных
			|	И НЕ #ИмяОбъекта.Ссылка ЕСТЬ NULL";
			
			Запрос.УстановитьПараметр("Ссылка", Отбор);
			
			Если Отбор <> Неопределено Тогда
				УсловиеОтбораДанных = "ТаблицаИзменений.Ссылка В (&Отбор)";
			КонецЕсли;
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
			И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			
			Если ПолныеИменаОбрабатываемыхОбъектов.Количество() > 1 Тогда
				ТекстИсключения = НСтр("ru = 'В массиве имен в параметре ""ПолноеИмяМетаданныеОбъекта"" в функцию ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки передан независимый регистр сведений.'");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;	
			
			ОтборУстановлен = Истина;
			
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	&ТекстВыборкиИзмерений
			|ИЗ
			|	#ТаблицаИзменений КАК ТаблицаИзменений
			|ГДЕ
			|	&УсловиеОтбораУзла
			|	И &УсловиеОтбораДанных";
			
			ТекстВыборкиИзмерений = "";
			Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
				Если Не Измерение.ОсновнойОтбор Тогда
					Продолжить;
				КонецЕсли;
				
				ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
				|	ТаблицаИзменений." + Измерение.Имя + " КАК " + Измерение.Имя + ",";
				
				Если Отбор <> Неопределено Тогда
					УсловиеОтбораДанных = УсловиеОтбораДанных + "
					|	И (ТаблицаИзменений." + Измерение.Имя + " В (&ЗначениеОтбора" + Измерение.Имя + ")
					|		ИЛИ ТаблицаИзменений." + Измерение.Имя + " = &ПустоеЗначение" + Измерение.Имя + ")";
					
					Если Отбор.Свойство(Измерение.Имя) Тогда
						Запрос.УстановитьПараметр("ЗначениеОтбора" + Измерение.Имя, Отбор[Измерение.Имя]);
					Иначе
						Запрос.УстановитьПараметр("ЗначениеОтбора" + Измерение.Имя, Измерение.Тип.ПривестиЗначение());
					КонецЕсли;
					
					Запрос.УстановитьПараметр("ПустоеЗначение" + Измерение.Имя, Измерение.Тип.ПривестиЗначение());
				КонецЕсли;
			КонецЦикла;
			
			Если ПустаяСтрока(ТекстВыборкиИзмерений) Тогда
				ТекстВыборкиИзмерений = "*";
			Иначе
				ТекстВыборкиИзмерений = Лев(ТекстВыборкиИзмерений, СтрДлина(ТекстВыборкиИзмерений) - 1);
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкиИзмерений", ТекстВыборкиИзмерений);
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТаблицаИзменений.Регистратор КАК Ссылка
			|ИЗ
			|	#ТаблицаИзменений КАК ТаблицаИзменений
			|ГДЕ
			|	&УсловиеОтбораУзла
			|	И &УсловиеОтбораДанных";
			
			Если Отбор <> Неопределено Тогда
				УсловиеОтбораДанных = "ТаблицаИзменений.Регистратор В (&Отбор)";
			КонецЕсли;
			
		Иначе
			ТекстИсключения = НСтр("ru = 'Для типа метаданных ""%МетаданныеОбъекта%"" не поддерживается проверка в функции ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%МетаданныеОбъекта%", Строка(МетаданныеОбъекта)); 
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаИзменений", ПолноеИмяОбъекта + ".Изменения");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОбъекта", ПолноеИмяОбъекта);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяОбъекта", ИмяОбъекта);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораДанных", УсловиеОтбораДанных);
		
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	Соединитель = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|";

	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, Соединитель);
	
	Если Очередь = Неопределено Тогда
		УсловиеОтбораУзла = "	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы ";
	Иначе
		УсловиеОтбораУзла = "	ТаблицаИзменений.Узел В (&Узлы) ";
		Если ТипЗнч(Очередь) = Тип("Массив") Тогда
			Запрос.УстановитьПараметр("Узлы", Очередь);
		Иначе
			Запрос.УстановитьПараметр("Узлы", ОчередьСсылкой(Очередь));
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораУзла", УсловиеОтбораУзла);
	
	Если Не ОтборУстановлен Тогда
		Запрос.УстановитьПараметр("Отбор", Отбор);
	КонецЕсли;
		
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Не Запрос.Выполнить().Пустой(); 
	
КонецФункции

// Проверяет, что все данные обработаны.
//
// Параметры:
//  Очередь    - Число        - очередь, к которой отнесен обработчик и в которой зарегистрированы данные,
//                              которые он будет обрабатывать.
//             - Неопределено - проверяется, завершена ли обработка в целом;
//             - Массив       - проверяется, есть ли данные для обработки в списке очередей.
//  ПолноеИмяМетаданныеОбъекта- Строка, ОбъектМетаданных - полное имя обрабатываемого объекта или 
//                              его метаданные. Например, "Документ.ПриходныйОрдерНаТовары"
//                            - Массив - массив полных имен объектов или объектов метаданных,
//                              в массиве не должно быть независимых регистров сведений.
//  Отбор - ЛюбаяСсылка, Структура, Неопределено, Массив - отбор данных для проверки.
//                              Если передано Неопределено - проверяется по всему типу объекта.
//                              Если объект - регистр, подчиненный регистратору, то в отборе - ссылка
//                                 на регистратор или массив ссылок.
//                              Если объект ссылочного типа, то в отборе - или ссылка, или массив ссылок.
//                              Если объект - независимый регистр сведений, то в отборе - структура со значениями измерений.
//                              Ключ структуры - имя измерения, значение - значение отбора (можно передать массив значений).
// 
// Возвращаемое значение:
//  Булево - Истина, если все данные обработаны.
//
Функция ОбработкаДанныхЗавершена(Очередь, ПолноеИмяМетаданныеОбъекта, Отбор = Неопределено) Экспорт
	
	Возврат Не ЕстьДанныеДляОбработки(Очередь, ПолноеИмяМетаданныеОбъекта, Отбор);
	
КонецФункции

// Проверяет, есть ли заблокированные меньшими очередями обработки данные.
//
// Параметры:
//  Очередь - Число, Неопределено - очередь, к которой отнесен обработчик и в которой
//                                  зарегистрированы данные, которые он будет обрабатывать.
//  ПолноеИмяМетаданныеОбъекта - Строка, ОбъектМетаданных - полное имя обрабатываемого объекта или
//                                        его метаданные. Например, "Документ.ПриходныйОрдерНаТовары"
//                             - Массив - массив полных имен объектов или объектов метаданных,
//                                        в массиве не должно быть независимых регистров сведений.
// 
// Возвращаемое значение:
//  Булево - Истина, если данный объект заблокирован для обработки меньшими очередями.
//
Функция ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Очередь, ПолноеИмяМетаданныеОбъекта) Экспорт
	
	Возврат ЕстьДанныеДляОбработки(УзлыМеньшейОчереди(Очередь), ПолноеИмяМетаданныеОбъекта);
	
КонецФункции

// Проверяет, завершилась ли обработка данных обработчиками, отнесенными на более раннюю очередь.
//
// Параметры:
//  Очередь    - Число        - очередь, к которой отнесен обработчик и в которой зарегистрированы данные,
//                              которые он будет обрабатывать.
//             - Неопределено - проверяется, завершена ли обработка в целом;
//             - Массив       - проверяется, есть ли данные для обработки в списке очередей.
//  Данные     - ЛюбаяСсылка, НаборЗаписей, Объект, ДанныеФормыСтруктура - ссылка на объект, сам объект
//                              или набор записей, который необходимо проверить.
//                              Если ДополнительныеПараметры.ЭтоДвижения = Истина, то Данные - это регистратор
//                              указанного в ДополнительныеПараметры регистра.
//  ДополнительныеПараметры   - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
//  МетаданныеИОтбор          - Структура - см. ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным.
// 
// Возвращаемое значение:
//  Булево - Истина, если переданный объект обновлен на новую версию и его можно изменять.
//
Функция МожноЧитатьИМенять(Очередь, Данные, ДополнительныеПараметры = Неопределено, МетаданныеИОтбор = Неопределено) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		Если Не ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ() Тогда
			Возврат Истина;
		ИначеЕсли ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеВГлавномУзлеЗавершеноУспешно") Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если МетаданныеИОтбор = Неопределено Тогда
		МетаданныеИОтбор = МетаданныеИОтборПоДанным(Данные, ДополнительныеПараметры);
	КонецЕсли;
	
	Если МетаданныеИОтбор.ЭтоНовый Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Очередь = Неопределено Тогда
		Возврат Не ЕстьДанныеДляОбработки(Неопределено, МетаданныеИОтбор.Метаданные, МетаданныеИОтбор.Отбор);
	Иначе
		Возврат Не ЕстьДанныеДляОбработки(УзлыМеньшейОчереди(Очередь), МетаданныеИОтбор.Метаданные, МетаданныеИОтбор.Отбор);
	КонецЕсли;
	
КонецФункции

// Создает временную таблицу заблокированных данных.
// Имя таблицы: ВТЗаблокированы<ИмяОбъекта>, например ВТЗаблокированоНоменклатура.
//  Колонки таблицы:
//      для объектов ссылочного типа
//          * Ссылка;
//      для регистров, подчиненных регистратору
//          * Регистратор;
//      для регистров с непосредственной записью
//          * колонки, соответствующие измерениям регистра.
//
// Параметры:
//  Очередь                 - Число, Неопределено - очередь обработки, в которой выполняется текущий обработчик.
//                             Если передано Неопределено, то проверяется во всех очередях.
//  ПолноеИмяОбъекта        - Строка - полное имя объекта, для которого выполняется проверка,
//                             например Справочник.Номенклатура.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором будет создана временная таблица.
//  ДополнительныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки,
//                             параметр ВыбиратьПорциями игнорируется, заблокированные данные всегда помещаются в
//                             таблицу целиком.
//
// Возвращаемое значение:
//  Структура - структура со свойствами:
//     * ЕстьЗаписиВоВременнойТаблице - Булево - в создаваемой таблице есть хотя бы одна запись.
//     * ИмяВременнойТаблицы          - Строка - имя созданной временной таблицы.
//
Функция СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
	
	Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта) Тогда
		Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ПустоеЗначение КАК Ссылка
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ГДЕ
			|	ЛОЖЬ";
			                                           
			Запрос.УстановитьПараметр("ПустоеЗначение", МетаданныеОбъекта.СтандартныеРеквизиты.Ссылка.Тип.ПривестиЗначение()); 
		Иначе	
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаИзменений.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ИЗ
			|	#ТаблицаИзменений КАК ТаблицаИзменений
			|ГДЕ
			|	&УсловиеОтбораУзла
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка";
		КонецЕсли;
	ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
		И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
		
		Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ТекстВыборкиИзмерений
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ГДЕ
			|	ЛОЖЬ";
			ТекстВыборкиИзмерений = "";
			Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
				Если Не Измерение.ОсновнойОтбор Тогда
					Продолжить;
				КонецЕсли;
				
				ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
				|	&ПустоеЗначениеИзмерения"+ Измерение.Имя + " КАК " + Измерение.Имя + ",";
				Запрос.УстановитьПараметр("ПустоеЗначениеИзмерения"+ Измерение.Имя, Измерение.Тип.ПривестиЗначение()); 
			КонецЦикла;
			
		Иначе
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ТекстВыборкиИзмерений
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ИЗ
			|	#ТаблицаИзменений КАК ТаблицаИзменений
			|ГДЕ
			|	&УсловиеОтбораУзла ";
			ТекстВыборкиИзмерений = "";
			Для Каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
				Если Не Измерение.ОсновнойОтбор Тогда
					Продолжить;
				КонецЕсли;
				
				ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
				|	ТаблицаИзменений." + Измерение.Имя + " КАК " + Измерение.Имя + ",";
			КонецЦикла;
		КонецЕсли;
		
		ПризнакНепериодический = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический;
		Если МетаданныеОбъекта.ПериодичностьРегистраСведений <> ПризнакНепериодический
			И МетаданныеОбъекта.ОсновнойОтборПоПериоду Тогда
			ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
				|	ТаблицаИзменений.Период КАК Период,";
		КонецЕсли;
		
		Если ПустаяСтрока(ТекстВыборкиИзмерений) Тогда
			ТекстВыборкиИзмерений = "*";
		Иначе
			ТекстВыборкиИзмерений = Лев(ТекстВыборкиИзмерений, СтрДлина(ТекстВыборкиИзмерений) - 1);
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкиИзмерений", ТекстВыборкиИзмерений);
		
	ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
		
		Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	&ПустоеЗначение КАК Регистратор
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ГДЕ
			|	ЛОЖЬ";
			
			Запрос.УстановитьПараметр("ПустоеЗначение", МетаданныеОбъекта.СтандартныеРеквизиты.Регистратор.Тип.ПривестиЗначение()); 
			
		Иначе
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаИзменений.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ИЗ
			|	#ТаблицаИзменений КАК ТаблицаИзменений
			|ГДЕ
			|	&УсловиеОтбораУзла
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Регистратор";
		КонецЕсли;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'Для этого типа метаданных не поддерживается проверка в функции ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		
		Если Очередь = Неопределено Тогда
			УсловиеОтбораУзла = "	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы ";
		Иначе
			УсловиеОтбораУзла = "	ТаблицаИзменений.Узел В (&Узлы) ";
			Запрос.УстановитьПараметр("Узлы", УзлыМеньшейОчереди(Очередь));
		КонецЕсли;	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораУзла", УсловиеОтбораУзла);
	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаИзменений", ПолноеИмяОбъекта + ".Изменения");
		
	КонецЕсли;
	
	ИмяОбъекта = СтрРазделить(ПолноеИмяОбъекта, ".")[1];
	
	Если ПустаяСтрока(ДополнительныеПараметры.ИмяВременнойТаблицы) Тогда
		ИмяВременнойТаблицы =  "ВТЗаблокировано"+ИмяОбъекта;
	Иначе
		ИмяВременнойТаблицы = ДополнительныеПараметры.ИмяВременнойТаблицы;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура("ЕстьЗаписиВоВременнойТаблице,ИмяВременнойТаблицы", Ложь, "");
	Результат.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
	Результат.ЕстьЗаписиВоВременнойТаблице = РезультатЗапроса.Выгрузить()[0].Количество <> 0;
			
	Возврат Результат;
	
КонецФункции

// Создает временную таблицу заблокированных ссылок.
//  Имя таблицы: ВТЗаблокировано.
//  Колонки таблицы:
//    * Ссылка.
//
// Параметры:
//  Очередь                 - Число, Неопределено - очередь обработки, в которой выполняется
//                             текущий обработчик. Если передано Неопределено, то проверяется во всех очередях.
//  ПолныеИменаОбъектов     - Строка, Массив - полные имена объектов, для которых выполняется проверка,
//                             например Справочник.Номенклатура.
//                             Могут быть переданы объекты ссылочного типа, или регистры, подчиненные регистратору.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором будет создана временная таблица.
//  ДополнительныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки,
//                             параметр ВыбиратьПорциями игнорируется, заблокированные данные
//                             всегда помещаются в таблицу целиком.
//
// Возвращаемое значение:
//  Структура - структура со свойствами:
//    * ЕстьЗаписиВоВременнойТаблице - Булево - в создаваемой таблице есть хотя бы одна запись.
//    * ИмяВременнойТаблицы          - Строка - имя созданной временной таблицы.
//
Функция СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(Очередь, ПолныеИменаОбъектов, МенеджерВременныхТаблиц, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НЕОПРЕДЕЛЕНО КАК Ссылка
		|ПОМЕСТИТЬ #ИмяВременнойТаблицы
		|ГДЕ
		|	ЛОЖЬ";
	Иначе	
		Если ТипЗнч(ПолныеИменаОбъектов) = Тип("Строка") Тогда
			ПолныеИменаОбъектовМассив = СтрРазделить(ПолныеИменаОбъектов,",",Ложь);
		ИначеЕсли ТипЗнч(ПолныеИменаОбъектов) = Тип("Массив") Тогда 
			ПолныеИменаОбъектовМассив = ПолныеИменаОбъектов;
		Иначе
			ПолныеИменаОбъектовМассив = Новый Массив;
			ПолныеИменаОбъектовМассив.Добавить(ПолныеИменаОбъектов);
		КонецЕсли;
		
		МассивТекстовЗапросов = Новый Массив;
		
		ЕстьРегистры = Ложь;
		
		Для Каждого ОбрабатываемыйТип Из ПолныеИменаОбъектовМассив Цикл
			
			Если ТипЗнч(ОбрабатываемыйТип) = Тип("ОбъектМетаданных") Тогда
				МетаданныеОбъекта = ОбрабатываемыйТип;
				ПолноеИмяОбъекта  = ОбрабатываемыйТип.ПолноеИмя();
			Иначе
				МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ОбрабатываемыйТип);
				ПолноеИмяОбъекта  = ОбрабатываемыйТип;
			КонецЕсли;
			
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
			
			Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта) Тогда
				Если МассивТекстовЗапросов.Количество() = 0 Тогда
					ТекстЗапроса =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ТаблицаИзменений.Ссылка КАК Ссылка
					|//ПервыйЗапрос
					|ИЗ
					|	#ТаблицаИзменений КАК ТаблицаИзменений
					|ГДЕ
					|	&УсловиеОтбораУзла";
				Иначе
					ТекстЗапроса =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ТаблицаИзменений.Ссылка КАК Ссылка
					|ИЗ
					|	#ТаблицаИзменений КАК ТаблицаИзменений
					|ГДЕ
					|	&УсловиеОтбораУзла";	
				КонецЕсли;
			ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
				Если МассивТекстовЗапросов.Количество() = 0 Тогда
					ТекстЗапроса =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ТаблицаИзменений.Регистратор КАК Ссылка
					|//ПервыйЗапрос
					|ИЗ
					|	#ТаблицаИзменений КАК ТаблицаИзменений
					|ГДЕ
					|	&УсловиеОтбораУзла";
				Иначе
					ТекстЗапроса =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ТаблицаИзменений.Регистратор КАК Ссылка
					|ИЗ
					|	#ТаблицаИзменений КАК ТаблицаИзменений
					|ГДЕ
					|	&УсловиеОтбораУзла";	
				КонецЕсли;
				
				ЕстьРегистры = Истина;
				
			Иначе
				ТекстИсключения = НСтр("ru = 'Для типа метаданных ""%МетаданныеОбъекта%"" не поддерживается проверка в функции ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%МетаданныеОбъекта%", Строка(МетаданныеОбъекта)); 
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаИзменений", ПолноеИмяОбъекта + ".Изменения");
			
			МассивТекстовЗапросов.Добавить(ТекстЗапроса);
		КонецЦикла;
		
		Соединитель = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
		
		ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, Соединитель); 
		
		Если ЕстьРегистры
			И МассивТекстовЗапросов.Количество() > 1 Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВложенныйЗапрос.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ #ИмяВременнойТаблицы
			|ИЗ
			|	(" + ТекстЗапроса + ") КАК ВложенныйЗапрос
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПервыйЗапрос", "");
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПервыйЗапрос", "ПОМЕСТИТЬ #ИмяВременнойТаблицы");
		КонецЕсли;
		
		Если Очередь = Неопределено Тогда
			УсловиеОтбораУзла = "	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы ";
		Иначе
			УсловиеОтбораУзла = "	ТаблицаИзменений.Узел В (&Узлы) ";
			Запрос.УстановитьПараметр("Узлы", УзлыМеньшейОчереди(Очередь));
		КонецЕсли;	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораУзла", УсловиеОтбораУзла);
	КонецЕсли;	
	
	Если ПустаяСтрока(ДополнительныеПараметры.ИмяВременнойТаблицы) Тогда
		ИмяВременнойТаблицы =  "ВТЗаблокировано";
	Иначе
		ИмяВременнойТаблицы = ДополнительныеПараметры.ИмяВременнойТаблицы;
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура("ЕстьЗаписиВоВременнойТаблице,ИмяВременнойТаблицы", Ложь, "");
	Результат.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
	Результат.ЕстьЗаписиВоВременнойТаблице = РезультатЗапроса.Выгрузить()[0].Количество <> 0;
			
	Возврат Результат;
	
КонецФункции

// Создает временную таблицу измерений регистров, подчиненных регистраторам, по которым еще есть не обработанные регистраторы.
//  Алгоритм вычисления:
//  - берутся заблокированные регистраторы;
//  - по ним соединяется с основной таблицей регистра;
//  - из основной таблицы получаются значения изменений;
//  - делается группировка.
//  Имя таблицы: ВТЗаблокированы<ИмяОбъекта>, например ВТЗаблокированоТоварыНаСкладах.
//  Колонки таблицы соответствуют переданным измерениям.
//
// Параметры:
//  Очередь                 - Число, Неопределено - очередь обработки, в которой выполняется
//                             текущий обработчик. Если передано Неопределено,
//                             то проверяется во всех очередях.
//  ПолноеИмяРегистра       - Строка - имя регистра, движения по которому нужно переформировать.
//                             Например, РегистрНакопления.ТоварыНаСкладах
//  Измерения               - Строка, Массив - имена измерений, по которым нужно проверить блокировку,
//                             перечисленные через запятую, или массив имен.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором будет создана временная таблица.
//  ДополнительныеПараметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки,
//                             параметр ВыбиратьПорциями игнорируется, заблокированные данные
//                             всегда помещаются в таблицу целиком.
//
// Возвращаемое значение:
//  Структура - структура со свойствами:
//   * ЕстьЗаписиВоВременнойТаблице - Булево - в создаваемой таблице есть хотя бы одна запись.
//   * ИмяВременнойТаблицы          - Строка - имя созданной временной таблицы.
//
Функция СоздатьВременнуюТаблицуЗначенийЗаблокированныхИзмерений(Очередь, ПолноеИмяРегистра, Измерения, МенеджерВременныхТаблиц, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ТипЗнч(Измерения) = Тип("Строка") Тогда
		ИзмеренияМассив = СтрРазделить(Измерения, ",", Ложь);
	Иначе
		ИзмеренияМассив = Измерения;
	КонецЕсли;
	
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяРегистра);
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&ЗначенияИзмерений
		|ПОМЕСТИТЬ #ИмяВременнойТаблицы
		|ГДЕ
		|	ЛОЖЬ";
		ЗначенияИзмерений = "";
		Для Каждого ИзмерениеСтр Из ИзмеренияМассив Цикл
			
			Измерение = МетаданныеОбъекта.Измерения.Найти(ИзмерениеСтр);
			
			ЗначенияИзмерений = ЗначенияИзмерений + "
			|	&ПустоеЗначениеИзмерения"+ Измерение.Имя + " КАК " + Измерение.Имя + ",";
			Запрос.УстановитьПараметр("ПустоеЗначениеИзмерения"+ Измерение.Имя, Измерение.Тип.ПривестиЗначение()); 
			
		КонецЦикла;
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&ЗначенияИзмерений
		|ПОМЕСТИТЬ #ИмяВременнойТаблицы
		|ИЗ
		|	#ТаблицаИзменений КАК ТаблицаИзменений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаРегистра КАК ТаблицаРегистра
		|		ПО ТаблицаИзменений.Регистратор = ТаблицаРегистра.Регистратор
		|ГДЕ
		|	&УсловиеОтбораУзла";
		
		ЗначенияИзмерений = "";
		Для Каждого Измерение Из ИзмеренияМассив Цикл
			
			ЗначенияИзмерений = ЗначенияИзмерений + "
			|	ТаблицаРегистра." + Измерение + " КАК " + Измерение + ","; 	
			
		КонецЦикла;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаИзменений", ПолноеИмяРегистра + ".Изменения");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаРегистра", ПолноеИмяРегистра);
		
		Если Очередь = Неопределено Тогда
			УсловиеОтбораУзла = "	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы ";
		Иначе
			УсловиеОтбораУзла = "	ТаблицаИзменений.Узел В (&Узлы) ";
			Запрос.УстановитьПараметр("Узлы", УзлыМеньшейОчереди(Очередь));
		КонецЕсли;	
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораУзла", УсловиеОтбораУзла);
		
		
	КонецЕсли;
	
	ИмяОбъекта = СтрРазделить(ПолноеИмяРегистра, ".")[1];
	Если ПустаяСтрока(ДополнительныеПараметры.ИмяВременнойТаблицы) Тогда
		ИмяВременнойТаблицы =  "ВТЗаблокировано" + ИмяОбъекта;
	Иначе
		ИмяВременнойТаблицы = ДополнительныеПараметры.ИмяВременнойТаблицы;
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	
	ЗначенияИзмерений = Лев(ЗначенияИзмерений, СтрДлина(ЗначенияИзмерений) - 1);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗначенияИзмерений", ЗначенияИзмерений);
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура("ЕстьЗаписиВоВременнойТаблице,ИмяВременнойТаблицы", Ложь, "");
	Результат.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
	Результат.ЕстьЗаписиВоВременнойТаблице = РезультатЗапроса.Выгрузить()[0].Количество <> 0;
			
	Возврат Результат;
	
КонецФункции

// Функция для проверки объектов при открытии форм и перед записью.
// Может использоваться как функция проверки по умолчанию, если
// достаточно логики - заблокированные объекты зарегистрированы на узлах плана обмена ОбновлениеИнформационнойБазы.
//
// Параметры:
//  МетаданныеИОтбор - Структура - см. ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным.
//
// Возвращаемое значение:
//  Булево - Истина, если объект обновлен и доступен для изменения.
//
Функция ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор) Экспорт
	
	Возврат МожноЧитатьИМенять(Неопределено, МетаданныеИОтбор.Данные,,МетаданныеИОтбор); 
	
КонецФункции

// Выборка данных через ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки().
//
// Возвращаемое значение:
//  Строка - константа "ИзмеренияНезависимогоРегистраСведений".
//
Функция СпособВыборкиИзмеренияНезависимогоРегистраСведений() Экспорт
	
	Возврат "ИзмеренияНезависимогоРегистраСведений";
	
КонецФункции

// Выборка данных через ВыбратьРегистраторыРегистраДляОбработки().
//
// Возвращаемое значение:
//  Строка - константа "РегистраторыРегистра".
//
Функция СпособВыборкиРегистраторыРегистра() Экспорт
	
	Возврат "РегистраторыРегистра";
	
КонецФункции

// Выборка данных через ВыбратьСсылкиДляОбработки().
//
// Возвращаемое значение:
//  Строка - константа "Ссылки".
//
Функция СпособВыборкиСсылки() Экспорт
	
	Возврат "Ссылки";
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры и функции.

// Проверить необходимость обновления информационной базы при смене версии конфигурации.
//
// Возвращаемое значение:
//   Булево - Истина, если требуется обновление.
//
Функция НеобходимоОбновлениеИнформационнойБазы() Экспорт
	
	Возврат ОбновлениеИнформационнойБазыСлужебныйПовтИсп.НеобходимоОбновлениеИнформационнойБазы();
	
КонецФункции

// Возвращает Истина, если в данный момент выполняется обновление ИБ.
//
// Возвращаемое значение:
//   Булево - Истина, если обновление выполняется.
//
Функция ВыполняетсяОбновлениеИнформационнойБазы() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено()
		И Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат НеобходимоОбновлениеИнформационнойБазы();
	КонецЕсли;
	
	Возврат ПараметрыСеанса.ВыполняетсяОбновлениеИБ;
	
КонецФункции

// Возвращает Истина, если вызов функции выполняется из обработчика обновления.
// Для любого вида обработчика обновления - монопольного, оперативного и отложенного.
//
// Параметры:
//  РежимВыполненияОбработчика - Строка - Отложенно, Оперативно, Монопольно или комбинация данных
//                               вариантов через запятую. Если указано, то проверяется только
//                               вызов из обработчиков обновления с данным режимом выполнения.
//
// Возвращаемое значение:
//  Булево - Истина, если вызов функции выполняется из обработчика обновления.
//
Функция ЭтоВызовИзОбработчикаОбновления(РежимВыполненияОбработчика = "") Экспорт
	
	РежимВыполнения = ПараметрыСеанса.ПараметрыОбработчикаОбновления.РежимВыполнения;
	Если Не ЗначениеЗаполнено(РежимВыполнения) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РежимВыполненияОбработчика) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат (СтрНайти(РежимВыполненияОбработчика, РежимВыполнения) > 0);
	
КонецФункции

// Возвращает пустую таблицу обработчиков обновления и первоначального заполнения ИБ.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - таблица с колонками:
//    1) Для всех типов обработчиков обновления:
//
//     * НачальноеЗаполнение - Булево - если Истина, то обработчик должен срабатывать при запуске на "пустой" базе.
//     * Версия              - Строка - например, "2.1.3.39". Номер версии конфигурации, при переходе
//                                      на которую должна быть выполнена процедура-обработчик обновления.
//                                      Если указана пустая строка, то это обработчик только для начального заполнения
//                                      (должно быть указано свойство НачальноеЗаполнение).
//     * Процедура           - Строка - полное имя процедуры-обработчика обновления/начального заполнения. 
//                                      Например, "ОбновлениеИнформационнойБазыУПП.ЗаполнитьНовыйРеквизит"
//                                      Обязательно должна быть экспортной.
//     * РежимВыполнения     - Строка - режим выполнения обработчика обновления. Допустимые значения:
//                                      Монопольно, Отложенно, Оперативно. Если значение не заполнено, обработчик
//                                      считается монопольным.
//
//    2) Для обработчиков обновления в модели сервиса:
//
//     * ОбщиеДанные         - Булево - если Истина, то обработчик должен срабатывать до
//                                      выполнения любых обработчиков, использующих разделенные данные.
//                                      Допустимо указывать только для обработчиков с режимом выполнения Монопольно и Оперативно.
//                                      Если указать значение Истина для обработчика с режимом
//                                      выполнения Отложенно, будет выдано исключение.
//     * УправлениеОбработчиками - Булево - если Истина, то обработчик должен иметь параметр типа Структура, в котором
//                                          есть свойство РазделенныеОбработчики - таблица значений со структурой,
//                                          возвращаемой этой функцией.
//                                      При этом колонка Версия игнорируется. В случае необходимости выполнения
//                                      разделенного обработчика в данную таблицу необходимо добавить строку с
//                                      описанием процедуры обработчика.
//                                      Имеет смысл только для обязательных (Версия = *) обработчиков обновления 
//                                      с установленным флагом ОбщиеДанные.
//
//    3) Для отложенных обработчиков обновления:
//
//     * Комментарий         - Строка - описание действий, выполняемых обработчиком обновления.
//     * Идентификатор       - УникальныйИдентификатор - необходимо заполнять для обработчиков отложенного обновления,
//                                                 для остальных заполнение не требуется. Требуется для идентификации
//                                                 обработчика в случае его переименования.
//     
//     * БлокируемыеОбъекты  - Строка - необходимо заполнять для обработчиков отложенного обновления,
//                                      для остальных заполнение не требуется. Полные имена объектов через запятую, 
//                                      которые следует блокировать от изменения до завершения процедуры обработки данных.
//                                      Если заполнено, то также требуется заполнить и свойство ПроцедураПроверки.
//     * ПроцедураПроверки   - Строка - необходимо заполнять для обработчиков отложенного обновления,
//                                      для остальных заполнение не требуется. Имя функции, которая для переданного объекта 
//                                      определяет, завершена ли для него процедура обработки данных. 
//                                      Если переданный объект обработан, то следует вернуть значение Истина. 
//                                      Вызывается из процедуры ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан.
//                                      Параметры, передаваемые в функцию:
//                                         Параметры - Структура - см. ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным.
//
//    4) Для обработчиков обновления в библиотеках (конфигурации) с параллельным режимом выполнения отложенных обработчиков:
//
//     * ПроцедураЗаполненияДанныхОбновления - Строка - указывается процедура, которая регистрирует данные,
//                                      подлежащие обновлению данным обработчиком.
//     * ЗапускатьТолькоВГлавномУзле  - Булево - только для обработчиков отложенного обновления с режимом выполнения Параллельно.
//                                      Указать Истина, если обработчик обновления должен выполняться только в главном
//                                      узле РИБ.
//     * ЗапускатьИВПодчиненномУзлеРИБСФильтрами - Булево - только для обработчиков отложенного обновления с режимом
//                                      выполнения Параллельно.
//                                      Указать Истина, если обработчик обновления должен также выполняться в
//                                      подчиненном узле РИБ с фильтрами.
//     * ЧитаемыеОбъекты              - Строка - объекты, которые обработчик обновления будет читать при обработке данных.
//     * ИзменяемыеОбъекты            - Строка - объекты, которые обработчик обновления будет изменять при обработке данных.
//     * ПриоритетыВыполнения         - ТаблицаЗначений - таблица приоритетов выполнения между отложенными обработчиками,
//                                      изменяющими или читающими одни и те же данные. Подробнее см. в комментарии
//                                      к функции ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика.
//
//    5) Для внутреннего использования:
//
//     * ВыполнятьВГруппеОбязательных - Булево - следует указывать, если обработчик требуется
//                                      выполнять в одной группе с обработчиками на версии "*".
//                                      При этом возможно менять порядок выполнения обработчика
//                                      относительно других путем изменения приоритета.
//     * Приоритет           - Число  - для внутреннего использования.
//
//    6) Устарели, используются для обратной совместимости (для новых обработчиков не указывать):
//
//     * МонопольныйРежим    - Неопределено, Булево - если указано Неопределено, то обработчик 
//                                      должен безусловно выполняться в монопольном режиме.
//                                      Для обработчиков перехода на конкретную версию (версия <> *):
//                                        Ложь   - обработчик не требует монопольного режима для выполнения.
//                                        Истина - обработчик требует монопольного режима для выполнения.
//                                      Для обязательных обработчиков обновления (Версия = "*"):
//                                        Ложь   - обработчик не требует монопольного режима.
//                                        Истина - обработчик может требовать монопольного режима для выполнения.
//                                                 В такие обработчики передается параметр типа структура
//                                                 со свойством МонопольныйРежим (типа Булево).
//                                                 При запуске обработчика в монопольном режиме передается
//                                                 значение Истина. В этом случае обработчик должен выполнить
//                                                 требуемые действия по обновлению. Изменение параметра
//                                                 в теле обработчика игнорируется.
//                                                 При запуске обработчика в немонопольном режиме передается
//                                                 значение Ложь. В этом случае обработчик не должен вносить никакие
//                                                 изменения в ИБ.
//                                                 Если в результате анализа выясняется, что обработчику требуется
//                                                 изменить данные ИБ, следует установить значение параметра в Истина
//                                                 и прекратить выполнение обработчика.
//                                                 В этом случае оперативное (немонопольное) обновление ИБ будет
//                                                 отменено и будет выдана ошибка с требованием выполнить обновление в
//                                                 монопольном режиме.
//
Функция НоваяТаблицаОбработчиковОбновления() Экспорт
	
	Обработчики = Новый ТаблицаЗначений;
	// Общие свойства.
	Обработчики.Колонки.Добавить("НачальноеЗаполнение", Новый ОписаниеТипов("Булево"));
	Обработчики.Колонки.Добавить("Версия",    Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Обработчики.Колонки.Добавить("Процедура", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Обработчики.Колонки.Добавить("РежимВыполнения", Новый ОписаниеТипов("Строка"));
	// Для библиотек.
	Обработчики.Колонки.Добавить("ВыполнятьВГруппеОбязательных", Новый ОписаниеТипов("Булево"));
	Обработчики.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2)));
	// Для модели сервиса.
	Обработчики.Колонки.Добавить("ОбщиеДанные",             Новый ОписаниеТипов("Булево"));
	Обработчики.Колонки.Добавить("УправлениеОбработчиками", Новый ОписаниеТипов("Булево"));
	// Для отложенных обработчиков обновления.
	Обработчики.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Обработчики.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
	Обработчики.Колонки.Добавить("ПроцедураПроверки", Новый ОписаниеТипов("Строка"));
	Обработчики.Колонки.Добавить("БлокируемыеОбъекты", Новый ОписаниеТипов("Строка"));
	// Для параллельного режима отложенного обновления.
	Обработчики.Колонки.Добавить("ПроцедураЗаполненияДанныхОбновления", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Обработчики.Колонки.Добавить("ОчередьОтложеннойОбработки",  Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(4)));
	Обработчики.Колонки.Добавить("ЗапускатьТолькоВГлавномУзле",  Новый ОписаниеТипов("Булево"));
	Обработчики.Колонки.Добавить("ЗапускатьИВПодчиненномУзлеРИБСФильтрами",  Новый ОписаниеТипов("Булево"));
	Обработчики.Колонки.Добавить("ЧитаемыеОбъекты", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Обработчики.Колонки.Добавить("ИзменяемыеОбъекты", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Обработчики.Колонки.Добавить("ПриоритетыВыполнения");
	Обработчики.Колонки.Добавить("Многопоточный", Новый ОписаниеТипов("Булево"));
	
	// Устарело. Обратная совместимость с редакцией "2.2".
	Обработчики.Колонки.Добавить("Опциональный");
	Обработчики.Колонки.Добавить("МонопольныйРежим");
	
	Возврат Обработчики;
	
КонецФункции

// Возвращает пустую таблицу приоритетов выполнения между отложенными обработчиками,
// изменяющими или читающими одни и те же данные. Для использования в описании обработчиков обновления.
//
// Возвращаемое значение:
//  ТаблицаЗначений - с колонками:
//    * Порядок       - Строка - порядок выполнения текущего обработчика относительно других.
//                               Допустимые варианты: "До", "После", "Любой".
//    * Идентификатор - УникальныйИдентификатор - идентификатор процедуры, с которой настраивается взаимосвязь.
//    * Процедура     - Строка - полное имя процедуры, с которой настраивается взаимосвязь.
//
// Пример:
//  Приоритет = ПриоритетыВыполненияОбработчика().Добавить();
//  Приоритет.Порядок = "До";
//  Приоритет.Процедура = "Документ.ЗаказПокупателя.ОбновитьДанныеДляПереходаНаНовуюВерсию";
//
Функция ПриоритетыВыполненияОбработчика() Экспорт
	
	Приоритеты = Новый ТаблицаЗначений;
	Приоритеты.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	Приоритеты.Колонки.Добавить("Идентификатор");
	Приоритеты.Колонки.Добавить("Процедура", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
	
	Возврат Приоритеты;
	
КонецФункции

// Выполнить обработчики обновления из списка ОбработчикиОбновления 
// для библиотеки ИдентификаторБиблиотеки до версии ВерсияМетаданныхИБ.
//
// Параметры:
//   ИдентификаторБиблиотеки   - Строка       - имя конфигурации или идентификатор библиотеки.
//   ВерсияМетаданныхИБ        - Строка       - версия метаданных, до которой необходимо выполнить обновление.
//   ОбработчикиОбновления     - Соответствие - список обработчиков обновления.
//   ОперативноеОбновление     - Булево       - Истина, если обновление оперативное.
//   ХодВыполненияОбработчиков - Структура    - со свойствами:
//       * ВсегоОбработчиков     - Строка - общее количество выполняемых обработчиков.
//       * ВыполненоОбработчиков - Булево - количество выполненных обработчиков.
//
// Возвращаемое значение:
//   ДеревоЗначений   - выполненные обработчики обновления.
//
Функция ВыполнитьИтерациюОбновления(Знач ИдентификаторБиблиотеки, Знач ВерсияМетаданныхИБ, 
	Знач ОбработчикиОбновления, Знач ХодВыполненияОбработчиков, Знач ОперативноеОбновление = Ложь) Экспорт
	
	ИтерацияОбновления = ОбновлениеИнформационнойБазыСлужебный.ИтерацияОбновления(ИдентификаторБиблиотеки, 
		ВерсияМетаданныхИБ, ОбработчикиОбновления);
		
	Параметры = Новый Структура;
	Параметры.Вставить("ХодВыполненияОбработчиков", ХодВыполненияОбработчиков);
	Параметры.Вставить("ОперативноеОбновление", ОперативноеОбновление);
	Параметры.Вставить("ВФоне", Ложь);
	
	Возврат ОбновлениеИнформационнойБазыСлужебный.ВыполнитьИтерациюОбновления(ИтерацияОбновления, Параметры);
	
КонецФункции

// Выполнить неинтерактивное обновление данных ИБ.
// Для вызова через внешнее соединение.
// При вызове метода с подключенными расширениями, модифицирующими роли конфигурации, будет вызвано исключение.
// Важно: Перед вызовом метода необходимо запустить удаление устаревших патчей,
// см. функцию ОбновлениеКонфигурации.ИсправленияИзменены().
// 
// Для использования в других библиотеках и конфигурациях.
//
// Параметры:
//  ВыполнятьОтложенныеОбработчики - Булево - если Истина, отложенное обновление будет выполнено
//    в основном цикле обновления. Только для клиент-серверного режима работы.
//
// Возвращаемое значение:
//  Строка -  признак выполнения обработчиков обновления:
//           "Успешно", "НеТребуется", "ОшибкаУстановкиМонопольногоРежима".
//
Функция ВыполнитьОбновлениеИнформационнойБазы(ВыполнятьОтложенныеОбработчики = Ложь) Экспорт
	
	ДатаНачала = ТекущаяДатаСеанса();
	Результат = ОбновлениеИнформационнойБазыСлужебныйВызовСервера.ВыполнитьОбновлениеИнформационнойБазы(,,
		ВыполнятьОтложенныеОбработчики);
	ДатаОкончания = ТекущаяДатаСеанса();
	ОбновлениеИнформационнойБазыСлужебный.ЗаписатьВремяВыполненияОбновления(ДатаНачала, ДатаОкончания);
	
	Возврат Результат;
	
КонецФункции

// Возвращает таблицу с версиями подсистем конфигурации.
// Для пакетной выгрузки/загрузки сведений о версиях подсистем.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * ИмяПодсистемы - Строка - имя подсистемы.
//     * Версия        - Строка - версия подсистемы.
//
Функция ВерсииПодсистем() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВерсииПодсистем.ИмяПодсистемы КАК ИмяПодсистемы,
	|	ВерсииПодсистем.Версия КАК Версия
	|ИЗ
	|	РегистрСведений.ВерсииПодсистем КАК ВерсииПодсистем";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции 

// Устанавливает версии всех подсистем.
// Для пакетной выгрузки/загрузки сведений о версиях подсистем.
//
// Параметры:
//   ВерсииПодсистем - ТаблицаЗначений - таблица с колонками:
//     * ИмяПодсистемы - Строка - имя подсистемы.
//     * Версия        - Строка - версия подсистемы.
//
Процедура УстановитьВерсииПодсистем(ВерсииПодсистем) Экспорт

	НаборЗаписей = РегистрыСведений.ВерсииПодсистем.СоздатьНаборЗаписей();
	
	Для каждого Версия Из ВерсииПодсистем Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ИмяПодсистемы = Версия.ИмяПодсистемы;
		НоваяЗапись.Версия = Версия.Версия;
		НоваяЗапись.ЭтоОсновнаяКонфигурация = (Версия.ИмяПодсистемы = Метаданные.Имя);
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры

// Получает версию конфигурации или родительской конфигурации (библиотеки),
// которая хранится в информационной базе.
//
// Параметры:
//  ИдентификаторБиблиотеки   - Строка - имя конфигурации или идентификатор библиотеки.
//
// Возвращаемое значение:
//   Строка   - версия.
//
// Пример:
//   ВерсияКонфигурацииИБ = ВерсияИБ(Метаданные.Имя);
//
Функция ВерсияИБ(Знач ИдентификаторБиблиотеки) Экспорт
	
	Возврат ОбновлениеИнформационнойБазыСлужебный.ВерсияИБ(ИдентификаторБиблиотеки);
	
КонецФункции

// Записывает в информационную базу версию конфигурации или родительской конфигурации (библиотеки).
//
// Параметры:
//  ИдентификаторБиблиотеки - Строка - имя конфигурации или родительской конфигурации (библиотеки).
//  НомерВерсии             - Строка - номер версии.
//  ЭтоОсновнаяКонфигурация - Булево - признак, что ИдентификаторБиблиотеки соответствует имени конфигурации.
//
Процедура УстановитьВерсиюИБ(Знач ИдентификаторБиблиотеки, Знач НомерВерсии, Знач ЭтоОсновнаяКонфигурация) Экспорт
	
	ОбновлениеИнформационнойБазыСлужебный.УстановитьВерсиюИБ(ИдентификаторБиблиотеки, НомерВерсии, ЭтоОсновнаяКонфигурация);
	
КонецПроцедуры

// Выполняет регистрацию новой подсистемы в регистре сведений ВерсииПодсистем.
// Необходима, например, в тех случаях, когда новая подсистема создается на
// основе уже существующих метаданных и не требуется выполнять обработчики начального заполнения.
// Если подсистема уже зарегистрирована, то повторная регистрация не выполняется.
// Вызывать данный метод следует из процедуры ПередОбновлениемИнформационнойБазы общего
// модуля ОбновлениеИнформационнойБазыПереопределяемый.
//
// Параметры:
//  ИмяПодсистемы - Строка - имя подсистемы в том виде, в котором оно задано в общем модуле
//                           ОбновлениеИнформационнойБазыХХХ.
//                           Например - "СтандартныеПодсистемы".
//  НомерВерсии   - Строка - полный номер версии, на которую необходимо зарегистрировать подсистему.
//                           Если не указан, то регистрируется на версию "0.0.0.1". Следует указывать,
//                           если нужно, чтобы выполнились не все обработчики, а только последние.
//
Процедура ЗарегистрироватьНовуюПодсистему(ИмяПодсистемы, НомерВерсии = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	| ВерсииПодсистем.ИмяПодсистемы КАК ИмяПодсистемы
	|ИЗ
	| РегистрСведений.ВерсииПодсистем КАК ВерсииПодсистем";
	
	ПодсистемыКонфигурации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяПодсистемы");
	
	Если ПодсистемыКонфигурации.Количество() > 0 Тогда
		// Это не первый запуск программы
		Если ПодсистемыКонфигурации.Найти(ИмяПодсистемы) = Неопределено Тогда
			Запись = РегистрыСведений.ВерсииПодсистем.СоздатьМенеджерЗаписи();
			Запись.ИмяПодсистемы = ИмяПодсистемы;
			Запись.Версия = ?(НомерВерсии = "", "0.0.0.1", НомерВерсии);
			Запись.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Сведения = ОбновлениеИнформационнойБазыСлужебный.СведенияОбОбновленииИнформационнойБазы();
	ИндексЭлемента = Сведения.НовыеПодсистемы.Найти(ИмяПодсистемы);
	Если ИндексЭлемента <> Неопределено Тогда
		Сведения.НовыеПодсистемы.Удалить(ИндексЭлемента);
		ОбновлениеИнформационнойБазыСлужебный.ЗаписатьСведенияОбОбновленииИнформационнойБазы(Сведения);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает номер очереди отложенного обработчика обновления по его полному
// имени или уникальному идентификатору.
//
// Параметры:
//  ИмяИлиИдентификатор - Строка, УникальныйИдентификатор - полное имя отложенного обработчика
//                        или его идентификатор. Подробнее см. НоваяТаблицаОбработчиковОбновления,
//                        описание свойств Процедура и Идентификатор.
//
// Возвращаемое значение:
//  Число, Неопределено - номер очереди переданного обработчика, если
//                        обработчик не найден - будет возвращено Неопределено.
//
Функция ОчередьОтложенногоОбработчикаОбновления(ИмяИлиИдентификатор) Экспорт
	
	Результат = ОбновлениеИнформационнойБазыСлужебныйПовтИсп.ОчередьОтложенногоОбработчикаОбновления();
	
	Если ТипЗнч(ИмяИлиИдентификатор) = Тип("УникальныйИдентификатор") Тогда
		ОчередьПоИдентификатору = Результат["ПоИдентификатору"];
		Возврат ОчередьПоИдентификатору[ИмяИлиИдентификатор];
	Иначе
		ОчередьПоИмени = Результат["ПоИмени"];
		Возврат ОчередьПоИмени[ИмяИлиИдентификатор];
	КонецЕсли;
	
КонецФункции

// Максимальное количество записей в выборке данных для обновления.
//
// Возвращаемое значение:
//  Число - константа 10000.
//
Функция МаксимальноеКоличествоЗаписейВВыборке() Экспорт
	
	Возврат 10000;
	
КонецФункции

// Возвращает таблицу с данными, которые нужно обновить.
// Используется в многопоточных обработчиках обновления.
//
// Параметры:
//  Параметры - Структура - тот параметр, который передается в обработчик обновления.
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с обновляемыми данными, состав колонок которой зависит от способа выборки,
//                    указанного в процедуре регистрации данных для обновления.
//                    Если данных для обновления нет, то возвращается пустая таблица значений без колонок.
//
Функция ДанныеДляОбновленияВМногопоточномОбработчике(Параметры) Экспорт
	
	НаборДанных = Параметры.ОбновляемыеДанные.НаборДанных;
	
	Если НаборДанных.Количество() > 0 Тогда
		Возврат НаборДанных[0].Данные;
	Иначе
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела: больше не требуется, т.к. данные действия выполняются автоматически механизмом обновления.
// 
// Удаляет отложенный обработчик из очереди выполняемых обработчиков на новую версию.
// Следует использовать, например, при переводе отложенного обработчика
// на монопольный (оперативный) режим выполнения.
// Для этого необходимо добавить новый разделенный обработчик обновления с режимом выполнения
// "Оперативно" и признаком "ОбщиеДанные = Ложь", после чего разместить в нем вызов данного метода.
//
// Параметры:
//  ИмяОбработчика - Строка - полное имя процедуры отложенного обработчика.
//
Процедура УдалитьОтложенныйОбработчикИзОчереди(ИмяОбработчика) Экспорт
	
	СведенияОбОбновлении = ОбновлениеИнформационнойБазыСлужебный.СведенияОбОбновленииИнформационнойБазы();
	
	ОтобранныйОбработчик = СведенияОбОбновлении.ДеревоОбработчиков.Строки.НайтиСтроки(Новый Структура("ИмяОбработчика", ИмяОбработчика), Истина);
	Если ОтобранныйОбработчик <> Неопределено И ОтобранныйОбработчик.Количество() > 0 Тогда
		
		Для Каждого СтрокаОбработчик Из ОтобранныйОбработчик Цикл
			СтрокаОбработчик.Родитель.Строки.Удалить(СтрокаОбработчик);
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого ШагОбновления Из СведенияОбОбновлении.ПланОтложенногоОбновления Цикл
		ОбработчикиШага = ШагОбновления.Обработчики;
		Индекс = 0;
		ОбработчикНайден = Ложь;
		Для Каждого ОписаниеОбработчика Из ОбработчикиШага Цикл
			Если ОписаниеОбработчика.ИмяОбработчика = ИмяОбработчика Тогда
				ОбработчикНайден = Истина;
				Прервать;
			КонецЕсли;
			Индекс = Индекс + 1;
		КонецЦикла;
		
		Если ОбработчикНайден Тогда
			ОбработчикиШага.Удалить(Индекс);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыСлужебный.ЗаписатьСведенияОбОбновленииИнформационнойБазы(СведенияОбОбновлении);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПроверкуБлокировкиДополнительныхИсточников(Очередь, ТекстЗапроса, ПолноеИмяОбъекта, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ЭтоСозданиеВременнойТаблицы, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДополнительныеИсточникиДанных.Количество() = 0 Тогда
				
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиПоШапке", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамСсылкам", "ИСТИНА");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамРегистрам", "ИСТИНА");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиРегистрами", "");
	
	Иначе	
		
		ДополнительныеИсточникиСсылки = Новый Массив;
		ДополнительныеИсточникиРегистры = Новый Массив;
		
		Для Каждого КлючЗначение Из ДополнительныеПараметры.ДополнительныеИсточникиДанных Цикл
			
			ИсточникДанных = КлючЗначение.Ключ;
			
			Если СтандартныеПодсистемыСервер.ЭтоТаблицаРегистра(ИсточникДанных)
				И СтрНайти(ИсточникДанных,".") <> 0 Тогда
				ДополнительныеИсточникиРегистры.Добавить(ИсточникДанных);
			Иначе
				ДополнительныеИсточникиСсылки.Добавить(ИсточникДанных);
			КонецЕсли;
			
		КонецЦикла;
		
		#Область ДополнительныеИсточникиСсылки
		
		Если ДополнительныеИсточникиСсылки.Количество() > 0 Тогда
			
			Если ПолноеИмяОбъекта = Неопределено Тогда
				ТекстИсключения = НСтр("ru = 'Ошибка вызова функции %ИмяФункции%: не передано имя документа, но переданы дополнительные источники данных.'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяФункции%", "ОбновлениеИнформационнойБазы.ДобавитьПроверкуБлокировкиДополнительныхИсточников");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			
			МетаданныеДокумента = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
			ТекстЗапросаСоединениеСДопИсточникамиПоШапке = "";
			ТекстЗапросаСоединениеСДопИсточникамиПоТЧ = "";
			ТекстыЗапросаСоединениеСДопИсточникамиПоТЧ = Новый Соответствие;
			УсловиеПоДопИсточникамСсылкам = "ИСТИНА";
			
			УсловиеПоДопИсточникамСсылкамТЧ = "ЛОЖЬ";
			
			ВременныеТаблицыЗаблокированныхДопИсточников = Новый Соответствие;
			
			Для Каждого ИсточникДанных Из ДополнительныеИсточникиСсылки Цикл
				
				Если СтрНайти(ИсточникДанных, ".") > 0 Тогда
					ЧастиИмени = СтрРазделить(ИсточникДанных, ".");
					ИмяТЧ = ЧастиИмени[0];
					ИмяРеквизита = ЧастиИмени[1];
				Иначе
					ИмяТЧ = "";
					ИмяРеквизита = ИсточникДанных;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ИмяТЧ) Тогда
					ТипыИсточника = МетаданныеДокумента.ТабличныеЧасти[ИмяТЧ].Реквизиты[ИмяРеквизита].Тип.Типы();
				Иначе
					ТипыИсточника = МетаданныеДокумента.Реквизиты[ИмяРеквизита].Тип.Типы();
				КонецЕсли;	
				
				Для Каждого ТипИсточника Из ТипыИсточника Цикл
					
					Если ЭтоПримитивныйТип(ТипИсточника) Тогда
						Продолжить;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ИмяТЧ)
						И СтрНайти(ТекстЗапросаСоединениеСДопИсточникамиПоТЧ, "КАК ТЧДокумента" + ИмяТЧ) = 0 Тогда
						
						Если ПолноеИмяРегистра <> Неопределено Тогда
							
							ТекстЗапросаСоединениеСДопИсточникамиПоТЧ = ТекстЗапросаСоединениеСДопИсточникамиПоТЧ + "
							|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяДокумента." + ИмяТЧ + " КАК ТЧДокумента" + ИмяТЧ + "
							|#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ" + ИмяТЧ + "
							|		ПО ТаблицаРегистраИзменения.Регистратор = ТЧДокумента" + ИмяТЧ + ".Ссылка
							|";
							
						Иначе
							
							ТекстЗапросаСоединениеСДопИсточникамиПоТЧ = ТекстЗапросаСоединениеСДопИсточникамиПоТЧ + "
							|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяДокумента." + ИмяТЧ + " КАК ТЧДокумента" + ИмяТЧ + "
							|#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ" + ИмяТЧ + "
							|		ПО ТаблицаИзменений.Ссылка = ТЧДокумента" + ИмяТЧ + ".Ссылка
							|";
							
						КонецЕсли;
					КонецЕсли;
					
					МетаданныеИсточника = Метаданные.НайтиПоТипу(ТипИсточника);
					
					ИмяВТЗаблокированногоДопИсточника = ВременныеТаблицыЗаблокированныхДопИсточников.Получить(МетаданныеИсточника);
					
					Если ИмяВТЗаблокированногоДопИсточника = Неопределено Тогда
						ПолноеИмяИсточника = МетаданныеИсточника.ПолноеИмя();
						ИмяВТЗаблокированногоДопИсточника = "ВТЗаблокировано" + СтрЗаменить(ПолноеИмяИсточника,".","_");
						
						ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
						ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = ИмяВТЗаблокированногоДопИсточника;
						СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ПолноеИмяИсточника, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
						
						ВременныеТаблицыЗаблокированныхДопИсточников.Вставить(МетаданныеИсточника, ИмяВТЗаблокированногоДопИсточника);
						
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ИмяТЧ) Тогда
						
						ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ = ТекстыЗапросаСоединениеСДопИсточникамиПоТЧ.Получить(ИмяТЧ);
						
						Если ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ = Неопределено Тогда
							ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ = "";
						КонецЕсли;
						
						ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ = ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ + "
						|			ЛЕВОЕ СОЕДИНЕНИЕ #ИмяВТ КАК #СинонимВТ
						|			ПО ТЧДокумента" + ИмяТЧ + "." + ИмяРеквизита + " = #СинонимВТ.Ссылка";
						
						ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ = СтрЗаменить(ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ,
																					"#ИмяВТ",
																					ИмяВТЗаблокированногоДопИсточника);
						СинонимВТЗаблокированногоДопИсточника = ИмяВТЗаблокированногоДопИсточника + ИмяТЧ + ИмяРеквизита;															 
						ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ = СтрЗаменить(ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ,
																					"#СинонимВТ",
																					СинонимВТЗаблокированногоДопИсточника);
						ТекстыЗапросаСоединениеСДопИсточникамиПоТЧ.Вставить(ИмяТЧ, ТекстЗапросаСоединениеСДопИсточникамиПоТЧИмяТЧ);
						
						УсловиеПоДопИсточникамСсылкамТЧ = УсловиеПоДопИсточникамСсылкамТЧ + "
						|	ИЛИ НЕ " + СинонимВТЗаблокированногоДопИсточника + ".Ссылка ЕСТЬ NULL ";
					Иначе
						Если ПолноеИмяРегистра <> Неопределено Тогда
							ТекстЗапросаСоединениеСДопИсточникамиПоШапке = ТекстЗапросаСоединениеСДопИсточникамиПоШапке + "
							|			ЛЕВОЕ СОЕДИНЕНИЕ #ИмяВТ КАК #СинонимВТ
							|			ПО ТаблицаДокумента." + ИмяРеквизита + " = #СинонимВТ.Ссылка";
						Иначе
							ТекстЗапросаСоединениеСДопИсточникамиПоШапке = ТекстЗапросаСоединениеСДопИсточникамиПоШапке + "
							|			ЛЕВОЕ СОЕДИНЕНИЕ #ИмяВТ КАК #СинонимВТ
							|			ПО ТаблицаОбъекта." + ИмяРеквизита + " = #СинонимВТ.Ссылка";
						КонецЕсли;
						ТекстЗапросаСоединениеСДопИсточникамиПоШапке = СтрЗаменить(ТекстЗапросаСоединениеСДопИсточникамиПоШапке,
																					"#ИмяВТ",
																					ИмяВТЗаблокированногоДопИсточника);
						СинонимВТЗаблокированногоДопИсточника = ИмяВТЗаблокированногоДопИсточника + "Шапка";															 
						ТекстЗапросаСоединениеСДопИсточникамиПоШапке = СтрЗаменить(ТекстЗапросаСоединениеСДопИсточникамиПоШапке,
																					"#СинонимВТ",
																					СинонимВТЗаблокированногоДопИсточника);
					
						УсловиеПоДопИсточникамСсылкам = УсловиеПоДопИсточникамСсылкам + "
						|	И " + СинонимВТЗаблокированногоДопИсточника + ".Ссылка ЕСТЬ NULL ";
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			
			Если Не ПустаяСтрока(ТекстЗапросаСоединениеСДопИсточникамиПоТЧ) Тогда
				Для Каждого ТекстСоединения Из ТекстыЗапросаСоединениеСДопИсточникамиПоТЧ Цикл
					
					ТекстЗапросаСоединениеСДопИсточникамиПоТЧ = СтрЗаменить(ТекстЗапросаСоединениеСДопИсточникамиПоТЧ,
					"#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ" + ТекстСоединения.Ключ,
					ТекстСоединения.Значение);
					
				КонецЦикла;
				
				Если ПолноеИмяРегистра <> Неопределено Тогда
					ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ТаблицаРегистраИзменения.Регистратор КАК Ссылка
					|ПОМЕСТИТЬ ЗаблокированныеПоТЧ
					|ИЗ
					|	#ТаблицаИзменений КАК ТаблицаРегистраИзменения
					|       #ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
					|ГДЕ
					|	&УсловиеПоДопИсточникамСсылкамТЧ";
				Иначе
					ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ТаблицаИзменений.Ссылка КАК Ссылка
					|ПОМЕСТИТЬ ЗаблокированныеПоТЧ
					|ИЗ
					|	#ТаблицаИзменений КАК ТаблицаИзменений
					|       #ТекстЗапросаСоединениеСДопИсточникамиПоТЧ
					|ГДЕ
					|	&УсловиеПоДопИсточникамСсылкамТЧ";
				КонецЕсли;
				
				ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ = СтрЗаменить(ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ,
																				"#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ",
																				ТекстЗапросаСоединениеСДопИсточникамиПоТЧ);
				
				ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ = СтрЗаменить(ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ,
																				"&УсловиеПоДопИсточникамСсылкамТЧ",
																				УсловиеПоДопИсточникамСсылкамТЧ);
				Если ПолноеИмяРегистра <> Неопределено Тогда
					ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ = СтрЗаменить(ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ,
																					"#ТаблицаИзменений",
																					ПолноеИмяРегистра + ".Изменения");	
				Иначе
					ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ = СтрЗаменить(ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ,
																					"#ТаблицаИзменений",
																					ПолноеИмяОбъекта + ".Изменения");	
				КонецЕсли;																
				
				ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ = СтрЗаменить(ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ,
																				"#ПолноеИмяДокумента",
																				ПолноеИмяОбъекта);
				Запрос = Новый Запрос;
				Запрос.Текст = ТекстЗапросаВременнойТаблицыЗаблокированныхПоТЧ;
				Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
				Запрос.Выполнить();
				
				Если ПолноеИмяРегистра <> Неопределено Тогда
					ТекстЗапросаСоединениеСДопИсточникамиПоТЧ = "
					|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаблокированныеПоТЧ КАК ЗаблокированныеПоТЧ 
					|		ПО ТаблицаРегистраИзменения.Регистратор = ЗаблокированныеПоТЧ.Ссылка
					|";
				Иначе
					ТекстЗапросаСоединениеСДопИсточникамиПоТЧ = "
					|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаблокированныеПоТЧ КАК ЗаблокированныеПоТЧ 
					|		ПО ТаблицаИзменений.Ссылка = ЗаблокированныеПоТЧ.Ссылка
					|";
				КонецЕсли;																
				
				УсловиеПоДопИсточникамСсылкам = УсловиеПоДопИсточникамСсылкам + "
				|	И ЗаблокированныеПоТЧ.Ссылка ЕСТЬ NULL ";
				
				ВременныеТаблицыЗаблокированныхДопИсточников.Вставить("ЗаблокированныеПоТЧ", "ЗаблокированныеПоТЧ");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекстЗапросаСоединениеСДопИсточникамиПоШапке) Тогда 
				Если ЭтоСозданиеВременнойТаблицы
					И ПолноеИмяРегистра <> Неопределено Тогда
					ТекстЗапросаСоединениеСДопИсточникамиПоШапке = СтрЗаменить("
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ПолноеИмяДокумента КАК ТаблицаДокумента
					|       	#ТекстЗапросаСоединениеСДопИсточникамиПоШапке
					|		ПО ТаблицаРегистраИзменения.Регистратор = ТаблицаДокумента.Ссылка",
					"#ТекстЗапросаСоединениеСДопИсточникамиПоШапке",
					ТекстЗапросаСоединениеСДопИсточникамиПоШапке);
				КонецЕсли;
			КонецЕсли;	
				
			ШаблонТекстаЗапросаУничтоженияВременнойТаблицы = "
			|УНИЧТОЖИТЬ
			|	#ИмяВТ
			|";
			
			ТекстыЗапроса = Новый Массив;
			ТекстыЗапроса.Добавить(ТекстЗапроса);
			
			Для Каждого КлючЗначение Из ВременныеТаблицыЗаблокированныхДопИсточников Цикл
				
				ТекстЗапросаУничтоженияВременнойТаблицы = СтрЗаменить(ШаблонТекстаЗапросаУничтоженияВременнойТаблицы, "#ИмяВТ", КлючЗначение.Значение);
				
				ТекстыЗапроса.Добавить(ТекстЗапросаУничтоженияВременнойТаблицы);
				
			КонецЦикла;
			
			ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ";");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиПоШапке", ТекстЗапросаСоединениеСДопИсточникамиПоШапке);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ", ТекстЗапросаСоединениеСДопИсточникамиПоТЧ);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамСсылкам", УсловиеПоДопИсточникамСсылкам);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ПолноеИмяДокумента", ПолноеИмяОбъекта);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиПоШапке", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиПоТЧ", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамСсылкам", "ИСТИНА");
		КонецЕсли;
		#КонецОбласти
		
		#Область ДополнительныеИсточникиРегистры

		Если ДополнительныеИсточникиРегистры.Количество() > 0 Тогда
			
			ТекстЗапросаСоединениеСДопИсточникамиРегистрами = "";
			УсловиеПоДопИсточникамРегистрам = "ИСТИНА";
			
			ВременныеТаблицыЗаблокированныхДопИсточников = Новый Соответствие;
			
			Для Каждого ИсточникДанных Из ДополнительныеИсточникиРегистры Цикл
				
				МетаданныеИсточника = Метаданные.НайтиПоПолномуИмени(ИсточникДанных);
				
				Если ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеИсточника)
					И МетаданныеИсточника.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
					
					ТекстИсключения = НСтр("ru = 'Регистр %ИсточникДанных% независимый. Поддерживается проверка только по регистрам, подчиненным регистраторам.'");
					ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИсточникДанных%",ИсточникДанных);
					ВызватьИсключение ТекстИсключения;
				КонецЕсли;
				
				ИмяВТЗаблокированногоДопИсточника = ВременныеТаблицыЗаблокированныхДопИсточников.Получить(МетаданныеИсточника);
				
				Если ИмяВТЗаблокированногоДопИсточника = Неопределено Тогда
					ИмяВТЗаблокированногоДопИсточника = "ВТЗаблокировано" + СтрЗаменить(ИсточникДанных,".","_");
					
					ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
					ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = ИмяВТЗаблокированногоДопИсточника;
					СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияДанных(Очередь, ИсточникДанных, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
					
					ВременныеТаблицыЗаблокированныхДопИсточников.Вставить(МетаданныеИсточника, ИмяВТЗаблокированногоДопИсточника);
					
				КонецЕсли;
				
				Если ПолноеИмяРегистра <> Неопределено Тогда
					ТекстЗапросаСоединениеСДопИсточникамиРегистрами = ТекстЗапросаСоединениеСДопИсточникамиРегистрами + "
					|			ЛЕВОЕ СОЕДИНЕНИЕ #ИмяВТ КАК #ИмяВТ
					|			ПО ТаблицаРегистраИзменения.Регистратор = #ИмяВТ.Регистратор";
				Иначе
					ТекстЗапросаСоединениеСДопИсточникамиРегистрами = ТекстЗапросаСоединениеСДопИсточникамиРегистрами + "
					|			ЛЕВОЕ СОЕДИНЕНИЕ #ИмяВТ КАК #ИмяВТ
					|			ПО ТаблицаОбъекта.Ссылка = #ИмяВТ.Регистратор";
				КонецЕсли;
				ТекстЗапросаСоединениеСДопИсточникамиРегистрами = СтрЗаменить(ТекстЗапросаСоединениеСДопИсточникамиРегистрами,
					"#ИмяВТ", ИмяВТЗаблокированногоДопИсточника);
					
				УсловиеПоДопИсточникамРегистрам = УсловиеПоДопИсточникамРегистрам + "
				|	И " + ИмяВТЗаблокированногоДопИсточника + ".Регистратор ЕСТЬ NULL ";
				
			КонецЦикла;	
			
			ШаблонТекстаЗапросаУничтоженияВременнойТаблицы = "
			|УНИЧТОЖИТЬ
			|	#ИмяВТ
			|";
			
			ТекстыЗапроса = Новый Массив;
			ТекстыЗапроса.Добавить(ТекстЗапроса);
			
			Для Каждого КлючЗначение Из ВременныеТаблицыЗаблокированныхДопИсточников Цикл
				
				ТекстЗапросаУничтоженияВременнойТаблицы = СтрЗаменить(ШаблонТекстаЗапросаУничтоженияВременнойТаблицы, "#ИмяВТ", КлючЗначение.Значение);
				
				ТекстыЗапроса.Добавить(ТекстЗапросаУничтоженияВременнойТаблицы);
				
			КонецЦикла;
			
			ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ";");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиРегистрами", ТекстЗапросаСоединениеСДопИсточникамиРегистрами);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамРегистрам", УсловиеПоДопИсточникамРегистрам);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамРегистрам", "ИСТИНА");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточникамиРегистрами", "");
		КонецЕсли;
		#КонецОбласти
	КонецЕсли;	
КонецПроцедуры

Функция ЭтоПримитивныйТип(ПроверяемыйТип)
	
	Если ПроверяемыйТип = Тип("Неопределено")
		Или ПроверяемыйТип = Тип("Булево")
		Или ПроверяемыйТип = Тип("Строка")
		Или ПроверяемыйТип = Тип("Число")
		Или ПроверяемыйТип = Тип("Дата")
		Или ПроверяемыйТип = Тип("УникальныйИдентификатор") Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьПроверкуБлокировкиДополнительныхИсточниковДляНезависимогоРегистра(Очередь, ТекстЗапроса, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДополнительныеИсточникиДанных.Количество() = 0 Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточниками", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамСсылкам", "ИСТИНА");
	
	Иначе
		
		МетаданныеРегистра = Метаданные.НайтиПоПолномуИмени(ПолноеИмяРегистра);
		ТекстЗапросаСоединениеСДопИсточниками = "";
		УсловиеПоДопИсточникамСсылкам = "ИСТИНА";
		
		Для Каждого КлючЗначение Из ДополнительныеПараметры.ДополнительныеИсточникиДанных Цикл
			
			ИсточникДанных = КлючЗначение.Ключ;
			
			ТипыИсточника = МетаданныеРегистра.Измерения[ИсточникДанных].Тип.Типы();
			МассивОбъектовМетаданных = Новый Массив;
			
			Для Каждого ТипИсточника Из ТипыИсточника Цикл
				
				Если ЭтоПримитивныйТип(ТипИсточника) Тогда
					Продолжить;
				КонецЕсли;
				
				МассивОбъектовМетаданных.Добавить(Метаданные.НайтиПоТипу(ТипИсточника));
				
			КонецЦикла;
			
			ДополнительныеПараметрыСозданияВТ = ДополнительныеПараметрыВыборкиДанныхДляОбработки();
			ИмяВременнойТаблицы = "ВТЗаблокировано" + ИсточникДанных;
			ДополнительныеПараметрыСозданияВТ.ИмяВременнойТаблицы = ИмяВременнойТаблицы;
			
			СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(Очередь, МассивОбъектовМетаданных, МенеджерВременныхТаблиц, ДополнительныеПараметрыСозданияВТ);
			
			ТекстЗапросаСоединениеСДопИсточниками = ТекстЗапросаСоединениеСДопИсточниками + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ " + ИмяВременнойТаблицы + " КАК " + ИмяВременнойТаблицы + "
			|		ПО ТаблицаИзменений." + ИсточникДанных + " = " + ИмяВременнойТаблицы + ".Ссылка";
			
			УсловиеПоДопИсточникамСсылкам = УсловиеПоДопИсточникамСсылкам + "
			|		И "  + ИмяВременнойТаблицы + ".Ссылка ЕСТЬ NULL ";
			
		КонецЦикла;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТекстЗапросаСоединениеСДопИсточниками", ТекстЗапросаСоединениеСДопИсточниками);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДопИсточникамСсылкам", УсловиеПоДопИсточникамСсылкам);

	КонецЕсли;
КонецПроцедуры

Процедура УстановитьНедостающиеОтборыВНаборе(Набор, МетаданныеНабора, УстанавливаемыеОтборы)
	Для Каждого Измерение Из МетаданныеНабора.Измерения Цикл
		
		ЕстьОтборПоИзмерению = Ложь;
		
		Если ТипЗнч(УстанавливаемыеОтборы) = Тип("ТаблицаЗначений") Тогда
			ЕстьОтборПоИзмерению = УстанавливаемыеОтборы.Колонки.Найти(Измерение.Имя) <> Неопределено;
		Иначе //Отбор
			ЕстьОтборПоИзмерению = УстанавливаемыеОтборы[Измерение.Имя].Использование;	
		КонецЕсли;
		
		Если Не ЕстьОтборПоИзмерению Тогда
			ПустоеЗначение = Измерение.Тип.ПривестиЗначение();
			Набор.Отбор[Измерение.Имя].Установить(ПустоеЗначение);
		КонецЕсли;
	КонецЦикла;
	
	Если МетаданныеНабора.ОсновнойОтборПоПериоду Тогда
		
		Если ТипЗнч(УстанавливаемыеОтборы) = Тип("ТаблицаЗначений") Тогда
			ЕстьОтборПоИзмерению = УстанавливаемыеОтборы.Колонки.Найти("Период") <> Неопределено;
		Иначе //Отбор
			ЕстьОтборПоИзмерению = УстанавливаемыеОтборы.Период.Использование;
		КонецЕсли;
		
		Если Не ЕстьОтборПоИзмерению Тогда
			ПустоеЗначение = '00010101';
			Набор.Отбор.Период.Установить(ПустоеЗначение);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьИзменения(Параметры, Узел, Данные, ВидДанных, ПолноеИмяОбъекта = "")
	
	ПланыОбмена.ЗарегистрироватьИзменения(Узел, Данные);
	
	Если Параметры.Свойство("ДанныеОбработчика") Тогда
		Если Не ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
			ПолноеИмя = Данные.Метаданные().ПолноеИмя();
		Иначе
			ПолноеИмя = ПолноеИмяОбъекта;
		КонецЕсли;
		
		ДанныеПоОбъекту = Параметры.ДанныеОбработчика[ПолноеИмя];
		Если ДанныеПоОбъекту = Неопределено Тогда
			ДанныеПоОбъекту = Новый Структура;
			ДанныеПоОбъекту.Вставить("Количество", 1);
			ДанныеПоОбъекту.Вставить("Очередь", Параметры.Очередь);
			Параметры.ДанныеОбработчика.Вставить(ПолноеИмя, ДанныеПоОбъекту);
		Иначе
			Параметры.ДанныеОбработчика[ПолноеИмя].Количество = ДанныеПоОбъекту.Количество + 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОбменДанными")
		И СтандартныеПодсистемыПовтИсп.ИспользуетсяРИБ("СФильтром")
		И Не Параметры.ПовторнаяРегистрация
		И Не ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ() Тогда
		МодульОбменДаннымиСервер = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиСервер");
		МодульОбменДаннымиСервер.ЗаписатьДанныеДляОбновленияВФайл(Параметры, Данные, ВидДанных, ПолноеИмяОбъекта);
	КонецЕсли;
	
КонецПроцедуры

Функция УзлыМеньшейОчереди(Очередь)
	Возврат ПланыОбмена.ОбновлениеИнформационнойБазы.УзлыМеньшейОчереди(Очередь);
КонецФункции

Функция ОчередьСсылкой(Очередь)
	Возврат ПланыОбмена.ОбновлениеИнформационнойБазы.УзелПоОчереди(Очередь);
КонецФункции

// Контекст построения выборки при помощи таких функций, как:
// - ВыбратьРегистраторыРегистраДляОбработки();
// - ВыбратьСсылкиДляОбработки();
// - ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки();
//
// Параметры:
//  ДополнительныеПараметры - Структура - см.
//                            ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки().
//  ИмяТаблицы - Строка - имя таблицы из которой выбираются данные.
//
// Возвращаемое значение:
//  Структура - со следующими полями:
//   * ДополнительныеПараметры - Структура - копия ссылки входного параметра для процедур механизма.
//   * ПостраничнаяВыборка - Булево - Истина, если выборка выполняется по страницам.
//   * ИмяТаблицы - Строка - имя таблицы из которой выбираются данные.
//   * ПоляВыборки - Массив - поля, которые подставляются в список выборки запроса.
//   * ПоляУпорядочивания - Массив - поля, которые подставляются в раздел упорядочивания запроса.
//   * ИспользуемыеПоляУпорядочивания - Соответствие - кеш полей, которые уже использованы для упорядочивания.
//   * Псевдонимы - Массив - псевдонимы имен выбираемых полей, подставляемых в запрос выборки.
//   * Направления - Массив - направления упорядочивания (УБЫВ, ВОЗР).
//
Функция ПараметрыПостроенияВыборки(ДополнительныеПараметры, ИмяТаблицы = Неопределено)
	
	ПроверитьПараметрыВыборки(ДополнительныеПараметры);
	
	ПараметрыПостроения = Новый Структура;
	ПараметрыПостроения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ПараметрыПостроения.Вставить("ПостраничнаяВыборка", ЭтоПостраничнаяВыборка(ДополнительныеПараметры));
	ПараметрыПостроения.Вставить("ИмяТаблицы", ИмяТаблицы);
	ПараметрыПостроения.Вставить("ПоляВыборки", Новый Массив);
	ПараметрыПостроения.Вставить("ПоляУпорядочивания", Новый Массив);
	ПараметрыПостроения.Вставить("ИспользуемыеПоляУпорядочивания", Новый Соответствие);
	ПараметрыПостроения.Вставить("Псевдонимы", Новый Массив);
	ПараметрыПостроения.Вставить("Направления", Новый Массив);
	
	Возврат ПараметрыПостроения;
	
КонецФункции

// Установить поля упорядочивания в ВыбратьСсылкиДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//  ЭтоДокумент - Булево - Истина, если обрабатываются ссылки документа.
//
Процедура УстановитьПоляУпорядочиванияСсылок(ПараметрыПостроения, ЭтоДокумент)
	
	ПоляВыборки = ПараметрыПостроения.ПоляВыборки;
	ПоляУпорядочивания = ПараметрыПостроения.ПоляУпорядочивания;
	ПостраничнаяВыборка = ПараметрыПостроения.ПостраничнаяВыборка;
	
	Если ЭтоДокумент Тогда
		Если ПостраничнаяВыборка Тогда
			ПоляВыборки.Добавить("ТаблицаОбъекта.Дата");
		КонецЕсли;
		
		ПоляУпорядочивания.Добавить("ТаблицаОбъекта.Дата");
	КонецЕсли;
	
	ПоляВыборки.Добавить("ТаблицаИзменений.Ссылка");
	
	Если ПостраничнаяВыборка Тогда
		ПоляУпорядочивания.Добавить("ТаблицаИзменений.Ссылка");
	КонецЕсли;
	
КонецПроцедуры

// Установить поля упорядочивания для регистра в ВыбратьРегистраторыРегистраДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
Процедура УстановитьПоляУпорядочиванияРегистра(ПараметрыПостроения)
	
	ПоляВыборки = ПараметрыПостроения.ПоляВыборки;
	Псевдонимы = ПараметрыПостроения.Псевдонимы;
	ПоляУпорядочивания = ПараметрыПостроения.ПоляУпорядочивания;
	
	ПоляВыборки.Добавить("ТаблицаРегистраИзменения.Регистратор");
	Псевдонимы.Добавить("Регистратор");
	
	Если ПараметрыПостроения.ПостраничнаяВыборка Тогда
		ПоляУпорядочивания.Добавить("ТаблицаРегистраИзменения.Регистратор");
	Иначе
		ПоляВыборки.Добавить("МАКСИМУМ(ЕСТЬNULL(ТаблицаРегистра.Период, ДАТАВРЕМЯ(3000, 1, 1)))");
		Псевдонимы.Добавить("Период");
		ПоляУпорядочивания.Добавить("МАКСИМУМ(ЕСТЬNULL(ТаблицаРегистра.Период, ДАТАВРЕМЯ(3000, 1, 1)))");
	КонецЕсли;
	
КонецПроцедуры

// Установить поля упорядочивания для документа в ВыбратьРегистраторыРегистраДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
Процедура УстановитьПоляУпорядочиванияРегистраПоДокументу(ПараметрыПостроения)
	
	ПоляВыборки = ПараметрыПостроения.ПоляВыборки;
	Псевдонимы = ПараметрыПостроения.Псевдонимы;
	ПоляУпорядочивания = ПараметрыПостроения.ПоляУпорядочивания;
	ПоляУпорядочивания.Добавить("ТаблицаДокумента.Дата");
	
	Если ПараметрыПостроения.ПостраничнаяВыборка Тогда
		ПоляВыборки.Добавить("ТаблицаДокумента.Дата");
		Псевдонимы.Добавить("Дата");
		ПоляУпорядочивания.Добавить("ТаблицаРегистраИзменения.Регистратор");
	КонецЕсли;
	
	ПоляВыборки.Добавить("ТаблицаРегистраИзменения.Регистратор");
	Псевдонимы.Добавить("Регистратор");
	
КонецПроцедуры

// Установить поля упорядочивания для документа в ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
Процедура УстановитьПоляУпорядочиванияНезависимогоРегистраСведений(ПараметрыПостроения)
	
	Разделители = " " + Символы.Таб + Символы.ПС;
	ПоляУпорядочивания = ПараметрыПостроения.ДополнительныеПараметры.ПоляУпорядочивания;
	
	Для ИндексПоля = 0 По ПоляУпорядочивания.ВГраница() Цикл
		Поле = ПоляУпорядочивания[ИндексПоля];
		Состав = СтрРазделить(Поле, Разделители, Ложь);
		ИмяПоля = Состав[0];
		ПараметрыПостроения.ПоляВыборки.Добавить(ИмяПоля);
		ПараметрыПостроения.ИспользуемыеПоляУпорядочивания[ИмяПоля] = Истина;
		
		Если Состав.Количество() > 1 Тогда
			ПараметрыПостроения.Направления.Добавить(Состав[1]);
		Иначе
			ПараметрыПостроения.Направления.Добавить(?(ИндексПоля = 0, "УБЫВ", ""));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Учесть измерение в параметрах формирования запроса в ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//  ИмяИзмерения - Строка - имя обрабатываемого измерения.
//
Процедура УстановитьИзмерение(ПараметрыПостроения, ИмяИзмерения)
	
	Если ПараметрыПостроения.ИспользуемыеПоляУпорядочивания[ИмяИзмерения] = Неопределено Тогда
		ПараметрыПостроения.ПоляВыборки.Добавить(ИмяИзмерения);
		ЗаданыПоляУпорядочивания = ЗаданыПоляУпорядочивания(ПараметрыПостроения.ДополнительныеПараметры);
		
		Если ЗаданыПоляУпорядочивания Или ПараметрыПостроения.ПостраничнаяВыборка Тогда
			ПараметрыПостроения.Направления.Добавить("");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Учесть период в параметрах формирования запроса в ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
Процедура УстановитьПериод(ПараметрыПостроения)
	
	ПараметрыПостроения.ПоляВыборки.Вставить(0, "Период");
	ПараметрыПостроения.Направления.Вставить(0, "");
	
КонецПроцедуры

// Учесть ресурсы в параметрах формирования запроса в ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
Процедура УстановитьРесурсы(ПараметрыПостроения, Ресурсы)
	
	Если ПараметрыПостроения.ПоляВыборки.Количество() = 0 Тогда
		Для Каждого Ресурс Из Ресурсы Цикл
			Если ПараметрыПостроения.ИспользуемыеПоляУпорядочивания[Ресурс.Имя] = Неопределено Тогда
				ПараметрыПостроения.ПоляВыборки.Добавить(Ресурс.Имя);
				ПараметрыПостроения.Направления.Добавить("");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Учесть реквизиты в параметрах формирования запроса в ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки().
//
// Параметры:
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
Процедура УстановитьРеквизиты(ПараметрыПостроения, Реквизиты)
	
	Если ПараметрыПостроения.ПоляВыборки.Количество() = 0 Тогда
		Для Каждого Реквизит Из Реквизиты Цикл
			Если ПараметрыПостроения.ИспользуемыеПоляУпорядочивания[Реквизит.Имя] = Неопределено Тогда
				ПараметрыПостроения.ПоляВыборки.Добавить(Реквизит.Имя);
				ПараметрыПостроения.Направления.Добавить("");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Выбрать данные в следующих функциях с учетом постраничной выборки:
// - ВыбратьРегистраторыРегистраДляОбработки();
// - ВыбратьСсылкиДляОбработки();
// - ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки();
//
// Параметры:
//  Запрос - Запрос - запрос выборки данных.
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
// Возвращаемое значение:
//  * ВыборкаИзРезультатаЗапроса - в случае обычной выборки.
//  * ТаблицаЗначений - в случае постраничной выборки.
//
Функция ВыбратьДанныеДляОбработки(Запрос, ПараметрыПостроения)
	
	Если ПараметрыПостроения.ПостраничнаяВыборка Тогда
		Возврат ВыбратьДанныеПостранично(Запрос, ПараметрыПостроения);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоСтраницам", "ИСТИНА");
		Возврат Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
КонецФункции

// Получить таблицу значений с данными для обработки с учетом постраничной выборки.
//
// Параметры:
//  Запрос - Запрос - запрос выборки данных.
//  ПараметрыПостроения - Структура - см. ПараметрыПостроенияВыборки().
//
// Возвращаемое значение:
//  ТаблицаЗначений - данные для обработчика обновления (многопоточного).
//
Функция ВыбратьДанныеПостранично(Запрос, ПараметрыПостроения)
	
	ПоляВыборки = ПараметрыПостроения.ПоляВыборки;
	Параметры = ПараметрыПостроения.ДополнительныеПараметры;
	ТаблицаИзменений = ПараметрыПостроения.ИмяТаблицы;
	Направления = ПараметрыПостроения.Направления;
	ПоследняяВыбраннаяЗапись = Параметры.ПоследняяВыбраннаяЗапись;
	ПерваяЗапись = Параметры.ПерваяЗапись;
	ПоследняяЗапись = Параметры.ПоследняяЗапись;
	ВыбратьПервые = ПоследняяВыбраннаяЗапись = Неопределено
	              И ПерваяЗапись = Неопределено
	              И ПоследняяЗапись = Неопределено;
	
	Если ВыбратьПервые Тогда
		ИзменитьМаксимумВыборки(Запрос.Текст, МаксимальноеКоличествоЗаписейВВыборке(), Параметры.МаксимумВыборки);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоСтраницам", "ИСТИНА");
		Результат = Запрос.Выполнить().Выгрузить();
	Иначе
		ВыбратьДиапазон = ПерваяЗапись <> Неопределено И ПоследняяЗапись <> Неопределено;
		БазовыйТекстЗапроса = Запрос.Текст;
		
		Если ВыбратьДиапазон Тогда
			Условия = УсловияДляДиапазонаСтраницы(ПоляВыборки, Параметры, Направления);
		Иначе
			Условия = УсловияДляСледующейСтраницы(ПоляВыборки, Параметры, Направления);
		КонецЕсли;
		
		Если Параметры.ОптимизироватьВыборкуПоСтраницам Тогда
			Результат = Неопределено;
			ИндексПоследнегоУсловия = Условия.Количество() - 1;
			ОтложитьУничтожениеВременныхТаблиц = Условия.Количество() > 1;
			
			Если ОтложитьУничтожениеВременныхТаблиц Тогда
				ТекстЗапросаУничтоженияВременныхТаблиц = ВырезатьУничтожениеВременныхТаблиц(БазовыйТекстЗапроса);
			КонецЕсли;
			
			Для Индекс = 0 По ИндексПоследнегоУсловия Цикл
				Если Результат = Неопределено Тогда
					Количество = Параметры.МаксимумВыборки;
				Иначе
					Количество = Параметры.МаксимумВыборки - Результат.Количество();
				КонецЕсли;
				
				Запрос.Текст = Строка(БазовыйТекстЗапроса);
				ИзменитьМаксимумВыборки(Запрос.Текст, МаксимальноеКоличествоЗаписейВВыборке(), Количество);
				УстановитьУсловияВыборкиПоСтраницам(Запрос, Условия, ТаблицаИзменений, Параметры, Истина, Индекс);
				
				Если ОтложитьУничтожениеВременныхТаблиц И Индекс = ИндексПоследнегоУсловия Тогда
					Запрос.Текст = Запрос.Текст + ТекстЗапросаУничтоженияВременныхТаблиц;
				КонецЕсли;
				
				Выгрузка = Запрос.Выполнить().Выгрузить();
				
				Если Результат = Неопределено Тогда
					Результат = Выгрузка;
				Иначе
					Для каждого СтрокаВыгрузки Из Выгрузка Цикл
						СтрокаРезультата = Результат.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтрокаВыгрузки);
					КонецЦикла;
				КонецЕсли;
				
				Если Результат.Количество() = Параметры.МаксимумВыборки Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ИзменитьМаксимумВыборки(Запрос.Текст, МаксимальноеКоличествоЗаписейВВыборке(), Параметры.МаксимумВыборки);
			УстановитьУсловияВыборкиПоСтраницам(Запрос, Условия, ТаблицаИзменений, Параметры, Истина);
			Результат = Запрос.Выполнить().Выгрузить();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Удалить из пакета запросов запросы удаления временных таблиц и вернуть их, как результат этой функции.
//
// Параметры:
//  ТекстЗапроса - Строка - изменяемый текст запроса.
//
// Возвращаемое значение:
//  Строка - фрагмент текста запроса с удалением временных таблиц.
//
Функция ВырезатьУничтожениеВременныхТаблиц(ТекстЗапроса)
	
	ЗапросыУничтожения = Новый Массив;
	ПозицияУничтожить = СтрНайти(ТекстЗапроса, "УНИЧТОЖИТЬ");
	
	Пока ПозицияУничтожить > 0 Цикл
		ПозицияРазделителя = СтрНайти(ТекстЗапроса, ";",, ПозицияУничтожить);
		
		Если ПозицияРазделителя > 0 Тогда
			ЗапросУничтожения = Сред(ТекстЗапроса, ПозицияУничтожить, ПозицияРазделителя - ПозицияУничтожить + 1);
		Иначе
			ЗапросУничтожения = Сред(ТекстЗапроса, ПозицияУничтожить);
		КонецЕсли;
		
		Если ЗапросыУничтожения.Количество() = 0 Тогда
			ЗапросыУничтожения.Добавить(Символы.ПС);
		КонецЕсли;
		
		ЗапросыУничтожения.Добавить(ЗапросУничтожения);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗапросУничтожения, "");
		ПозицияУничтожить = СтрНайти(ТекстЗапроса, "УНИЧТОЖИТЬ");
	КонецЦикла;
	
	Возврат СтрСоединить(ЗапросыУничтожения, Символы.ПС);
	
КонецФункции

// Добавить в запрос условия, ограничивающие выборку по страницам.
//
// Постраничная выборка работает в двух режимах:
// - выборка сверху - записи большие указанной (аналогично динамическому списку);
// - выборка диапазона - записи между двумя указанными записями включительно.
//
// Параметры:
//  Запрос - Запрос - запрос выборки данных.
//  Условия - см. УсловияДляДиапазонаСтраницы().
//  Таблица - Строка - имя таблицы из которой выполняется выборка.
//  Параметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
//  Первые - Булево - Истина, если это первые условия в запросе.
//  НомерУсловия - Число - номер обрабатываемого условия.
//
Процедура УстановитьУсловияВыборкиПоСтраницам(Запрос, Условия, Таблица, Параметры, Первые, НомерУсловия = Неопределено)
	
	ПерваяЗапись = Параметры.ПерваяЗапись;
	ПоследняяЗапись = Параметры.ПоследняяЗапись;
	ВыбратьДиапазон = ПерваяЗапись <> Неопределено
	                И ПоследняяЗапись <> Неопределено;
	
	Если Не ВыбратьДиапазон Тогда
		ПерваяЗапись = Параметры.ПоследняяВыбраннаяЗапись;
	КонецЕсли;
	
	Колонки = Условия.Колонки;
	КоличествоКолонок = Колонки.Количество();
	УсловияИ = Новый Массив;
	УсловияИли = Новый Массив;
	ЕстьУсловияИли = (НомерУсловия = Неопределено);
	ЕстьТаблица = Не ПустаяСтрока(Таблица);
	ШаблонУсловияИ = ?(ЕстьТаблица, Таблица + ".%1 %2 &%3", "%1 %2 &%3");
	ШаблонУсловияИли = "(%1)";
	РазделительУсловийИ =
		"
		|	И ";
	РазделительУсловийИли =
		"
		|	) ИЛИ (
		|	";
	
	Если ЕстьУсловияИли Тогда
		ИндексНачала = 0;
		ИндексОкончания = Условия.Количество() - 1;
	Иначе
		ИндексНачала = НомерУсловия;
		ИндексОкончания = НомерУсловия;
	КонецЕсли;
	
	Для ИндексСтроки = ИндексНачала По ИндексОкончания Цикл
		УсловияИ.Очистить();
		
		Для ИндексКолонки = 0 По КоличествоКолонок - 1 Цикл
			Оператор = Условия[ИндексСтроки][ИндексКолонки];
			ИндексПоля = ?(ВыбратьДиапазон, Цел(ИндексКолонки / 2), ИндексКолонки) + 2;
			
			Если Не ПустаяСтрока(Оператор) Тогда
				Колонка = Колонки[ИндексКолонки];
				ИмяПоляПолное = Колонка.Заголовок;
				ИмяПараметра = Колонка.Имя + "Значение";
				ИмяПоля = ИмяКолонкиДляЗапроса(ИмяПоляПолное);
				Условие = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУсловияИ, ИмяПоля, Оператор, ИмяПараметра);
				УсловияИ.Добавить(Условие);
				
				Если ЭтоИмяКолонкиКонцаДиапазона(ИмяПоляПолное) Тогда
					ЗначениеПараметра = ПоследняяЗапись[ИндексПоля].Значение;
				Иначе
					ЗначениеПараметра = ПерваяЗапись[ИндексПоля].Значение;
				КонецЕсли;
				
				Запрос.УстановитьПараметр(ИмяПараметра, ЗначениеПараметра);
			КонецЕсли;
		КонецЦикла;
		
		ТекстУсловий = СтрСоединить(УсловияИ, РазделительУсловийИ);
		
		Если ЕстьУсловияИли Тогда
			УсловияИли.Добавить(ТекстУсловий);
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьУсловияИли Тогда
		ТекстУсловийИли = СтрСоединить(УсловияИли, РазделительУсловийИли);
		ТекстУсловий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонУсловияИли, ТекстУсловийИли);
	КонецЕсли;
	
	Если Не Первые Тогда
		ТекстУсловий = "	И " + ТекстУсловий;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоСтраницам", ТекстУсловий);
	
КонецПроцедуры

// Получить условия для выборки записей большие указанной (аналогично динамическому списку).
//
// Параметры:
//  ПоляВыборки - Массив - выбираемые запросом поля.
//  Параметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
//  Направления - Массив - направления упорядочивания (ВОЗР, УБЫВ) в количестве равном количеству ПоляВыборки.
//              - Неопределено - порядок не указывается (всегда ВОЗР).
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыеУсловияПостраничнойВыборки().
//
Функция УсловияДляСледующейСтраницы(ПоляВыборки, Параметры, Направления)
	
	ВсеУсловия = НовыеУсловияПостраничнойВыборки(ПоляВыборки);
	КоличествоПолей = ПоляВыборки.Количество();
	
	Пока КоличествоПолей > 0 Цикл
		НовыеУсловия = ВсеУсловия.Добавить();
		
		Для НомерУсловия = 1 По КоличествоПолей Цикл
			ИмяКолонкиПоля = ПоляВыборки[НомерУсловия - 1];
			
			Если НомерУсловия < КоличествоПолей Тогда
				Оператор = "=";
			Иначе
				Оператор = ОператорБольше(Направления[НомерУсловия - 1]);
			КонецЕсли;
			
			НовыеУсловия[ИмяКолонкиИзПоляВыборки(ИмяКолонкиПоля)] = Оператор;
		КонецЦикла;
		
		КоличествоПолей = КоличествоПолей - 1;
	КонецЦикла;
	
	Возврат ВсеУсловия;
	
КонецФункции

// Получить условия для выборки записей между двумя указанными записями включительно.
//
// Параметры:
//  ПоляВыборки - Массив - выбираемые запросом поля.
//  Параметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки.
//  Направления - Массив - направления упорядочивания (ВОЗР, УБЫВ) в количестве равном количеству ПоляВыборки.
//              - Неопределено - порядок не указывается (всегда ВОЗР).
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыеУсловияПостраничнойВыборки().
//
Функция УсловияДляДиапазонаСтраницы(ПоляВыборки, Параметры, Направления)
	
	ВсеУсловия = НовыеУсловияПостраничнойВыборки(ПоляВыборки, Истина);
	ПерваяЗапись = Параметры.ПерваяЗапись;
	ПоследняяЗапись = Параметры.ПоследняяЗапись;
	КоличествоПолей = ПоляВыборки.Количество();
	ВсегоПолей = ПоляВыборки.Количество();
	ПозицияВставки = 0;
	
	Пока КоличествоПолей > 0 Цикл
		ТекущиеПоляРавны = ЗаписиРавны(ПерваяЗапись, ПоследняяЗапись, КоличествоПолей);
		
		Если ТекущиеПоляРавны И КоличествоПолей <> ВсегоПолей Тогда
			Прервать;
		КонецЕсли;
		
		ПервыеУсловия = ВсеУсловия.Вставить(ПозицияВставки);
		ПозицияВставки = ПозицияВставки + 1;
		ПредыдущиеПоляРавны = ЗаписиРавны(ПерваяЗапись, ПоследняяЗапись, КоличествоПолей - 1);
		
		Если Не ПредыдущиеПоляРавны Тогда
			ПоследниеУсловия = ВсеУсловия.Вставить(ПозицияВставки);
		КонецЕсли;
		
		Для НомерУсловия = 1 По КоличествоПолей Цикл
			ИмяКолонкиПоля = ИмяКолонкиИзПоляВыборки(ПоляВыборки[НомерУсловия - 1]);
			ИмяКолонкиПоляПоДиапазону = ИмяКолонкиДиапазона(ИмяКолонкиПоля);
			
			Если НомерУсловия < КоличествоПолей Или ТекущиеПоляРавны И КоличествоПолей = ВсегоПолей Тогда
				ОператорПервый = "=";
				ОператорПоследний = "=";
			Иначе
				Направление = Направления[НомерУсловия - 1];
				
				Если КоличествоПолей = ВсегоПолей Тогда
					ОператорПервый = ОператорБольшеИлиРавно(Направление);
					ОператорПоследний = ОператорМеньшеИлиРавно(Направление);
				Иначе
					ОператорПервый = ОператорБольше(Направление);
					ОператорПоследний = ОператорМеньше(Направление);
				КонецЕсли;
				
				// Ограничение по диапазону
				Если ПредыдущиеПоляРавны Тогда
					ПервыеУсловия[ИмяКолонкиПоляПоДиапазону] = ОператорПоследний;
				КонецЕсли;
			КонецЕсли;
			
			// Выборка по первой записи
			ПервыеУсловия[ИмяКолонкиПоля] = ОператорПервый;
			
			// Выборка по последней записи
			Если Не ПредыдущиеПоляРавны Тогда
				ПоследниеУсловия[ИмяКолонкиПоляПоДиапазону] = ОператорПоследний;
			КонецЕсли;
		КонецЦикла;
		
		КоличествоПолей = КоличествоПолей - 1;
	КонецЦикла;
	
	Возврат ВсеУсловия;
	
КонецФункции

// Возвращает результат сравнения двух записей.
//
// Параметры:
//  ПерваяЗапись - см. ОбновлениеИнформационнойБазыСлужебный.НовыйКлючЗаписи().
//  ПоследняяЗапись - см. ОбновлениеИнформационнойБазыСлужебный.НовыйКлючЗаписи().
//  КоличествоПолей - Число - количество прикладных полей в ключе записи.
//
// Возвращаемое значение:
//  Булево - Истина, если записи равны.
//
Функция ЗаписиРавны(ПерваяЗапись, ПоследняяЗапись, КоличествоПолей)
	
	Для Индекс = 2 По КоличествоПолей + 2 - 1 Цикл
		Если ПерваяЗапись[Индекс].Значение <> ПоследняяЗапись[Индекс].Значение Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Возвращает таблицу значений с условиями постраничной выборки.
//
// Параметры:
//  ПоляВыборки - Массив - выбираемые запросом поля.
//  ДляДиапазона - Булево - Истина, если таблица будет использоваться для ограничения по диапазону.
//                 В этом случае, добавляются дополнительные колонки для нижнего условия диапазона.
//
// Возвращаемое значение:
//  ТаблицаЗначений - колонки таблицы описывают поля запроса, где в заголовке хранится полное имя поля.
//                    Имена колонок формируются динамические из имен полей выборки запроса.
//                    Каждая строка описывает условия для одного запроса выбирающего порции данных для страницы
//                    или группы условий при выборке через ИЛИ. В ячейках таблицы хранятся условия
//                    ("=", ">", "<", ">=", "<=")..
// 
Функция НовыеУсловияПостраничнойВыборки(ПоляВыборки, ДляДиапазона = Ложь)
	
	Условия = Новый ТаблицаЗначений;
	Колонки = Условия.Колонки;
	
	Для каждого ПолеВыборки Из ПоляВыборки Цикл
		Имя = ИмяКолонкиИзПоляВыборки(ПолеВыборки);
		Колонки.Добавить(Имя,, ПолеВыборки);
		
		Если ДляДиапазона Тогда
			Колонки.Добавить(ИмяКолонкиДиапазона(Имя),, ПолеВыборки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Условия;
	
КонецФункции

// Возвращает имя колонки для условия по нижней границе диапазона постраничной выборки.
//
// Параметры:
//  ИмяПоля - Строка - имя поля.
//
// Возвращаемое значение:
//  Строка - имя поля для условия по нижней границе диапазона.
//
Функция ИмяКолонкиДиапазона(ИмяПоля)
	
	Возврат ИмяПоля + "_";
	
КонецФункции

// Возвращает имя, например, для колонки таблицы значений, полученное из полного имени поля запроса.
//
// Параметры:
//  Имя - Строка - полное имя поля запроса (возможно, разделенное точкой).
//
// Возвращаемое значение:
//  Строка - имя поля.
//
Функция ИмяКолонкиИзПоляВыборки(Имя)
	
	ЗаменяемыеСимволы = ".,() ";
	ИмяКолонки = Строка(Имя);
	
	Для Индекс = 1 По СтрДлина(ЗаменяемыеСимволы) Цикл
		Символ = Сред(ЗаменяемыеСимволы, Индекс, 1);
		ИмяКолонки = СтрЗаменить(ИмяКолонки, Символ, "_");
	КонецЦикла;
	
	Возврат ИмяКолонки;
	
КонецФункции

// Имя поля, полученное из колонки таблицы условий постраничной выборки (см. НовыеУсловияПостраничнойВыборки()).
//
// Параметры:
//  ИмяПоля - Строка - имя поля.
//
// Возвращаемое значение:
//  Строка - имя поля без служебных символов.
//
Функция ИмяКолонкиДляЗапроса(ИмяПоля)
	
	Если ЭтоИмяКолонкиКонцаДиапазона(ИмяПоля) Тогда
		Возврат Лев(ИмяПоля, СтрДлина(ИмяПоля) - 1);
	Иначе
		Возврат ИмяПоля;
	КонецЕсли;
	
КонецФункции

// Определяет, описывает ли колонка конец диапазона постраничной выборки.
//
// Параметры:
//  ИмяПоля - Строка - имя поля.
//
// Возвращаемое значение:
//  Булево - Истина, если это поле описывает конец диапазона постраничной выборки.
//
Функция ЭтоИмяКолонкиКонцаДиапазона(ИмяПоля)
	
	Возврат Прав(ИмяПоля, 1) = "_";
	
КонецФункции

// Проверка корректности заполнения параметров выборки данных.
//
// Параметры - Структура - см.
//             ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
//
// Возвращаемое значение:
//  Булево - Истина, если выборка многопоточная.
//
Процедура ПроверитьПараметрыВыборки(Параметры)
	
	Если Не Параметры.ВыбиратьПорциями И ЭтоПостраничнаяВыборка(Параметры) Тогда
		ВызватьИсключение НСтр("ru = 'Многопоточный обработчик обновления обязан выбирать данные порциями.'");
	КонецЕсли;
	
КонецПроцедуры

// Проверка, что имя начинается на букву или подчеркивание и кроме них может содержать только цифры.
//
// Параметры:
//  Имя - Строка - проверяемое имя.
//
// Возвращаемое значение:
//  Булево - Истина, если это правильный идентификатор.
//
Функция ИмяСоответствуетТребованиямИменованияСвойств(Имя)
	
	Цифры = "1234567890";
	Если Имя = "" Или СтрНайти(Цифры, Лев(Имя, 1)) > 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НедопустимыеСимволы = """/\[]:;|=-?*<>,.()+№@!%^&~" + " ";
	НовоеИмя = СтрСоединить(СтрРазделить(Имя, НедопустимыеСимволы, Истина));
	Возврат (НовоеИмя = Имя);
	
КонецФункции

// Формирует фрагмент текста запроса с выбираемыми полями.
//
// Параметры:
//  ИменаПолей - Массив - имена полей в виде массива.
//  Псевдонимы - Массив - псевдонимы в виде массива с тем же количеством элементов, что и в ИменаПолей.
//             - Неопределено - в этом случае, псевдонимы равны именам полей.
//  ИмяТаблицы - Строка - имя таблицы в которой расположены поля.
//                        если указана пустая строка, то имя таблицы не подставляется.
//  Дополнительные - Булево - Истина, если это не первые поля в выборке и перед ними нужна ",".
//
// Возвращаемое значение:
//  Строка - фрагмент текста запроса с выбираемыми полями.
//
Функция ПоляДляЗапроса(ИменаПолей, Псевдонимы = Неопределено, ИмяТаблицы = "", Дополнительные = Ложь)
	
	КоличествоПолей = ИменаПолей.Количество();
	
	Если КоличествоПолей = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ЕстьПсевдонимы = Псевдонимы <> Неопределено И Псевдонимы.Количество() = КоличествоПолей;
	ПолноеИмяТаблицы = ?(ПустаяСтрока(ИмяТаблицы), "", ИмяТаблицы + ".");
	ИспользуемыеПсевдонимы = Новый Соответствие;
	Поля = Новый Массив;
	
	Для Индекс = 0 По КоличествоПолей - 1 Цикл
		ИмяПоля = ИменаПолей[Индекс];
		
		Если ЕстьПсевдонимы Тогда
			Псевдоним = Псевдонимы[Индекс];
		Иначе
			Состав = СтрРазделить(ИмяПоля, ".");
			Псевдоним = Состав[Состав.Количество() - 1];
			ИспользуемыйПсевдоним = ИспользуемыеПсевдонимы[Псевдоним];
			
			Если ИспользуемыйПсевдоним = Неопределено Тогда
				ИспользуемыеПсевдонимы[Псевдоним] = 1;
			Иначе
				ИспользуемыеПсевдонимы[Псевдоним] = ИспользуемыеПсевдонимы[Псевдоним] + 1;
				Псевдоним = Псевдоним + Формат(ИспользуемыеПсевдонимы[Псевдоним], "ЧГ=0");
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяСоответствуетТребованиямИменованияСвойств(Псевдоним) Тогда
			Псевдоним = " КАК " + Псевдоним;
		Иначе
			Псевдоним = "";
		КонецЕсли;
		
		Имя = ПолноеИмяТаблицы + ИмяПоля + Псевдоним;
		Поля.Добавить(Имя);
	КонецЦикла;
	
	Разделитель = ",
		|	";
	
	Возврат ?(Дополнительные, Разделитель, "") + СтрСоединить(Поля, Разделитель);
	
КонецФункции

// Формирует фрагмент текста запроса с указанным порядком.
//
// Параметры:
//  ИменаПолей - Массив - имена полей в виде массива.
//  Направления - Массив - направления упорядочивания (ВОЗР, УБЫВ) в количестве равном количеству ИменаПолей.
//              - Неопределено - порядок не указывается (всегда ВОЗР).
//  ИмяТаблицы - Строка - имя таблицы в которой расположены поля.
//                        если указана пустая строка, то имя таблицы не подставляется.
//  Дополнительные - Булево - Истина, если это не первые поля в выборке и перед ними нужна ",".
//
// Возвращаемое значение:
//  Строка - фрагмент текста запроса для упорядочивания.
//
Функция УпорядочиванияДляЗапроса(ИменаПолей, Направления = Неопределено, ИмяТаблицы = "", Дополнительные = Ложь)
	
	КоличествоПолей = ИменаПолей.Количество();
	
	Если КоличествоПолей = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ПолноеИмяТаблицы = ?(ПустаяСтрока(ИмяТаблицы), "", ИмяТаблицы + ".");
	ЕстьНаправления = Направления <> Неопределено И Направления.Количество() = КоличествоПолей;
	Упорядочивание = Новый Массив;
	
	Для Индекс = 0 По КоличествоПолей - 1 Цикл
		ИмяПоля = ИменаПолей[Индекс];
		
		Если ЕстьНаправления Тогда
			ТекущееНаправление = Направления[Индекс];
			Направление = ?(ПустаяСтрока(ТекущееНаправление), "", " " + ТекущееНаправление);
		Иначе
			Направление = "";
		КонецЕсли;
		
		Порядок = ПолноеИмяТаблицы + ИмяПоля + Направление;
		Упорядочивание.Добавить(Порядок);
	КонецЦикла;
	
	Разделитель = ",
		|	";
	
	Возврат ?(Дополнительные, Разделитель, "") + СтрСоединить(Упорядочивание, Разделитель);
	
КонецФункции

// Установить размер выборки запроса, получающего данные для обновления.
//
// Параметры:
//  ТекстЗапроса - Строка - текст модифицируемого запроса.
//  Параметры - Структура - см.
//              ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
//
Процедура УстановитьРазмерВыборки(ТекстЗапроса, Параметры)
	
	РазмерВыборки = ?(Параметры.ВыбиратьПорциями, Параметры.МаксимумВыборки, Неопределено);
	ИзменитьМаксимумВыборки(ТекстЗапроса, 10000, РазмерВыборки);
	
КонецПроцедуры

// Установить размер выборки запроса (ПЕРВЫЕ N).
//
// Параметры:
//  ТекстЗапроса - Строка - текст изменяемого запроса.
//  ТекущееКоличество - Число - количество, установленное в текущем тексте запроса.
//  НовоеКоличество - Число - новое значение для "ПЕРВЫЕ N".
//                  - Неопределено - выбрать все (без ПЕРВЫЕ N).
//
Процедура ИзменитьМаксимумВыборки(ТекстЗапроса, ТекущееКоличество, НовоеКоличество)
	
	ТекстПоиска = "ПЕРВЫЕ " + Формат(ТекущееКоличество, "ЧН=0; ЧГ=0");
	
	Если НовоеКоличество = Неопределено Тогда
		ТекстЗамены = "";
	Иначе
		ТекстЗамены = "ПЕРВЫЕ " + Формат(НовоеКоличество, "ЧН=0; ЧГ=0");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
	
КонецПроцедуры

// Установить поля выборки с учетом многопоточных обработчиков обновления.
//
// Параметры:
//  Запрос - Запрос - модифицируемый запрос.
//  ИменаПолей - Массив - имена полей для выборки.
//  Псевдонимы - Массив - псевдонимы выбираемых полей.
//  ИмяТаблицы - Строка - имя таблицы в которой расположены поля.
//                        если указана пустая строка, то имя таблицы не подставляется.
//
Процедура УстановитьПоляПоСтраницам(Запрос, ПараметрыПостроения)
	
	ВыбираемыеПоля = ПоляДляЗапроса(ПараметрыПостроения.ПоляВыборки,
		ПараметрыПостроения.Псевдонимы,
		ПараметрыПостроения.ИмяТаблицы);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВыбираемыеПоля", ВыбираемыеПоля);
	
КонецПроцедуры

// Установить порядок выборки с учетом многопоточных обработчиков обновления.
//
// Параметры:
//  Запрос - Запрос - модифицируемый запрос.
//  ИменаПолей - Массив - имена полей для выборки.
//  Параметры - Структура - см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
//  ИмяТаблицы - Строка - имя таблицы в которой расположены поля.
//                        если указана пустая строка, то имя таблицы не подставляется.
//  Направления - Массив - направления упорядочивания (ВОЗР, УБЫВ) в количестве равном количеству ИменаПолей.
//              - Неопределено - порядок не указывается (всегда ВОЗР).
//
Процедура УстановитьПорядокПоСтраницам(Запрос, ПараметрыПостроения)
	
	ПоляВыборки = ПараметрыПостроения.ПоляВыборки;
	ИмяТаблицы = ПараметрыПостроения.ИмяТаблицы;
	Направления = ПараметрыПостроения.Направления;
	
	Если Направления.Количество() = 0 Тогда
		Направления.Добавить("УБЫВ");
		
		Если ПараметрыПостроения.ПостраничнаяВыборка Тогда
			Для Индекс = 1 По ПоляВыборки.Количество() - 1 Цикл
				Направления.Добавить("");
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПорядокВыборки = УпорядочиванияДляЗапроса(ПоляВыборки, Направления, ИмяТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПорядокВыборки", ПорядокВыборки);
	
КонецПроцедуры

// Позволяет определить постраничная ли это выборка.
//
// Параметры:
//   Параметры - Структура - см.
//             ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки.
//
// Возвращаемое значение:
//  Булево - Истина, если выборка многопоточная.
//
Функция ЭтоПостраничнаяВыборка(Параметры)
	
	Возврат Параметры.Свойство("СпособВыборки") И Параметры.ВыбиратьПорциями;
	
КонецФункции

// Возвращает, выполняется ли упорядочивание по возрастанию.
//
// Параметры:
//  Направление - Строка - Проверяемое направление упорядочивания.
//
// Возвращаемое значение:
//  Булево - Истина, если упорядочивание по возрастанию, иначе Ложь.
//
Функция УпорядочиваниеПоВозрастанию(Направление)
	
	Если Направление = Неопределено Тогда
		Возврат Истина;
	Иначе
		ПорядокВРег = ВРег(Направление);
	
		// НеПереводить: "УБЫВ".
		Возврат НЕ (ПорядокВРег = "УБЫВ" Или ПорядокВРег = "DESC");
	КонецЕсли;
	
КонецФункции

// Возвращает оператор ">" для страничной выборки, с учетом порядка упорядочивания.
//
// Параметры:
//  Направление - Строка - Проверяемое направление упорядочивания.
//
// Возвращаемое значение:
//  Строка - оператор ">", если Порядок = "ВОЗР", иначе "<".
//
Функция ОператорБольше(Направление)
	
	Возврат ?(УпорядочиваниеПоВозрастанию(Направление), ">", "<");
	
КонецФункции

// Возвращает оператор "<" для страничной выборки, с учетом порядка упорядочивания.
//
// Параметры:
//  Направление - Строка - Проверяемое направление упорядочивания.
//
// Возвращаемое значение:
//  Строка - оператор "<", если Порядок = "ВОЗР", иначе ">".
//
Функция ОператорМеньше(Направление)
	
	Возврат ?(УпорядочиваниеПоВозрастанию(Направление), "<", ">");
	
КонецФункции

// Возвращает оператор ">=" для страничной выборки, с учетом порядка упорядочивания.
//
// Параметры:
//  Направление - Строка - Проверяемое направление упорядочивания.
//
// Возвращаемое значение:
//  Строка - оператор ">=", если Порядок = "ВОЗР", иначе "<=".
//
Функция ОператорБольшеИлиРавно(Направление)
	
	Возврат ?(УпорядочиваниеПоВозрастанию(Направление), ">=", "<=");
	
КонецФункции

// Возвращает оператор "<=" для страничной выборки, с учетом порядка упорядочивания.
//
// Параметры:
//  Направление - Строка - Проверяемое направление упорядочивания.
//
// Возвращаемое значение:
//  Строка - оператор "<=", если Порядок = "ВОЗР", иначе ">=".
//
Функция ОператорМеньшеИлиРавно(Направление)
	
	Возврат ?(УпорядочиваниеПоВозрастанию(Направление), "<=", ">=");
	
КонецФункции

// Возвращает имя объекта метаданных из его полного имени.
//
// Параметры:
//  ПолноеИмя - Строка - полное имя объекта метаданных.
//
// Возвращаемое значение:
//  Строка - имя объекта метаданных (после точки).
//
Функция ИмяОбъектаМетаданных(ПолноеИмя)
	
	Позиция = СтрНайти(ПолноеИмя, ".", НаправлениеПоиска.СКонца);
	
	Если Позиция > 0  Тогда
		Возврат Сред(ПолноеИмя, Позиция + 1);
	Иначе
		Возврат ПолноеИмя;
	КонецЕсли;
	
КонецФункции

// Возвращает признак наличия полей упорядочивания.
//
// Параметры:
//  
Функция ЗаданыПоляУпорядочивания(ДополнительныеПараметры)
	
	Возврат ДополнительныеПараметры.ПоляУпорядочивания.Количество() > 0;
	
КонецФункции

// Возвращает вид источника данных (см. ДополнительныеПараметрыВыборкиДанныхДляОбработки(), п. 1 и п. 2.
//
// Параметры:
//  ДополнительныеИсточникиДанных - Структура - см. ДополнительныеПараметрыВыборкиДанныхДляОбработки().
//
// Возвращаемое значение:
//  Булево - Истина, если это простой набор источников, как в п.1, Ложь - если это иерархия соответствий,
//           как в п. 2.
//
Функция ЭтоПростойИсточникДанных(ДополнительныеИсточникиДанных)
	
	ПростойИсточник = Ложь;
	СложныйИсточник = Ложь;
	ТипСоответствие = Тип("Соответствие");
	
	Для каждого КлючИЗначение Из ДополнительныеИсточникиДанных Цикл
		Если ТипЗнч(КлючИЗначение.Значение) = ТипСоответствие Тогда
			СложныйИсточник = Истина;
		Иначе
			ПростойИсточник = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ПростойИсточник И СложныйИсточник Тогда
		Ошибка = НСтр("ru = 'Источник данных задан неверно (см. ДополнительныеПараметрыВыборкиДанныхДляОбработки()).'");
		ВызватьИсключение Ошибка;
	Иначе
		Возврат ПростойИсточник;
	КонецЕсли;
	
КонецФункции

#КонецОбласти