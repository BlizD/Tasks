///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик события ПриИзменении элемента формы, подлежащего склонению.
// 
// Параметры:
// 	Форма - УправляемаяФорма - форма, содержащая элемент, подлежащий склонению.
//
Процедура ПриИзмененииПредставления(Форма) Экспорт
	
	Форма.ИзмененоПредставление = Истина;
	Форма.ПодключитьОбработчикОжидания("Подключаемый_ПросклонятьПредставлениеПоВсемПадежам", 2, Истина);
		
КонецПроцедуры

// Склоняет переданную фразу по всем падежам.
// Для вызова из обработчика формы Подключаемый_ПросклонятьПредставлениеПоВсемПадежам.
//
// Параметры:
//  Форма 			- УправляемаяФорма - форма объекта склонения.
//  Представление   - Строка - Строка для склонения.
//  ЭтоФИО       	- Булево - Признак склонения ФИО.
//	Пол				- Число	- Пол физического лица (в случае склонения ФИО): 1 - мужской, 2 - женский.
//  ПоказыватьСообщения - Булево - Признак, определяющий нужно ли показывать пользователю сообщения об ошибках.
//
Процедура ПросклонятьПредставлениеПоВсемПадежам(Форма, Представление, ЭтоФИО = Ложь, Пол = Неопределено, ПоказыватьСообщения = Ложь) Экспорт

	Если Форма.ИзмененоПредставление Или Форма.Склонения = Неопределено Тогда
		Склонения = Форма.Склонения;
		СклонениеПредставленийОбъектовВызовСервера.ПросклонятьПредставлениеПоВсемПадежам(Представление, Склонения, ЭтоФИО, Пол, ПоказыватьСообщения);
		Форма.Склонения = Склонения;
		Форма.ИзмененоПредставление = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команды "Склонения" формы объекта склонения.
//
// Параметры:
//  Форма 			- УправляемаяФорма - форма объекта склонения.
//  Представление   - Строка - Строка для склонения.
//  ЭтоФИО       	- Булево - Признак склонения ФИО.
//	Пол				- Число	- Пол физического лица (в случае склонения ФИО)
//							1 - мужской 
//							2 - женский.
//
Процедура ОбработатьКомандуСклонения(Форма, Представление, ЭтоФИО = Ложь, Пол = Неопределено) Экспорт
			
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);

	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуСклоненияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ЕстьПравоДоступаКОбъекту = СклонениеПредставленийОбъектовВызовСервера.ЕстьПравоДоступаКОбъекту(Форма.Параметры.Ключ);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Склонения", Форма.Склонения);
	СтруктураПараметров.Вставить("ЭтоФИО", ЭтоФИО);
	СтруктураПараметров.Вставить("Пол", Пол);
	СтруктураПараметров.Вставить("Представление", Представление);
	СтруктураПараметров.Вставить("ИзмененоПредставление", Форма.ИзмененоПредставление);
	СтруктураПараметров.Вставить("ТолькоПросмотр", Не ЕстьПравоДоступаКОбъекту);	
	
	ОткрытьФорму("ОбщаяФорма.Склонения", СтруктураПараметров, Форма, , , , Оповещение);
	
	Форма.ОтключитьОбработчикОжидания("Подключаемый_ПросклонятьПредставлениеПоВсемПадежам");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Завершение процедуры ОбработатьКомандуСклонения.
//
Процедура ОткрытьФормуСклоненияЗавершение(РезультатРедактирования, ДополнительныеПараметры) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	Если РезультатРедактирования <> Неопределено Тогда
		
		Форма.Склонения = Новый ФиксированнаяСтруктура(РезультатРедактирования);
		Форма.Модифицированность = Истина;
		Форма.ИзмененоПредставление = Ложь;
		
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти