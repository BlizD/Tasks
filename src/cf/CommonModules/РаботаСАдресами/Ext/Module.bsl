///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает наименование субъекта РФ для адреса или пустую строку, если субъект не определен.
// Если переданная строка не содержит информации об адресе, то будет вызвано исключение.
//
// Параметры:
//    Адрес - Строка, Структура - Строка в формате JSON или XML соответствующая XDTO пакету Адрес.
//
// Возвращаемое значение:
//    Строка - Наименование региона.
//
Функция РегионАдресаКонтактнойИнформации(Знач Адрес) Экспорт
	
	Если ТипЗнч(Адрес) = Тип("Строка") Тогда
		
		Если ПустаяСтрока(Адрес) Тогда
			Возврат "";
		КонецЕсли;
	
		Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Адрес) Тогда
			Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВJSON(Адрес, Перечисления.ТипыКонтактнойИнформации.Адрес);
		КонецЕсли;
		
		Адрес = УправлениеКонтактнойИнформациейСлужебный.JSONВКонтактнуюИнформациюПоПолям(Адрес, Перечисления.ТипыКонтактнойИнформации.Адрес);
		
	ИначеЕсли ТипЗнч(Адрес) <> Тип("Структура") Тогда
		
		ВызватьИсключение НСтр("ru = 'Невозможно определить субъекта РФ, ожидается адрес.'");
		
	КонецЕсли;
	
	СубъектРФ = СокрЛП(Адрес.Area + " " + Адрес.AreaType);
	Возврат СубъектРФ;
	
КонецФункции

// Возвращает наименование города для адреса РФ или пустую строку для иностранного адреса.
// Если переданная строка не содержит информации об адресе, то будет вызвано исключение.
//
// Параметры:
//    Адрес - Строка, Структура - Строка в формате JSON или строка XML соответствующая XDTO пакету Адрес.
//
// Возвращаемое значение:
//    Строка - Наименование города.
//
Функция ГородАдресаКонтактнойИнформации(Знач Адрес) Экспорт
	
	Если ТипЗнч(Адрес) = Тип("Строка") Тогда
		
		Если ПустаяСтрока(Адрес) Тогда
			Возврат "";
		КонецЕсли;
	
		Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Адрес) Тогда
			Адрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВJSON(Адрес, Перечисления.ТипыКонтактнойИнформации.Адрес);
		КонецЕсли;
		
		Адрес = УправлениеКонтактнойИнформациейСлужебный.JSONВКонтактнуюИнформациюПоПолям(Адрес, Перечисления.ТипыКонтактнойИнформации.Адрес);
		
	ИначеЕсли ТипЗнч(Адрес) <> Тип("Структура") Тогда
		
		ВызватьИсключение НСтр("ru = 'Невозможно определить город, ожидается адрес.'");
		
	КонецЕсли;
	
	Регион = СокрЛП(Адрес.Area + " " + Адрес.AreaType);
	
	Если ЭтоГородФедеральногоЗначения(Регион) Тогда
		Возврат Регион;
	КонецЕсли;
	
	Город = СокрЛП(Адрес.City + " " + Адрес.CityType);
	
	Возврат Город;
	
КонецФункции

// Возвращает сведения об адресе в виде отдельных частей адреса и различных кодов (код региона, ОКТМО и др.).
//
// Параметры:
//   Адреса                 - Массив - адреса во внутреннем формате JSON или в XML, соответствующий XDTO-пакету Адрес
//                                     или XDTO объектов, соответствующих XDTO-пакету Адрес.
//   ДополнительныеПараметры - Структура - для уточнения возвращаемого значения:
//       * БезПредставлений - Булево - Если Истина, то поле Представление будет отсутствовать. По умолчанию, Ложь.
//       * КодыАдреса       - Булево - Если Истина, то результат содержит поля ИдентификаторАдресногоОбъекта, ИдентификаторДома
//                                     и структура с кодами адреса(Идентификаторы, ДополнительныеКоды, КодыКЛАДР).
//                                     Подробнее см. в возвращаемом значении структуры Идентификаторы и
//                                     ДополнительныеКоды. По умолчанию, Ложь. Если в адресе отсутствуют идентификаторы
//                                     и в программу не загружены идентификаторы адресных объектов адреса, то получение
//                                     идентификаторов может инициировать http запрос к веб-сервису 1С orgaddress.
//       * КодыКЛАДР        - Булево - Если Истина, то возвращается структура КодыКЛАДР. По умолчанию Ложь.
//                                     Если в адресе отсутствуют коды и в программу не загружены адресные объекты адреса,
//                                     Получение кодов может инициировать http запрос к веб-сервису 1С orgaddress.
//       * ПолноеНаименованиеСокращений - Булево - Если Истина, то возвращается полное наименование адресных объектов.
//       * НаименованиеВключаетСокращение - Булево - Если Истина, то поля содержат сокращения в наименованиях адресных объектов.
//       * ПроверитьАдрес   - Булево - Если Истина, то адрес будет проверен на соответствие ФИАС. По умолчанию, Ложь.
//                                     Если в программу не загружены проверяемые адресные объекты,
//                                     то проверка адреса может инициировать http запрос к веб-сервису 1С orgaddress.
//
// Возвращаемое значение:
//   Массив - Содержит массив структур, содержимое структуры см. в описании функции СведенияОбАдресе.
//
Функция СведенияОбАдресах(Адреса, ДополнительныеПараметры = Неопределено) Экспорт
	Возврат СведенияОбАдресахВВидеСтруктуры(Адреса, ДополнительныеПараметры);
КонецФункции

// Возвращает сведения об адресе в виде отдельных частей адреса и различных кодов (код региона, ОКТМО и др.).
//
// Параметры:
//   Адрес                  - Строка - Адрес во внутреннем формате JSON или в XML, соответствующем XDTO-пакету Адрес.
//                          - ОбъектXDTO - XDTO-объект, соответствующий XDTO пакету Адрес.
//                          - Неопределено - конструктор для получения пустых полей адреса.
//   ДополнительныеПараметры - Структура - для уточнения возвращаемого значения:
//       * БезПредставлений - Булево - Если Истина, то поле Представление будет отсутствовать. По умолчанию Ложь.
//       * КодыАдреса       - Булево - Если Истина, то результат содержит поля ИдентификаторАдресногоОбъекта, ИдентификаторДома
//                                     и структура с кодами адреса(Идентификаторы, ДополнительныеКоды, КодыКЛАДР).
//                                     Подробнее см. в возвращаемом значении структуры Идентификаторы и
//                                     ДополнительныеКоды. По умолчанию, Ложь. Если в адресе отсутствуют идентификаторы
//                                     и в программу не загружены идентификаторы адресных объектов адреса, то получение
//                                     идентификаторов может инициировать http запрос к веб-сервису 1С orgaddress.
//       * КодыКЛАДР        - Булево - Если Истина, то возвращается структура КодыКЛАДР. По умолчанию Ложь.
//                                     Если в адресе отсутствуют коды и в программу не загружены адресные объекты адреса,
//                                     Получение кодов может инициировать http запрос к веб-сервису 1С orgaddress.
//       * НаименованиеВключаетСокращение - Булево - Если Истина, то поля содержат сокращениям в наименованиях адресных объектов.
//       * ПроверитьАдрес   - Булево - Если Истина, то адрес будет проверен на соответствие ФИАС. По умолчанию Ложь.
//                                     Если в программу не загружены проверяемые адресные объекты,
//                                     то проверка адреса может инициировать http запрос к веб-сервису 1С orgaddress.
//       * ПолноеНаименованиеСокращений - Булево - Устаревший параметр. Следует использовать свойства ТипПолный и ТипКраткий.
//
// Возвращаемое значение:
//   Структура - сведения об адресе:
//        * Представление    - Строка - текстовое представление адреса по административно-территориальному делению.
//        * МуниципальноеПредставление - Строка - текстовое представление адреса по муниципальному делению.
//        * ТипАдреса        - Строка - основной тип адреса (только для адресов РФ).
//                                      Варианты: "Муниципальный", "Административно-территориальный".
//        * Страна           - Строка - текстовое представление страны.
//        * КодСтраны        - Строка - код страны по ОКСМ.
//        * Индекс           - Строка - почтовый индекс.
//        * КодРегиона       - Строка - код региона РФ.
//        * Регион           - Строка - текстовое представление региона РФ.
//        * РегионТипПолный  - Строка - полное наименование типа региона. Например: "область".
//        * РегионТипКраткий - Строка - краткое наименование типа региона. Например: "обл".
//        * РегионСокращение - Строка - устаревшее свойство. Сокращение региона, например: "обл", если
//                                      ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ, или "область", если
//                                      ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА.
//        * Округ            - Строка - устаревшее свойство. Текстовое представление округа.
//        * ОкругСокращение  - Строка - устаревшее свойство. Сокращение округа.
//        * Район            - Строка - текстовое представление района у адресов по административно-территориальному делению.
//        * РайонТипПолный   - Строка - полное наименование типа района для адреса по административно-территориальному
//                                      делению. Например: "район".
//        * РайонТипКраткий  - Строка - краткое наименование типа района для адреса по административно-территориальному
//                                      делению. Например: "р-н".
//        * РайонСокращение  - Строка - устаревшее свойство. Сокращение района для адреса по
//                                      административно-территориальному делению, например: "р-н", если
//                                      ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ, или "район", если
//                                      ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА.
//        * МуниципальныйРайон - Строка - текстовое представление муниципального района для адреса по муниципальному делению.
//        * МуниципальныйРайонТипПолный - Строка - полное наименование типа муниципального района по муниципальному
//                                                 делению. Например: "городской округ".
//        * МуниципальныйРайонТипКраткий - Строка - краткое наименование типа муниципального района для адреса по
//                                                  муниципальному делению. Например: "мун.р-н".
//        * МуниципальныйРайонСокращение - Строка - устаревшее свойство. Сокращение муниципального района для адреса по
//                                         муниципальному делению, например: "мун.р-н", если
//                                         ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ, или
//                                         "муниципальный район", если
//                                         ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА.
//        * КодМуниципальногоРайона - Строка - код муниципального района:
//                                             1- муниципальный район; 2- городской округ; 3 - внутригородская территория 
//                                             города федерального значения; 4 - муниципальный округ.
//                                             Если не удалось определить код, то возвращается пустая строка.
//        * Город            - Строка - текстовое представление города у адресов по административно-территориальному делению.
//        * ГородТипПолный   - Строка - полное наименование типа города у адресов по административно-территориальному делению.
//        * ГородТипКраткий  - Строка - текстовое представление города у адресов по административно-территориальному делению.
//        * ГородСокращение  - Строка - сокращение города  у адресов по административно-территориальному делению.
//        * Поселение            - Строка - текстовое представление поселения у адресов по муниципальному делению.
//        * ПоселениеТипПолный  - Строка - полное наименование типа сельского поселения по муниципальному делению.
//                                         Например: "сельское поселение".
//        * ПоселениеТипКраткий  - Строка - краткое наименование типа муниципального района для адреса по муниципальному
//                                          делению. Например: "с. п.".
//        * ПоселениеСокращение  - Строка - устаревшее свойство. Сокращение сельского поселения для адреса по
//                                         муниципальному делению, например: "с. п.", если
//                                         ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ, или "сельское
//                                         поселение", если ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА
//        * КодПоселения       - Строка - код поселения: 1 - городское поселение; 2 - сельское поселение; 3-  межселенная
//                                        территория в составе муниципального района; 4 - внутригородской район городского округа;
//                                        Если не удалось определить код, то возвращается пустая строка.
//        * ВнутригородскойРайон - Строка  - текстовое представление внутригородского района.
//        * ВнутригородскойРайонТипПолный  - Строка - полное наименование типа внутригородского района. Например: "микрорайон".
//        * ВнутригородскойРайонТипКраткий - Строка - краткое наименование типа внутригородского района. Например: "мкр".
//        * ВнутригородскойРайонСокращение - Строка - устаревшее свойство. Сокращение внутригородского района, например:
//                                                    "мкр", если ДополнительныеПараметры.ПолноеНаименованиеСокращений =
//                                                    ЛОЖЬ, или "микрорайон", если
//                                                    ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА
//        * НаселенныйПункт  - Строка - текстовое представление населенного пункта.
//        * НаселенныйПунктТипПолный - Строка - полное наименование типа населенного пункта. Например: "деревня".
//        * НаселенныйПунктТипКраткий - Строка - краткое наименование типа  населенного пункта. Например: "д".
//        * НаселенныйПунктСокращение - Строка - устаревшее свойство. Сокращение населенного пункта, например: "д",
//                                               если ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ,
//                                               или "деревня", если
//                                               ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА
//        * Территория            - Строка - текстовое представление территории.
//        * ТерриторияТипПолный  - Строка - полное наименование типа территории. Например: "Гаражно-строительный кооп.".
//        * ТерриторияТипКраткий  - Строка - краткое наименование типа территории. Например: "гск".
//        * ТерриторияСокращение  - Строка - устаревшее свойство. Сокращение населенного пункта, например: "гск",
//                                               если ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ,
//                                               или "Гаражно-строительный кооп.", если
//                                               ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА
//        * Улица            - Строка - текстовое представление улицы.
//        * УлицаТипПолный  - Строка - полное наименование типа улицы. Например: "Улица".
//        * УлицаТипКраткий  - Строка - краткое наименование типа улицы. Например: "ул".
//        * УлицаСокращение  - Строка - устаревшее свойство. Сокращение улицы, например: "гск",
//                                     если ДополнительныеПараметры.ПолноеНаименованиеСокращений = ЛОЖЬ,
//                                     или "Улица", если ДополнительныеПараметры.ПолноеНаименованиеСокращений = ИСТИНА
//        * ДополнительнаяТерритория - Строка - устаревшее свойство. Текстовое представление дополнительной территории.
//        * ДополнительнаяТерриторияСокращение - Строка - устаревшее свойство. Сокращение дополнительной территории.
//        * ЭлементДополнительнойТерритории - Строка - устаревшее свойство. Текстовое представление элемента
//                                                     дополнительной территории.
//        * ЭлементДополнительнойТерриторииСокращение - Строка - устаревшее свойство. Сокращение элемента дополнительной
//                                                               территории.
//        * Здание - Структура - структура с информацией о здании адреса.
//            ** ТипЗдания - Строка - тип объекта адресации адреса РФ согласно приказу Минфина России от 5.11.2015 г. N171н.
//            ** Номер - Строка  - текстовое представление номера дома (только для адресов РФ).
//        * Корпуса   - Массив - содержит структуры (поля структуры: ТипКорпуса, Номер) с перечнем корпусов адреса.
//        * Помещения - Массив - содержит структуры (поля структуры: ТипПомещения, Номер) с перечнем помещений адреса.
//        * НомерЗемельногоУчастка - Строка - текстовое представление номера земельного участка (только для адресов РФ).
//        * Комментарий - Строка - комментарий об адресе.
//        * ИдентификаторАдресногоОбъекта - УникальныйИдентификатор - Идентификационный код последнего адресного объекта
//                                        в иерархи адреса. Например, для адреса: Москва г., Дмитровское ш., д.9 это
//                                        будет идентификатор улицы.
//                                        Поле отсутствует, если дополнительный параметр КодыАдреса равен Ложь.
//        * ИдентификаторДома             - УникальныйИдентификатор - Идентификационный код дома(строения) адресного объекта.
//                                        Поле отсутствует, если дополнительный параметр КодыАдреса равен Ложь.
//        * ИдентификаторЗемельногоУчастка - УникальныйИдентификатор - Идентификационный код земельного участка
//                                       адресного объекта. При отсутствии значения - пустая строка.
//        * Идентификаторы - Структура - Идентификаторы адресных объектов адреса, если установлен параметр КодыАдреса
//                                       или КодыКЛАДР. Поле отсутствует, если дополнительный параметр КодыАдреса или
//                                       КодыКЛАДР равен Ложь.
//            ** Регион               - УникальныйИдентификатор - идентификатор региона.
//            ** Район                - УникальныйИдентификатор - идентификатор района.
//            ** МуниципальныйРайон   - УникальныйИдентификатор - идентификатор муниципального района.
//            ** Город                - УникальныйИдентификатор - идентификатор города.
//            ** Поселение            - УникальныйИдентификатор - идентификатор поселения.
//            ** ВнутригородскойРайон - УникальныйИдентификатор - идентификатор внутригородского района.
//            ** НаселенныйПункт      - УникальныйИдентификатор - идентификатор населенного пункта.
//            ** Территория           - УникальныйИдентификатор - идентификатор территории.
//            ** Улица                - УникальныйИдентификатор - идентификатор улица.
//        * КодыКЛАДР           - Структура - Коды КЛАДР, если параметр КодыКЛАДР установлен в Истина.
//           ** Регион          - Строка    - Код КЛАДР региона.
//           ** Район           - Строка    - Код КЛАДР район.
//           ** Город           - Строка    - Код КЛАДР города.
//           ** НаселенныйПункт - Строка    - Код КЛАДР населенного пункта.
//           ** Улица           - Строка    - Код КЛАДР улицу.
//        * ДополнительныеКоды  - Структура - Коды ОКТМО, ОКТМО, ОКАТО, КодИФНСФЛ, КодИФНСЮЛ, КодУчасткаИФНСФЛ, КодУчасткаИФНСЮЛ.
//                                            Поле отсутствует, если дополнительный параметр КодыАдреса равен Ложь.
//        * РезультатПроверкиАдреса - Строка - "Успех", если адрес корректный, "Ошибка" - при наличии ошибок проверки,
//                                             "Отказ", если не удалось проверить адрес, т.к. не доступен классификатор.
//                                             Пустая строка, если в параметре ДополнительныеПараметры.ПроверитьАдрес не
//                                             установлен флаг ПроверитьАдрес.
//        * ОшибкиПроверкиАдреса - Строка  - описание ошибок в адресе, выявленных в ходе проверки.
//
Функция СведенияОбАдресе(Адрес, ДополнительныеПараметры = Неопределено) Экспорт
	Возврат СведенияОбАдресеВВидеСтруктуры(Адрес, ДополнительныеПараметры);
КонецФункции

// Проверяет адрес на соответствие требованиям к адресной информации.
//
// Параметры:
//   Адрес              - Строка - строка JSON или XML контактной информации, соответствующая XDTO-пакету КонтактнаяИнформация.
//   ПараметрыПроверки  - Структура, СправочникСсылка.ВидыКонтактнойИнформации - флаги проверки адреса:
//          ТолькоНациональныйАдрес - Булево - адрес должен быть только российским. По умолчанию Истина.
//          ФорматАдреса - Строка - Устарело. По какому классификатору проверять: "КЛАДР" или "ФИАС". По умолчанию "КЛАДР".
// Возвращаемое значение:
//   Структура - содержит структуру с полями:
//        * Результат - Строка - Результат проверки: "Корректный", "НеПроверен", "СодержитОшибки".
//        * СписокОшибок - СписокЗначений - Информация о ошибках.
Функция ПроверитьАдрес(Знач Адрес, ПараметрыПроверки = Неопределено) Экспорт
	Возврат УправлениеКонтактнойИнформациейСлужебный.ПроверитьАдрес(Адрес, ПараметрыПроверки);
КонецФункции

// Преобразует структуру описывающую адрес во внутренний формат хранения контактной информации JSON.
//
// Параметры:
//  ПоляАдреса - Структура - адрес с разбивкой по полям. Список полей см. РаботаСАдресамиКлиентСервер.ПоляАдреса.
// 
// Возвращаемое значение:
//  Строка - адрес во внутреннем формате JSON.
//
Функция ПоляАдресаВJSON(ПоляАдреса) Экспорт
	
	Результат = РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(
		Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	ТипУникальныйИдентификатор = Тип("УникальныйИдентификатор");
	
	Результат.AddressType = ПоляАдреса.ТипАдреса;
	Если ПоляАдреса.ТипАдреса <> РаботаСАдресамиКлиентСервер.МуниципальныйАдрес()
		И ПоляАдреса.ТипАдреса <> РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес() Тогда
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректный тип адреса (%1)'"),
			ПоляАдреса.ТипАдреса);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ПоляАдреса.ТипАдреса <> РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес() Тогда
		Результат.Value = ПоляАдреса.Представление;
	Иначе
		Результат.Value = ПоляАдреса.МуниципальноеПредставление;
	КонецЕсли;
	
	Результат.Comment     = ПоляАдреса.Комментарий;
	
	Результат.Country     = ПоляАдреса.Страна;
	Результат.CountryCode = ПоляАдреса.КодСтраны;
	
	Результат.ZIPcode     = ПоляАдреса.Индекс;
	
	Результат.AreaCode    = ПоляАдреса.КодРегиона;
	
	Результат.Area     = ПоляАдреса.Регион;
	Результат.AreaType = ПоляАдреса.РегионСокращение;
	
	Результат.City     = ПоляАдреса.Город;
	Результат.CityType = ПоляАдреса.ГородСокращение;
	
	Результат.Street     = ПоляАдреса.Улица;
	Результат.StreetType = ПоляАдреса.УлицаСокращение;
	
	Результат.District     = ПоляАдреса.Район;
	Результат.DistrictType = ПоляАдреса.РайонСокращение;
	
	Результат.MunDistrict     = ПоляАдреса.МуниципальныйРайон;
	Результат.MunDistrictType = ПоляАдреса.МуниципальныйРайонСокращение;
	
	Результат.Settlement     = ПоляАдреса.Поселение;
	Результат.SettlementType = ПоляАдреса.ПоселениеСокращение;
	
	Результат.CityDistrict     = ПоляАдреса.ВнутригородскойРайон;
	Результат.CityDistrictType = ПоляАдреса.ВнутригородскойРайонСокращение;
	
	Результат.Locality     = ПоляАдреса.НаселенныйПункт;
	Результат.LocalityType = ПоляАдреса.НаселенныйПунктСокращение;
	
	Результат.Territory     = ПоляАдреса.Территория;
	Результат.TerritoryType = ПоляАдреса.ТерриторияСокращение;
	
	Результат.HouseType   = ПоляАдреса.Здание.ТипЗдания;
	Результат.HouseNumber = ПоляАдреса.Здание.Номер;
	
	Если ПоляАдреса.Свойство("Идентификаторы") Тогда
		Если ТипЗнч(ПоляАдреса.Идентификаторы.РегионИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.РегионИдентификатор) Тогда
			Результат.AreaID = Строка(ПоляАдреса.Идентификаторы.РегионИдентификатор);
			Результат.ID = Результат.AreaID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.РайонИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.РайонИдентификатор) Тогда
			Результат.DistrictID = Строка(ПоляАдреса.Идентификаторы.РайонИдентификатор);
			Результат.ID         = Результат.DistrictID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.ГородИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.ГородИдентификатор) Тогда
			Результат.CityID = Строка(ПоляАдреса.Идентификаторы.ГородИдентификатор);
			Результат.ID     = Результат.CityID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.МуниципальныйРайонИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.МуниципальныйРайонИдентификатор) Тогда
			Результат.MunDistrictID = Строка(ПоляАдреса.Идентификаторы.МуниципальныйРайонИдентификатор);
			Результат.ID            = Результат.MunDistrictID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.ПоселениеИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.ПоселениеИдентификатор) Тогда
			Результат.SettlementID = Строка(ПоляАдреса.Идентификаторы.ПоселениеИдентификатор);
			Результат.ID           = Результат.SettlementID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.ВнутригородскойРайонИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.ВнутригородскойРайонИдентификатор) Тогда
			Результат.CityDistrictID = Строка(ПоляАдреса.Идентификаторы.ВнутригородскойРайонИдентификатор);
			Результат.ID             = Результат.CityDistrictID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.НаселенныйПунктИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.НаселенныйПунктИдентификатор) Тогда
			Результат.LocalityID = Строка(ПоляАдреса.Идентификаторы.НаселенныйПунктИдентификатор);
			Результат.ID         = Результат.LocalityID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.ТерриторияИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.ТерриторияИдентификатор) Тогда
			Результат.TerritoryID = Строка(ПоляАдреса.Идентификаторы.ТерриторияИдентификатор);
			Результат.ID          = Результат.TerritoryID;
		КонецЕсли;
		
		Если ТипЗнч(ПоляАдреса.Идентификаторы.УлицаИдентификатор) = ТипУникальныйИдентификатор
			И ЗначениеЗаполнено(ПоляАдреса.Идентификаторы.УлицаИдентификатор) Тогда
			Результат.StreetID = Строка(ПоляАдреса.Идентификаторы.УлицаИдентификатор);
			Результат.ID       = Результат.StreetID;
		КонецЕсли;
	КонецЕсли;
	
	Если ПоляАдреса.Свойство("ИдентификаторДома")
		И ТипЗнч(ПоляАдреса.ИдентификаторДома) = ТипУникальныйИдентификатор
		И ЗначениеЗаполнено(ПоляАдреса.ИдентификаторДома) Тогда
		Результат.HouseID = Строка(ПоляАдреса.ИдентификаторДома);
	КонецЕсли;
	
	Если ПоляАдреса.Свойство("ИдентификаторАдресногоОбъекта")
		И ТипЗнч(ПоляАдреса.ИдентификаторАдресногоОбъекта) = ТипУникальныйИдентификатор
		И ЗначениеЗаполнено(ПоляАдреса.ИдентификаторАдресногоОбъекта) Тогда
		Результат.ID = Строка(ПоляАдреса.ИдентификаторАдресногоОбъекта);
	КонецЕсли;
	
	Для Каждого ТекущийКорпус Из ПоляАдреса.Корпуса Цикл
		Результат.Buildings.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ТекущийКорпус.ТипКорпуса, ТекущийКорпус.Номер));
	КонецЦикла;
	
	Для Каждого ТекущееПомещение Из ПоляАдреса.Помещения Цикл
		Результат.Apartments.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ТекущееПомещение.ТипПомещения, ТекущееПомещение.Номер));
	КонецЦикла;
	
	// Заполнение кода КЛАДР последнего в иерархии адресного объекта.
	Если ПоляАдреса.Свойство("КодыКЛАДР") Тогда
		Если Не ПустаяСтрока(ПоляАдреса.КодыКЛАДР.Улица) Тогда
			Результат.CodeKLADR = ПоляАдреса.КодыКЛАДР.Улица;
		ИначеЕсли Не ПустаяСтрока(ПоляАдреса.КодыКЛАДР.НаселенныйПункт) Тогда
			Результат.CodeKLADR = ПоляАдреса.КодыКЛАДР.НаселенныйПункт;
		ИначеЕсли Не ПустаяСтрока(ПоляАдреса.КодыКЛАДР.Город) Тогда
			Результат.CodeKLADR = ПоляАдреса.КодыКЛАДР.Город;
		ИначеЕсли Не ПустаяСтрока(ПоляАдреса.КодыКЛАДР.Район) Тогда
			Результат.CodeKLADR = ПоляАдреса.КодыКЛАДР.Район;
		ИначеЕсли Не ПустаяСтрока(ПоляАдреса.КодыКЛАДР.Регион) Тогда
			Результат.CodeKLADR = ПоляАдреса.КодыКЛАДР.Регион;
		КонецЕсли;
	КонецЕсли;
	
	Если ПоляАдреса.Свойство("ДополнительныеКоды") Тогда
		Результат.OKTMO          = ПоляАдреса.ДополнительныеКоды.ОКТМО;
		Результат.OKATO          = ПоляАдреса.ДополнительныеКоды.ОКАТО;
		Результат.IFNSFLCode     = ПоляАдреса.ДополнительныеКоды.КодИФНСФЛ;
		Результат.IFNSULCode     = ПоляАдреса.ДополнительныеКоды.КодИФНСЮЛ;
		Результат.IFNSFLAreaCode = ПоляАдреса.ДополнительныеКоды.КодУчасткаИФНСФЛ;
		Результат.IFNSULAreaCode = ПоляАдреса.ДополнительныеКоды.КодУчасткаИФНСЮЛ;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат.Value) Тогда
		РаботаСАдресамиКлиентСервер.ОбновитьПредставлениеАдреса(Результат, Ложь);
	КонецЕсли;
	
	Возврат УправлениеКонтактнойИнформациейСлужебный.СтруктураВСтрокуJSON(Результат);
	
КонецФункции

// Возвращает адрес в виде строки XML в соответствии со структурой XDTO Контактная информация и Адрес.
//
// Параметры:
//  ИдентификаторАдреса - Строка - глобальный уникальный идентификационный код адресного объекта.
//  ДополнительнаяИнформацияАдреса - Структура - поля адреса, которые будет добавлены в адрес:
//   * АдресВJSON               - Булево - возвращает адрес в формате JSON.
//   * ДополнительнаяИнформация - Строка - комментарий адреса.
//   * Страна                   - Строка - наименование страны адреса.
//   * НомерДома                - Строка - номер дома.
//   * НомерОфиса               - Строка - номер офиса.
//   * НомерСтроения            - Строка - номер строения.
//   * ПочтовыйИндекс           - Строка - почтовый индекс адреса.
//   * АбонентскийЯщик          - Строка - абонентский ящик адреса.
//   * Муниципальный            - Булево - если Истина, адрес будет сформирован в муниципальном формате.
//                                         По умолчанию Ложь.
//
// Возвращаемое значение:
//  Строка, Неопределено - XML в соответствии со структурой XDTO пакетов Контактная информация и Адрес.
//                         JSON, если в ДополнительнаяИнформацияАдреса параметр АдресВJSON установлен в Истина.
//                         Неопределено, если не удалось сформировать адрес по идентификатору.
//
Функция АдресПоИдентификатору(ИдентификаторАдреса, ДополнительнаяИнформацияАдреса = Неопределено) Экспорт
	
	Если ДополнительнаяИнформацияАдреса = Неопределено Тогда
		ДополнительнаяИнформацияАдреса = Новый Структура();
	КонецЕсли;
	
	Муниципальный = ?(ДополнительнаяИнформацияАдреса.Свойство("Муниципальный"), Булево(ДополнительнаяИнформацияАдреса.Муниципальный), Ложь);
	
	Сведения = Новый Структура();
	Сведения.Вставить("Идентификатор", ИдентификаторАдреса);
	Сведения.Вставить("Муниципальный", Муниципальный);
	
	МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
	ПолученныеАдрес = МодульАдресныйКлассификаторСлужебный.АктуальныеАдресныеСведения(Сведения);
	
	Если ПолученныеАдрес.Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Адрес = ПолученныеАдрес.Данные;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("НомерДома") И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.НомерДома) Тогда
		
		ОписаниеДома = РазделитьДомаСтроения(ДополнительнаяИнформацияАдреса.НомерДома, "Дом");
		Адрес.Вставить("houseType",   ОписаниеДома.Тип);
		Адрес.Вставить("houseNumber", ОписаниеДома.Номер);
		Адрес.Вставить("houseId",     "");
		
	КонецЕсли;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("НомерСтроения") И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.НомерСтроения) Тогда
		
		ОписаниеСтроения = РазделитьДомаСтроения(ДополнительнаяИнформацияАдреса.НомерСтроения, "Корпус");
		Строение = УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ОписаниеСтроения.Тип,  ОписаниеСтроения.Номер);
		Адрес.buildings.Добавить(Строение);
		
	КонецЕсли;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("НомерОфиса") И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.НомерОфиса) Тогда
		
		ОписаниеПомещения = РазделитьДомаСтроения(ДополнительнаяИнформацияАдреса.НомерОфиса, "Офис");
		Помещение = УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ОписаниеПомещения.Тип,  ОписаниеПомещения.Номер);
		Адрес.apartments.Добавить(Помещение);
		
	КонецЕсли;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("АбонентскийЯщик") И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.АбонентскийЯщик) Тогда
		
		ОписаниеАбонентскийЯщик = РазделитьДомаСтроения(ДополнительнаяИнформацияАдреса.АбонентскийЯщик, "А/Я");
		АбонентскийЯщик = УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ОписаниеАбонентскийЯщик.Тип,  ОписаниеАбонентскийЯщик.Номер);
		Адрес.apartments.Добавить(АбонентскийЯщик);
	КонецЕсли;

	Если ДополнительнаяИнформацияАдреса.Свойство("ПочтовыйИндекс") И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.ПочтовыйИндекс) Тогда
		Адрес.ZIPcode = ДополнительнаяИнформацияАдреса.ПочтовыйИндекс;
	КонецЕсли;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("ДополнительнаяИнформация") И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.ДополнительнаяИнформация) Тогда
		Адрес.comment = ДополнительнаяИнформацияАдреса.ДополнительнаяИнформация;
	КонецЕсли;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("Страна")  И ЗначениеЗаполнено(ДополнительнаяИнформацияАдреса.Страна) Тогда
		Адрес.country = ВРег(ДополнительнаяИнформацияАдреса.Страна);
	КонецЕсли;
	
	ВидИнформации = Новый Структура("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	РасчетноеПредставление = УправлениеКонтактнойИнформациейСлужебный.ПредставлениеКонтактнойИнформации(Адрес, ВидИнформации);
	Адрес.value = РасчетноеПредставление;
	
	Если ДополнительнаяИнформацияАдреса.Свойство("АдресВJSON") И ДополнительнаяИнформацияАдреса.АдресВJSON = Истина Тогда
		
		Возврат УправлениеКонтактнойИнформациейСлужебный.СтруктураВСтрокуJSON(Адрес);
		
	КонецЕсли;
	
	Возврат УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияИзJSONВXML(Адрес, Перечисления.ТипыКонтактнойИнформации.Адрес);
	
КонецФункции

// Возвращает общепринятые сокращения адресных объектов.
//
// Возвращаемое значение:
//  Соответствие - Список общепринятых сокращений.
//   * Ключ - Строка - полное наименование адресного объекта. Например, "Дом", "Квартира"
//   * Значение - Строка - сокращенное наименование адресного объекта. Например, "Д.", "Кв."
//
Функция СокращенияОбъектовАдресацииАдресаРФ() Экспорт
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("Дом", "Д.");
	Результат.Вставить("Владение", "Вл.");
	Результат.Вставить("Домовладение", "Домовл.");
	
	Результат.Вставить("Корпус", "Корп.");
	Результат.Вставить("Строение", "Стр.");
	Результат.Вставить("Литера", "Лит.");
	Результат.Вставить("Сооружение", "Сооруж.");
	Результат.Вставить("Участок", "Уч.");
	
	Результат.Вставить("Квартира", "Кв.");
	Результат.Вставить("Офис", "Оф.");
	Результат.Вставить("Бокс", "Бокс");
	Результат.Вставить("Помещение", "Пом.");
	Результат.Вставить("Комната", "Ком.");
	Результат.Вставить("Этаж", "Этаж");
	Результат.Вставить("А/я", "а/я");
	Результат.Вставить("П/о", "п/о");
	Результат.Вставить("В/ч", "в/ч");
	
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обратная совместимость.

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать РаботаСАдресами.СведенияОбАдресе.
// Преобразует данные формата XML в предыдущий формат контактной информации.
//
// Параметры:
//    Данные                 - Строка - Строка XML соответствующая XDTO пакету Адрес.
//    СокращенныйСоставПолей - Булево - Если Ложь, то из состава полей будут исключены
//                                      поля, отсутствующие в версиях БСП младше 2.1.3.
//
// Возвращаемое значение:
//    Строка - набор пар ключ-значение, разделенных переносом строки.
//
Функция ПредыдущийФорматКонтактнойИнформацииXML(Знач Данные, Знач СокращенныйСоставПолей = Ложь) Экспорт
	
	Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Данные) Тогда
		СтарыйФормат = Обработки.РасширенныйВводКонтактнойИнформации.КонтактнаяИнформацияВСтаруюСтруктуру(Данные, СокращенныйСоставПолей);
		Возврат ПреобразоватьСписокПолейВСтроку(СтарыйФормат.ЗначенияПолей, Ложь);
	КонецЕсли;
	
	Возврат Данные;
КонецФункции

// Устарела. Следует использовать РаботаСАдресами.СведенияОбАдресе.
// Преобразует данные нового формата XML контактной информации в структуру старого формата.
//
// Параметры:
//   Данные                  - Строка - Строка XML соответствующая XDTO пакету Адрес.
//   ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации, Структура - Параметры контактной информации.
//     * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - Тип контактной информации.
//
// Возвращаемое значение:
//   Структура - набор пар ключ-значение. Состав свойств для адреса:
//        ** Страна           - Строка - Представление страны.
//        ** КодСтраны        - Строка - Код страны по ОКСМ.
//        ** Индекс           - Строка - Почтовый индекс (только для адресов РФ).
//        ** Регион           - Строка - Представление региона РФ (только для адресов РФ).
//        ** КодРегиона       - Строка - Код региона РФ (только для адресов РФ).
//        ** РегионСокращение - Строка - Сокращение региона (если СтарыйСоставПолей = Ложь).
//        ** Район            - Строка - Представление района (только для адресов РФ).
//        ** РайонСокращение  - Строка - Сокращение района (если СтарыйСоставПолей = Ложь).
//        ** Город            - Строка - Представление города (только для адресов РФ).
//        ** ГородСокращение  - Строка - Сокращение города (только для адресов РФ).
//        ** НаселенныйПункт  - Строка - Представление населенного пункта (только для адресов РФ).
//        ** НаселенныйПунктСокращение - Строка - сокращение населенного пункта (если СтарыйСоставПолей = Ложь).
//        ** Улица            - Строка - Представление улицы (только для адресов РФ).
//        ** УлицаСокращение  - Строка - Сокращение улицы (если СтарыйСоставПолей = Ложь).
//        ** ТипДома          - Строка - Тип дома см. РаботаСАдресамиКлиентСервер.ТипыОбъектовАдресацииАдресаРФ.
//        ** Дом              - Строка - Представление дома (только для адресов РФ).
//        ** ТипКорпуса       - Строка - Тип корпуса см. РаботаСАдресамиКлиентСервер.ТипыОбъектовАдресацииАдресаРФ.
//        ** Корпус           - Строка - Представление корпуса (только для адресов РФ).
//        ** ТипКвартиры      - Строка - Тип квартиры см. РаботаСАдресамиКлиентСервер.ТипыОбъектовАдресацииАдресаРФ.
//        ** Квартира         - Строка - Представление квартиры (только для адресов РФ).
//       Состав свойств для телефона:
//        ** КодСтраны        - Строка - Код страны. Например, +7.
//        ** КодГорода        - Строка - Код города. Например, 495.
//        ** НомерТелефона    - Строка - Номер телефона.
//        ** Добавочный       - Строка - Добавочный номер телефона.
//
Функция ПредыдущаяСтруктураКонтактнойИнформацииXML(Знач Данные, Знач ВидКонтактнойИнформации = Неопределено) Экспорт
	
	Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Данные) Тогда
		// Новый формат КИ
		Возврат УправлениеКонтактнойИнформациейСлужебный.СтруктураЗначенийПолей(
			ПредыдущийФорматКонтактнойИнформацииXML(Данные));
		
	КонецЕсли;
	
	Если ВидКонтактнойИнформации <> Неопределено
		И ((ТипЗнч(ВидКонтактнойИнформации) = Тип("Структура") И ВидКонтактнойИнформации.Свойство("Тип"))
		ИЛИ ТипЗнч(ВидКонтактнойИнформации) = Тип("СправочникСсылка.ВидыКонтактнойИнформации")) Тогда
			ТипКонтактнойИнформации = ВидКонтактнойИнформации.Тип;
	Иначе
		ТипКонтактнойИнформации = Неопределено;
	КонецЕсли;
	
	Если ПустаяСтрока(Данные)  Тогда
		// Генерируем по типу
		Возврат РаботаСАдресамиКлиентСервер.СтруктураКонтактнойИнформацииПоТипу(ТипКонтактнойИнформации);
		
	КонецЕсли;
	
	// Возвращаем полную структуру для данного вида с заполненными полями.
	Результат = РаботаСАдресамиКлиентСервер.СтруктураКонтактнойИнформацииПоТипу(ТипКонтактнойИнформации);
	СтруктураЗначенийПолей = УправлениеКонтактнойИнформациейСлужебный.СтруктураЗначенийПолей(Данные, ВидКонтактнойИнформации);
	Если ТипКонтактнойИнформации <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Результат, СтруктураЗначенийПолей);
		Возврат Результат;
	КонецЕсли;
	
	Возврат СтруктураЗначенийПолей;
	
КонецФункции

// Устарела. Следует использовать РаботаСАдресами.СведенияОбАдресе.
// Преобразует адреса нового формата XML ФИАС в адрес формата КЛАДР.
//
// Параметры:
//   Данные                  - Строка - строка XML соответствующая XDTO пакету Адрес.
//
// Возвращаемое значение:
//   Структура - набор пар ключ-значение. Состав свойств для адреса:
//        ** Страна           - Строка - Представление страны.
//        ** КодСтраны        - Строка - Код страны по ОКСМ.
//        ** Индекс           - Строка - Почтовый индекс (только для адресов РФ).
//        ** Регион           - Строка - Представление региона РФ (только для адресов РФ).
//        ** КодРегиона       - Строка - Код региона РФ (только для адресов РФ).
//        ** РегионСокращение - Строка - Сокращение региона.
//        ** Район            - Строка - Представление района (только для адресов РФ).
//        ** РайонСокращение  - Строка - Сокращение района.
//        ** Город            - Строка - Представление города (только для адресов РФ).
//        ** ГородСокращение  - Строка - Сокращение города (только для адресов РФ).
//        ** НаселенныйПункт  - Строка - Представление населенного пункта (только для адресов РФ).
//        ** НаселенныйПунктСокращение - Строка - Сокращение населенного пункта.
//        ** Улица            - Строка - Представление улицы (только для адресов РФ).
//        ** УлицаСокращение  - Строка - Сокращение улицы.
//        ** ТипДома          - Строка - Тип дома см. РаботаСАдресамиКлиентСервер.ТипыОбъектовАдресацииАдресаРФ.
//        ** Дом              - Строка - Представление дома (только для адресов РФ).
//        ** ТипКорпуса       - Строка - Тип корпуса см. РаботаСАдресамиКлиентСервер.ТипыОбъектовАдресацииАдресаРФ.
//        ** Корпус           - Строка - Представление корпуса (только для адресов РФ).
//        ** ТипКвартиры      - Строка - Тип квартиры см. РаботаСАдресамиКлиентСервер.ТипыОбъектовАдресацииАдресаРФ.
//        ** Квартира         - Строка - Представление квартиры (только для адресов РФ).
//        ** АдресРФ          - Булево - Если Истина, то адрес российский.
//        ** Представление    - Строка - Представление адреса.
//
Функция АдресВФорматеКЛАДР(Знач Данные) Экспорт
	
	Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Данные) Тогда
		// Новый формат КИ
		Результат = УправлениеКонтактнойИнформациейСлужебный.СтруктураЗначенийПолей(
				ПредыдущийФорматКонтактнойИнформацииXML(Данные));
			Представление = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(Данные);
			
	ИначеЕсли ПустаяСтрока(Данные) Тогда
		// Генерируем пустую структуру по виду
		Результат = РаботаСАдресамиКлиентСервер.СтруктураКонтактнойИнформацииПоТипу(
			Перечисления.ТипыКонтактнойИнформации.Адрес);
		Представление = "";
	КонецЕсли;
	
	Если Результат.Свойство("Страна") И СтрСравнить(Результат.Страна, РаботаСАдресамиКлиентСервер.ОсновнаяСтрана().Наименование) = 0 Тогда
		Результат.Вставить("АдресРФ", Истина);
	Иначе
		Результат.Вставить("АдресРФ", Ложь);
	КонецЕсли;
	Результат.Вставить("Представление", Представление);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает пространство имен для оперирования с XDTO контактной информации.
//
// Возвращаемое значение:
//      Строка - пространство имен.
//
Функция ПространствоИмен() Экспорт
	Возврат "http://www.v8.1c.ru/ssl/contactinfo_ru";
КонецФункции

// Преобразует XML. Обратная совместимость.
//
Функция ПередЧтениемXDTOКонтактнаяИнформация(ТекстXML) Экспорт
	
	Если СтрНайти(ТекстXML, "Адрес") = 0 Тогда
		Возврат ТекстXML;
	КонецЕсли;
	
	Если СтрНайти(ТекстXML, "http://www.v8.1c.ru/ssl/contactinfo_ru") > 0 Тогда
		Возврат ТекстXML;
	КонецЕсли;
	
	ТекстXML = СтрЗаменить(ТекстXML, "xsi:type=""АдресРФ""", "xmlns:rf=""http://www.v8.1c.ru/ssl/contactinfo_ru"" xsi:type=""rf:АдресРФ""");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<СубъектРФ", "<rf:СубъектРФ");
	ТекстXML = СтрЗаменить(ТекстXML, "/СубъектРФ>", "/rf:СубъектРФ>");
	ТекстXML = СтрЗаменить(ТекстXML, "<СубъектРФ/>", "<rf:СубъектРФ/>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<Округ", "<rf:Округ");
	ТекстXML = СтрЗаменить(ТекстXML, "/Округ>", "/rf:Округ>");
	ТекстXML = СтрЗаменить(ТекстXML, "<Округ/>", "<rf:Округ/>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<СвРайМО", "<rf:СвРайМО");
	ТекстXML = СтрЗаменить(ТекстXML, "/СвРайМО>", "/rf:СвРайМО>");
	ТекстXML = СтрЗаменить(ТекстXML, "<СвРайМО/>", "<rf:СвРайМО/>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<Район", "<rf:Район");
	ТекстXML = СтрЗаменить(ТекстXML, "/Район>", "/rf:Район>");
	ТекстXML = СтрЗаменить(ТекстXML, "</Район>", "</rf:Район>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<Город", "<rf:Город");
	ТекстXML = СтрЗаменить(ТекстXML, "/Город>", "/rf:Город>");
	ТекстXML = СтрЗаменить(ТекстXML, "<Город/>", "<rf:Город/>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "ВнутригРайон", "rf:ВнутригРайон");
	
	ТекстXML = СтрЗаменить(ТекстXML, "НаселПункт", "rf:НаселПункт");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<Улица", "<rf:Улица");
	ТекстXML = СтрЗаменить(ТекстXML, "/Улица>", "/rf:Улица>");
	ТекстXML = СтрЗаменить(ТекстXML, "<Улица/>", "<rf:Улица/>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "ОКТМО", "rf:ОКТМО");
	ТекстXML = СтрЗаменить(ТекстXML, "ОКАТО", "rf:ОКАТО");
	
	ТекстXML = СтрЗаменить(ТекстXML, "ДопАдрЭл", "rf:ДопАдрЭл");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<Номер", "<rf:Номер");
	ТекстXML = СтрЗаменить(ТекстXML, "/Номер>", "/rf:Номер>");
	ТекстXML = СтрЗаменить(ТекстXML, "<Номер/>", "<rf:Номер/>");
	
	ТекстXML = СтрЗаменить(ТекстXML, "<Местоположение", "<rf:Местоположение");
	ТекстXML = СтрЗаменить(ТекстXML, "/Местоположение>", "/rf:Местоположение>");
	ТекстXML = СтрЗаменить(ТекстXML, "<Местоположение/>", "<rf:Местоположение/>");
	
	Возврат ТекстXML;
	
КонецФункции

Функция ПередЗаписьюXDTOКонтактнаяИнформация(ТекстXML) Экспорт
	
	Позиция = СтрНайти(ТекстXML, "АдресРФ""");
	Если Позиция > 0 Тогда
		ПозицияНачало = СтрНайти(ТекстXML, """", НаправлениеПоиска.СКонца, Позиция);
		Префикс = Сред(ТекстXML, ПозицияНачало + 1, Позиция - ПозицияНачало - 2);
		
		ТекстXML = СтрЗаменить(ТекстXML, Префикс +":", "");
		ТекстXML = СтрЗаменить(ТекстXML, " xmlns:"+ Префикс + "=""http://www.v8.1c.ru/ssl/contactinfo_ru""", "");
	КонецЕсли;
	
	Возврат ТекстXML;
КонецФункции

Функция ДополнительныеПравилаПреобразования() Экспорт
	
	КодыДополнительныхАдресныхЭлементов = Новый ТекстовыйДокумент;
	Для Каждого ДополнительныйАдресныйЭлемент Из ТипыОбъектовАдресацииАдресаРФ() Цикл
		КодыДополнительныхАдресныхЭлементов.ДобавитьСтроку("<data:item data:title=""" + ДополнительныйАдресныйЭлемент.Наименование + """>" + ДополнительныйАдресныйЭлемент.Код + "</data:item>");
		КодыДополнительныхАдресныхЭлементов.ДобавитьСтроку("<data:item data:title=""" + НРег(ДополнительныйАдресныйЭлемент.Наименование) + """>" + ДополнительныйАдресныйЭлемент.Код + "</data:item>");
	КонецЦикла;
	
	КодыРегионов = Новый ТекстовыйДокумент;
	ВсеРегионы = ВсеРегионы();
	Если ВсеРегионы <> Неопределено Тогда
		Для Каждого Строка Из ВсеРегионы Цикл
			КодыРегионов.ДобавитьСтроку("<data:item data:code=""" + Формат(Строка.КодСубъектаРФ, "ЧН=; ЧГ=") + """>" 
			+ Строка.Представление + "</data:item>");
		КонецЦикла;
	КонецЕсли;
	
	РасширенныйТекстПреобразования = "
	|  <xsl:template match=""/"" mode=""domestic"">
	|    <xsl:element name=""Состав"">
	|      <xsl:attribute name=""xsi:type"">АдресРФ</xsl:attribute>
	|    
	|      <xsl:element name=""СубъектРФ"">
	|        <xsl:variable name=""value"" select=""tns:Structure/tns:Property[@name='Регион']/tns:Value/text()"" />
	|
	|        <xsl:choose>
	|          <xsl:when test=""0=count($value)"">
	|            <xsl:variable name=""regioncode"" select=""tns:Structure/tns:Property[@name='КодРегиона']/tns:Value/text()""/>
	|            <xsl:variable name=""regiontitle"" select=""$enum-regioncode-nodes/data:item[@data:code=number($regioncode)]"" />
	|              <xsl:if test=""0!=count($regiontitle)"">
	|                <xsl:value-of select=""$regiontitle""/>
	|              </xsl:if>
	|          </xsl:when>
	|          <xsl:otherwise>
	|            <xsl:value-of select=""$value"" />
	|          </xsl:otherwise> 
	|        </xsl:choose>
	|
	|      </xsl:element>
	|   
	|      <xsl:element name=""Округ"">
	|        <xsl:value-of select=""tns:Structure/tns:Property[@name='Округ']/tns:Value/text()""/>
	|      </xsl:element>
	|
	|      <xsl:element name=""СвРайМО"">
	|        <xsl:element name=""Район"">
	|          <xsl:value-of select=""tns:Structure/tns:Property[@name='Район']/tns:Value/text()""/>
	|        </xsl:element>
	|      </xsl:element>
	|  
	|      <xsl:element name=""Город"">
	|        <xsl:value-of select=""tns:Structure/tns:Property[@name='Город']/tns:Value/text()""/>
	|      </xsl:element>
	|    
	|      <xsl:element name=""ВнутригРайон"">
	|        <xsl:value-of select=""tns:Structure/tns:Property[@name='ВнутригРайон']/tns:Value/text()""/>
	|      </xsl:element>
	|
	|      <xsl:element name=""НаселПункт"">
	|        <xsl:value-of select=""tns:Structure/tns:Property[@name='НаселенныйПункт']/tns:Value/text()""/>
	|      </xsl:element>
	|
	|      <xsl:element name=""Улица"">
	|        <xsl:value-of select=""tns:Structure/tns:Property[@name='Улица']/tns:Value/text()""/>
	|      </xsl:element>
	|
	|      <xsl:variable name=""index"" select=""tns:Structure/tns:Property[@name='Индекс']/tns:Value/text()"" />
	|      <xsl:if test=""0!=count($index)"">
	|        <xsl:element name=""ДопАдрЭл"">
	|          <xsl:attribute name=""ТипАдрЭл"">" + КодСериализацииПочтовогоИндекса() + "</xsl:attribute>
	|          <xsl:attribute name=""Значение""><xsl:value-of select=""$index""/></xsl:attribute>
	|        </xsl:element>
	|      </xsl:if>
	|
	|      <xsl:call-template name=""add-elem-number"">
	|        <xsl:with-param name=""source"" select=""tns:Structure/tns:Property[@name='ТипДома']/tns:Value/text()"" />
	|        <xsl:with-param name=""defsrc"" select=""'Дом'"" />
	|        <xsl:with-param name=""value""  select=""tns:Structure/tns:Property[@name='Дом']/tns:Value/text()"" />
	|      </xsl:call-template>
	|
	|      <xsl:call-template name=""add-elem-number"">
	|        <xsl:with-param name=""source"" select=""tns:Structure/tns:Property[@name='ТипКорпуса']/tns:Value/text()"" />
	|        <xsl:with-param name=""defsrc"" select=""'Корпус'"" />
	|        <xsl:with-param name=""value""  select=""tns:Structure/tns:Property[@name='Корпус']/tns:Value/text()"" />
	|      </xsl:call-template>
	|
	|      <xsl:call-template name=""add-elem-number"">
	|        <xsl:with-param name=""source"" select=""tns:Structure/tns:Property[@name='ТипКвартиры']/tns:Value/text()"" />
	|        <xsl:with-param name=""defsrc"" select=""'Квартира'"" />
	|        <xsl:with-param name=""value""  select=""tns:Structure/tns:Property[@name='Квартира']/tns:Value/text()"" />
	|      </xsl:call-template>
	|    
	|    </xsl:element>
	|  </xsl:template>
	|
	|  <xsl:param name=""enum-codevalue"">
	|" + КодыДополнительныхАдресныхЭлементов.ПолучитьТекст() + "
	|  </xsl:param>
	|  <xsl:variable name=""enum-codevalue-nodes"" select=""exsl:node-set($enum-codevalue)"" />
	|
	|  <xsl:param name=""enum-regioncode"">
	|" + КодыРегионов.ПолучитьТекст() + "
	|  </xsl:param>
	|  <xsl:variable name=""enum-regioncode-nodes"" select=""exsl:node-set($enum-regioncode)"" />
	|  
	|  <xsl:template name=""add-elem-number"">
	|    <xsl:param name=""source"" />
	|    <xsl:param name=""defsrc"" />
	|    <xsl:param name=""value"" />
	|
	|    <xsl:if test=""0!=count($value)"">
	|
	|      <xsl:choose>
	|        <xsl:when test=""0!=count($source)"">
	|          <xsl:variable name=""type-code"" select=""$enum-codevalue-nodes/data:item[@data:title=$source]"" />
	|          <xsl:element name=""ДопАдрЭл"">
	|            <xsl:element name=""Номер"">
	|              <xsl:attribute name=""Тип""><xsl:value-of select=""$type-code"" /></xsl:attribute>
	|              <xsl:attribute name=""Значение""><xsl:value-of select=""$value""/></xsl:attribute>
	|            </xsl:element>
	|          </xsl:element>
	|
	|        </xsl:when>
	|        <xsl:otherwise>
	|          <xsl:variable name=""type-code"" select=""$enum-codevalue-nodes/data:item[@data:title=$defsrc]"" />
	|          <xsl:element name=""ДопАдрЭл"">
	|            <xsl:element name=""Номер"">
	|              <xsl:attribute name=""Тип""><xsl:value-of select=""$type-code"" /></xsl:attribute>
	|              <xsl:attribute name=""Значение""><xsl:value-of select=""$value""/></xsl:attribute>
	|            </xsl:element>
	|          </xsl:element>
	|
	|        </xsl:otherwise>
	|      </xsl:choose>
	|
	|    </xsl:if>
	|  
	|  </xsl:template>
	|  
	|</xsl:stylesheet>";
	
	Возврат РасширенныйТекстПреобразования;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает XPath для района.
//
// Возвращаемое значение:
//      Строка - XPath
//
Функция XPathРайона() Экспорт
	
	Возврат "СвРайМО/Район";
	
КонецФункции

// Возвращает массив структур с информацией о частях адреса согласно приказу ФНС ММВ-7-1/525 от 31.08.2011.
//
// Возвращаемое значение:
//      Массив - содержит структуры - описания.
//
Функция ТипыОбъектовАдресацииАдресаРФ()
	
	Результат = Новый Массив;
	
	// Код, Наименование, Тип, Порядок, КодФИАС
	// Тип: 1 - владение, 2 - здание, 3 - помещение.
	
	Результат.Добавить(СтрокаОбъектаАдресации("1010", "Дом",          1, 1, 2)); // Тип владения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1020", "Владение",     1, 2, 1)); // Тип владения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1030", "Домовладение", 1, 3, 3)); // Тип владения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1034", "Гараж",        1, 4, 4)); // Тип владения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1036", "Здание",       1, 5, 5)); // Тип владения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1038", "Шахта",        1, 6, 6)); // Тип владения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1039", РаботаСАдресамиКлиентСервер.НаименованиеЗемельногоУчастка()
		, 1, 9, 9));
	
	Результат.Добавить(СтрокаОбъектаАдресации("1050", "Корпус",     2, 1)); // Тип здания не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1060", "Строение",   2, 2, 1)); // Тип здания не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1080", "Литера",     2, 3, 3)); // Тип здания не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1090", "Литер",      2, 6, 3)); // Тип здания не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1070", "Сооружение", 2, 4, 2)); // Тип здания не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("1040", "Участок",    2, 5)); // Тип здания не локализуется
	
	Результат.Добавить(СтрокаОбъектаАдресации("2010", "Квартира",  3, 1)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2030", "Офис",      3, 2)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2040", "Бокс",      3, 3)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2020", "Помещение", 3, 4)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2050", "Комната",   3, 5)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2060", "Этаж",      3, 6)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2070", "А/я",       3, 7)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2080", "В/ч",       3, 8)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2090", "П/о",       3, 9)); // Тип помещения не локализуется
	//  Наши сокращения для поддержки обратной совместимости при парсинге.
	Результат.Добавить(СтрокаОбъектаАдресации("2010", "кв.",       3, 6)); // Тип помещения не локализуется
	Результат.Добавить(СтрокаОбъектаАдресации("2030", "оф.",       3, 7)); // Тип помещения не локализуется
	// Ввод помещения вручную.
	Результат.Добавить(СтрокаОбъектаАдресации("2000", "",          3, 0));
	
	// Уточняющие объекты
	Результат.Добавить(СтрокаОбъектаАдресации("10100000", НСтр("ru = 'Почтовый индекс'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10200000", НСтр("ru = 'Адресная точка'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10300000", НСтр("ru = 'Садовое товарищество'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10400000", НСтр("ru = 'Элемент улично-дорожной сети, планировочной структуры дополнительного адресного элемента'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10500000", НСтр("ru = 'Промышленная зона'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10600000", НСтр("ru = 'Гаражно-строительный кооператив'")));
	Результат.Добавить(СтрокаОбъектаАдресации("10700000", НСтр("ru = 'Территория'")));
	
	Возврат Результат;
КонецФункции

Функция СтрокаОбъектаАдресации(Код, Наименование, Тип = 0, Порядок = 0, КодФИАС = 0)
	
	СтруктураОбъектаАдресации = Новый Структура;
	СтруктураОбъектаАдресации.Вставить("Код", Код);
	СтруктураОбъектаАдресации.Вставить("Наименование", Наименование);
	СтруктураОбъектаАдресации.Вставить("Тип", Тип);
	СтруктураОбъектаАдресации.Вставить("Порядок", Порядок);
	СтруктураОбъектаАдресации.Вставить("КодФИАС", КодФИАС);
	СтруктураОбъектаАдресации.Вставить("Сокращение", НРег(Наименование));
	СтруктураОбъектаАдресации.Вставить("Ключ", ВРег(Наименование));
	Возврат СтруктураОбъектаАдресации;
	
КонецФункции

// Возвращает код дополнительной части адреса для сериализации.
//
//  Параметры:
//      СтрокаЗначения - Строка - значение для поиска, например "Дом", "Корпус", "Литера".
//
// Возвращаемое значение:
//      Число - код
// 
Функция КодСериализацииОбъектаАдресации(СтрокаЗначения) Экспорт
	
	Ключ = ВРег(СокрЛП(СтрокаЗначения));
	Для Каждого Элемент Из ТипыОбъектовАдресацииАдресаРФ() Цикл
		Если Элемент.Ключ = Ключ Тогда
			Возврат Элемент.Код;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

// Возвращает код дополнительной части адреса для почтового индекса.
//
// Возвращаемое значение:
//      Строка - код
//
Функция КодСериализацииПочтовогоИндекса() Экспорт
	
	Возврат КодСериализацииОбъектаАдресации(НСтр("ru = 'Почтовый индекс'"));
	
КонецФункции

// Возвращает XPath для почтового индекса.
//
// Возвращаемое значение:
//      Строка - XPath
//
Функция XPathПочтовогоИндекса() Экспорт
	
	Возврат "ДопАдрЭл[ТипАдрЭл='" + КодСериализацииПочтовогоИндекса() + "']";
	
КонецФункции

// Возвращает XPath для номера дополнительного объекта адресации.
//
//  Параметры;
//      СтрокаЗначения - Строка - искомый тип, например "Дом", "Корпус".
//
// Возвращаемое значение:
//      Строка - XPath
//
Функция XPathНомераДополнительногоОбъектаАдресации(СтрокаЗначения) Экспорт
	
	Код = КодСериализацииОбъектаАдресации(СтрокаЗначения);
	Если Код = Неопределено Тогда
		Код = СтрЗаменить(СтрокаЗначения, "'", "");
	КонецЕсли;
	
	Возврат "ДопАдрЭл/Номер[Тип='" + Код + "']";
КонецФункции

// Возвращает строку с описанием типа по коду части адреса.
//  Противоположность функции "КодСериализацииОбъектаАдресации".
//
// Параметры:
//      Код - Строка - код
//
// Возвращаемое значение:
//      Число - Тип
//
Функция ТипОбъектаПоКодуСериализации(Код) Экспорт
	Для Каждого Элемент Из ТипыОбъектовАдресацииАдресаРФ() Цикл
		Если Элемент.Код = Код Тогда
			Возврат Элемент;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

Процедура ОбработкаЧастыхСокращенийВАдресах(Знач ДанныеАнализа) Экспорт
	
	СубъектРФ = Новый Соответствие();
	СубъектРФ.Вставить(НСтр("ru='САНКТ-ПЕТЕРБУРГ'"), "Санкт-Петербург");
	СубъектРФ.Вставить(НСтр("ru='МОСКВА'"), "Москва");
	СубъектРФ.Вставить(НСтр("ru='МСК'"), "Москва");
	СубъектРФ.Вставить(НСтр("ru='СПБ'"), "Санкт-Петербург");
	
	Для каждого ЧастьАдреса Из ДанныеАнализа Цикл
		Наименование = ВРег(ЧастьАдреса.Наименование);
		Если СубъектРФ.Получить(Наименование ) <> Неопределено И ПустаяСтрока(ЧастьАдреса.Сокращение) Тогда
			ЧастьАдреса.Наименование = СубъектРФ.Получить(Наименование );
			ЧастьАдреса.Сокращение = "г";
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ОпределитьФорматКонтактнойИнформации(ФорматКонтактнойИнформации) Экспорт
	ФорматКонтактнойИнформации.Вставить("ФорматАдреса", "ФИАС");
КонецПроцедуры

// Возвращает массив вариантов наименований по типу (по признаку владения, строения, и т.п).
//
// Параметры:
//      Тип                  - Число  - запрашиваемый тип.
//      ДопускатьПовторыКода - Булево - Истина - будут возвращены все варианты с повторами ("квартира" - "кв." и т.п.).
//
// Возвращаемое значение:
//      Массив - содержит структуры - описания.
//
Функция НаименованияОбъектовАдресацииПоТипу(Тип, ДопускатьПовторыКода = Истина) Экспорт
	Результат = Новый Массив;
	Повторы   = Новый Соответствие;
	
	Для Каждого Элемент Из ТипыОбъектовАдресацииАдресаРФ() Цикл
		Если Элемент.Тип = Тип Тогда
			Если ДопускатьПовторыКода Тогда
				Результат.Добавить(Элемент.Наименование);
			Иначе
				Если Повторы.Получить(Элемент.Код) = Неопределено Тогда
					Результат.Добавить(Элемент.Наименование);
				КонецЕсли;
				Повторы.Вставить(Элемент.Код, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Внутреннее для сериализации.
Функция КонвертироватьАдресИзJSONВXML(Знач ЗначенияПолей, Знач Представление, Знач ОжидаемыйТип = Неопределено) Экспорт
	
	// Старый формат через разделитель строк и равенство.
	ПространствоИмен = УправлениеКонтактнойИнформациейСлужебный.ПространствоИмен();
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	Результат.Состав      = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Адрес"));
	
	НазваниеОсновнойСтраны  = ВРег(РаботаСАдресамиКлиентСервер.ОсновнаяСтрана().Наименование);
	
	// Национальный
	НациональныйАдрес = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен(), "АдресРФ"));
	
	// Общий состав
	Адрес = Результат.Состав;
	Адрес.Страна = НазваниеОсновнойСтраны; // Страна по умолчанию
	АдресНациональный = Истина;
	
	ПолеПредставления      = "";
	
	Для Каждого ЭлементСписка Из ЗначенияПолей Цикл
		
		Если ПустаяСтрока(ЭлементСписка.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПоля = ВРег(ЭлементСписка.Ключ);
		
		Если ИмяПоля="ZIPCODE" Тогда
			ЭлементИндекс = СоздатьДопАдрЭлемента(НациональныйАдрес);
			ЭлементИндекс.ТипАдрЭл = КодСериализацииПочтовогоИндекса();
			ЭлементИндекс.Значение = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля = "COMMENT" Тогда
			Результат.Комментарий = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля = "COUNTRY" Тогда
			Адрес.Страна = Строка(ЭлементСписка.Значение);
			Если ВРег(ЭлементСписка.Значение) <> НазваниеОсновнойСтраны Тогда
				АдресНациональный = Ложь;
			КонецЕсли;
			
		ИначеЕсли ИмяПоля = "COUNTRYCODE" Тогда
			// действия не требуется
			
		ИначеЕсли ИмяПоля = "AREACODE" Тогда
			Если ПустаяСтрока(НациональныйАдрес.СубъектРФ) Тогда
				НациональныйАдрес.СубъектРФ = РегионКода(ЭлементСписка.Значение);
			КонецЕсли;
			
		ИначеЕсли ИмяПоля = "AREA" Тогда
			НациональныйАдрес.СубъектРФ = ЭлементСписка.Значение + " " + ЗначенияПолей.AreaType;
			
		ИначеЕсли ИмяПоля = "DISTRICT" Тогда
			Если НациональныйАдрес.СвРайМО = Неопределено Тогда
				НациональныйАдрес.СвРайМО = ФабрикаXDTO.Создать(НациональныйАдрес.Тип().Свойства.Получить("СвРайМО").Тип)
			КонецЕсли;
			НациональныйАдрес.СвРайМО.Район = СокрЛП(ЭлементСписка.Значение + " " + ЗначенияПолей.DistrictType);
			
		ИначеЕсли ИмяПоля = "CITY" Тогда
			Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
				НациональныйАдрес.Город = СокрЛП(ЭлементСписка.Значение + " " + ЗначенияПолей.CityType);
			КонецЕсли;
			
		ИначеЕсли ИмяПоля = "TERRITORY" Тогда
			
			ПутьXPath = XPathДополнительногоОбъектаАдресации(90, ЗначенияПолей.TerritoryType);
			Обработки.РасширенныйВводКонтактнойИнформации.УстановитьXDTOРеквизитОбъекта(НациональныйАдрес, ПутьXPath, 
			ЭлементСписка.Значение + " " + ЗначенияПолей.TerritoryType);
			
		ИначеЕсли ИмяПоля = "LOCALITY" Тогда
			НациональныйАдрес.НаселПункт = СокрЛП(ЭлементСписка.Значение + " " + ЗначенияПолей.LocalityType);
			
		ИначеЕсли ИмяПоля = "CITYDISTRICT" Тогда
			НациональныйАдрес.ВнутригРайон = СокрЛП(ЭлементСписка.Значение + " " + ЗначенияПолей.CityDistrictType);
			
		ИначеЕсли ИмяПоля = "STREET" Тогда
			НациональныйАдрес.Улица = СокрЛП(ЭлементСписка.Значение  + " " + ЗначенияПолей.StreetType);
			
		ИначеЕсли ИмяПоля = "HOUSETYPE" Тогда
			
			ЭлементДом = СоздатьНомерДопАдрЭлемента(НациональныйАдрес);
			ЭлементДом.Тип = КодСериализацииОбъектаАдресации(ЭлементСписка.Значение);
			Если ЭлементДом.Тип = Неопределено Тогда
				ЭлементДом.Тип = КодСериализацииОбъектаАдресации("Дом");
			КонецЕсли;
			ЭлементДом.Значение = ЗначенияПолей.HouseNumber;
			
		ИначеЕсли ИмяПоля = "BUILDINGS" Тогда
			
			Для каждого СписокСтроение Из ЭлементСписка.Значение Цикл
				
				// тип корпуса
				ЭлементКорпус          = СоздатьНомерДопАдрЭлемента(НациональныйАдрес);
				ЭлементКорпус.Тип      = КодСериализацииОбъектаАдресации(СписокСтроение.Type);
				Если ЭлементКорпус.Тип = Неопределено Тогда
					ЭлементКорпус.Тип  = КодСериализацииОбъектаАдресации("Корпус");
				КонецЕсли;
				ЭлементКорпус.Значение = СписокСтроение.Number;
				
			КонецЦикла;
			
		ИначеЕсли ИмяПоля = "APARTMENTS" Тогда
			
			Для каждого СписокСтроение Из ЭлементСписка.Значение Цикл
				
				// тип квартиры
				ЭлементКвартира          = СоздатьНомерДопАдрЭлемента(НациональныйАдрес);
				ЭлементКвартира.Тип      = КодСериализацииОбъектаАдресации(СписокСтроение.Type);
				Если ЭлементКвартира.Тип = Неопределено Тогда
					ЭлементКвартира.Тип  = КодСериализацииОбъектаАдресации("Квартира");
				КонецЕсли;
				ЭлементКвартира.Значение = СписокСтроение.Number;
				
			КонецЦикла;
			
		ИначеЕсли ИмяПоля = "OKTMO" Тогда
			НациональныйАдрес.ОКТМО = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля = "ОКАТО" Тогда
			НациональныйАдрес.ОКАТО = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля = "VALUE" Тогда
			ПолеПредставления = СокрЛП(ЭлементСписка.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Представление с приоритетами.
	Если Не ПустаяСтрока(Представление) Тогда
		Результат.Представление = Представление;
	Иначе
		Результат.Представление = ПолеПредставления;
	КонецЕсли;
	
	Адрес.Состав = ?(АдресНациональный, НациональныйАдрес, Результат.Представление);
	
	Возврат Результат;
КонецФункции

// Преобразует формат из XML в JSON
//
Функция КонтактнаяИнформацияВСтруктуруJSON(КонтактнаяИнформация, Знач Тип = Неопределено, Представление = "", ОбновлятьИдентификаторы = Истина) Экспорт
	
	Если Тип <> Неопределено И ТипЗнч(Тип) <> Тип("ПеречислениеСсылка.ТипыКонтактнойИнформации") Тогда
		Тип = УправлениеКонтактнойИнформациейСлужебныйПовтИсп.ТипВидаКонтактнойИнформации(Тип);
	КонецЕсли;
	
	Если Тип = Неопределено Тогда
		
		Если ТипЗнч(КонтактнаяИнформация) = Тип("Строка") Тогда
			Тип = УправлениеКонтактнойИнформацией.ТипКонтактнойИнформации(КонтактнаяИнформация);
		ИначеЕсли ТипЗнч(КонтактнаяИнформация) = Тип("ОбъектXDTO") Тогда
			ПространствоИмен = УправлениеКонтактнойИнформациейСлужебный.ПространствоИмен();
			
			НайденТип = ?(КонтактнаяИнформация.Состав = Неопределено, Неопределено, КонтактнаяИнформация.Состав.Тип());
			Тип = УправлениеКонтактнойИнформациейСлужебный.СоответствиеXDTOТиповКонтактнойИнформации(НайденТип);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Метаданные.ОбщиеМодули.Найти("РаботаСАдресамиКлиентСервер") <> Неопределено Тогда
		МодульРаботаСАдресамиКлиентСервер = ОбщегоНазначения.ОбщийМодуль("РаботаСАдресамиКлиентСервер");
		Результат = МодульРаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(Тип);
		ОсновнаяСтрана = МодульРаботаСАдресамиКлиентСервер.ОсновнаяСтрана();
	Иначе
		Результат = УправлениеКонтактнойИнформациейКлиентСервер.ОписаниеНовойКонтактнойИнформации(Тип);
		ОсновнаяСтрана = "";
	КонецЕсли;
	
	НаименованиеСтраны = "";
	Формат9Запятых = Ложь;
	
	Если ТипЗнч(КонтактнаяИнформация) = Тип("Строка") Тогда
		
		Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВXML(КонтактнаяИнформация) Тогда
			
			РезультатПреобразования = Новый Структура;
			XDTOКонтактнаяИнформация = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияИзXML(КонтактнаяИнформация, Тип, РезультатПреобразования, Представление);
			Результат.Value   = XDTOКонтактнаяИнформация.Представление;
			Результат.Comment = XDTOКонтактнаяИнформация.Комментарий;
		Иначе
			Если СтрЧислоВхождений(КонтактнаяИнформация, ",") = 9 И СтрНайти(КонтактнаяИнформация, "=") = 0 Тогда
				Формат9Запятых = Истина;
				АдресРФ        = КонтактнаяИнформация;
			Иначе
				XDTOКонтактнаяИнформация      = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияИзXML(КонтактнаяИнформация, Тип,, Представление);
				Результат.Value          = XDTOКонтактнаяИнформация.Представление;
				Результат.Comment        = XDTOКонтактнаяИнформация.Комментарий;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(КонтактнаяИнформация) = Тип("Структура") Тогда
		
		Возврат СтруктураАдресаВСтруктуруJSON(КонтактнаяИнформация);
		
	ИначеЕсли ТипЗнч(КонтактнаяИнформация) = Тип("ОбъектXDTO") Тогда
		
		XDTOКонтактнаяИнформация = КонтактнаяИнформация;
		Результат.Value          = XDTOКонтактнаяИнформация.Представление;
		Результат.Comment        = XDTOКонтактнаяИнформация.Комментарий;
		
	КонецЕсли;
	
	Если Тип <> Перечисления.ТипыКонтактнойИнформации.Адрес И Тип <> Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		Возврат Результат;
	КонецЕсли;

	Если НЕ Формат9Запятых Тогда
		
		ПространствоИмен = УправлениеКонтактнойИнформациейСлужебный.ПространствоИмен();
		Состав = XDTOКонтактнаяИнформация.Состав;
		
		Если Состав = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
		XDTOТип = Состав.Тип();
		
		Если XDTOТип = ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
			
			Результат.Вставить("Country", Состав.Страна);
			Страна = ?(ПустаяСтрока(Состав.Страна),
					ОсновнаяСтрана,
					Справочники.СтраныМира.НайтиПоНаименованию(Состав.Страна, Истина));
			НаименованиеСтраны = Страна.Наименование;
			Результат.Вставить("CountryCode", СокрЛП(Страна.Код));
			
			АдресРФ = Состав.Состав;
			
		ИначеЕсли 
			XDTOТип = ФабрикаXDTO.Тип(УправлениеКонтактнойИнформациейСлужебный.ПространствоИмен(), "НомерТелефона")
			Или XDTOТип = ФабрикаXDTO.Тип(УправлениеКонтактнойИнформациейСлужебный.ПространствоИмен(), "НомерФакса") Тогда
			
			Результат.CountryCode = Состав.КодСтраны;
			Результат.AreaCode    = Состав.КодГорода;
			Результат.Number      = Состав.Номер;
			Результат.ExtNumber   = Состав.Добавочный;
			
			Возврат Результат;
			
		ИначеЕсли XDTOТип = ФабрикаXDTO.Тип(ПространствоИмен(), "АдресРФ") Тогда
			АдресРФ = Состав;
		Иначе
			Возврат Результат;
		КонецЕсли;
		
		Если АдресРФ = Неопределено Тогда
			Возврат Результат;
		ИначеЕсли ТипЗнч(АдресРФ) = Тип("Строка") Тогда
			
			Если СтрЧислоВхождений(АдресРФ, ",") = 9 Тогда
				
				Если УправлениеКонтактнойИнформацией.ЭтоСтранаУчастникЕАЭС(Результат.Country) Тогда
					Результат.AddressType = УправлениеКонтактнойИнформациейКлиентСервер.АдресЕАЭС();
				Иначе
					Результат.AddressType = УправлениеКонтактнойИнформациейКлиентСервер.ИностранныйАдрес();
				КонецЕсли;
				
				ЧастиАдреса = СтрРазделить(АдресРФ, ",");
				Результат.ZIPCode = ЧастиАдреса[1];
				
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[2]);
				Результат.Area     = НаименованиеСокращение.Наименование;
				Результат.AreaType = НаименованиеСокращение.Сокращение;
				
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[3]);
				Результат.District     = НаименованиеСокращение.Наименование;
				Результат.DistrictType = НаименованиеСокращение.Сокращение;
				
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[4]);
				Результат.City         = НаименованиеСокращение.Наименование;
				Результат.CityType     = НаименованиеСокращение.Сокращение;
				
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[5]);
				Результат.Locality     = НаименованиеСокращение.Наименование;
				Результат.LocalityType = НаименованиеСокращение.Сокращение;
				
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[6]);
				Результат.Street       = НаименованиеСокращение.Наименование;
				Результат.StreetType   = НаименованиеСокращение.Сокращение;
				
				НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[7]);
				Результат.HouseNumber  = НаименованиеСокращение.Сокращение;
				Результат.HouseType    = НаименованиеСокращение.Наименование;
				
				Если ЗначениеЗаполнено(ЧастиАдреса[8]) Тогда
					НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[8]);
					Результат.Buildings.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(
						НаименованиеСокращение.Наименование, НаименованиеСокращение.Сокращение));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЧастиАдреса[9]) Тогда
					НаименованиеСокращение = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЧастиАдреса[9]);
					Результат.Apartments.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(
						НаименованиеСокращение.Наименование, НаименованиеСокращение.Сокращение));
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			Если ЗначениеЗаполнено(АдресРФ.Адрес_по_документу) Тогда
				Результат.AddressType = УправлениеКонтактнойИнформациейКлиентСервер.АдресВСвободнойФорме();
			Иначе
				Результат.AddressType = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес();
			КонецЕсли;
			
			Результат.Country = НаименованиеСтраны;
			Результат.ZIPCode = Обработки.РасширенныйВводКонтактнойИнформации.ПочтовыйИндексАдреса(АдресРФ);
			Результат.OKTMO = Формат(АдресРФ.ОКТМО, "ЧГ=0");
			Результат.OKATO = Формат(АдресРФ.ОКАТО, "ЧГ=0");
			
			СубъектРФ = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(АдресРФ.СубъектРФ);
			Результат.Area     = Строка(СубъектРФ.Наименование);
			Результат.AreaType = Строка(СубъектРФ.Сокращение);
			
			РайонАдреса = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(РайонАдреса(АдресРФ));
			Результат.District     = Строка(РайонАдреса.Наименование);
			Результат.DistrictType = Строка(РайонАдреса.Сокращение);
			
			Город = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(АдресРФ.Город);
			Результат.City     = Строка(Город.Наименование);
			Результат.CityType = Строка(Город.Сокращение);
			
			НаселПункт = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(АдресРФ.НаселПункт);
			Результат.Locality     = Строка(НаселПункт.Наименование);
			Результат.LocalityType = Строка(НаселПункт.Сокращение);
			
			Улица = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(АдресРФ.Улица);
			Результат.Street     = Строка(Улица.Наименование);
			Результат.StreetType = Строка(Улица.Сокращение);
			
			ВнутригРайон = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(АдресРФ.ВнутригРайон);
			Результат.CityDistrict     = Строка(ВнутригРайон.Наименование);
			Результат.CityDistrictType = Строка(ВнутригРайон.Сокращение);
			
			ЗначениеДополнительныхЭлементов = Обработки.РасширенныйВводКонтактнойИнформации.ЗначениеДополнительныхЭлементов(АдресРФ);
			Если ЗначениеЗаполнено(ЗначениеДополнительныхЭлементов.ДополнительныйЭлемент) Тогда
				ДополнительныйЭлемент = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЗначениеДополнительныхЭлементов.ДополнительныйЭлемент);
				Результат.Territory     = Строка(ДополнительныйЭлемент.Наименование);
				Результат.TerritoryType = Строка(ДополнительныйЭлемент.Сокращение);
			КонецЕсли;
			Если ЗначениеЗаполнено(ЗначениеДополнительныхЭлементов.ПодчиненныйЭлемент) Тогда
				ПодчиненныйЭлемент = УправлениеКонтактнойИнформациейКлиентСервер.НаименованиеСокращение(ЗначениеДополнительныхЭлементов.ПодчиненныйЭлемент);
				Результат.Street     = Строка(ПодчиненныйЭлемент.Наименование);
				Результат.StreetType = Строка(ПодчиненныйЭлемент.Сокращение);
			КонецЕсли;
			
			ЗданияИПомещения = Обработки.РасширенныйВводКонтактнойИнформации.ЗданияИПомещенияАдреса(АдресРФ);
			Для каждого Здание Из ЗданияИПомещения.Здания Цикл
				Если Здание.Вид = 1 Тогда
					Результат.HouseType = Здание.Тип;
					Результат.HouseNumber = Здание.Значение;
				Иначе
					Результат.Buildings.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(Здание.Тип, Здание.Значение));
				КонецЕсли;
			КонецЦикла;
			
			Для каждого Помещение Из ЗданияИПомещения.Помещения Цикл
				Результат.Apartments.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(Помещение.Тип, Помещение.Значение));
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ОбновлятьИдентификаторы И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		МодульАдресныйКлассификаторСлужебный.УстановитьИдентификаторыАдреса(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьАдресДляВвода(Данные) Экспорт
	
	Если Данные.Свойство("id") И ПустаяСтрока(Данные.id) И ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		МодульАдресныйКлассификаторСлужебный.УстановитьИдентификаторыАдреса(Данные);
	КонецЕсли;
	
	НаселенныйПунктДетально = РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	ЗаполнитьЗначенияСвойств(НаселенныйПунктДетально, Данные);
	
	Если ТипЗнч(НаселенныйПунктДетально.Buildings) <> Тип("Массив") Тогда
		НаселенныйПунктДетально.Buildings = Новый Массив;
	КонецЕсли;
	
	Если ТипЗнч(НаселенныйПунктДетально.Apartments) <> Тип("Массив") Тогда
		НаселенныйПунктДетально.Buildings = Новый Массив;
	КонецЕсли;
	
	Для каждого ЭлементАдреса Из НаселенныйПунктДетально Цикл
		
		Если СтрЗаканчиваетсяНа(ЭлементАдреса.Ключ, "Id")
			И ТипЗнч(ЭлементАдреса.Значение) = Тип("Строка")
			И СтрДлина(ЭлементАдреса.Значение) = 36 Тогда
				НаселенныйПунктДетально[ЭлементАдреса.Ключ] = Новый УникальныйИдентификатор(ЭлементАдреса.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Данные.Свойство("house") Тогда
		НаселенныйПунктДетально.HouseNumber = Данные.house;
	КонецЕсли;
	
	Возврат НаселенныйПунктДетально;
	
КонецФункции

// Читает и устанавливает район адреса.
//
//  Параметры:
//      XDTOАдрес     - ОбъектXDTO - Контактная информация или XDTO адреса.
//      НовоеЗначение - Строка - устанавливаемое значение.
//
//  Возвращаемое значение:
//      Строка - новое значение.
//
Функция РайонАдреса(XDTOАдрес, НовоеЗначение = Неопределено)
	
	Если НовоеЗначение = Неопределено Тогда
		
		XDTOТип = XDTOАдрес.Тип();
		Если XDTOТип = ФабрикаXDTO.Тип(ПространствоИмен(), "АдресРФ") Тогда
			АдресРФ = XDTOАдрес;
		Иначе
			АдресРФ = XDTOАдрес.Состав;
		КонецЕсли;
		
		Если ТипЗнч(АдресРФ) = Тип("ОбъектXDTO") Тогда
			Возврат УправлениеКонтактнойИнформацией.ПолучитьXDTOРеквизитОбъекта(АдресРФ, XPathРайона());
		КонецЕсли;
		
		Возврат Неопределено;
	КонецЕсли;
	
	// Запись
	Запись = СвРайМО(XDTOАдрес);
	Запись.Район = НовоеЗначение;
	Возврат НовоеЗначение;
	
КонецФункции

Функция СвРайМО(АдресРФ)
	Если АдресРФ.СвРайМО <> Неопределено Тогда
		Возврат АдресРФ.СвРайМО;
	КонецЕсли;
	
	АдресРФ.СвРайМО = ФабрикаXDTO.Создать( АдресРФ.Свойства().Получить("СвРайМО").Тип );
	Возврат АдресРФ.СвРайМО;
КонецФункции

// Возвращает XPath для дополнительного объекта адресации по умолчанию.
//
//  Параметры;
//      Уровень - Число - уровень объекта. 90  - дополнительный(Варианты: ГСК, СНТ, ТЕР), 91 - подчиненный, -1 -
//                        Ориентир.
//
// Возвращаемое значение:
//      Строка - XPath
//
Функция XPathДополнительногоОбъектаАдресации(Уровень, ТипаАдресногоЭлемента = "") Экспорт
	КодСериализации = КодСериализацииДополнительногоОбъектаАдресации(Уровень, ТипаАдресногоЭлемента);
	Возврат "ДопАдрЭл[ТипАдрЭл='" + КодСериализации + "']";
КонецФункции

Функция КодСериализацииДополнительногоОбъектаАдресации(Уровень, ТипаАдресногоЭлемента = "")
	
	Если Уровень = 90 Тогда
		Если ВРег(ТипаАдресногоЭлемента) = "ГСК" Тогда
			Возврат "10600000";
		ИначеЕсли ВРег(ТипаАдресногоЭлемента) = "СНТ" Тогда
			Возврат "10300000";
		ИначеЕсли ВРег(ТипаАдресногоЭлемента) = "ТЕР" Тогда
			Возврат "10700000";
		Иначе
			Возврат "10200000";
		КонецЕсли;
	ИначеЕсли Уровень = 91 Тогда
		Возврат "10400000";
	КонецЕсли;
	
	// Все остальное - считаем ориентиром.
	Возврат "Местоположение";
КонецФункции

// Возвращает список всех регионов адресного классификатора.
//
// Возвращаемое значение:
//   ТаблицаЗначений - содержит колонки:
//      * КодСубъектаРФ - Число                   - Код региона.
//      * Идентификатор - УникальныйИдентификатор - Идентификатор региона.
//      * Представление - Строка                  - Наименование и сокращение региона.
//      * Загружено     - Булево                  - Истина, если классификатор по данному региону сейчас загружен.
//      * ДатаВерсии    - Дата                    - UTC версия загруженных данных.
//   Неопределено    - если нет подсистемы адресного классификатора.
// 
Функция ВсеРегионы()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		Возврат МодульАдресныйКлассификаторСлужебный.СведенияОЗагрузкеСубъектовРФ();
	КонецЕсли;
	Возврат Неопределено;
	
КонецФункции

Функция СоздатьНомерДопАдрЭлемента(АдресРФ)
	ДопАдрЭл = СоздатьДопАдрЭлемента(АдресРФ);
	ДопАдрЭл.Номер = ФабрикаXDTO.Создать(ДопАдрЭл.Тип().Свойства.Получить("Номер").Тип);
	Возврат ДопАдрЭл.Номер;
КонецФункции

Функция СоздатьДопАдрЭлемента(АдресРФ)
	СвойствоДопАдрЭлемента = АдресРФ.ДопАдрЭл.ВладеющееСвойство;
	ДопАдрЭлемента = ФабрикаXDTO.Создать(СвойствоДопАдрЭлемента.Тип);
	АдресРФ.ДопАдрЭл.Добавить(ДопАдрЭлемента);
	Возврат ДопАдрЭлемента;
КонецФункции

// Возвращает наименование региона по его коду.
//
//  Параметры:
//      Код - Строка, Число - код региона.
//
// Возвращаемое значение:
//      Строка - полное наименование региона с сокращением.
//      Неопределено - если нет ни одной подсистемы адресного классификатора.
// 
Функция РегионКода(Знач Код)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификатор = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификатор");
		Возврат МодульАдресныйКлассификатор.НаименованиеРегионаПоКоду(Код);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает сведения об адресах в виде структуру частей адреса и кодов КЛАДР.
//
Функция СведенияОбАдресахВВидеСтруктуры(Адреса, ДополнительныеПараметры)
	
	Результат = Новый Массив;
	
	Для каждого Адрес Из Адреса Цикл
		Результат.Добавить(СведенияОбАдресеВВидеСтруктуры(Адрес, ДополнительныеПараметры));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает сведения об адресе в виде отдельных частей адреса и различных кодов (код региона, ОКТМО и др.).
//
Функция СведенияОбАдресеВВидеСтруктуры(Знач АдресСтрокой, ДополнительныеПараметры)
	
	Результат = РаботаСАдресамиКлиентСервер.КонструкторПолейАдреса();
	
	Если АдресСтрокой = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Параметры = ИнициализироватьПараметрыСведенийОбАдресе(ДополнительныеПараметры);
	СформироватьПоляСведенийОбАдресе(Результат, Параметры);
	
	СоответствиеСокращений = Новый Соответствие;
	
	Если НЕ (ТипЗнч(АдресСтрокой) = Тип("Структура") И АдресСтрокой.Свойство("Value")) Тогда
		
		Если НЕ УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВJSON(АдресСтрокой) Тогда
			АдресСтрокой = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияВJSON(АдресСтрокой, Перечисления.ТипыКонтактнойИнформации.Адрес);
		КонецЕсли;
		
	КонецЕсли;
	
	Адрес = УправлениеКонтактнойИнформациейСлужебный.JSONВКонтактнуюИнформациюПоПолям(АдресСтрокой, Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	Если ПустаяСтрока(Адрес.ID) И Параметры.ВозвращатьКоды Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
			МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
			МодульАдресныйКлассификаторСлужебный.УстановитьИдентификаторыАдреса(Адрес);
		КонецЕсли;
		
	КонецЕсли;
	
	// Основные сведения
	Результат.Комментарий = Адрес.Comment;
	Результат.Индекс      = Адрес.ZipCode;
	Результат.ТипАдреса   = Адрес.AddressType;
	Результат.Страна      = Адрес.Country;
	Результат.КодСтраны   = КодыСтраны(Адрес);
	НаименованиеРегиона = СокрЛП(Адрес["Area"] + " " +Адрес["AreaType"]);
	
	ЭтоНациональныйАдрес = ?(СтрСравнить(Адрес.Country, РаботаСАдресамиКлиентСервер.ОсновнаяСтрана().Наименование) = 0, Истина, Ложь);
	КодРегиона = ?(ЭтоНациональныйАдрес, КодРегиона(НаименованиеРегиона), "");
	Результат.КодРегиона = КодРегиона;
	
	СоответствиеУровней = Новый Соответствие;
	СоответствиеУровней.Вставить("area",         Новый Структура("Имя, Уровень", "Регион", 1));
	СоответствиеУровней.Вставить("district",     Новый Структура("Имя, Уровень", "Район", 3));
	СоответствиеУровней.Вставить("munDistrict",  Новый Структура("Имя, Уровень", "МуниципальныйРайон", 31));
	СоответствиеУровней.Вставить("city",         Новый Структура("Имя, Уровень", "Город", 4));
	СоответствиеУровней.Вставить("settlement",   Новый Структура("Имя, Уровень", "Поселение", 41));
	СоответствиеУровней.Вставить("cityDistrict", Новый Структура("Имя, Уровень", "ВнутригородскойРайон", 5));
	СоответствиеУровней.Вставить("locality",     Новый Структура("Имя, Уровень", "НаселенныйПункт", 6));
	СоответствиеУровней.Вставить("territory",    Новый Структура("Имя, Уровень", "Территория", 65));
	СоответствиеУровней.Вставить("street",       Новый Структура("Имя, Уровень", "Улица", 7));
	
	Если Параметры.ВозвращатьКоды Тогда
		Идентификаторы = Новый Структура();
		Результат.Идентификаторы = Идентификаторы;
	КонецЕсли;
	
	ИдентификаторАдресногоОбъекта = "";
	МаксимальныйУровеньИдентификатора = 0;
	Для каждого ЧастьАдреса Из СоответствиеУровней Цикл
		
		ИдентификаторТекущегоУровня = Адрес[ЧастьАдреса.Ключ + "Id"];
		ТипКраткий = Адрес[ЧастьАдреса.Ключ + "Type"];
		
		НаименованиеУровня = ?(Параметры.НаименованиеВключаетСокращение, СокрЛП(Адрес[ЧастьАдреса.Ключ] + " " + ТипКраткий),
			Адрес[ЧастьАдреса.Ключ]);
			
		Результат[ЧастьАдреса.Значение.Имя] = НаименованиеУровня;
		Результат[ЧастьАдреса.Значение.Имя + "Сокращение"] = ТипКраткий;
		Результат[ЧастьАдреса.Значение.Имя + "ТипКраткий"] = ТипКраткий;
		СоответствиеСокращений.Вставить(ЧастьАдреса.Значение.Уровень, ТипКраткий);
		
		Если Параметры.КодыАдреса Или Параметры.КодыКЛАДР Тогда
			Идентификаторы.Вставить(ЧастьАдреса.Значение.Имя + "Идентификатор", ИдентификаторТекущегоУровня);
			Идентификаторы.Вставить(ЧастьАдреса.Значение.Имя, ИдентификаторТекущегоУровня);
		КонецЕсли;
		
		УровеньИдентификатора = ?(ЧастьАдреса.Значение.Уровень < 10, ЧастьАдреса.Значение.Уровень * 10, ЧастьАдреса.Значение.Уровень);
		Если ЗначениеЗаполнено(ИдентификаторТекущегоУровня) И МаксимальныйУровеньИдентификатора < УровеньИдентификатора Тогда
			ИдентификаторАдресногоОбъекта = ИдентификаторТекущегоУровня;
		КонецЕсли;
		
	КонецЦикла;
	
	// Коды мун. уровней
	Результат.КодМуниципальногоРайона = КодМуниципальногоРайона(Адрес.munDistrictType, Адрес.oktmo,
		ЭтоГородФедеральногоЗначения(НаименованиеРегиона));
	Результат.КодПоселения = КодПоселения(Адрес.settlementType);
	
	Если Параметры.КодыАдреса Тогда
		Результат.ИдентификаторАдресногоОбъекта = ИдентификаторАдресногоОбъекта;
		Результат.ИдентификаторДома             = ?(ЗначениеЗаполнено(Адрес.houseId), Адрес.houseId, "");
	КонецЕсли;
	
	// Установка полных наименований типов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
		МодульАдресныйКлассификаторСлужебный.ПолныеНаименованияСокращений(СоответствиеСокращений);
		
		Для каждого ЧастьАдреса Из СоответствиеУровней Цикл
			
			Результат[ЧастьАдреса.Значение.Имя + "ТипПолный"] = СоответствиеСокращений[ЧастьАдреса.Значение.Уровень];
			
			Если Параметры.ПолноеНаименованиеСокращений  = Истина Тогда
				Результат[ЧастьАдреса.Значение.Имя + "Сокращение"] = СоответствиеСокращений[ЧастьАдреса.Значение.Уровень];
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Результат.НомерЗемельногоУчастка         = Адрес.stead;
	Результат.ИдентификаторЗемельногоУчастка = Адрес.steadId;
	
	// Дома, здания, строения
	Результат.Здание = Новый Структура("ТипЗдания, Номер");
	Результат.Корпуса = Новый Массив;
	Результат.Помещения = Новый Массив;
	
	Результат.Здание.Вставить("ТипЗдания", Адрес.HouseType);
	Результат.Здание.Вставить("Номер",     Адрес.HouseNumber);
	
	Для каждого Корпус Из Адрес.Buildings Цикл
		Результат.Корпуса.Добавить(Новый Структура("ТипКорпуса, Номер", Корпус.Type, Корпус.Number));
	КонецЦикла;
	
	Для каждого Корпус Из Адрес.Apartments Цикл
		Результат.Помещения.Добавить(Новый Структура("ТипПомещения, Номер", Корпус.Type, Корпус.Number));
	КонецЦикла;
	
	Если ЭтоНациональныйАдрес Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
			МодульАдресныйКлассификаторСлужебный = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификаторСлужебный");
			Если Параметры.ВозвращатьКоды Тогда
				ДополнительныеКоды = МодульАдресныйКлассификаторСлужебный.КодыАдресаИКодыКЛАДР(Адрес, ИдентификаторАдресногоОбъекта);
				Результат.Вставить("ДополнительныеКоды", ДополнительныеКоды.КодыАдреса);
				Результат.Вставить("КодыКЛАДР",          ДополнительныеКоды.КодыКЛАДР);
				Если Параметры.КодыАдреса И ДополнительныеКоды.КодыАдреса.Свойство("ИдентификаторДома") Тогда
					Результат.ИдентификаторДома = ДополнительныеКоды.КодыАдреса.ИдентификаторДома;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЗаполнитьКодыАдреса(Результат, Адрес);
		КонецЕсли;
		
		Если Параметры.ПроверитьАдрес Тогда
			
			РезультатПроверки = СведенияОбПроверкеАдреса(АдресСтрокой);
			Результат.РезультатПроверкиАдреса = РезультатПроверки.Результат;
			Результат.ОшибкиПроверкиАдреса = РезультатПроверки.СписокОшибок;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Параметры.БезПредставлений Тогда
		
		Результат.Представление = РаботаСАдресамиКлиентСервер.ПредставлениеАдреса(Адрес,
			Параметры.ВключатьСтрануВПредставление, РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес());
			
		Если ПустаяСтрока(Результат.Представление) Тогда
			Результат.Представление = Адрес.value;
		КонецЕсли;
		
		Результат.МуниципальноеПредставление = РаботаСАдресамиКлиентСервер.ПредставлениеАдреса(Адрес,
			Параметры.ВключатьСтрануВПредставление, РаботаСАдресамиКлиентСервер.МуниципальныйАдрес());
			
		Если ПустаяСтрока(Результат.МуниципальноеПредставление) Тогда
			Результат.МуниципальноеПредставление = Адрес.value;
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеКонтактнойИнформациейСлужебный.ЗаменитьВСтруктуреНеопределеноНаПустуюСтроку(Результат);
	
	Возврат Результат;
	
КонецФункции

Функция ИнициализироватьПараметрыСведенийОбАдресе(Знач ДополнительныеПараметры)
	
	Параметры = Новый Структура();
	Параметры.Вставить("БезПредставлений",                 Ложь);
	Параметры.Вставить("НаименованиеВключаетСокращение",   Ложь);
	Параметры.Вставить("КодыКЛАДР",                        Ложь);
	Параметры.Вставить("ПолноеНаименованиеСокращений",     Ложь);
	Параметры.Вставить("ПроверитьАдрес",                   Ложь);
	Параметры.Вставить("ВключатьСтрануВПредставление",     Ложь);
	Параметры.Вставить("КодыАдреса",                       Ложь);
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Параметры, ДополнительныеПараметры);
	КонецЕсли;
	
	Параметры.Вставить("ВозвращатьКоды", Параметры.КодыАдреса Или Параметры.КодыКЛАДР);
	
	Возврат Параметры;
	
КонецФункции

Функция КодМуниципальногоРайона(Знач ТипМуниципальногоРайона, ОКТМО, ЭтоГородФедеральногоЗначения)
	
	КодМуниципальногоРайона = "";
	
	Если СтрДлина(ОКТМО) > 3 Тогда
		
		КодВторойСтупениКлассификации = Сред(ОКТМО, 3, 1);
		Если КодВторойСтупениКлассификации = "3"
			ИЛИ ЭтоГородФедеральногоЗначения И КодВторойСтупениКлассификации = "9" Тогда
			КодМуниципальногоРайона = "3";
		ИначеЕсли КодВторойСтупениКлассификации = "6" Тогда
			КодМуниципальногоРайона = "1";
		ИначеЕсли КодВторойСтупениКлассификации = "7" Тогда
			КодМуниципальногоРайона = "2";
		ИначеЕсли КодВторойСтупениКлассификации = "9" И НЕ ЭтоГородФедеральногоЗначения Тогда
			КодМуниципальногоРайона = "4";
		ИначеЕсли КодВторойСтупениКлассификации = "8" Тогда
			КодМуниципальногоРайона = "4";
		КонецЕсли;
		
	Иначе
		
		Если СтрСравнить(ТипМуниципальногоРайона, НСтр("ru = 'м.р-н'")) = 0 Тогда
			КодМуниципальногоРайона = "1";
		ИначеЕсли СтрСравнить(ТипМуниципальногоРайона, НСтр("ru = 'г.о.'")) = 0 Тогда
			КодМуниципальногоРайона = "2";
		ИначеЕсли СтрСравнить(ТипМуниципальногоРайона, НСтр("ru = 'вн.тер.'")) = 0
			Или СтрСравнить(ТипМуниципальногоРайона, НСтр("ru = 'вн.тер. г.'")) = 0 Тогда
			КодМуниципальногоРайона = "3";
		ИначеЕсли СтрСравнить(ТипМуниципальногоРайона, НСтр("ru = 'мун. окр.'")) = 0 Тогда
			КодМуниципальногоРайона = "4";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КодМуниципальногоРайона;

КонецФункции

Функция КодПоселения(Знач ТипПоселения)
	
	КодПоселения = "";
	
	Если СтрСравнить(ТипПоселения, НСтр("ru='г. п.'")) = 0 Тогда
		КодПоселения = "1";
	ИначеЕсли СтрСравнить(ТипПоселения, НСтр("ru='с.п.'")) = 0 Тогда
		КодПоселения = "2";
	ИначеЕсли СтрСравнить(ТипПоселения, НСтр("ru='меж.тер.'")) = 0 Тогда
		КодПоселения = "3";
	ИначеЕсли СтрСравнить(ТипПоселения, НСтр("ru='вн.р-н'")) = 0 Тогда
		КодПоселения = "4";
	КонецЕсли;
	
	Возврат КодПоселения;
	
КонецФункции

Функция КодыСтраны(Знач Адрес)
	
	КодСтраны = Адрес.CountryCode;
	
	Если НЕ ЗначениеЗаполнено(КодСтраны) Тогда
		СведенияОСтране = УправлениеКонтактнойИнформацией.ДанныеСтраныМира(Неопределено, Адрес.Country);
		Если СведенияОСтране <> Неопределено Тогда
			КодСтраны = СведенияОСтране.Код;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КодСтраны;
	
КонецФункции

Функция СведенияОбПроверкеАдреса(Знач АдресСтрокой)
	
	СтатусПроверки = Новый Структура();
	СтатусПроверки.Вставить("Результат", "");
	СтатусПроверки.Вставить("СписокОшибок", "");
	
	РезультатПроверки = ПроверитьАдрес(АдресСтрокой);
	
	Если РезультатПроверки.Результат = "Корректный" Тогда
		СтатусПроверки.Результат = "Успех";
	ИначеЕсли РезультатПроверки.Результат = "СодержитОшибки" Тогда
		СтатусПроверки.Результат = "Ошибка";
		СтатусПроверки.СписокОшибок = РезультатПроверки.СписокОшибок;
	Иначе
		СтатусПроверки.Результат = "Отказ";
		СтатусПроверки.СписокОшибок = РезультатПроверки.СписокОшибок;
	КонецЕсли;
	
	Возврат СтатусПроверки;
	
КонецФункции

Процедура СформироватьПоляСведенийОбАдресе(Результат, Знач Параметры)
	
	Результат.Вставить("КодМуниципальногоРайона", "");
	Результат.Вставить("КодПоселения", "");
	
	Если Не Параметры.КодыАдреса И Не Параметры.КодыКЛАДР Тогда
		Результат.Удалить("ДополнительныеКоды");
		Результат.Удалить("КодыКЛАДР");
		Результат.Удалить("Идентификаторы");
	КонецЕсли;
	
	Если Не Параметры.КодыАдреса Тогда
		Результат.Удалить("ИдентификаторАдресногоОбъекта");
		Результат.Удалить("ИдентификаторДома");
	КонецЕсли;
	
	Если Не Параметры.БезПредставлений Тогда
		Результат.Вставить("Представление", "");
		Результат.Вставить("МуниципальноеПредставление", "");
	КонецЕсли;
	
	Результат.Вставить("РезультатПроверкиАдреса", "");
	Результат.Вставить("ОшибкиПроверкиАдреса", "")
	
КонецПроцедуры

Функция СтруктураАдресаВСтруктуруJSON(Знач КонтактнаяИнформация)
	
	ОписаниеКонтактнойИнформации = РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
	
	СоответствиеПолей = Новый Соответствие();
	СоответствиеПолей.Вставить("ТипАдреса",                      "addressType");
	СоответствиеПолей.Вставить("Представление",                  "value");
	СоответствиеПолей.Вставить("Комментарий",                    "comment");
	СоответствиеПолей.Вставить("НаименованиеСтраны",             "country");
	СоответствиеПолей.Вставить("Страна",                         "country");
	СоответствиеПолей.Вставить("Индекс",                         "ZIPCode");
	СоответствиеПолей.Вставить("ОКТМО",                          "oktmo");
	СоответствиеПолей.Вставить("ОКАТО",                          "okato");
	СоответствиеПолей.Вставить("Регион",                         "area");
	СоответствиеПолей.Вставить("РегионСокращение",               "areaType");
	СоответствиеПолей.Вставить("Район",                          "district");
	СоответствиеПолей.Вставить("РайонСокращение",                "districtType");
	СоответствиеПолей.Вставить("Город",                          "city");
	СоответствиеПолей.Вставить("ГородСокращение",                "cityType");
	СоответствиеПолей.Вставить("НаселенныйПункт",                "locality");
	СоответствиеПолей.Вставить("НаселенныйПунктСокращение",      "localityType");
	СоответствиеПолей.Вставить("Улица",                          "street");
	СоответствиеПолей.Вставить("УлицаСокращение",                "streetType");
	СоответствиеПолей.Вставить("КодРегиона",                     "areaCode");
	СоответствиеПолей.Вставить("МуниципальныйРайон",             "munDistrict");
	СоответствиеПолей.Вставить("МуниципальныйРайонСокращение",   "munDistrictType");
	СоответствиеПолей.Вставить("Поселение",                      "settlement");
	СоответствиеПолей.Вставить("ПоселениеСокращение",            "settlementType");
	СоответствиеПолей.Вставить("ВнутригородскойРайон",           "cityDistrict");
	СоответствиеПолей.Вставить("ВнутригородскойРайонСокращение", "cityDistrictType");
	СоответствиеПолей.Вставить("Территория",                     "territory");
	СоответствиеПолей.Вставить("ТерриторияСокращение",           "territoryType");
	СоответствиеПолей.Вставить("ИдентификаторАдресногоОбъекта",  "id");
	СоответствиеПолей.Вставить("ИдентификаторДома",              "houseId");
	СоответствиеПолей.Вставить("Дом",                            "houseNumber");
	СоответствиеПолей.Вставить("ТипДома",                        "houseType");
	
	ОписаниеКонтактнойИнформации.AddressType = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес();
	
	Для каждого ПолеКонтактнойИнформации Из КонтактнаяИнформация Цикл
		ИмяПоля = СоответствиеПолей.Получить(ПолеКонтактнойИнформации.Ключ);
		Если ИмяПоля <> Неопределено Тогда
			ОписаниеКонтактнойИнформации[ИмяПоля] = ПолеКонтактнойИнформации.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если КонтактнаяИнформация.Свойство("Здание")
		 И ТипЗнч(КонтактнаяИнформация.Здание) = Тип("Структура")
		 И КонтактнаяИнформация.Здание.Свойство("Номер") Тогда
		
			ОписаниеКонтактнойИнформации.HouseNumber = ?(КонтактнаяИнформация.Здание.Свойство("Номер"), КонтактнаяИнформация.Здание.Номер, "");
			ОписаниеКонтактнойИнформации.HouseType = ?(КонтактнаяИнформация.Здание.Свойство("ТипЗдания"), КонтактнаяИнформация.Здание.ТипЗдания, "Дом");
		
	КонецЕсли;
	
	Если КонтактнаяИнформация.Свойство("Корпус")И ЗначениеЗаполнено(КонтактнаяИнформация.Корпус) Тогда
		
		ТипКорпуса = ?(КонтактнаяИнформация.Свойство("ТипКорпуса"), КонтактнаяИнформация.ТипКорпуса, "Корпус"); // Тип владения не локализуется
		ОписаниеКонтактнойИнформации.buildings.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ТипКорпуса, КонтактнаяИнформация.Корпус));
	ИначеЕсли КонтактнаяИнформация.Свойство("Корпуса")И ТипЗнч(КонтактнаяИнформация.Корпуса) = Тип("Массив") Тогда
		Для каждого Корпус Из КонтактнаяИнформация.Корпуса Цикл
			ОписаниеКонтактнойИнформации.buildings.Добавить(
				УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(Корпус.Тип, Корпус.Номер));
		КонецЦикла;
		
	КонецЕсли;
	
	Если КонтактнаяИнформация.Свойство("Квартира") И ЗначениеЗаполнено(КонтактнаяИнформация.Квартира) Тогда
		
		ТипКвартиры = ?(КонтактнаяИнформация.Свойство("ТипКвартиры"), КонтактнаяИнформация.ТипКвартиры, "Квартира"); // Тип помещения не локализуется
		ОписаниеКонтактнойИнформации.apartments.Добавить(УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(ТипКвартиры, КонтактнаяИнформация.Квартира));
		
	ИначеЕсли КонтактнаяИнформация.Свойство("Помещения")И ТипЗнч(КонтактнаяИнформация.Помещения) = Тип("Массив") Тогда
		
		Для каждого Помещение Из КонтактнаяИнформация.Помещения Цикл
			ОписаниеКонтактнойИнформации.apartments.Добавить(
				УправлениеКонтактнойИнформациейКлиентСервер.ЗначениеСтроенияИлиПомещения(Помещение.Тип, Помещение.Номер));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ОписаниеКонтактнойИнформации;
	
КонецФункции

// Возвращает код региона по его полному наименованию.
//
//  Параметры:
//      НаименованиеРегиона - Строка - полное наименование региона с сокращением.
//
// Возвращаемое значение:
//      Строка - код региона из двух цифр. Пустая строка, если наименование определить не удалось.
//      Неопределено - если нет ни одной подсистемы адресного классификатора.
// 
Функция КодРегиона(Знач ПолноеНаименование) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификатор = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификатор");
		Код = МодульАдресныйКлассификатор.КодРегионаПоНаименованию(ПолноеНаименование);
		Возврат Формат(Код, "ЧЦ=2; ЧН=; ЧВН=");
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет коды по структуре адреса.
//
Процедура ЗаполнитьКодыАдреса(Результат, Адрес)
	
	ДополнительныеКоды = Новый Структура();
	ДополнительныеКоды.Вставить("ОКТМО", "");
	ДополнительныеКоды.Вставить("ОКАТО", "");
	ДополнительныеКоды.Вставить("КодИФНСФЛ", "");
	ДополнительныеКоды.Вставить("КодИФНСЮЛ", "");
	ДополнительныеКоды.Вставить("КодУчасткаИФНСФЛ", "");
	ДополнительныеКоды.Вставить("КодУчасткаИФНСЮЛ", "");
	ДополнительныеКоды.Вставить("ИдентификаторДома", "");
	ДополнительныеКоды.Вставить("КодУчасткаИФНСЮЛ", "");
	
	Результат.Вставить("ДополнительныеКоды", ДополнительныеКоды);

	УстановитьЗначенияПоля(Результат, Адрес, "ОКТМО", "OKTMO");
	УстановитьЗначенияПоля(Результат, Адрес, "ОКАТО", "OKATO");
	УстановитьЗначенияПоля(Результат, Адрес, "КодИФНСФЛ", "IFNSFLCode");
	УстановитьЗначенияПоля(Результат, Адрес, "КодИФНСЮЛ", "IFNSULCode");
	УстановитьЗначенияПоля(Результат, Адрес, "КодУчасткаИФНСФЛ", "IFNSFLAreaCode");
	УстановитьЗначенияПоля(Результат, Адрес, "КодУчасткаИФНСЮЛ", "IFNSULAreaCode");
	УстановитьЗначенияПоля(Результат, Адрес, "ИдентификаторДома", "HouseId");
	
	КодКЛАДР = "";
	Если Адрес.Свойство("CodeKLADR") И ЗначениеЗаполнено(Адрес.CodeKLADR) Тогда
		КодКЛАДР = Адрес.CodeKLADR;
	КонецЕсли;
	Результат.Вставить("КодыКЛАДР", ОпределитьКодыКодыКЛАДР(Адрес, КодКЛАДР));
	
КонецПроцедуры

Процедура УстановитьЗначенияПоля(Приемник, Источник, ИмяПоляПриемник, ИмяПоляИсточник)
	Если Источник.Свойство(ИмяПоляИсточник) Тогда
		Значение = ?(ЗначениеЗаполнено(Источник), Источник[ИмяПоляИсточник], ""); // Неопределено, Null и др. в пустую строку
	КонецЕсли;
	Приемник[ИмяПоляПриемник] = Значение;
КонецПроцедуры

// Заполняет коды по структуре адреса.
//
Функция ОпределитьКодыКодыКЛАДР(Адрес, Знач КодКЛАДР)
	
	КодыКЛАДР = Новый Структура();
	КодыКЛАДР.Вставить("Регион",               "");
	КодыКЛАДР.Вставить("Округ",                "");
	КодыКЛАДР.Вставить("Город",                "");
	КодыКЛАДР.Вставить("ВнутригородскойРайон", "");
	КодыКЛАДР.Вставить("НаселенныйПункт",      "");
	КодыКЛАДР.Вставить("Улица",                "");
	
	// Заполнить коды КЛАДР.
	Если ЗначениеЗаполнено(КодКЛАДР) Тогда
		КодКЛАДР = Формат(КодКЛАДР, "ЧРГ=''; ЧГ=0");
		
		Если СтрДлина(КодКЛАДР) = 17 Тогда
			КодыКЛАДР.Улица = КодКЛАДР;
		КонецЕсли;
		КодКЛАДР = Лев(КодКЛАДР, 13);
		
		Если СтрДлина(КодКЛАДР) = 12 Тогда
			КодКЛАДР = "0" + КодКЛАДР;
		КонецЕсли;
		
		Если СтрДлина(КодКЛАДР) = 13 Тогда
			Если ЗначениеЗаполнено(Адрес.Area) Тогда
				КодыКЛАДР.Регион = Лев(КодКЛАДР, 2) + "00000000000";
			КонецЕсли;
			Если ЗначениеЗаполнено(Адрес.District) Тогда
				КодыКЛАДР.Район = Лев(КодКЛАДР, 5) + "00000000";
			КонецЕсли;
			Если ЗначениеЗаполнено(Адрес.City) Тогда
				КодыКЛАДР.Город = Лев(КодКЛАДР, 8) + "00000";
			КонецЕсли;
			Если ЗначениеЗаполнено(Адрес.Settlement) Тогда
				КодыКЛАДР.НаселенныйПункт = Лев(КодКЛАДР, 11) + "00";
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат КодыКЛАДР;
	
КонецФункции

Функция РазделитьДомаСтроения(Значение, ТипПоУмолчанию)
	
	Результат = Новый Структура("Номер, Тип");
	
	Номер = СокрЛП(Значение);
	Позиция = СтрНайти(Номер, " ");
	Если Позиция > 0 Тогда
		Результат.Тип = Лев(Номер, Позиция);
		Результат.Номер = Сред(Номер, Позиция + 1);
	Иначе
		Результат.Тип = ТипПоУмолчанию;
		Результат.Номер = Номер;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает строку списка полей.
//
// Параметры:
//    СоответствиеПолей - СписокЗначений - соответствия полей.
//    БезПустыхПолей    - Булево - необязательный флаг сохранения полей с пустыми значениями.
//
//  Возвращаемое значение:
//     Строка - результат, преобразованный из списка.
//
Функция ПреобразоватьСписокПолейВСтроку(СоответствиеПолей, БезПустыхПолей = Истина)
	
	КвартираДобавлена = Ложь;
	КорпусДобавлен = Ложь;
	ПредыдущиеЗначение = Неопределено;
	
	СтруктураЗначенийПолей = Новый Структура;
	Для Каждого Элемент Из СоответствиеПолей Цикл
		
		Если Элемент.Представление = "Корпус" ИЛИ Элемент.Представление = "ТипКорпуса" Тогда
			Если ПредыдущиеЗначение <> Неопределено И ПредыдущиеЗначение.Представление = "ТипКорпуса"
				И ПредыдущиеЗначение.Значение = "Корпус" Тогда
				СтруктураЗначенийПолей.Вставить(Элемент.Представление, Элемент.Значение);
				КорпусДобавлен = Истина;
			ИначеЕсли НЕ КорпусДобавлен Тогда
				СтруктураЗначенийПолей.Вставить(Элемент.Представление, Элемент.Значение);
			КонецЕсли;
		ИначеЕсли Элемент.Представление = "Квартира" ИЛИ Элемент.Представление = "ТипКвартиры" Тогда
			Если ПредыдущиеЗначение <> Неопределено И ПредыдущиеЗначение.Представление = "ТипКвартиры"
				И ПредыдущиеЗначение.Значение = "Квартира" Тогда
				СтруктураЗначенийПолей.Вставить(Элемент.Представление, Элемент.Значение);
				КвартираДобавлена = Истина;
			ИначеЕсли НЕ КвартираДобавлена Тогда
				СтруктураЗначенийПолей.Вставить(Элемент.Представление, Элемент.Значение);				
			КонецЕсли;
		Иначе
			СтруктураЗначенийПолей.Вставить(Элемент.Представление, Элемент.Значение);	
		КонецЕсли;
		ПредыдущиеЗначение = Элемент;
	КонецЦикла;
	
	Возврат СтрокаПолей(СтруктураЗначенийПолей, БезПустыхПолей);
КонецФункции

//  Возвращает строку списка полей.
//
//  Параметры:
//    СтруктураЗначенийПолей - Структура - структура значений полей.
//    БезПустыхПолей         - Булево - необязательный флаг сохранения полей с пустыми значениями.
//
//  Возвращаемое значение:
///     Строка - результат преобразования из структуры.
//
Функция СтрокаПолей(СтруктураЗначенийПолей, БезПустыхПолей = Истина) Экспорт
	
	Результат = "";
	Для Каждого ЗначениеПоля Из СтруктураЗначенийПолей Цикл
		Если БезПустыхПолей И ПустаяСтрока(ЗначениеПоля.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат = Результат + ?(Результат = "", "", Символы.ПС)
		            + ЗначениеПоля.Ключ + "=" + СтрЗаменить(ЗначениеПоля.Значение, Символы.ПС, Символы.ПС + Символы.Таб);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция НаборБуквВНижнемРегистре() Экспорт
	Возврат "абвгдеёжзийклмнопрстуфхцчшщыъьэюяabcdefghijklmnopqrstuvwxyz";
КонецФункции

Функция НаборБуквВВерхнемРегистре() Экспорт
	Возврат "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЫЪЬЭЮЯABCDEFGHIJKLMNOPQRSTUVWXYZ";
КонецФункции

Процедура ДополнитьНастройкиПроверкиАдреса(ПараметрыВида, ВидКонтактнойИнформации) Экспорт
	
	НастройкиПроверки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидКонтактнойИнформации, "ПроверятьПоФИАС, УказыватьОКТМО");
	ПараметрыВида.НастройкиПроверки.ПроверятьПоФИАС = НастройкиПроверки.ПроверятьПоФИАС;
	ПараметрыВида.НастройкиПроверки.УказыватьОКТМО  = НастройкиПроверки.УказыватьОКТМО;
	
КонецПроцедуры

Функция ЭтоГородФедеральногоЗначения(Город) Экспорт
	
	НазванияГородовФедеральногоЗначения = НазванияГородовФедеральногоЗначения();
	Возврат НазванияГородовФедеральногоЗначения.Получить(ВРег(Город)) = Истина;
	
КонецФункции

// Возвращает массив наименований регионов - городов федерального значения.
Функция НазванияГородовФедеральногоЗначения()
	
	Результат = Новый Соответствие;
	Результат.Вставить("МОСКВА Г", Истина);
	Результат.Вставить("САНКТ-ПЕТЕРБУРГ Г", Истина);
	Результат.Вставить("СЕВАСТОПОЛЬ Г", Истина);
	Результат.Вставить("БАЙКОНУР Г", Истина);
	
	Возврат Результат;
КонецФункции

// Общероссийский классификатор стран мира

// Возвращает полные данные ОКСМ классификатора.
// Таблица значений в макете проиндексирована по полям "Код", "Наименование".
//
// Возвращаемое значение:
//     ТаблицаЗначений - данные классификатора с колонками:
//         * Код                - Строка - данные страны.
//         * Наименование       - Строка - данные страны.
//         * НаименованиеПолное - Строка - данные страны.
//         * КодАльфа2          - Строка - данные страны.
//         * КодАльфа3          - Строка - данные страны.
//
Функция ТаблицаКлассификатора() Экспорт
	Макет = Справочники.СтраныМира.ПолучитьМакет("Классификатор");
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(Макет.ПолучитьТекст());
	
	Возврат СериализаторXDTO.ПрочитатьXML(Чтение);
КонецФункции

Функция ЗаполнитьДанныеВыбораАвтоподбораПоСтранам(Знач Параметры) Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;
	
	// Будем имитировать поведение платформы - поиск по всем доступным полям поиска с формированием подобного списка.
	// Подразумеваем, что поля справочника и классификатора совпадают, за исключением отсутствующего в классификаторе поля "Ссылка".
	Запрос = Новый Запрос;
	
	ПрефиксПараметраОтбора = "ПараметрыОтбора";
	
	ПоляОтбора = Новый Массив;
	ПоляОтбора.Добавить("Код");
	ПоляОтбора.Добавить("Наименование");
	
	// Общий отбор по параметрам
	Отборы = Новый Массив;
	Для Каждого КлючЗначение Из Параметры.Отбор Цикл
		ЗначениеПоля = КлючЗначение.Значение;
		ИмяПоля      = КлючЗначение.Ключ;
		ИмяПараметра = ПрефиксПараметраОтбора + ИмяПоля;
		
		Если ТипЗнч(ЗначениеПоля) = Тип("Массив") Или ТипЗнч(ЗначениеПоля) = Тип("ФиксированныйМассив") Тогда
			Отборы.Добавить(" %1." + ИмяПоля + " В (&" + ИмяПараметра + ")");
		Иначе
			Отборы.Добавить(" %1." + ИмяПоля + " = &" + ИмяПараметра);
		КонецЕсли;
		
		ПоляОтбора.Добавить(ИмяПоля);
		Запрос.УстановитьПараметр(ИмяПараметра, КлючЗначение.Значение);
	КонецЦикла;
	
	// Дополнительные отборы
	Если Параметры.Свойство("ВыборГруппИЭлементов") Тогда
		Если Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы Тогда
			
			Отборы.Добавить(" %1.ЭтоГруппа");
			
		ИначеЕсли Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы Тогда
			
			Отборы.Добавить(" (НЕ %1.ЭтоГруппа)");
			
		КонецЕсли;
	КонецЕсли;
	ШаблонОтбора = СтрСоединить(Отборы, " И ");
	
	// Источники данных
	Если (Параметры.Свойство("ТолькоДанныеКлассификатора") И Параметры.ТолькоДанныеКлассификатора) Тогда
		// Запрос только по классификатору.
		ШаблонЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ 50
		|	NULL                       КАК Ссылка,
		|	Классификатор.Код          КАК Код,
		|	Классификатор.Наименование КАК Наименование,
		|	ЛОЖЬ                       КАК ПометкаУдаления,
		|	%2                         КАК Представление
		|ИЗ
		|	Классификатор КАК Классификатор
		|ГДЕ
		|	Классификатор.%1 ПОДОБНО &СтрокаПоиска";
		
		Если ЗначениеЗаполнено(ШаблонОтбора) Тогда
			ШаблонЗапроса = ШаблонЗапроса + "
			|И (
			|	" + СтрЗаменить(ШаблонОтбора, "%1", "Классификатор") + "
			|	)
			|";
		КонецЕсли;
	Иначе
		// Запрос и по справочнику и по классификатору.
		ШаблонЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ 50 
		|	СтраныМира.Ссылка                                             КАК Ссылка,
		|	ЕСТЬNULL(СтраныМира.Код, Классификатор.Код)                   КАК Код,
		|	ЕСТЬNULL(СтраныМира.Наименование, Классификатор.Наименование) КАК Наименование,
		|	ЕСТЬNULL(СтраныМира.ПометкаУдаления, ЛОЖЬ)                    КАК ПометкаУдаления,
		|
		|	ВЫБОР КОГДА СтраныМира.Ссылка ЕСТЬ NULL ТОГДА 
		|		%2 
		|	ИНАЧЕ 
		|		%3
		|	КОНЕЦ КАК Представление
		|
		|ИЗ
		|	Справочник.СтраныМира КАК СтраныМира
		|ПОЛНОЕ СОЕДИНЕНИЕ
		|	Классификатор
		|ПО
		|	Классификатор.Код = СтраныМира.Код
		|	И Классификатор.Наименование = СтраныМира.Наименование
		|ГДЕ 
		|	(СтраныМира.%1 ПОДОБНО &СтрокаПоиска ИЛИ Классификатор.%1 ПОДОБНО &СтрокаПоиска)";
		
		Если ЗначениеЗаполнено(ШаблонОтбора) Тогда
			ШаблонЗапроса = ШаблонЗапроса  + "
			|	И ((" + СтрЗаменить(ШаблонОтбора, "%1", "Классификатор") + ")
			|	ИЛИ (" + СтрЗаменить(ШаблонОтбора, "%1", "СтраныМира") + "))
			|
			|";
		КонецЕсли;
		
	КонецЕсли;
	
	ИменаПолей = ПоляПоиска(ПоляОтбора);
	ИменаПолейСтрокой = СтрСоединить(ИменаПолей.ИменаПолейДляОтбораВКлассификаторе, ", ");
	
	ТекстЗапросаКлассификатор = "ВЫБРАТЬ
	|	" + ИменаПолейСтрокой + "
	|ПОМЕСТИТЬ
	|	Классификатор
	|ИЗ
	|	&Классификатор КАК Классификатор
	|ИНДЕКСИРОВАТЬ ПО
	|	" + ИменаПолейСтрокой + "
	|";
	
	НаборЗапросов = Новый Массив;
	Для Каждого ДанныеПоля Из ИменаПолей.СписокПолей Цикл
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "%1", ДанныеПоля.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%2", СтрЗаменить(ДанныеПоля.ШаблонПредставления, "%1", "Классификатор"));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%3", СтрЗаменить(ДанныеПоля.ШаблонПредставления, "%1", "СтраныМира"));
		
		НаборЗапросов.Добавить(ТекстЗапроса);
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапросаКлассификатор + "
	|;
	|" + СтрСоединить(НаборЗапросов, " ОБЪЕДИНИТЬ ВСЕ ") + "
	|УПОРЯДОЧИТЬ ПО Представление";
	
	Запрос.УстановитьПараметр("Классификатор", ТаблицаКлассификатора());
	Запрос.УстановитьПараметр("СтрокаПоиска", ЭкранироватьСимволыПодобия(Параметры.СтрокаПоиска) + "%");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДанныеВыбора;
	КонецЕсли;
	
	ДлинаСтрокиПоиска = СтрДлина(Параметры.СтрокаПоиска);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			// Данные справочника
			ЭлементВыбора = Выборка.Ссылка;
		Иначе
			// Данные классификатора
			РезультатВыбора = Новый Структура("Код, Наименование",
				Выборка.Код, Выборка.Наименование);
			
			ЭлементВыбора = Новый Структура("Значение, ПометкаУдаления, Предупреждение",
				РезультатВыбора, Выборка.ПометкаУдаления, Неопределено);
		КонецЕсли;
		
		ВыделенныйТекст = Новый ФорматированнаяСтрока(Лев(Выборка.Представление, ДлинаСтрокиПоиска), Новый Шрифт(,, Истина), ЦветаСтиля.РезультатУспехЦвет);
		Представление = Новый ФорматированнаяСтрока(ВыделенныйТекст, Сред(Выборка.Представление, ДлинаСтрокиПоиска + 1));
		
		ДанныеВыбора.Добавить(ЭлементВыбора, Представление);
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

// Возвращает поля поиска в порядке предпочтения для справочника стран мира.
//
// Возвращаемое значение:
//    Массив - содержит структуры с полями:
//      * Имя                 - Строка - имя реквизита поиска.
//      * ШаблонПредставления - Строка - шаблон для формирования значения представления по именам реквизитов, 
//                                       например: "%1.Наименование (%1.Код)". Здесь "Наименование" и "Код" - имена
//                                       реквизитов,
//                                       "%1" - заполнитель для передачи псевдонима таблицы.
//
Функция ПоляПоиска(ПоляОтбора)
	
	Результат = Новый Массив;
	
	СписокПолей = Справочники.СтраныМира.ПустаяСсылка().Метаданные().ВводПоСтроке;
	ГраницаПолей = СписокПолей.Количество() - 1;
	
	Для Индекс = 0 По ГраницаПолей Цикл
		ИмяПоля = СписокПолей[Индекс].Имя;
		
		Если ПоляОтбора.Найти(ИмяПоля) = Неопределено Тогда
			ПоляОтбора.Добавить(ИмяПоля);
		КонецЕсли;
		
		ШаблонПредставления = "%1." + ИмяПоля;
		
		ОстальныеПоля = Новый Массив;
		Для Позиция = 0 По ГраницаПолей Цикл
			Если Позиция <> Индекс Тогда
				ОстальныеПоля.Добавить("%1." + СписокПолей[Позиция].Имя);
			КонецЕсли;
		КонецЦикла;
		
		Если ОстальныеПоля.Количество() > 0 Тогда
			ШаблонПредставления = ШаблонПредставления
				+ " + "" ("" + " + СтрСоединить(ОстальныеПоля, " + "", "" + ") + " + "")""";
		КонецЕсли;
		
		Результат.Добавить(
			Новый Структура("Имя, ШаблонПредставления", ИмяПоля, ШаблонПредставления));
	КонецЦикла;
	
	Возврат Новый Структура("СписокПолей, ИменаПолейДляОтбораВКлассификаторе", Результат, ПоляОтбора);
КонецФункции

// Экранирует символы для использования в функции запроса ПОДОБНО.
//
Функция ЭкранироватьСимволыПодобия(Знач Текст, Знач СпецСимвол = "\")
	Результат = Текст;
	СимволыПодобия = "%_[]^" + СпецСимвол;
	
	Для Позиция=1 По СтрДлина(СимволыПодобия) Цикл
		ТекущийСимвол = Сред(СимволыПодобия, Позиция, 1);
		Результат = СтрЗаменить(Результат, ТекущийСимвол, СпецСимвол + ТекущийСимвол);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти