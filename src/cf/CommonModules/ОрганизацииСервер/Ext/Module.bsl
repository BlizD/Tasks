///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОрганизацииСервер

// Возвращает структуру, содержащую сведения об организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.*).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - 
//            см. ОрганизацииСервер.РегистрационныеДанныеОрганизации
//            см. ОрганизацииСервер.ДополнительныеСведенияОрганизации
//            см. ОрганизацииСервер.КонтактнаяИнформацияОрганизации
//            см. ОрганизацииСервер.БанковскийСчетОрганизации
//            см. ОрганизацииСервер.РегистрационныеДанныеОбособленногоПодразделения
//            см. ОрганизацииСервер.КонтактнаяИнформацияОбособленногоПодразделения
//            см. ОрганизацииСервер.РегистрационныеДанныеИностраннойОрганизации
//            см. ОрганизацииСервер.КонтактнаяИнформацияИностраннойОрганизации
//            см. ОрганизацииСервер.РегистрационныеДанныеИндивидуальногоПредпринимателя
//            см. ОрганизацииСервер.РегистрационныеДанныеРуководителяОрганизации
//            см. ОрганизацииСервер.КонтактнаяИнформацияРуководителяОрганизации
//            см. ОрганизацииСервер.РегистрационныеДанныеГлавногоБухгалтера
//            см. ОрганизацииСервер.КонтактнаяИнформацияГлавногоБухгалтера
//            см. ОрганизацииСервер.РегистрационныеДанныеУполномоченногоПредставителя.
//
// Пример:
//       СведенияОбОрганизации = ОрганизацииСервер.СведенияОбОрганизации(Организация);
//
Функция СведенияОбОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	ОбщиеСведения = Новый Массив;
	ОбщиеСведения.Добавить(РегистрационныеДанныеОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(ДополнительныеСведенияОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(БанковскийСчетОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(РегистрационныеДанныеОбособленногоПодразделения(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияОбособленногоПодразделения(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(РегистрационныеДанныеОтделенияИностраннойОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияОтделенияИностраннойОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(РегистрационныеДанныеИндивидуальногоПредпринимателя(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияИндивидуальногоПредпринимателя(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(РегистрационныеДанныеРуководителяОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияРуководителяОрганизации(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(РегистрационныеДанныеГлавногоБухгалтера(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияГлавногоБухгалтера(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(РегистрационныеДанныеУполномоченногоПредставителя(Организация, Поля, Дата));
	ОбщиеСведения.Добавить(КонтактнаяИнформацияУполномоченногоПредставителя(Организация, Поля, Дата));
	
	СведенияОбОрганизации = Новый Структура();
	
	Для каждого Сведения Из ОбщиеСведения Цикл
		
		Для каждого Элемент Из Сведения Цикл
			
			Если НЕ СведенияОбОрганизации.Свойство(Элемент.Ключ) Тогда
				СведенияОбОрганизации.Вставить(Элемент.Ключ, Элемент.Значение);
			Иначе
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Элемент с таким ключем ""%1"" уже определен.'"), Элемент.Ключ);
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат СведенияОбОрганизации;
	
КонецФункции

// Возвращает структуру, содержащую сведения о регистрационной информации организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * ДатаРегистрации - Дата - дата регистрации юридического лица.
//            * ИНН - Строка - ИНН организации.
//            * КодНалоговогоОргана - Строка - код налогового органа.
//            * КодОКВЭД - Строка - код ОКВЭД.
//            * КодОКВЭД2 - Строка - код ОКВЭД2.
//            * КодОКФС - Строка - код ОКФС.
//            * КодПоОКАТО - Строка - код по ОКАТО.
//            * КодПоОКТМО - Строка - код по ОКТМО.
//            * КПП - Строка - КПП организации.
//            * НаименованиеНалоговогоОргана - Строка - наименование налогового органа.
//            * НаименованиеОКВЭД - Строка - наименование ОКВЭД.
//            * НаименованиеОКВЭД2 - Строка - наименование ОКВЭД2.
//            * НаименованиеПолное - Строка - полное наименование.
//            * НаименованиеСокращенное - Строка - сокращенное наименование.
//            * ОГРН - Строка - ОГРН.
//            * ПризнакНалогоплательщикаКатегория - Строка - признак налогоплательщика категория.
//            * ПризнакНалогоплательщикаКод - Число - признак налогоплательщика код.
//            * РегистрационныйНомерПФР - Строка - регистрационный номер ПФР.
//            * РегистрационныйНомерТФОМС - Строка - регистрационный номер ТФОМС.
//            * СвидетельствоДатаВыдачи - Дата - дата выдачи свидетельства.
//            * СвидетельствоКодОргана - Строка - код органа выдавшего свидетельство.
//            * СвидетельствоНаименованиеОргана - Строка - наименование органа выдавшего свидетельство.
//            * СвидетельствоСерияНомер - Строка - серия и номер свидетельства.
//            * СтавкаНалогаНаПрибыльМестный - Число - местная ставка налога на прибыль.
//            * СтавкаНалогаНаПрибыльСубъектРФ - Число - ставка налога на прибыль субъекта РФ.
//            * СтавкаНалогаНаПрибыльФБ - Число - ставка налога на прибыль федерального бюджета.
//            * СтавкаНалогаУСН - Число - ставка налога УСН.
//            * ЭтоФизическоеЛицо - Булево - признак что это физическое лицо.
//            * ЭтоЮридическоеЛицо - Булево - признак что это юридическое лицо.
//
Функция РегистрационныеДанныеОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанные = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхОрганизации(Организация, Поля, Дата, РегистрационныеДанные);
	
	Возврат РегистрационныеДанные;
		
КонецФункции

// Возвращает структуру, содержащую сведения о дополнительной информации организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииДополнительныхСведенийОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * АдресЭлектроннойПочтыОрганизации - Строка - email организации.
//			  * АдресОрганизацииФакт - Строка - адрес организации.
//			  * АдресОрганизацииФактXML - Строка - адрес организации xml.
//			  * АдресОрганизацииФактJSON - Строка - адрес организации json.
//			  * АдресИностраннойОрганизации - Строка - адрес иностранной организации.
//			  * АдресИностраннойОрганизацииXML - Строка - адрес иностранной организации.
//			  * АдресИностраннойОрганизацииJSON - Строка - адрес иностранной организации.
//            * ВидПлательщикаСтраховыхВзносов - Строка - вид плательщика страховых взносов.
//            * ГоловнаяОрганизацияКПП - Строка - КПП головная организация.
//            * ГоловнаяОрганизацияНаименование - Строка - наименование головной организации.
//            * ДатаПереходаНаУСН - Дата - дата перехода на УСН.
//            * ДолжностьОтправителя - Строка - должность отправителя.
//            * КодОКОНХ - Строка - код ОКОНХ.
//            * КодОрганаПФР - Строка - код органа ПФР.
//            * КодОрганаФСГС - Строка - код органа ФСГС.
//            * КодПодчиненностиФСС - Строка - код подчиненности ФСС.
//            * КодПодчиненностиФССДобрСтрах - Строка - код подчиненности ФСС добр. страх.
//            * КодПоОКТМО - Строка - код по ОКТМО.
//            * НаименованиеТерриториальногоОрганаФСС - Строка - наименование территориального органа ФСС.
//            * ОбъектНалогообложенияУСН - Число - объект налогообложения УСН.
//            * ОтветственноеЛицоСтатистикаФИО - Строка - ФИО ответственного лица за статистическую отчетность.
//            * ОтветственноеЛицоСтатистикаДолжность - Строка - должность ответственного лица за статистическую отчетность.
//            * ОтветственноеЛицоСтатистикаТелефон - Строка - телефон ответственного лица за статистическую отчетность.
//            * ОтветственноеЛицоСтатистикаАдресЭлектроннойПочты - Строка - email ответственного лица за статистическую отчетность.
//            * ПочтовыйАдресОрганизации - Строка - почтовый адрес организации.
//            * ПочтовыйАдресОрганизацииXML - Строка - почтовый адрес организации в формате xml.
//            * ПочтовыйАдресОрганизацииJSON - Строка - почтовый адрес организации в формате json.
//            * РегистрационныйНомерТФОМС - Строка - регистрационный номер ТФОМС.
//            * РегистрационныйНомерФСС - Строка - регистрационный номер ФСС.
//            * СведенияОбИсполнителеДолжность - Строка - должность исполнителя.
//            * СведенияОбИсполнителеТелефон - Строка - телефон исполнителя.
//            * СведенияОбИсполнителеФИО - Строка - ФИО.
//            * БухгалтерИмя - Строка - имя бухгалтера.
//            * БухгалтерОтчество - Строка - отчество бухгалтера.
//            * БухгалтерФамилия - Строка - фамилия бухгалтера.
//			  * БухгалтерАдресЭлектроннойПочты - Строка - адрес электронной почты бухгалтера	
//            * РуководительДолжность - Строка - должность руководителя.
//            * РуководительИмя - Строка - имя руководителя.
//            * РуководительОтчество - Строка - отчество руководителя.
//            * РуководительФамилия - Строка - фамилия руководителя.
//			  * РуководительАдресЭлектроннойПочты - Строка - адрес электронной почты руководителя.
//            * СистемыНалогообложения - Строка - система налогообложения.
//            * СтраховойТариф - Число - страховой тариф.
//            * ТелефонОрганизации - Строка - телефон организации.
//            * ФаксОрганизации - Строка - факс организации.
//            * ШифрСтрахователяФСС - Строка - шифр страхователя ФСС.
//  
Функция ДополнительныеСведенияОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	ДополнительныеСведения = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииДополнительныхСведенийОрганизации(Организация, Поля, Дата, ДополнительныеСведения);
	
	Возврат ДополнительныеСведения; 
	
КонецФункции

// Возвращает структуру, содержащую сведения о контактной информации организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * АдресОрганизации - Строка - адрес организации.
//            * АдресОрганизацииXML - Строка - адрес организации xml.
//            * АдресОрганизацииJSON - Строка - адрес организации json.
//            * АдресОрганизацииГород - Строка - город.
//            * АдресОрганизацииДом - Строка - дом.
//            * АдресОрганизацииИндекс - Строка - индекс.
//            * АдресОрганизацииКвартира - Строка - квартира.
//            * АдресОрганизацииКодРегиона - Строка - код региона.
//            * АдресОрганизацииКорпус - Строка - корпус.
//            * АдресОрганизацииНаселенныйПункт - Строка - населенный пункт.
//            * АдресОрганизацииРайон - Строка - район.
//            * АдресОрганизацииРегион - Строка - регион.
//            * АдресОрганизацииУлица - Строка - улица.
//
Функция КонтактнаяИнформацияОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформация = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииОрганизации(Организация, Поля, Дата, КонтактнаяИнформация);
	
	Возврат КонтактнаяИнформация;
	
КонецФункции

// Возвращает структуру, содержащую сведения о банковском счете организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииБанковскогоСчетаОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * БанковскийСчетБанк - Строка - банк.
//            * БанковскийСчетБИК - Строка - БИК.
//            * БанковскийСчетКоррСчет - Строка - корр. счет.
//            * БанковскийСчетНомерСчета - Строка - номер счета.
//
Функция БанковскийСчетОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	БанковскийСчет = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииБанковскогоСчетаОрганизации(Организация, Поля, Дата, БанковскийСчет);
	
	Возврат БанковскийСчет;
		
КонецФункции

#КонецОбласти

#Область СведенияОбОбособленномПодразделении

// Возвращает структуру, содержащую сведения о регистрационной информации обособленного подразделения.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхОбособленногоПодразделения).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * КППОбособленногоПодразделения - Строка - КПП обособленного подразделения.
//            * НаименованиеОбособленногоПодразделенияПолное - Строка - наименование обособленного подразделения.
//
Функция РегистрационныеДанныеОбособленногоПодразделения(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанные = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхОбособленногоПодразделения(Организация, Поля, Дата, РегистрационныеДанные);
	
	Возврат РегистрационныеДанные;
			
КонецФункции


// Возвращает структуру, содержащую сведения о контактной информации обособленного подразделения.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииОбособленногоПодразделения).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * АдресОбособленногоПодразделения - Строка - адрес обособленного подразделения.
//            * АдресОбособленногоПодразделенияXML - Строка - адрес обособленного подразделения в xml.
//            * АдресОбособленногоПодразделенияJSON - Строка - адрес обособленного подразделения в json.
//            * АдресОбособленногоПодразделенияГород - Строка - город.
//            * АдресОбособленногоПодразделенияДом - Строка - дом.
//            * АдресОбособленногоПодразделенияИндекс - Строка - индекс.
//            * АдресОбособленногоПодразделенияКвартира - Строка - квартира.
//            * АдресОбособленногоПодразделенияКодРегиона - Строка - регион.
//            * АдресОбособленногоПодразделенияКорпус - Строка - корпус.
//            * АдресОбособленногоПодразделенияНаселенныйПункт - Строка - населенный пункт.
//            * АдресОбособленногоПодразделенияРайон - Строка - район.
//            * АдресОбособленногоПодразделенияРегион - Строка - регион.
//            * АдресОбособленногоПодразделенияУлица - Строка - улица.
//
Функция КонтактнаяИнформацияОбособленногоПодразделения(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформация = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииОбособленногоПодразделения(Организация, Поля, Дата, КонтактнаяИнформация);
	
	Возврат КонтактнаяИнформация;
		
КонецФункции

#КонецОбласти

#Область СведенияОбИностраннойОрганизации

// Возвращает структуру, содержащую сведения о регистрационной информации иностранной организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхОбособленногоПодразделения).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * ОтделениеИностраннойОрганизацииИНН - Строка - ИНН отделения иностранной организации.
//            * ОтделениеИностраннойОрганизацииКПП - Строка - КПП отделения иностранной организации.
//            * ОтделениеИностраннойОрганизацииНаименование - Строка - наименование.
//
Функция РегистрационныеДанныеОтделенияИностраннойОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанные = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхОбособленногоПодразделения(Организация, Поля, Дата, РегистрационныеДанные);
	
	Возврат РегистрационныеДанные;
	
КонецФункции


// Возвращает структуру, содержащую сведения о контактной информации иностранной организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииИностраннойОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * ОтделениеИностраннойОрганизацииАдресРФ - Строка - адрес иностранной организации.
//            * ОтделениеИностраннойОрганизацииАдресРФXML - Строка - адрес иностранной организации.
//            * ОтделениеИностраннойОрганизацииАдресРФJSON - Строка - адрес иностранной организации.
//            * ОтделениеИностраннойОрганизацииАдресРФГород - Строка - город.
//            * ОтделениеИностраннойОрганизацииАдресРФДом - Строка - дом.
//            * ОтделениеИностраннойОрганизацииАдресРФИндекс - Строка - индекс.
//            * ОтделениеИностраннойОрганизацииАдресРФКвартира - Строка - квартира.
//            * ОтделениеИностраннойОрганизацииАдресРФКодРегиона - Строка - код региона.
//            * ОтделениеИностраннойОрганизацииАдресРФКорпус - Строка - корпус.
//            * ОтделениеИностраннойОрганизацииАдресРФНаселенныйПункт - Строка - населенный пункт.
//            * ОтделениеИностраннойОрганизацииАдресРФРайон - Строка - район.
//            * ОтделениеИностраннойОрганизацииАдресРФРегион - Строка - регион.
//            * ОтделениеИностраннойОрганизацииАдресРФУлица - Строка - улица.
//
Функция КонтактнаяИнформацияОтделенияИностраннойОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформация = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииИностраннойОрганизации(Организация, Поля, Дата, КонтактнаяИнформация);
	
	Возврат КонтактнаяИнформация;
	
КонецФункции

#КонецОбласти

#Область СведенияОбИндивидуальномПредпринимателе

// Возвращает структуру, содержащую сведения о регистрационной информации ИП.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхИндивидуальногоПредпринимателя).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * ИПВидДокументаУдостоверенияЛичности - Строка - вид документа ИП.
//            * ИПГражданствоКод - Строка - гражданство ИП.
//            * ИПГражданствоНаименование - Строка - гражданство наименование.
//            * ИПДатаВыдачиУдостоверенияЛичности - Дата - дата выдачи удостоверения личности.
//            * ИПДатаРождения - Дата - дата рождения.
//            * ИндивидуальныйПредпринимательИНН - Строка - ИНН индивидуального предпринимателя.
//            * ИПНаименование - Строка - наименование.
//            * ИПКемВыданУдостоверенияЛичности - Строка - кем выдано удостоверение личности.
//            * ИПКодПодразделенияУдостоверенияЛичности - Строка - код подразделения удостоверение личности.
//            * ИПКодУдостоверенияЛичности - Строка - код удостоверения личности.
//            * ИПМестоРождения - Строка - место рождения.
//            * ИПНомерУдостоверенияЛичности - Строка - номер удостоверения личности.
//            * ИПРегистрационныйНомерПФР - Строка - регистрационный номер ПФР.
//            * ИПРегистрационныйНомерТФОМС - Строка - регистрационный номер ТФОМС.
//            * ИПАдресЭлектроннойПочты - Строка - адрес электронной почты индивидуального предпринимателя.
//            * ФизическоеЛицоПол - Число - пол.
//            * ИПСерияУдостоверенияЛичности - Строка - серия удостоверения личности.
//            * ИндивидуальныйПредпринимательСНИЛС - Строка - СНИЛС.
//            * ФизическоеЛицоНаименование - Строка - наименование физлица.
//            * ЭтоИП - Булево - признак, что это индивидуальный предприниматель.
//
Функция РегистрационныеДанныеИндивидуальногоПредпринимателя(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанные = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхИндивидуальногоПредпринимателя(Организация, Поля, Дата, РегистрационныеДанные);
	
	Возврат РегистрационныеДанные;
			
КонецФункции


// Возвращает структуру, содержащую сведения о контактной информации ИП.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииИндивидуальногоПредпринимателя).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * АдресМестаЖительства - Строка - адрес места жительства.
//            * АдресМестаЖительстваXML - Строка - адрес места жительства в формате xml.
//            * АдресМестаЖительстваJSON - Строка - адрес места жительства в формате json.
//            * АдресРегистрации - Строка - адрес регистрации.
//            * АдресРегистрацииXML - Строка - адрес регистрации xml.
//            * АдресРегистрацииJSON - Строка - адрес регистрации json.
//            * ГородМестаЖительства - Строка - город.
//            * ДомашнийТелефонФизЛица - Строка - телефон.
//            * ДомМестаЖительства - Строка - дом.
//            * ИндексМестаЖительства - Строка - индекс.
//            * КвартираМестаЖительства - Строка - квартира.
//            * КодРегионаМестаЖительства - Строка - код региона.
//            * КорпусМестаЖительства - Строка - корпус.
//            * НаселенныйПунктМестаЖительства - Строка - населенный пункт.
//            * РайонМестаЖительства - Строка - район места жительства.
//            * РегионМестаЖительства - Строка - регион.
//            * ТелефонОрганизации - Строка - телефон организации.
//            * УлицаМестаЖительства - Строка - улица.
//
Функция КонтактнаяИнформацияИндивидуальногоПредпринимателя(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформация = Новый Структура(); 
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииИндивидуальногоПредпринимателя(Организация, Поля, Дата, КонтактнаяИнформация);
	
	Возврат КонтактнаяИнформация;
		
КонецФункции

#КонецОбласти

#Область РуководительОрганизации

// Возвращает структуру, содержащую сведения о регистрационной информации руководителя организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхРуководителяОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * РуководительГражданствоКод - Строка - код гражданства.
//            * РуководительГражданствоНаименование - Строка - наименование гражданства.
//            * РуководительДатаРождения - Дата - дата рождения.
//            * РуководительИНН - Строка - ИНН.
//            * РуководительМестоРождения - Строка - место рождения.
//            * РуководительПол - Строка - пол.
//            * РуководительТелефон - Строка - телефон.
//            * РуководительУдостоверениеЛичностиВидДокумента - Строка - вид документа.
//            * РуководительУдостоверениеЛичностиДатаВыдачи - Дата - дата выдачи.
//            * РуководительУдостоверениеЛичностиКемВыдан - Строка - кем выдан.
//            * РуководительУдостоверениеЛичностиКод - Строка - код.
//            * РуководительУдостоверениеЛичностиКодПодразделения - Строка - код подразделения.
//            * РуководительУдостоверениеЛичностиНомер - Строка - номер.
//            * РуководительУдостоверениеЛичностиСерия - Строка - серия.
//            * РуководительФИО - Строка - ФИО.
//
Функция РегистрационныеДанныеРуководителяОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанныеРуководителя = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхРуководителяОрганизации(Организация, Поля, Дата, РегистрационныеДанныеРуководителя);
	
	Возврат РегистрационныеДанныеРуководителя;
			
КонецФункции


// Возвращает структуру, содержащую сведения о контактной информации руководителя организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииРуководителяОрганизации).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * РуководительАдресМестаЖительства - Строка - адрес.
//            * РуководительАдресМестаЖительстваXML - Строка - адрес xml.
//            * РуководительАдресМестаЖительстваJSON - Строка - адрес json.
//            * РуководительАдресМестаЖительстваГород - Строка - город.
//            * РуководительАдресМестаЖительстваДом - Строка - дом.
//            * РуководительАдресМестаЖительстваИндекс - Строка - индекс.
//            * РуководительАдресМестаЖительстваКвартира - Строка - квартира.
//            * РуководительАдресМестаЖительстваКодРегиона - Строка - код региона.
//            * РуководительАдресМестаЖительстваКорпус - Строка - корпус.
//            * РуководительАдресМестаЖительстваНаселенныйПункт - Строка - населенный пункт.
//            * РуководительАдресМестаЖительстваРайон - Строка - район.
//            * РуководительАдресМестаЖительстваРегион - Строка - регион.
//            * РуководительАдресМестаЖительстваУлица - Строка - улица.
//
Функция КонтактнаяИнформацияРуководителяОрганизации(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформацияРуководителя = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииРуководителяОрганизации(Организация, Поля, Дата, КонтактнаяИнформацияРуководителя);
	
	Возврат КонтактнаяИнформацияРуководителя;
	
КонецФункции

#КонецОбласти

#Область СведенияОГлавномБухгалтере

// Возвращает структуру, содержащую сведения о регистрационной информации главного бухгалтера организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхГлавногоБухгалтера).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * БухгалтерГражданствоКод - Строка - код гражданства бухгалтера.
//            * БухгалтерГражданствоНаименование - Строка - наименование гражданства бухгалтера.
//            * БухгалтерГражданствоУполномоченногоПредставителя - Строка - гражданство уполномоченного представителя.
//            * БухгалтерДатаРождения - Строка - дата рождения.
//            * БухгалтерИНН - Строка - ИНН.
//            * БухгалтерМестоРождения - Строка - место рождения.
//            * БухгалтерПол - Строка - пол.
//            * БухгалтерПолУполномоченногоПредставителя - Строка - пол уполномоченного представителя.
//            * БухгалтерТелефон - Строка - телефон.
//            * БухгалтерУдостоверениеЛичностиВидДокумента - Строка - вид документа.
//            * БухгалтерУдостоверениеЛичностиДатаВыдачи - Строка - дата выдачи.
//            * БухгалтерУдостоверениеЛичностиКемВыдан - Строка - кем выдан.
//            * БухгалтерУдостоверениеЛичностиКод - Строка - код.
//            * БухгалтерУдостоверениеЛичностиКодПодразделения - Строка - код подразделения.
//            * БухгалтерУдостоверениеЛичностиНомер - Строка - номер.
//            * БухгалтерУдостоверениеЛичностиСерия - Строка - серия.
//            * БухгалтерУполномоченномПредставителеГражданствоНаименование - Строка - наименование гражданства
//                                                                                     уполномоченного представителя.
//            * БухгалтерФИО - Строка - ФИО.
//
Функция РегистрационныеДанныеГлавногоБухгалтера(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанные = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхГлавногоБухгалтера(Организация, Поля, Дата, РегистрационныеДанные);
	
	Возврат РегистрационныеДанные;
		
КонецФункции


// Возвращает структуру, содержащую сведения о контактной информации главного бухгалтера организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииГлавногоБухгалтера).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * БухгалтерАдресМестаЖительства - Строка - адрес.
//            * БухгалтерАдресМестаЖительстваXML - Строка - адрес xml.
//            * БухгалтерАдресМестаЖительстваJSON - Строка - адрес json.
//            * БухгалтерАдресМестаЖительстваГород - Строка - город.
//            * БухгалтерАдресМестаЖительстваДом - Строка - дом.
//            * БухгалтерАдресМестаЖительстваИндекс - Строка - индекс.
//            * БухгалтерАдресМестаЖительстваКвартира - Строка - квартира.
//            * БухгалтерАдресМестаЖительстваКодРегиона - Строка - код региона.
//            * БухгалтерАдресМестаЖительстваКорпус - Строка - корпус.
//            * БухгалтерАдресМестаЖительстваНаселенныйПункт - Строка - населенный пункт.
//            * БухгалтерАдресМестаЖительстваРайон - Строка - район.
//            * БухгалтерАдресМестаЖительстваРегион - Строка - регион.
//            * БухгалтерАдресМестаЖительстваУлица - Строка - улица.
//
Функция КонтактнаяИнформацияГлавногоБухгалтера(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформация = Новый Структура();	
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииГлавногоБухгалтера(Организация, Поля, Дата, КонтактнаяИнформация);
	
	Возврат КонтактнаяИнформация;
	
КонецФункции

#КонецОбласти

#Область СведенияОбУполномоченномПредставителе

// Возвращает структуру, содержащую сведения о регистрационной информации уполномоченного представителя организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхУполномоченногоПредставителя).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * УполномоченныйПредставительДатаРождения - Строка - дата рождения.
//            * УполномоченныйПредставительИНН - Строка - ИНН.
//            * УполномоченныйПредставительТелефон - Строка - телефон.
//            * УполномоченныйПредставительУдостоверениеЛичностиВидДокумента - Строка - вид документа.
//            * УполномоченныйПредставительУдостоверениеЛичностиДата - Дата - дата.
//            * УполномоченныйПредставительУдостоверениеЛичностиКемВыдан - Строка - кем выдан.
//            * УполномоченныйПредставительУдостоверениеЛичностиКод - Строка - код.
//            * УполномоченныйПредставительУдостоверениеЛичностиНомер - Строка - номер.
//            * УполномоченныйПредставительУдостоверениеЛичностиСерия - Строка - серия.
//            * УполномоченныйПредставительФИО - Строка - ФИО.
//
Функция РегистрационныеДанныеУполномоченногоПредставителя(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	РегистрационныеДанные = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииРегистрационныхДанныхУполномоченногоПредставителя(Организация, Поля, Дата, РегистрационныеДанные);
	
	Возврат РегистрационныеДанные;
	
КонецФункции


// Возвращает структуру, содержащую сведения о контактной информации уполномоченного представителя организации.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, сведения по которой необходимо получить.
//  Поля - Строка - имена полей, перечисленные через запятую, в формате требований к свойствам структуры.
//                  Список допустимых имен полей см. в описании возвращаемого значения (это максимально возможный список
//                  полей, конечный состав определяется в ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииУполномоченногоПредставителя).
//                  Если указана пустая строка, то возвращаются значения всех полей.
//  Дата - Дата, Неопределено - дата, на которую требуется получить данные. Если значение не указано,
//                  то возвращается значение на текущую дату.
// Возвращаемое значение:
//   Структура - содержит имена (ключи) и значения затребованных полей.
//            * УполномоченныйПредставительАдресМестаЖительстваНаселенныйПункт - Строка - населенный пункт.
//            * УполномоченныйПредставительАдресМестаЖительства - Строка - адрес.
//            * УполномоченныйПредставительАдресМестаЖительстваXML - Строка - адрес xml.
//            * УполномоченныйПредставительАдресМестаЖительстваJSON - Строка - адрес json.
//            * УполномоченныйПредставительАдресМестаЖительстваГород - Строка - город.
//            * УполномоченныйПредставительАдресМестаЖительстваДом - Строка - дом.
//            * УполномоченныйПредставительАдресМестаЖительстваИндекс - Строка - индекс.
//            * УполномоченныйПредставительАдресМестаЖительстваКвартира - Строка - квартира.
//            * УполномоченныйПредставительАдресМестаЖительстваКод - Строка - код.
//            * УполномоченныйПредставительАдресМестаЖительстваКорпус - Строка - корпус.
//            * УполномоченныйПредставительАдресМестаЖительстваРайон - Строка - район.
//            * УполномоченныйПредставительАдресМестаЖительстваРегион - Строка - регион.
//            * УполномоченныйПредставительАдресМестаЖительстваУлица - Строка - улица.
//            * УполномоченныйПредставительМестоРождения - Строка - место рождения.
//
Функция КонтактнаяИнформацияУполномоченногоПредставителя(Знач Организация, Знач Поля = "", Знач Дата = Неопределено) Экспорт
	
	КонтактнаяИнформация = Новый Структура();
	ОрганизацииПереопределяемый.ПриОпределенииКонтактнойИнформацииУполномоченногоПредставителя(Организация, Поля, Дата, КонтактнаяИнформация);
	
	Возврат КонтактнаяИнформация;
	
КонецФункции

#КонецОбласти

#КонецОбласти