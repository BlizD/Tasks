///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Устанавливает отчету переданную схему и на основании нее, инициализирует компоновщик настроек.
// Если контекстом является форма отчета (настроек), то обновляет основной реквизит формы - Отчет.
// В результате, контекст объекта и формы отчета синхронизируются.
// Вызывается например, из обработчика ПередЗагрузкойНастроекВКомпоновщик объекта универсального отчета
// для установки схемы, программно сформированной на основании другого объекта метаданных.
//
// Параметры:
//  Отчет - ОтчетОбъект, ВнешнийОтчетОбъект - отчет, которому необходимо установить схему.
//  Контекст - УправляемаяФорма - форма отчета или форма настройки отчета.
//                                Передается "как есть" из одноименного параметра события 
//                                ПередЗагрузкойНастроекВКомпоновщик.
//           - Структура - параметры отчета: см. ВариантыОтчетов.ПодключитьОтчетИЗагрузитьНастройки.
//  Схема - СхемаКомпоновкиДанных - схема, которую необходимо установить отчету.
//  КлючСхемы - Строка - идентификатор новой схемы, который будет записан в дополнительные свойства 
//                       пользовательских настроек.
//
// Пример:
//  // В обработчике объекта отчета ПередЗагрузкойНастроекВКомпоновщик компоновщик настроек
//  // инициализируется на основании схемы из общих макетов:
//  Если КлючСхемы <> "1" Тогда
//  	КлючСхемы = "1";
//  	Схема = ПолучитьОбщийМакет("МояОбщаяСхемаКомпоновки");
//  	ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, Схема, КлючСхемы);
//  КонецЕсли;
//
Процедура ПодключитьСхему(Отчет, Контекст, Схема, КлючСхемы) Экспорт
	СобытиеФормы = (ТипЗнч(Контекст) = Тип("УправляемаяФорма"));
	
	Отчет.СхемаКомпоновкиДанных = Схема;
	Если СобытиеФормы Тогда
		НастройкиОтчета = Контекст.НастройкиОтчета;
		АдресСхемы = НастройкиОтчета.АдресСхемы;
		НастройкиОтчета.Вставить("СхемаМодифицирована", Истина);
	Иначе
		АдресСхемыЗаполнен = (ТипЗнч(Контекст.АдресСхемы) = Тип("Строка") И ЭтоАдресВременногоХранилища(Контекст.АдресСхемы));
		Если Не АдресСхемыЗаполнен Тогда
			ИдентификаторФормы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст, "ИдентификаторФормы");
			Если ТипЗнч(ИдентификаторФормы) = Тип("УникальныйИдентификатор") Тогда
				АдресСхемыЗаполнен = Истина;
				Контекст.АдресСхемы = ПоместитьВоВременноеХранилище(Схема, ИдентификаторФормы);
			КонецЕсли;
		КонецЕсли;
		Если АдресСхемыЗаполнен Тогда
			АдресСхемы = Контекст.АдресСхемы;
		Иначе
			АдресСхемы = ПоместитьВоВременноеХранилище(Схема);
		КонецЕсли;
		Контекст.СхемаМодифицирована = Истина;
	КонецЕсли;
	ПоместитьВоВременноеХранилище(Схема, АдресСхемы);
	
	ВариантОтчета = ?(СобытиеФормы, НастройкиОтчета.ВариантСсылка, Неопределено);
	ИнициализироватьКомпоновщикНастроек(Отчет.КомпоновщикНастроек, АдресСхемы, Отчет, ВариантОтчета);
	
	Если СобытиеФормы Тогда
		ЗначениеВДанныеФормы(Отчет, Контекст.Отчет);
	КонецЕсли;
КонецПроцедуры

// Инициализирует компоновщик настроек компоновки данных, с обработкой исключения.
//
// Параметры:
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик настроек, который необходимо инициализировать.
//  Схема - СхемаКомпоновкиДанных, URL - см. синтакс-помощник: ИсточникДоступныхНастроекКомпоновкиДанных.
//  Отчет - ОтчетОбъект, Неопределено - отчет, чей компоновщик инициализируется.
//  ВариантОтчета - СправочникСсылка.ВариантыОтчета, Неопределено - хранилище варианта отчета.
//
Процедура ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, Схема, Отчет = Неопределено, ВариантОтчета = Неопределено) Экспорт 
	Попытка
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));
	Исключение
		ИмяСобытия = НСтр("ru = 'Ошибка инициализации компоновщика настроек компоновки данных.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ОбъектМетаданных = Неопределено;
		Если Отчет <> Неопределено Тогда 
			ОбъектМетаданных = Отчет.Метаданные();
		ИначеЕсли ВариантОтчета <> Неопределено Тогда 
			ОбъектМетаданных = ВариантОтчета.Метаданные();
		КонецЕсли;
		
		Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, ОбъектМетаданных, ВариантОтчета, Комментарий);
		
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

// Выводит команду в форму отчета в виде кнопки в указанную группу.
// Также регистрирует команду, защищая ее от удаления при перерисовке формы.
// Для вызова из события ПриСозданииНаСервере формы отчета.
//
// Параметры:
//   ФормаОтчета       - УправляемаяФорма - форма отчета, в которую добавляется команда.
//   КомандаИлиКоманды - КомандаФормы     - команда, с которой будут связаны выводимые кнопки.
//					       Если в свойстве Действие указана пустая строка,
//					       то при выполнении команды будет вызвана процедура ОтчетыКлиентПереопределяемый.ОбработчикКоманды.
//					       Если в свойстве Действие указана строка вида "<ИмяКлиентскогоОбщегоМодуля>.<ИмяЭкспортнойПроцедуры>",
//					       то при выполнении команды в указанном модуле будет вызвана указанная процедура с двумя параметрами,
//					       аналогичным двум первым параметрам процедуры ОтчетыКлиентПереопределяемый.ОбработчикКоманды.
//				       - Массив - набор команд (КомандаФормы), которые будут выведены в указанную группу.
//   ТипГруппы         - Строка - условное имя группы, в которой требуется вывести кнопку.
//					       "Главное"          - группа с кнопками "Сформировать" и "Формировать сразу".
//					       "Настройки"        - группа с кнопками "Настройки", "Изменить вариант отчета" и т.п.
//					       "РаботаСТабличнымДокументом" - группа с кнопками "Найти", "Развернуть все группы" и т.п.
//					       "Интеграция"       - группа такими кнопками как "Печать, Сохранить, Отправить" и т.п.
//					       "ПодменюОтправить" - подменю в группе "Интеграция" для отправки по почте.
//					       "Прочее"           - группа с кнопками "Изменить форму", "Справка" и т.п.
//   ВНачалоГруппы      - Булево - если Истина, то кнопка будет выведена в начале группы. Иначе в конце группы.
//   ТолькоВоВсехДействиях - Булево - если Истина, то кнопка будет выведена только в подменю "Еще".
//                                    Иначе и в подменю "Еще", и в командной панели формы.
//   СуффиксПодгруппы   - Строка - если заполнен, то команды будут объединены в подгруппу.
//                                 СуффиксПодгруппы добавляется к имени группы справа.
//
Процедура ВывестиКоманду(ФормаОтчета, КомандаИлиКоманды, ТипГруппы, ВНачалоГруппы = Ложь, ТолькоВоВсехДействиях = Ложь, СуффиксПодгруппы = "") Экспорт
	ПередЧемВставить = Неопределено;
	ГруппаЕще = Неопределено;
	
	Если ТипГруппы = "Главное" Тогда
		Группа = ФормаОтчета.Элементы.ГруппаГлавное;
		ГруппаЕще = ФормаОтчета.Элементы.ГруппаГлавноеЕще;
	ИначеЕсли ТипГруппы = "Настройки" Тогда
		Группа = ФормаОтчета.Элементы.ГруппаНастройкиОтчета;
		ГруппаЕще = ФормаОтчета.Элементы.ГруппаНастройкиОтчетаЕще;
	ИначеЕсли ТипГруппы = "РаботаСТабличнымДокументом" Тогда
		Группа = ФормаОтчета.Элементы.ГруппаРаботаВТаблице;
		ГруппаЕще = ФормаОтчета.Элементы.ГруппаРаботаВТаблицеЕще;
	ИначеЕсли ТипГруппы = "Интеграция" Тогда
		Группа = ФормаОтчета.Элементы.ГруппаВывод;
		ГруппаЕще = ФормаОтчета.Элементы.ГруппаВыводЕще;
	ИначеЕсли ТипГруппы = "ПодменюОтправить" Тогда
		Группа = ФормаОтчета.Элементы.ГруппаОтправить;
		ГруппаЕще = ФормаОтчета.Элементы.ГруппаОтправитьЕще;
	ИначеЕсли ТипГруппы = "Прочее" Тогда
		Группа = ФормаОтчета.Элементы.ОсновнаяКоманднаяПанель;
		ГруппаЕще = ФормаОтчета.Элементы.КоманднаяПанельЕще;
		ПередЧемВставить = ?(ВНачалоГруппы, ФормаОтчета.Элементы.НовоеОкно, Неопределено);
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При вызове процедуры ""%1"" передано недопустимо значение параметра ""%2"".'"),
			"ОтчетыСервер.ВывестиКоманду",
			"ТипГруппы");
	КонецЕсли;
	
	Если ТолькоВоВсехДействиях Тогда 
		Группа = ГруппаЕще;
		ГруппаЕще = Неопределено;
	КонецЕсли;
	
	Если ВНачалоГруппы И ПередЧемВставить = Неопределено Тогда
		ПередЧемВставить = Группа.ПодчиненныеЭлементы[0];
	КонецЕсли;
	
	Если ТипЗнч(КомандаИлиКоманды) = Тип("КомандаФормы") Тогда
		Команды = Новый Массив;
		Команды.Добавить(КомандаИлиКоманды);
	Иначе
		Команды = КомандаИлиКоманды;
	КонецЕсли;
	
	Если СуффиксПодгруппы <> "" Тогда
		Подгруппа = ФормаОтчета.Элементы.Найти(Группа.Имя + СуффиксПодгруппы);
		Если Подгруппа = Неопределено Тогда
			Подгруппа = ФормаОтчета.Элементы.Вставить(Группа.Имя + СуффиксПодгруппы, Тип("ГруппаФормы"), Группа, ПередЧемВставить);
			Если Подгруппа.Вид = ВидГруппыФормы.Подменю Тогда
				Подгруппа.Вид = ВидГруппыФормы.ГруппаКнопок;
			КонецЕсли;
		КонецЕсли;
		Группа = Подгруппа;
		ПередЧемВставить = Неопределено;
	КонецЕсли;
	
	Для Каждого Команда Из Команды Цикл
		Обработчик = ?(СтрЧислоВхождений(Команда.Действие, ".") = 0, "", Команда.Действие);
		ФормаОтчета.ПостоянныеКоманды.Добавить(Команда.Имя, Обработчик);
		Команда.Действие = "Подключаемый_Команда";
		
		Кнопка = ФормаОтчета.Элементы.Вставить(Команда.Имя, Тип("КнопкаФормы"), Группа, ПередЧемВставить);
		Кнопка.ИмяКоманды = Команда.Имя;
		Кнопка.ТолькоВоВсехДействиях = ТолькоВоВсехДействиях;
		
		Если ГруппаЕще = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		Кнопка = ФормаОтчета.Элементы.Вставить(Команда.Имя + "Еще", Тип("КнопкаФормы"), ГруппаЕще);
		Кнопка.ИмяКоманды = Команда.Имя;
		Кнопка.ТолькоВоВсехДействиях = Истина;
	КонецЦикла;
КонецПроцедуры

// Оформляет ячейку в виде гиперссылки и заполняет поля адреса и представления ссылки.
//
// Параметры:
//   Ячейка      - ОбластьЯчеекТабличногоДокумента - область табличного документа.
//   АдресСсылки - Строка                          - адрес ссылки, которую необходимо вывести в указанной ячейке.
//			       В стандартной форме отчета автоматически открываются ссылки следующих форматов:
//			       "http://<адрес>", "https://<адрес>", "e1cib/<адрес>", "e1c://<адрес>"
//			       Такие ссылки открываются при помощи процедуры ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку.
//			       См. также ПредставлениеНавигационнойСсылки.НавигационнаяСсылка в синтакс-помощнике.
//			       Для открытия ссылок других форматов следует написать код
//			       в процедуре ОтчетыКлиентПереопределяемый.ОбработкаВыбораТабличногоДокумента.
//   ПредставлениеСсылки - Строка, Неопределено - наименование, которую необходимо вывести в указанной ячейке.
//                                                Если Неопределено, то выводится как есть АдресСсылки.
//
Процедура ВывестиГиперссылку(Ячейка, АдресСсылки, ПредставлениеСсылки = Неопределено) Экспорт
	Ячейка.Гиперссылка = Истина;
	Ячейка.Шрифт       = Новый Шрифт(Ячейка.Шрифт, , , , , Истина);
	Ячейка.ЦветТекста  = ЦветаСтиля.ГиперссылкаЦвет;
	Ячейка.Расшифровка = АдресСсылки;
	Ячейка.Текст       = ?(ПредставлениеСсылки = Неопределено, АдресСсылки, ПредставлениеСсылки);
КонецПроцедуры

// Определяет что отчет пустой.
//
// Параметры:
//   ОтчетОбъект - ОтчетОбъект, ВнешнийОтчетОбъект - проверяемый отчет.
//   ПроцессорКД - ПроцессорКомпоновкиДанных - объект, выполняющий компоновку данных в отчете.
//
// Возвращаемое значение:
//   Булево - Истина, если отчет пустой. Ложь, если отчет содержит данные.
//
Функция ОтчетПустой(ОтчетОбъект, ПроцессорКД = Неопределено) Экспорт
	Если ПроцессорКД = Неопределено Тогда
		
		Если ОтчетОбъект.СхемаКомпоновкиДанных = Неопределено Тогда
			Возврат Ложь; // Не СКД отчет.
		КонецЕсли;
		
		// Объект для создания макета компоновки данных.
		КомпоновщикМакетаКД = Новый КомпоновщикМакетаКомпоновкиДанных;
		
		// Выполняет компоновку макета.
		МакетКД = КомпоновщикМакетаКД.Выполнить(ОтчетОбъект.СхемаКомпоновкиДанных, ОтчетОбъект.КомпоновщикНастроек.ПолучитьНастройки());
		
		// Пропуск проверки на то, что отчет пустой.
		Если ЕстьВнешнийНаборДанных(МакетКД.НаборыДанных) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		// Объект, выполняющий компоновку данных.
		ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
		
		// Инициализировать объект.
		ПроцессорКД.Инициализировать(МакетКД, , , Истина);
		
	Иначе
		
		// Встать в начало компоновки.
		ПроцессорКД.Сбросить();
		
	КонецЕсли;
	
	// Объект для вывода результата компоновки в табличный документ.
	ПроцессорВыводаРезультатаКД = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	// Устанавливает табличный документ, в который нужно выводить результат.
	ПроцессорВыводаРезультатаКД.УстановитьДокумент(Новый ТабличныйДокумент);
	
	// Последовательный вывод
	ПроцессорВыводаРезультатаКД.НачатьВывод();
	
	// Получает следующий элемент результата компоновки.
	ЭлементРезультатаКД = ПроцессорКД.Следующий();
	Пока ЭлементРезультатаКД <> Неопределено Цикл
		
		// Вывести элемент результата компоновки отчета в документ.
		ПроцессорВыводаРезультатаКД.ВывестиЭлемент(ЭлементРезультатаКД);
		
		// Определить не пустой результат.
		Для Каждого ЗначениеПараметраМакетаКД Из ЭлементРезультатаКД.ЗначенияПараметров Цикл
			Попытка
				ЗначениеЗаполнено = ЗначениеЗаполнено(ЗначениеПараметраМакетаКД.Значение);
			Исключение
				ЗначениеЗаполнено = Ложь; // Линия, Рамка, Цвет и другие объекты КД, которые могут фигурировать при выводе.
			КонецПопытки;
			Если ЗначениеЗаполнено Тогда
				ПроцессорВыводаРезультатаКД.ЗакончитьВывод();
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			// Получает следующий элемент результата компоновки.
			ЭлементРезультатаКД = ПроцессорКД.Следующий();
		Исключение
			Возврат Ложь;
		КонецПопытки;
		
	КонецЦикла;
	
	// Указание объекту о том, что вывод результата завершен.
	ПроцессорВыводаРезультатаКД.ЗакончитьВывод();
	
	Возврат Истина;
КонецФункции

// Конструктор свойств группы элементов формы пользовательских настроек.
//
// Возвращаемое значение:
//   Структура - содержит значения свойств группы:
//    * Отображение - ОтображениеОбычнойГруппы - см. Синтакс-помощник - ОтображениеОбычнойГруппы.
//    * Группировка - ГруппировкаПодчиненныхЭлементовФормы - значения определяют количество
//       групп-колонок элементов:
//       ** Вертикальная - ГруппировкаПодчиненныхЭлементовФормы - эквивалентно 1-ой колонке;
//       ** ГоризонтальнаяЕслиВозможно - ГруппировкаПодчиненныхЭлементовФормы - эквивалентно 2-м колонкам;
//       ** ГоризонтальнаяВсегда - ГруппировкаПодчиненныхЭлементовФормы - количество колонок равно количеству элементов
//                                                                        в группе.
//
Функция СвойстваГруппыЭлементовФормы() Экспорт 
	СвойстваГруппы = Новый Структура;
	СвойстваГруппы.Вставить("Отображение", ОтображениеОбычнойГруппы.Нет);
	СвойстваГруппы.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно);
	
	Возврат СвойстваГруппы;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбновитьЭлементыФормыНастроек(Форма, УзелИерархииЭлементов, ПараметрыОбновления = Неопределено) Экспорт 
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда 
		Форма.СоздатьЭлементыФормыПользовательскихНастроек(УзелИерархииЭлементов);
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	НастройкиОтчета = Форма.НастройкиОтчета;
	
	ВидыСтилизованныхЭлементов = СтрРазделить("Период, Список, Флажок", ", ", Ложь);
	ИменаРеквизитов = ИменаРеквизитовЭлементовНастроек(Форма, ВидыСтилизованныхЭлементов);
	
	ПодготовитьФормуКПерегруппировкеЭлементов(Форма, УзелИерархииЭлементов, ИменаРеквизитов, ВидыСтилизованныхЭлементов);
	
	ВременнаяГруппа = Элементы.Добавить("Временная", Тип("ГруппаФормы"));
	ВременнаяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	Режим = РежимОтображенияНастроекКомпоновкиДанных.БыстрыйДоступ;
	Если Форма.ТипФормыОтчета = ТипФормыОтчета.Настройка Тогда 
		Режим = РежимОтображенияНастроекКомпоновкиДанных.Все;
	КонецЕсли;
	
	Форма.СоздатьЭлементыФормыПользовательскихНастроек(ВременнаяГруппа, Режим, 1);
	
	СвойстваЭлементов = СвойстваЭлементовФормыНастроек(
		Форма.ТипФормыОтчета, Форма.Отчет.КомпоновщикНастроек, НастройкиОтчета);
	
	ПерегруппироватьЭлементыФормыНастроек(
		Форма, УзелИерархииЭлементов, СвойстваЭлементов, ИменаРеквизитов, ВидыСтилизованныхЭлементов);
	
	Элементы.Удалить(ВременнаяГруппа);
	
	// Вызов переопределяемого модуля.
	Если НастройкиОтчета.События.ПослеЗаполненияПанелиБыстрыхНастроек Тогда
		ОтчетОбъект = ОтчетОбъект(НастройкиОтчета.ПолноеИмя);
		ОтчетОбъект.ПослеЗаполненияПанелиБыстрыхНастроек(Форма, ПараметрыОбновления);
	КонецЕсли;
КонецПроцедуры

Функция ДоступныеНастройки(ПараметрыЗагрузки, НастройкиОтчета) Экспорт 
	Настройки = Неопределено;
	ПользовательскиеНастройки = Неопределено;
	ФиксированныеНастройки = Неопределено;
	
	Если ПараметрыЗагрузки.Свойство("КомпоновщикНастроекКД") Тогда
		Настройки = ПараметрыЗагрузки.КомпоновщикНастроекКД.Настройки;
		ПользовательскиеНастройки = ПараметрыЗагрузки.КомпоновщикНастроекКД.ПользовательскиеНастройки;
		ФиксированныеНастройки = ПараметрыЗагрузки.КомпоновщикНастроекКД.ФиксированныеНастройки;
	Иначе
		Если ПараметрыЗагрузки.Свойство("НастройкиКД") Тогда
			Настройки = ПараметрыЗагрузки.НастройкиКД;
		КонецЕсли;
		Если ПараметрыЗагрузки.Свойство("ПользовательскиеНастройкиКД") Тогда
			ПользовательскиеНастройки = ПараметрыЗагрузки.ПользовательскиеНастройкиКД;
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиОтчета.События.ПередЗагрузкойНастроекВКомпоновщик Тогда
		НастройкиXML = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиОтчета, "НовыеНастройкиXML");
		Если ТипЗнч(НастройкиXML) = Тип("Строка") Тогда
			Попытка
				Настройки = ОбщегоНазначения.ЗначениеИзСтрокиXML(НастройкиXML);
			Исключение
				Настройки = Неопределено;
			КонецПопытки;
			НастройкиОтчета.НовыеНастройкиXML = Неопределено;
		КонецЕсли;
		
		ПользовательскиеНастройкиXML = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиОтчета, "НовыеПользовательскиеНастройкиXML");
		Если ТипЗнч(ПользовательскиеНастройкиXML) = Тип("Строка") Тогда
			Попытка
				ПользовательскиеНастройки = ОбщегоНазначения.ЗначениеИзСтрокиXML(ПользовательскиеНастройкиXML);
			Исключение
				ПользовательскиеНастройки = Неопределено;
			КонецПопытки;
			НастройкиОтчета.НовыеПользовательскиеНастройкиXML = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Настройки, ПользовательскиеНастройки, ФиксированныеНастройки",
		Настройки, ПользовательскиеНастройки, ФиксированныеНастройки);
КонецФункции

Процедура СброситьПользовательскиеНастройки(ДоступныеНастройки, ПараметрыЗагрузки) Экспорт 
	СброситьПользовательскиеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗагрузки, "СброситьПользовательскиеНастройки", Ложь);
	
	Если Не СброситьПользовательскиеНастройки Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДоступныеНастройки.ПользовательскиеНастройки = Неопределено Тогда 
		КэшЗначенийОтборов = Неопределено;
	Иначе
		КэшЗначенийОтборов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДоступныеНастройки.ПользовательскиеНастройки.ДополнительныеСвойства, "КэшЗначенийОтборов");
	КонецЕсли;
	
	ДоступныеНастройки.ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	Если КэшЗначенийОтборов <> Неопределено Тогда 
		ДоступныеНастройки.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(
			"КэшЗначенийОтборов", КэшЗначенийОтборов);
	КонецЕсли;
КонецПроцедуры

Процедура ВосстановитьЗначенияОтборов(Форма) Экспорт 
	ПользовательскиеНастройки = Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	ПутьКДаннымЭлементов = Форма.ПутьКДаннымЭлементов;
	
	КэшЗначенийОтборов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПользовательскиеНастройки.ДополнительныеСвойства, "КэшЗначенийОтборов");
	
	Если КэшЗначенийОтборов = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементКэша Из КэшЗначенийОтборов Цикл 
		ЗначениеОтбора = ЭлементКэша.Значение;
		Если ЗначениеОтбора.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ЭлементНастройки = ПользовательскиеНастройки.Элементы.Найти(ЭлементКэша.Ключ);
		Если ЭлементНастройки = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		Индекс = ПользовательскиеНастройки.Элементы.Индекс(ЭлементНастройки);
		ИмяСписка = ПутьКДаннымЭлементов.ПоИндексу[Индекс];
		Если ИмяСписка = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		Список = Форма[ИмяСписка];
		Если Список = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		Для Каждого Элемент Из ЗначениеОтбора Цикл 
			Если Список.НайтиПоЗначению(Элемент.Значение) = Неопределено Тогда 
				Список.Добавить(Элемент.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьДоступныеЗначения(Отчет, Форма) Экспорт 
	КомпоновщикНастроек = Форма.Отчет.КомпоновщикНастроек;
	
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	Для Каждого ЭлементПользовательскойНастройки Из ПользовательскиеНастройки.Элементы Цикл 
		Если ТипЗнч(ЭлементПользовательскойНастройки) <> Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
			И ТипЗнч(ЭлементПользовательскойНастройки) <> Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
			Продолжить;
		КонецЕсли;
		
		ЭлементНастройки = ОтчетыКлиентСервер.ПолучитьОбъектПоПользовательскомуИдентификатору(
			КомпоновщикНастроек.Настройки,
			ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки,,
			ПользовательскиеНастройки);
		
		ОписаниеНастройки = ОтчетыКлиентСервер.НайтиДоступнуюНастройку(КомпоновщикНастроек.Настройки, ЭлементНастройки);
		
		СвойстваНастройки = СвойстваЭлементаПользовательскихНастроек(
			КомпоновщикНастроек, ЭлементПользовательскойНастройки, ЭлементНастройки, ОписаниеНастройки);
		
		// Механизмы расширения.
		// Глобальные настройки вывода типов.
		ОтчетыПереопределяемый.ПриОпределенииПараметровВыбора(Неопределено, СвойстваНастройки);
		// Локальное переопределение для отчета.
		Если Форма.НастройкиОтчета.События.ПриОпределенииПараметровВыбора Тогда 
			Отчет.ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки);
		КонецЕсли;
		
		// Автоматическое заполнение.
		Если СвойстваНастройки.ЗапросЗначенийВыбора.Текст <> "" Тогда
			ДобавляемыеЗначения = СвойстваНастройки.ЗапросЗначенийВыбора.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
			Для Каждого Элемент Из ДобавляемыеЗначения Цикл
				ОтчетыКлиентСервер.ДобавитьУникальноеЗначениеВСписок(
					СвойстваНастройки.ЗначенияДляВыбора, Элемент, Неопределено, Ложь);
			КонецЦикла;
			СвойстваНастройки.ЗначенияДляВыбора.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
		КонецЕсли;
		
		Если ТипЗнч(СвойстваНастройки.ЗначенияДляВыбора) = Тип("СписокЗначений")
			И СвойстваНастройки.ЗначенияДляВыбора.Количество() > 0 Тогда 
			ОписаниеНастройки.ДоступныеЗначения = СвойстваНастройки.ЗначенияДляВыбора;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ЭтоТехническийОбъект(Знач ПолноеИмяОбъекта) Экспорт
	
	Возврат ПолноеИмяОбъекта = ВРег("Справочник.ПредопределенныеВариантыОтчетовРасширений")
		Или ПолноеИмяОбъекта = ВРег("Справочник.ПредопределенныеВариантыОтчетов");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Анализ

// Возвращает информацию о настройках компоновщика.
//  После завершения работы с настройками следует вызвать ОчиститьРасширеннуюИнформациюОНастройках
//  для очистки от цикличных ссылок для корректного освобождение памяти.
//
// Вызовы (6):
//  1. ОбщийМодуль.ВариантыОтчетов.УменьшитьКоличествоБыстрыхНастроек()
//     * Используется только свойство ПользовательскиеНастройки;
//     * Является параметром вызываемого метода ОтчетыСервер.ОчиститьРасширеннуюИнформациюОНастройках().
//  2. ХранилищеНастроек.ХранилищеВариантовОтчетов.Форма.УсловияОтборовОтчета.ПриСозданииНаСервере()
//     * Используется только свойство ПользовательскиеНастройки;
//     * Является параметром вызываемого метода ОтчетыСервер.ОчиститьРасширеннуюИнформациюОНастройках().
//  3. ОбщаяФорма.ФормаНастроекОтчета:
//    3.1. БыстрыеНастройкиЗаполнить()
//         * Используется наиболее полно.
//    3.2. ОчиститьНастройкиОтНесуществующихПолейНаСервере()
//         * Используются свойства: ДеревоВарианта, НастройкиВарианта.
//  4. ОбщаяФорма.ФормаОтчета.БыстрыеНастройкиЗаполнить()
//     * Используются свойства: СоответствиеИменОбъектовМетаданных, Связи, ПользовательскиеНастройки.
//  5. Отчет.БыстрыеНастройкиОтчетов.МодульОбъекта.ПроанализироватьПользовательскиеНастройки()
//     * Используется только свойство ПользовательскиеНастройки;
//
// Параметры:
//  КомпоновщикНастроекКД - КомпоновщикНастроекКомпоновкиДанных - Отчет.КомпоновщикНастроек.
//                          Свойство отчета.
//  НастройкиОтчета - Структура - см. ВариантыОтчетов.НастройкиФормыОтчета().
//                    Содержит свойства отчета. Хранится в реквизите формы (отчета, настроек).
//  ОтчетОбъектИлиПолноеИмя - ОтчетОбъект, Строка - объект компонуемого отчета или его полное имя метаданных.
//  УсловияВывода - Структура, Неопределено - дополнительные признаки:
//                  * ТолькоПользовательские - Булево.
//                  * ТолькоБыстрые - Булево.
//                  * ИдентификаторТекущегоУзлаКД - ИдентификаторКомпоновкиДанных, Неопределено - идентификатор текущей настройки.
//
// Возвращаемое значение:
//   Структура - содержит:
//               * ТолькоПользовательские - Булево.
//               * ТолькоБыстрые - Булево.
//               * ИдентификаторТекущегоУзлаКД - ИдентификаторКомпоновкиДанных, Неопределено - идентификатор текущей настройки.
//               * НастройкиКД - НастройкиКомпоновкиДанных - см. Синтакс-помощник: КомпоновщикНастроекКомпоновкиДанных.Настройки.
//               * НастройкиОтчета - Структура - см. ВариантыОтчетов.НастройкиФормыОтчета().
//               * ОтчетОбъектИлиПолноеИмя - ОтчетОбъект, Строка - объект компонуемого отчета или его полное имя метаданных.
//               * ДеревоВарианта - ДеревоЗначений - см. ОтчетыСервер.ДеревоВарианта().
//                                  Хранит свойства и признаки настройки компоновки данных и всей иерархии ее структуры.
//               * НастройкиВарианта - ДеревоЗначений - см. ОтчетыСервер.ТаблицаНастроекВарианта().
//                                     Хранит ссылку на каждую строку раннее инициализированного дерева ДеревоВарианта,
//                                     а также свойства и признаки соответствующих коллекций настроек: Отбор, Порядок и т.д.
//               * ПользовательскиеНастройки - ТаблицыЗначений - см. ОтчетыСервер.ТаблицаПользовательскихНастроек().
//                                             Хранит ссылку на каждую строку раннее инициализированного дерева
//                                             ДеревоВарианта, а также свойства и признаки по каждой из пользовательских
//                                             настроек.
//               * ОтключаемыеСвязи - Массив:
//                                    * ПоТипу - ТаблицыЗначений - см. ОтчетыСервер.ТаблицаСвязейПоТипу().
//                                               Хранит ссылку на строку дерева раннее инициализированного свойства ДеревоВарианта,
//                                               а также значения свойств параметра редактирования СвязьПоТипу
//                                               доступных полей отбора и параметров данных настроек компоновки данных.
//                                    * ПараметровВыбора - ТаблицыЗначений - см. ОтчетыСервер.ТаблицаСвязейПараметровВыбора().
//                                                         Хранит ссылку на строку дерева раннее инициализированного
//                                                         свойства ДеревоВарианта, а также значения свойств параметра
//                                                         редактирования СвязиПараметровВыбора доступных полей отбора и
//                                                         параметров данных настроек компоновки данных.
//                                    * ОбъектовМетаданных - ТаблицыЗначений - см. ОтчетыСервер.ТаблицаСвязейОбъектовМетаданных().
//               * ДополнительныеНастройкиЭлементов - Соответствие - хранится в КомпоновщикНастроекКомпоновкиДанных.ПользовательскиеНастройки.ДополнительныеСвойства,
//                                                    по ключу ЭлементыФормы:
//                                                    * Ключ - Строка - производная от универсального идентификатора
//                                                             пользовательской настройки - составная часть элемента
//                                                             управления, ссылающегося на пользовательскую настройку.
//                                                    * Значение - Структура - свойства пользовательских настроек.
//               * СоответствиеИменОбъектовМетаданных - Соответствие - содержит:
//                                                      * Ключ - Тип - ссылочный тип.
//                                                      * Значение - Строка - полное имя объекта метаданных
//                                                                            соответствующего текущему ключу.
//               * Поиск - Структура - содержит:
//                         * НастройкиВариантаПоПолюКД - Соответствие:
//                                                       * Ключ - ПолеКомпоновкиДанных.
//                                                       * Значение - СтрокаДереваЗначений - ссылка на строку дерева
//                                                           раннее инициализированного свойства ДеревоВарианта.
//                         * ПользовательскиеНастройки - Соответствие.
//                                                       * Ключ - Строка - производная от универсального идентификатора
//                                                                пользовательской настройки - составная часть элемента
//                                                                управления, ссылающегося на пользовательскую настройку.
//                                                       * Значение - СтрокаТаблицыЗначений - ссылка на строку таблицы
//                                                                    значений раннее инициализированного свойства ПользовательскиеНастройки.
//               * ЕстьБыстрыеНастройки - Булево - Истина, если хотя бы для одной из пользовательских настроек
//                                        РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ
//                                        или РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто.
//               * ЕстьОбычныеНастройки - Булево - Истина, если хотя бы для одной из пользовательских настроек
//                                        РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный.
//               * ЕстьНесуществующиеПоля - Булево - признак того, что поле отсутствует в доступных полях.
//               * ЕстьВложенныеОтчеты - Булево - признак того, что тип хотя бы одного из элементов настроек - НастройкиВложенногоОбъектаКомпоновкиДанных.
//               * ЕстьВложенноеОформление - Булево - см. Синтакс-помощник - НастройкиКомпоновкиДанных.НаличиеУсловногоОформленияУЭлемента().
//               * ЕстьВложенныеПоля - Булево - см. Синтакс-помощник - НастройкиКомпоновкиДанных.НаличиеВыбораУЭлемента().
//               * ЕстьВложенныеСортировки - Булево - см. Синтакс-помощник - НастройкиКомпоновкиДанных.НаличиеПорядкаУЭлемента().
//               * СвойстваГруппЭлементов - Структура - см. ОтчетыСервер.СвойстваГруппыЭлементовФормы() -
//                                          настройки группы элементов быстрых (выводимых на форму) настроек по умолчанию.
//               * ГруппыЭлементов - Соответствие - коллекция для установки частных свойств групп элементов быстрых настроек.
//               * ДеревоВариантаКорневаяСтрока - СтрокаДереваЗначений - ссылка на строку раннее инициализированного
//                                                дерева ДеревоВарианта, соответствующую текущему элементу настройки.
//
Функция РасширеннаяИнформацияОНастройках(КомпоновщикНастроекКД, НастройкиОтчета, ОтчетОбъектИлиПолноеИмя, УсловияВывода = Неопределено) Экспорт
	НастройкиКД = КомпоновщикНастроекКД.Настройки;
	ПользовательскиеНастройкиКД = КомпоновщикНастроекКД.ПользовательскиеНастройки;
	
	ДополнительныеНастройкиЭлементов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПользовательскиеНастройкиКД.ДополнительныеСвойства, "ЭлементыФормы");
	Если ДополнительныеНастройкиЭлементов = Неопределено Тогда
		ДополнительныеНастройкиЭлементов = Новый Соответствие;
	КонецЕсли;
	
	Информация = Новый Структура;
	Информация.Вставить("ТолькоПользовательские", Ложь);
	Информация.Вставить("ТолькоБыстрые", Ложь);
	Информация.Вставить("ИдентификаторТекущегоУзлаКД", Неопределено);
	Если УсловияВывода <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Информация, УсловияВывода);
	КонецЕсли;
	
	Информация.Вставить("НастройкиКД", НастройкиКД);
	
	Информация.Вставить("НастройкиОтчета",           НастройкиОтчета);
	Информация.Вставить("ОтчетОбъектИлиПолноеИмя",   ОтчетОбъектИлиПолноеИмя);
	Информация.Вставить("ДеревоВарианта",            ДеревоВарианта());
	Информация.Вставить("НастройкиВарианта",         ТаблицаНастроекВарианта());
	Информация.Вставить("ПользовательскиеНастройки", ТаблицаПользовательскихНастроек());
	
	Информация.Вставить("ОтключаемыеСвязи", Новый Массив);
	Информация.Вставить("Связи", Новый Структура);
	Информация.Связи.Вставить("ПоТипу",             ТаблицаСвязейПоТипу());
	Информация.Связи.Вставить("ПараметровВыбора",   ТаблицаСвязейПараметровВыбора());
	Информация.Связи.Вставить("ОбъектовМетаданных", ТаблицаСвязейОбъектовМетаданных(НастройкиОтчета, Информация.ОтчетОбъектИлиПолноеИмя));
	
	Информация.Вставить("ДополнительныеНастройкиЭлементов",   ДополнительныеНастройкиЭлементов);
	Информация.Вставить("СоответствиеИменОбъектовМетаданных", Новый Соответствие);
	Информация.Вставить("Поиск", Новый Структура);
	Информация.Поиск.Вставить("НастройкиВариантаПоПолюКД", Новый Соответствие);
	Информация.Поиск.Вставить("ПользовательскиеНастройки", Новый Соответствие);
	Информация.Вставить("ЕстьБыстрыеНастройки", Ложь);
	Информация.Вставить("ЕстьОбычныеНастройки", Ложь);
	Информация.Вставить("ЕстьНесуществующиеПоля", Ложь);
	
	Информация.Вставить("ЕстьВложенныеОтчеты", Ложь);
	Информация.Вставить("ЕстьВложенныеОтборы", Ложь);
	Информация.Вставить("ЕстьВложенноеОформление", Ложь);
	Информация.Вставить("ЕстьВложенныеПоля", Ложь);
	Информация.Вставить("ЕстьВложенныеСортировки", Ложь);
	
	// Настройки группы элементов быстрых (выводимых на форму) настроек по умолчанию.
	Информация.Вставить("СвойстваГруппЭлементов", СвойстваГруппыЭлементовФормы());
	// Коллекция для установки частных свойств групп элементов быстрых настроек.
	Информация.Вставить("ГруппыЭлементов", Новый Соответствие);
	
	Для Каждого ПользовательскаяНастройкаКД Из ПользовательскиеНастройкиКД.Элементы Цикл
		СвойстваНастройки = Информация.ПользовательскиеНастройки.Добавить();
		СвойстваНастройки.ПользовательскаяНастройкаКД = ПользовательскаяНастройкаКД;
		СвойстваНастройки.Идентификатор               = ПользовательскаяНастройкаКД.ИдентификаторПользовательскойНастройки;
		СвойстваНастройки.ИндексВКоллекции = ПользовательскиеНастройкиКД.Элементы.Индекс(ПользовательскаяНастройкаКД);
		СвойстваНастройки.ИдентификаторКД  = ПользовательскиеНастройкиКД.ПолучитьИдентификаторПоОбъекту(ПользовательскаяНастройкаКД);
		СвойстваНастройки.Тип              = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(ПользовательскаяНастройкаКД));
		Информация.Поиск.ПользовательскиеНастройки.Вставить(СвойстваНастройки.Идентификатор, СвойстваНастройки);
		
		// Положение заголовка по умолчанию.
		СвойстваНастройки.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЦикла;
	
	СтрокаДерева = ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, НастройкиКД, Информация.ДеревоВарианта.Строки, "Отчет");
	СтрокаДерева.Глобальная = Истина;
	Информация.Вставить("ДеревоВариантаКорневаяСтрока", СтрокаДерева);
	Если Информация.ИдентификаторТекущегоУзлаКД = Неопределено Тогда
		Информация.ИдентификаторТекущегоУзлаКД = СтрокаДерева.ИдентификаторКД;
		Если Не Информация.ТолькоПользовательские Тогда
			СтрокаДерева.ВыводРазрешен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ЗарегистрироватьНастройкиВарианта(НастройкиКД, Информация);
	ЗарегистрироватьСвязиОтВедущих(Информация);
	
	Возврат Информация;
КонецФункции

// Очистить от циклических ссылок для освобождения памяти.
Процедура ОчиститьРасширеннуюИнформациюОНастройках(ИнформацияОНастройках) Экспорт
	
	ОчиститьДеревоЗначений(ИнформацияОНастройках.ДеревоВарианта);
	ОчиститьДеревоЗначений(ИнформацияОНастройках.НастройкиВарианта);
	ИнформацияОНастройках.Поиск.Очистить();
	ИнформацияОНастройках.ПользовательскиеНастройки.Колонки.Очистить();
	ИнформацияОНастройках.Связи.Очистить();
	ИнформацияОНастройках.Очистить();

КонецПроцедуры

// Очистить дерево значений от циклических ссылок для освобождения памяти.
//
Процедура ОчиститьДеревоЗначений(Знач Дерево)
	
	СтрокиДерева = Дерево.Строки;
	ОчищаемыеКолонки = Новый Массив;
	ИндексКолонки = 0;
	Для каждого КолонкаДерева Из Дерево.Колонки Цикл
		Если КолонкаДерева.ТипЗначения <> Новый ОписаниеТипов("Строка")
			И КолонкаДерева.ТипЗначения <> Новый ОписаниеТипов("Булево")
			И КолонкаДерева.ТипЗначения <> Новый ОписаниеТипов("Число") 
			И КолонкаДерева.ТипЗначения <> Новый ОписаниеТипов("Дата") Тогда
			ОчищаемыеКолонки.Добавить(ИндексКолонки);
		КонецЕсли;	
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;
	
	Если ОчищаемыеКолонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		ОчиститьСтрокиДереваЗначений(СтрокаДерева.Строки, ОчищаемыеКолонки);
	КонецЦикла;
	Дерево.Колонки.Очистить();
	
КонецПроцедуры

Процедура ОчиститьСтрокиДереваЗначений(Знач СтрокиДерева, Знач ОчищаемыеКолонки)
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		ОчиститьСтрокиДереваЗначений(СтрокаДерева.Строки, ОчищаемыеКолонки);
	КонецЦикла;
	
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		Для каждого Колонка Из ОчищаемыеКолонки Цикл
			СтрокаДерева[Колонка] = Неопределено;
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Дерево варианта

Функция ДеревоВарианта()
	Результат = Новый ДеревоЗначений;
	
	// Узлы СКД.
	Результат.Колонки.Добавить("УзелКД");
	Результат.Колонки.Добавить("ДоступнаяНастройкаКД");
	Результат.Колонки.Добавить("ПользовательскаяНастройкаКД");
	
	// Прикладная структура.
	Результат.Колонки.Добавить("ПользовательскаяНастройка");
	
	// Поиск этой настройки в узле.
	Результат.Колонки.Добавить("ИдентификаторКД");
	
	// Связь с узлами СКД.
	Результат.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	
	// Тип настройки.
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Состояние", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ЕстьСтруктура", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ЕстьПоляИОформление", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Глобальная", Новый ОписаниеТипов("Булево"));
	
	// Содержимое настройки.
	Результат.Колонки.Добавить("СодержитОтборы", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СодержитПоля", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СодержитСортировки", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СодержитУсловноеОформление", Новый ОписаниеТипов("Булево"));
	
	// Вывод.
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ПредставлениеПоУмолчанию", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Заголовок", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьТолькоФлажок", Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
КонецФункции

Функция ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, УзелКД, НаборСтрокДерева, Подтип = "")
	СтрокаДерева = НаборСтрокДерева.Добавить();
	СтрокаДерева.УзелКД = УзелКД;
	СтрокаДерева.Тип = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(УзелКД));
	СтрокаДерева.Подтип = Подтип;
	Если СтрокаДерева.Тип <> "Настройки" Тогда
		СтрокаДерева.Идентификатор = УзелКД.ИдентификаторПользовательскойНастройки;
	КонецЕсли;
	
	СтрокаДерева.ИдентификаторКД = НастройкиКД.ПолучитьИдентификаторПоОбъекту(УзелКД);
	СтрокаДерева.Глобальная = (Подтип = "Отчет");
	
	Если СтрокаДерева.Тип = "Настройки" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "Группировка"
		Или СтрокаДерева.Тип = "ГруппировкаДиаграммы"
		Или СтрокаДерева.Тип = "ГруппировкаТаблицы" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "Таблица" Тогда
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "Диаграмма" Тогда
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "НастройкиВложенногоОбъекта" Тогда
		СтрокаДерева.ДоступнаяНастройкаКД = НастройкиКД.ДоступныеОбъекты.Элементы.Найти(УзелКД.ИдентификаторОбъекта);
	ИначеЕсли СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
		Или СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыДиаграммы" Тогда
		// см. далее.
	Иначе
		Возврат СтрокаДерева;
	КонецЕсли;
	
	ЗаполнитьПредставлениеИСостояниеНастройки(
		СтрокаДерева,
		СтрокаДерева.УзелКД,
		Неопределено,
		СтрокаДерева.ДоступнаяНастройкаКД);
	
	Если СтрокаДерева.ЕстьПоляИОформление Тогда
		СтрокаДерева.Заголовок = ЗаголовокИзПараметровВывода(УзелКД.ПараметрыВывода);
		СтрокаДерева.СодержитПоля               = НастройкиКД.НаличиеВыбораУЭлемента(УзелКД);
		СтрокаДерева.СодержитУсловноеОформление = НастройкиКД.НаличиеУсловногоОформленияУЭлемента(УзелКД);
	КонецЕсли;
	
	Если Не Информация.ТолькоБыстрые Тогда
		СтрокаДерева.ВыводРазрешен = (СтрокаДерева.ИдентификаторКД = Информация.ИдентификаторТекущегоУзлаКД);
	КонецЕсли;
	
	Если ТипЗнч(СтрокаДерева.Идентификатор) = Тип("Строка") И Не ПустаяСтрока(СтрокаДерева.Идентификатор) Тогда
		СвойстваНастройки = Информация.Поиск.ПользовательскиеНастройки.Получить(СтрокаДерева.Идентификатор);
		Если СвойстваНастройки <> Неопределено Тогда
			СтрокаДерева.ПользовательскаяНастройка   = СвойстваНастройки;
			СтрокаДерева.ПользовательскаяНастройкаКД = СвойстваНастройки.ПользовательскаяНастройкаКД;
			ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, СтрокаДерева, Неопределено);
			Если ЗначениеЗаполнено(СтрокаДерева.Заголовок) Тогда
				СвойстваНастройки.Представление = СтрокаДерева.Заголовок;
			КонецЕсли;
			Если Информация.ТолькоПользовательские Тогда
				СтрокаДерева.ВыводРазрешен = СвойстваНастройки.ВыводРазрешен;
				СтрокаДерева.Состояние = СвойстваНастройки.Состояние;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаДерева.ЕстьСтруктура Тогда
		Для Каждого ВложенныйЭлемент Из УзелКД.Структура Цикл
			ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, ВложенныйЭлемент, СтрокаДерева.Строки);
		КонецЦикла;
		СтрокаДерева.СодержитОтборы     = НастройкиКД.НаличиеОтбораУЭлемента(УзелКД);
		СтрокаДерева.СодержитСортировки = НастройкиКД.НаличиеПорядкаУЭлемента(УзелКД);
	КонецЕсли;
	
	Если СтрокаДерева.Тип = "Таблица" Тогда
		ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, УзелКД.Строки, СтрокаДерева.Строки, "ТаблицаСтроки");
		ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, УзелКД.Колонки, СтрокаДерева.Строки, "ТаблицаКолонки");
	ИначеЕсли СтрокаДерева.Тип = "Диаграмма" Тогда
		ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, УзелКД.Точки, СтрокаДерева.Строки, "ДиаграммаТочки");
		ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, УзелКД.Серии, СтрокаДерева.Строки, "ДиаграммаСерии");
	ИначеЕсли СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
		Или СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыДиаграммы" Тогда
		Для Каждого ВложенныйЭлемент Из УзелКД Цикл
			ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, ВложенныйЭлемент, СтрокаДерева.Строки);
		КонецЦикла;
	ИначеЕсли СтрокаДерева.Тип = "НастройкиВложенногоОбъекта" Тогда
		Информация.ЕстьВложенныеОтчеты = Истина;
		ЗарегистрироватьУзелДереваВарианта(Информация, НастройкиКД, УзелКД.Настройки, СтрокаДерева.Строки);
	КонецЕсли;
	
	Если Не СтрокаДерева.Глобальная Тогда
		Если СтрокаДерева.СодержитПоля Тогда
			Информация.ЕстьВложенныеПоля = Истина;
		КонецЕсли;
		Если СтрокаДерева.СодержитУсловноеОформление Тогда
			Информация.ЕстьВложенноеОформление = Истина;
		КонецЕсли;
		Если СтрокаДерева.СодержитОтборы Тогда
			Информация.ЕстьВложенныеОтборы = Истина;
		КонецЕсли;
		Если СтрокаДерева.СодержитСортировки Тогда
			Информация.ЕстьВложенныеСортировки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрокаДерева;
КонецФункции

Функция ЗаголовокИзПараметровВывода(ПараметрыВывода)
	ВыводитьЗаголовокКД = ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьЗаголовок"));
	Если ВыводитьЗаголовокКД = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	Если ВыводитьЗаголовокКД.Использование = Истина
		И ВыводитьЗаголовокКД.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить Тогда
		Возврат "";
	КонецЕсли;
	// В значении Авто считается что заголовок выводится.
	// Когда параметр ВыводитьЗаголовок отключен это эквивалент значения Авто.
	ЗаголовокКД = ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Заголовок"));
	Если ЗаголовокКД = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	Возврат ЗаголовокКД.Значение;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Настройки варианта

Функция ТаблицаНастроекВарианта()
	Результат = Новый ДеревоЗначений;
	
	// Узлы СКД.
	Результат.Колонки.Добавить("ЭлементКД");
	Результат.Колонки.Добавить("ДоступнаяНастройкаКД");
	Результат.Колонки.Добавить("ПользовательскаяНастройкаКД");
	
	// Прикладная структура.
	Результат.Колонки.Добавить("СтрокаДерева");
	Результат.Колонки.Добавить("ПользовательскаяНастройка");
	Результат.Колонки.Добавить("Владелец");
	Результат.Колонки.Добавить("Глобальная", Новый ОписаниеТипов("Булево"));
	
	// Поиск этой настройки в узле.
	Результат.Колонки.Добавить("ИмяКоллекции", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИдентификаторКД");
	
	// Связь с узлами СКД.
	Результат.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИдентификаторЭлемента", Новый ОписаниеТипов("Строка"));
	
	// Описание настройки.
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Состояние", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ПолеКД");
	Результат.Колонки.Добавить("Значение");
	Результат.Колонки.Добавить("ВидСравнения");
	Результат.Колонки.Добавить("ВводСписком", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ИнформацияОТипах");
	Результат.Колонки.Добавить("ФормаВыбора", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ОтмеченныеЗначения");
	Результат.Колонки.Добавить("ПараметрыВыбора");
	
	Результат.Колонки.Добавить("СвязьПоТипу");
	Результат.Колонки.Добавить("СвязиПараметровВыбора");
	Результат.Колонки.Добавить("СвязиПоМетаданным");
	Результат.Колонки.Добавить("ОграничениеТипа");
	
	// API
	Результат.Колонки.Добавить("ОписаниеТипов");
	Результат.Колонки.Добавить("ЗначенияДляВыбора");
	Результат.Колонки.Добавить("ЗапросЗначенийВыбора");
	Результат.Колонки.Добавить("СписокЗначенийПереопределен",           Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("БыстрыйВыбор",                          Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ОграничиватьВыборУказаннымиЗначениями", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СобытиеПриИзменении", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Ширина", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("ВыводитьВГруппеОсновныхНастроек", Новый ОписаниеТипов("Булево"));
	
	// Вывод.
	Результат.Колонки.Добавить("ПредставлениеПоУмолчанию", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьФлажок", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыборГруппИЭлементов");
	Результат.Колонки.Добавить("ВыводитьТолькоФлажок", Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
КонецФункции

Функция ТаблицаПользовательскихНастроек()
	Результат = Новый ТаблицаЗначений;
	
	// Узлы СКД.
	Результат.Колонки.Добавить("УзелКД");
	Результат.Колонки.Добавить("НастройкаВариантаКД");
	Результат.Колонки.Добавить("ПользовательскаяНастройкаКД");
	Результат.Колонки.Добавить("ДоступнаяНастройкаКД");
	
	// Прикладная структура.
	Результат.Колонки.Добавить("СтрокаДерева");
	Результат.Колонки.Добавить("НастройкаВарианта");
	
	// Поиск этой настройки в узле.
	Результат.Колонки.Добавить("ИдентификаторКД");
	Результат.Колонки.Добавить("ИндексВКоллекции", Новый ОписаниеТипов("Число"));
	
	// Связь с узлами СКД.
	Результат.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИдентификаторЭлемента", Новый ОписаниеТипов("Строка"));
	
	// Описание настройки.
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Состояние", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ПолеКД");
	Результат.Колонки.Добавить("Значение");
	Результат.Колонки.Добавить("ВидСравнения");
	Результат.Колонки.Добавить("ВводСписком", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ИнформацияОТипах");
	
	Результат.Колонки.Добавить("ОтмеченныеЗначения");
	Результат.Колонки.Добавить("ПараметрыВыбора");
	
	// API
	Результат.Колонки.Добавить("ОписаниеТипов");
	Результат.Колонки.Добавить("ЗначенияДляВыбора");
	Результат.Колонки.Добавить("ЗапросЗначенийВыбора");
	Результат.Колонки.Добавить("БыстрыйВыбор",                          Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ОграничиватьВыборУказаннымиЗначениями", Новый ОписаниеТипов("Булево"));
	
	// Вывод.
	Результат.Колонки.Добавить("ПредставлениеПоУмолчанию", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Быстрая", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Обычная", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьФлажок", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьТолькоФлажок", Новый ОписаниеТипов("Булево"));
	
	// Имя группы элементов, которой будет подчинена быстрая настройка.
	Результат.Колонки.Добавить("ГруппаЭлементов", Новый ОписаниеТипов("Строка"));
	// Свойство, определяющее поведение заголовка элемента быстрой настройки.
	Результат.Колонки.Добавить("ПоложениеЗаголовка", Новый ОписаниеТипов("ПоложениеЗаголовкаЭлементаФормы"));
	
	Результат.Колонки.Добавить("ТипЭлементов", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВыборГруппИЭлементов");
	
	// Дополнительные свойства.
	Результат.Колонки.Добавить("Дополнительно", Новый ОписаниеТипов("Структура"));
	
	Возврат Результат;
КонецФункции

Функция ТаблицаСвязейПоТипу()
	// Связи из СКД.
	ТаблицаСвязейПоТипу = Новый ТаблицаЗначений;
	ТаблицаСвязейПоТипу.Колонки.Добавить("Ведущий");
	ТаблицаСвязейПоТипу.Колонки.Добавить("ВедущийПолеКД");
	ТаблицаСвязейПоТипу.Колонки.Добавить("Подчиненный");
	ТаблицаСвязейПоТипу.Колонки.Добавить("ПодчиненныйИмяПараметра");
	
	Возврат ТаблицаСвязейПоТипу;
КонецФункции

Функция ТаблицаСвязейПараметровВыбора()
	ТаблицаСвязей = Новый ТаблицаЗначений;
	ТаблицаСвязей.Колонки.Добавить("Ведущий");
	ТаблицаСвязей.Колонки.Добавить("ВедущийПолеКД");
	ТаблицаСвязей.Колонки.Добавить("Подчиненный");
	ТаблицаСвязей.Колонки.Добавить("ПодчиненныйИмяПараметра");
	ТаблицаСвязей.Колонки.Добавить("Действие");
	
	Возврат ТаблицаСвязей;
КонецФункции

Функция ТаблицаСвязейОбъектовМетаданных(НастройкиОтчета, ОтчетОбъектИлиПолноеИмя)
	// Связи из метаданных.
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ВедущийТип",          Новый ОписаниеТипов("Тип"));
	Результат.Колонки.Добавить("ПодчиненныйТип",      Новый ОписаниеТипов("Тип"));
	Результат.Колонки.Добавить("ПодчиненныйРеквизит", Новый ОписаниеТипов("Строка"));
	
	// Механизмы расширения.
	ОтчетыПереопределяемый.ДополнитьСвязиОбъектовМетаданных(Результат); // Глобальные связи...
	Если НастройкиОтчета.События.ДополнитьСвязиОбъектовМетаданных Тогда // ... можно переопределить локально для отчета.
		ОтчетОбъект(ОтчетОбъектИлиПолноеИмя).ДополнитьСвязиОбъектовМетаданных(Результат);
	КонецЕсли;
	
	Результат.Колонки.Добавить("ЕстьВедущие",     Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ЕстьПодчиненные", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Ведущие",     Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("Подчиненные", Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("ВедущийПолноеИмя",     Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ПодчиненныйПолноеИмя", Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;
КонецФункции

Процедура ЗарегистрироватьНастройкиВарианта(НастройкиКД, Информация)
	
	ДеревоВарианта = Информация.ДеревоВарианта;
	
	Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьСтруктура", Истина), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		
		// Настройки, свойство Отбор
		// Группировка, свойство Отбор
		// ГруппировкаТаблицы, свойство Отбор.
		// ГруппировкаДиаграммы, свойство Отбор.
		
		// Настройки, свойство Отбор.Элементы.
		// Группировка, свойство Отбор.Элементы
		// ГруппировкаТаблицы, свойство Отбор.Элементы
		// ГруппировкаДиаграммы, свойство Отбор.Элементы.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Отбор");
		
		// Настройки, свойство Порядок.
		// Группировка, свойство Порядок
		// ГруппировкаТаблицы, свойство Порядок.
		// ГруппировкаДиаграммы, свойство Порядок.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Порядок");
		
		// Настройки, свойство Структура.
		// Группировка, свойство Структура.
		// ГруппировкаТаблицы, свойство Структура.
		// ГруппировкаДиаграммы, свойство Структура.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Структура");
		
	КонецЦикла;
	
	Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьПоляИОформление", Истина), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		
		// Настройки, свойство Выбор
		// Таблица, свойство Выбор
		// Диаграмма, свойство Выбор
		// Группировка, свойство Выбор
		// ГруппировкаДиаграммы, свойство Выбор.
		// ГруппировкаТаблицы, свойство Выбор.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Выбор");
		
		// Настройки, свойство УсловноеОформление.
		// Таблица, свойство УсловноеОформление.
		// Диаграмма, свойство УсловноеОформление.
		// Группировка, свойство УсловноеОформление.
		// ГруппировкаДиаграммы, свойство УсловноеОформление.
		// ГруппировкаТаблицы, свойство УсловноеОформление.
		
		// Настройки, свойство УсловноеОформление.Элементы.
		// Таблица, свойство УсловноеОформление.Элементы.
		// Диаграмма, свойство УсловноеОформление.Элементы.
		// Группировка, свойство УсловноеОформление.Элементы
		// ГруппировкаДиаграммы, свойство УсловноеОформление.Элементы
		// ГруппировкаТаблицы, свойство УсловноеОформление.Элементы.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "УсловноеОформление");
		
		// Настройки, свойство ПараметрыВывода.
		// Таблица, свойство ПараметрыВывода.
		// Диаграмма, свойство ПараметрыВывода.
		// Группировка, свойство ПараметрыВывода
		// ГруппировкаДиаграммы, свойство ПараметрыВывода
		// ГруппировкаТаблицы, свойство ПараметрыВывода.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "ПараметрыВывода");
		
	КонецЦикла;
	
	Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("Тип", "Настройки"), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		
		// Настройки, свойство ПараметрыДанных, метод НайтиЗначениеПараметра().
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "ПараметрыДанных");
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, ИмяКоллекции, НаборЭлементов = Неопределено, Родитель = Неопределено, Владелец = Неопределено)
	УзелКД = СтрокаДерева.УзелКД[ИмяКоллекции];
	
	Владелец = Информация.НастройкиВарианта.Строки.Добавить();
	Владелец.СтрокаДерева = СтрокаДерева;
	Если ИмяКоллекции <> "ПараметрыДанных" И ИмяКоллекции <> "ПараметрыВывода" Тогда
		Владелец.Идентификатор = УзелКД.ИдентификаторПользовательскойНастройки;
	КонецЕсли;
	Владелец.Тип           = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(УзелКД));
	Владелец.ИмяКоллекции  = ИмяКоллекции;
	Владелец.Глобальная    = СтрокаДерева.Глобальная;
	Владелец.ЭлементКД     = УзелКД;
	Владелец.ВыводРазрешен = Не Информация.ТолькоБыстрые И СтрокаДерева.ВыводРазрешен;
	
	Если ТипЗнч(Владелец.Идентификатор) = Тип("Строка") И Не ПустаяСтрока(Владелец.Идентификатор) Тогда
		СвойстваНастройки = Информация.Поиск.ПользовательскиеНастройки.Получить(Владелец.Идентификатор);
		Если СвойстваНастройки <> Неопределено Тогда
			Владелец.ПользовательскаяНастройка = СвойстваНастройки;
			ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, Неопределено, Владелец);
			СвойстваНастройки.ПараметрыВыбора = Новый Массив;
			Владелец.ПараметрыВыбора          = Новый Массив;
			Владелец.СвязиПараметровВыбора    = Новый Массив;
			Владелец.СвязиПоМетаданным        = Новый Массив;
			Если Информация.ТолькоПользовательские Тогда
				Владелец.ВыводРазрешен = СвойстваНастройки.ВыводРазрешен;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Владелец.ВыводРазрешен Тогда
		Если Владелец.ПользовательскаяНастройка = Неопределено Тогда
			ЗаполнитьПредставлениеИСостояниеНастройки(
				Владелец,
				Владелец.ЭлементКД,
				Неопределено,
				Неопределено);
		Иначе
			ЗаполнитьЗначенияСвойств(Владелец, Владелец.ПользовательскаяНастройка, "Представление, ВыводитьТолькоФлажок");
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяКоллекции = "Отбор"
		Или ИмяКоллекции = "ПараметрыДанных"
		Или ИмяКоллекции = "ПараметрыВывода"
		Или ИмяКоллекции = "УсловноеОформление" Тогда
		ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, УзелКД.Элементы, Владелец, Владелец);
	ИначеЕсли Не Информация.ТолькоБыстрые // (Оптимизация) Поля и сортировки не выводятся в быстрых настройках.
		И (ИмяКоллекции = "Порядок" Или ИмяКоллекции = "Выбор") Тогда
		ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, УзелКД.Элементы, Владелец, Владелец);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, НаборЭлементов, Владелец, Родитель)
	ЕстьНеПомеченные = Ложь;
	ЕстьПомеченные = Ложь;
	
	Для Каждого ЭлементКД Из НаборЭлементов Цикл
		НастройкаВарианта = Родитель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НастройкаВарианта, Владелец, "СтрокаДерева, ИмяКоллекции, Глобальная");
		НастройкаВарианта.Тип = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(ЭлементКД));
		НастройкаВарианта.ИдентификаторКД = УзелКД.ПолучитьИдентификаторПоОбъекту(ЭлементКД);
		НастройкаВарианта.Владелец = Владелец;
		НастройкаВарианта.ЭлементКД = ЭлементКД;
		НастройкаВарианта.ВыводРазрешен = Не Информация.ТолькоБыстрые И Владелец.ВыводРазрешен;
		НастройкаВарианта.ВыводитьФлажок = Истина;
		
		Если НастройкаВарианта.Тип = "АвтоЭлементПорядка"
			Или НастройкаВарианта.Тип = "АвтоВыбранноеПоле"
			Или НастройкаВарианта.Тип = "ГруппаВыбранныхПолей" Тогда
			// Действие не требуется.
		ИначеЕсли НастройкаВарианта.Тип = "ЭлементПорядка" Тогда
			НастройкаВарианта.Значение = ЭлементКД.ТипУпорядочивания;
			НастройкаВарианта.ПолеКД = ЭлементКД.Поле;
			НастройкаВарианта.ДоступнаяНастройкаКД = УзелКД.ДоступныеПоляПорядка.НайтиПоле(ЭлементКД.Поле);
			Если НастройкаВарианта.ДоступнаяНастройкаКД = Неопределено Тогда
				НастройкаВарианта.Состояние = "ПометкаУдаления";
			КонецЕсли;
		ИначеЕсли НастройкаВарианта.Тип = "ВыбранноеПоле" Тогда
			НастройкаВарианта.ПолеКД = ЭлементКД.Поле;
			НастройкаВарианта.ДоступнаяНастройкаКД = УзелКД.ДоступныеПоляВыбора.НайтиПоле(ЭлементКД.Поле);
			Если НастройкаВарианта.ДоступнаяНастройкаКД = Неопределено Тогда
				НастройкаВарианта.Состояние = "ПометкаУдаления";
			КонецЕсли;
		Иначе
			НастройкаВарианта.Идентификатор = ЭлементКД.ИдентификаторПользовательскойНастройки;
		КонецЕсли;
		
		Если НастройкаВарианта.Тип = "ВыбранныеПоля"
			Или НастройкаВарианта.Тип = "Порядок"
			Или НастройкаВарианта.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
			Или НастройкаВарианта.Тип = "КоллекцияЭлементовСтруктурыДиаграммы"
			Или НастройкаВарианта.Тип = "Отбор"
			Или НастройкаВарианта.Тип = "УсловноеОформление"
			Или НастройкаВарианта.Тип = "СтруктураНастроек" Тогда
			НастройкаВарианта.ВыводитьФлажок = Ложь;
		КонецЕсли;
		
		СвойстваНастройки = Неопределено;
		Если ТипЗнч(НастройкаВарианта.Идентификатор) = Тип("Строка") И Не ПустаяСтрока(НастройкаВарианта.Идентификатор) Тогда
			СвойстваНастройки = Информация.Поиск.ПользовательскиеНастройки.Получить(НастройкаВарианта.Идентификатор);
		КонецЕсли;
		
		Если НастройкаВарианта.Тип = "ЭлементОтбора" Или НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
			// Пропускаем все настройки СКД, которые не включены в пользовательские настройки, кроме параметров СКД.
			Если СвойстваНастройки = Неопределено И Владелец.ИмяКоллекции <> "ПараметрыДанных" Тогда
				Родитель.Строки.Удалить(НастройкаВарианта); // В целях оптимизации.
				Продолжить;
			КонецЕсли;
			ЗарегистрироватьПоле(Информация, УзелКД, ЭлементКД, НастройкаВарианта);
			ЗарегистрироватьТипыИСвязи(Информация, НастройкаВарианта);
		КонецЕсли;
		
		Если СвойстваНастройки <> Неопределено Тогда
			НастройкаВарианта.ПользовательскаяНастройка = СвойстваНастройки;
			ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, Неопределено, НастройкаВарианта);
			Если Информация.ТолькоПользовательские Тогда
				НастройкаВарианта.ВыводРазрешен = СвойстваНастройки.ВыводРазрешен;
				НастройкаВарианта.Значение      = СвойстваНастройки.Значение;
				НастройкаВарианта.ВидСравнения  = СвойстваНастройки.ВидСравнения;
			КонецЕсли;
		КонецЕсли;
		
		Если НастройкаВарианта.ВыводРазрешен Тогда
			ЗаполнитьПредставлениеИСостояниеНастройки(
				НастройкаВарианта,
				НастройкаВарианта.ЭлементКД,
				Неопределено,
				НастройкаВарианта.ДоступнаяНастройкаКД);
			Если НастройкаВарианта.Состояние = "ПометкаУдаления" Тогда
				Информация.ЕстьНесуществующиеПоля = Истина;
				НастройкаВарианта.ВыводРазрешен = Ложь;
			ИначеЕсли НастройкаВарианта.Тип = "ЭлементОтбора"
				Или НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
				ПриОпределенииПараметровВыбора(Информация, НастройкаВарианта);
			КонецЕсли;
		КонецЕсли;
		
		Если НастройкаВарианта.Тип = "ГруппаЭлементовОтбора" Тогда
			НастройкаВарианта.Значение = ЭлементКД.ТипГруппы;
			ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, ЭлементКД.Элементы, Владелец, НастройкаВарианта);
		ИначеЕсли НастройкаВарианта.Тип = "ГруппаВыбранныхПолей" Тогда
			НастройкаВарианта.Значение = ЭлементКД.Расположение;
			ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, ЭлементКД.Элементы, Владелец, НастройкаВарианта);
		ИначеЕсли НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
			Если СвойстваНастройки <> Неопределено Тогда // В целях оптимизации.
				ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, ЭлементКД.ЗначенияВложенныхПараметров, Владелец, НастройкаВарианта);
			КонецЕсли;
		КонецЕсли;
		
		Если СвойстваНастройки <> Неопределено Тогда
			СвойстваНастройки.ЗначенияДляВыбора = НастройкаВарианта.ЗначенияДляВыбора;
			СвойстваНастройки.ПараметрыВыбора   = НастройкаВарианта.ПараметрыВыбора;
			СвойстваНастройки.БыстрыйВыбор      = НастройкаВарианта.БыстрыйВыбор;
			СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями = НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями;
		КонецЕсли;
		
		Если НастройкаВарианта.Состояние = "ПометкаУдаления" Тогда
			Информация.ЕстьНесуществующиеПоля = Истина;
			ЕстьПомеченные = Истина;
		Иначе
			ЕстьНеПомеченные = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПомеченные И Не ЕстьНеПомеченные И Родитель <> Владелец Тогда
		Родитель.Состояние = "ПометкаУдаления";
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьПоле(Информация, УзелКД, ЭлементКД, НастройкаВарианта)
	Если ПустаяСтрока(НастройкаВарианта.Идентификатор) Тогда
		Идентификатор = Строка(НастройкаВарианта.СтрокаДерева.ИдентификаторКД);
		Если Не ПустаяСтрока(Идентификатор) Тогда
			Идентификатор = Идентификатор + "_";
		КонецЕсли;
		НастройкаВарианта.Идентификатор = Идентификатор + НастройкаВарианта.ИмяКоллекции + "_" + Строка(НастройкаВарианта.ИдентификаторКД);
	КонецЕсли;
	НастройкаВарианта.ИдентификаторЭлемента = ПривестиИдентификаторКИмени(НастройкаВарианта.Идентификатор);
	
	Если НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
		НастройкаВарианта.ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(ЭлементКД.Параметр));
		ДоступныеПараметры = УзелКД.ДоступныеПараметры;
		Если ДоступныеПараметры = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ДоступнаяНастройкаКД = ДоступныеПараметры.НайтиПараметр(ЭлементКД.Параметр);
		Если ДоступнаяНастройкаКД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		// ДоступнаяНастройкаКД имеет тип ДоступныйПараметрКомпоновкиДанных:
		//   Видимость - Булево - Видимость параметра при редактировании значений.
		//   Параметр - ПараметрКомпоновкиДанных - Имя параметра.
		//   Значение - Произвольный - Начальное значение.
		//   ДоступенСписокЗначений - Булево - Можно ли указывать несколько значений.
		//   ДоступныеЗначения - СписокЗначений, Неопределено - Значения, доступные для выбора.
		//   БыстрыйВыбор, ВыборГруппИЭлементов, ЗапрещатьНезаполненныеЗначения,
		//   Использование, Маска, СвязьПоТипу, ФормаВыбора, ФорматРедактирования.
		Если Не ДоступнаяНастройкаКД.Видимость Тогда
			НастройкаВарианта.ВыводРазрешен = Ложь;
		КонецЕсли;
		НастройкаВарианта.ДоступнаяНастройкаКД = ДоступнаяНастройкаКД;
		НастройкаВарианта.Значение = ЭлементКД.Значение;
		Если ДоступнаяНастройкаКД.ДоступенСписокЗначений Тогда
			НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		Если ДоступнаяНастройкаКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда Тогда
			НастройкаВарианта.ВыводитьФлажок = Ложь;
		КонецЕсли;
	Иначе
		НастройкаВарианта.ПолеКД       = ЭлементКД.ЛевоеЗначение;
		НастройкаВарианта.Значение     = ЭлементКД.ПравоеЗначение;
		НастройкаВарианта.ВидСравнения = ЭлементКД.ВидСравнения;
		ДоступныеПоляОтбора = УзелКД.ДоступныеПоляОтбора;
		Если ДоступныеПоляОтбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ДоступнаяНастройкаКД = ДоступныеПоляОтбора.НайтиПоле(ЭлементКД.ЛевоеЗначение);
		Если ДоступнаяНастройкаКД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		// ДоступнаяНастройкаКД имеет тип ДоступноеПолеОтбораКомпоновкиДанных.
		НастройкаВарианта.ДоступнаяНастройкаКД = ДоступнаяНастройкаКД;
	КонецЕсли;
	
	Если НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		НастройкаВарианта.ВводСписком = Истина;
		НастройкаВарианта.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	ИначеЕсли НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
		НастройкаВарианта.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	Иначе
		НастройкаВарианта.ВыборГруппИЭлементов = ПривестиЗначениеКТипуГруппыИЭлементы(ДоступнаяНастройкаКД.ВыборГруппИЭлементов);
	КонецЕсли;
	
	НастройкаВарианта.ОписаниеТипов = ДоступнаяНастройкаКД.ТипЗначения;
	НастройкаВарианта.ФормаВыбора   = ДоступнаяНастройкаКД.ФормаВыбора;
	НастройкаВарианта.БыстрыйВыбор  = ДоступнаяНастройкаКД.БыстрыйВыбор;
	
	Если Информация.Поиск.НастройкиВариантаПоПолюКД.Получить(НастройкаВарианта.ПолеКД) = Неопределено Тогда
		Информация.Поиск.НастройкиВариантаПоПолюКД.Вставить(НастройкаВарианта.ПолеКД, НастройкаВарианта);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьТипыИСвязи(Информация, НастройкаВарианта)
	
	///////////////////////////////////////////////////////////////////
	// Информация о типах.
	
	НастройкаВарианта.СвязиПоМетаданным     = Новый Массив;
	НастройкаВарианта.СвязиПараметровВыбора = Новый Массив;
	НастройкаВарианта.ПараметрыВыбора       = Новый Массив;
	
	Если НастройкаВарианта.ВводСписком Тогда
		НастройкаВарианта.ОтмеченныеЗначения = ОтчетыКлиентСервер.ЗначенияСписком(НастройкаВарианта.Значение);
	КонецЕсли;
	НастройкаВарианта.ЗапросЗначенийВыбора = Новый Запрос;
	НастройкаВарианта.ЗначенияДляВыбора = Новый СписокЗначений;
	
	ИнформацияОТипах = РасширенноеОписаниеТипов(НастройкаВарианта.ОписаниеТипов, Истина);
	ИнформацияОТипах.Вставить("СодержитСсылочныеТипы", Ложь);
	ИнформацияОТипах.Вставить("КоличествоСБыстрымВыбором", 0);
	ВсеТипыСБыстрымВыбором = ИнформацияОТипах.КоличествоТипов < 10
		И (ИнформацияОТипах.КоличествоТипов = ИнформацияОТипах.ОбъектныеТипы.Количество());
	
	Для Каждого Тип Из ИнформацияОТипах.ОбъектныеТипы Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		ПолноеИмя = Информация.СоответствиеИменОбъектовМетаданных.Получить(Тип);
		Если ПолноеИмя = Неопределено Тогда // Регистрация имени объекта метаданных.
			Если ОбъектМетаданных = Неопределено Тогда
				ПолноеИмя = -1;
			Иначе
				ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
			КонецЕсли;
			Информация.СоответствиеИменОбъектовМетаданных.Вставить(Тип, ПолноеИмя);
		КонецЕсли;
		Если ПолноеИмя = -1 Тогда
			ВсеТипыСБыстрымВыбором = Ложь;
			Продолжить;
		КонецЕсли;
		
		ИнформацияОТипах.СодержитСсылочныеТипы = Истина;
		
		Если ВсеТипыСБыстрымВыбором Тогда
			Вид = ВРег(СтрРазделить(ПолноеИмя, ".")[0]);
			Если Вид <> "ПЕРЕЧИСЛЕНИЕ" Тогда
				Если Вид = "СПРАВОЧНИК"
					Или Вид = "ПЛАНВИДОВРАСЧЕТА"
					Или Вид = "ПЛАНВИДОВХАРАКТЕРИСТИК"
					Или Вид = "ПЛАНОБМЕНА"
					Или Вид = "ПЛАНСЧЕТОВ" Тогда
					Если ОбъектМетаданных.СпособВыбора <> Метаданные.СвойстваОбъектов.СпособВыбора.БыстрыйВыбор Тогда
						ВсеТипыСБыстрымВыбором = Ложь;
					КонецЕсли;
				Иначе
					ВсеТипыСБыстрымВыбором = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Поиск типа в глобальных связях среди подчиненных.
		Найденные = Информация.Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ПодчиненныйТип", Тип));
		Для Каждого СвязьПоМетаданным Из Найденные Цикл // Регистрация настройки как подчиненной.
			СвязьПоМетаданным.ЕстьПодчиненные = Истина;
			СвязьПоМетаданным.Подчиненные.Добавить(НастройкаВарианта);
		КонецЦикла;
		
		// Поиск типа в глобальных связях среди ведущих.
		Если НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			// Поле может быть ведущим если имеет вид сравнения "Равно".
			Найденные = Информация.Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ВедущийТип", Тип));
			Для Каждого СвязьПоМетаданным Из Найденные Цикл // Регистрация настройки как ведущей.
				СвязьПоМетаданным.ЕстьВедущие = Истина;
				СвязьПоМетаданным.Ведущие.Добавить(НастройкаВарианта);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Только перечисления или типы с быстрым выбором.
	Если ВсеТипыСБыстрымВыбором И ИнформацияОТипах.ОбъектныеТипы.Количество() = ИнформацияОТипах.КоличествоТипов Тогда
		НастройкаВарианта.БыстрыйВыбор = Истина;
	КонецЕсли;
	
	НастройкаВарианта.ИнформацияОТипах = ИнформацияОТипах;
	НастройкаВарианта.ОписаниеТипов = ИнформацияОТипах.ОписаниеТиповДляФормы;
	
	///////////////////////////////////////////////////////////////////
	// Информация о связях и параметрах выбора.
	
	ДоступнаяНастройкаКД = НастройкаВарианта.ДоступнаяНастройкаКД;
	Если ДоступнаяНастройкаКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоступнаяНастройкаКД.СвязьПоТипу) Тогда
		СтрокаСвязи = Информация.Связи.ПоТипу.Добавить();
		СтрокаСвязи.Подчиненный   = НастройкаВарианта;
		СтрокаСвязи.ВедущийПолеКД = ДоступнаяНастройкаКД.СвязьПоТипу.Поле;
		СтрокаСвязи.ПодчиненныйИмяПараметра = ДоступнаяНастройкаКД.СвязьПоТипу.ЭлементСвязи;
	КонецЕсли;
	
	Для Каждого СтрокаСвязи Из ДоступнаяНастройкаКД.ПолучитьСвязиПараметровВыбора() Цикл
		Если ПустаяСтрока(Строка(СтрокаСвязи.Поле)) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаСвязиПараметров = Информация.Связи.ПараметровВыбора.Добавить();
		СтрокаСвязиПараметров.Подчиненный             = НастройкаВарианта;
		СтрокаСвязиПараметров.ПодчиненныйИмяПараметра = СтрЗаменить(СтрокаСвязи.Имя, "@", "");
		СтрокаСвязиПараметров.ВедущийПолеКД           = СтрокаСвязи.Поле;
		СтрокаСвязиПараметров.Действие                = СтрокаСвязи.ИзменениеЗначения;
	КонецЦикла;
	
	Для Каждого ПараметрВыбораКД Из ДоступнаяНастройкаКД.ПолучитьПараметрыВыбора() Цикл
		НастройкаВарианта.ПараметрыВыбора.Добавить(Новый ПараметрВыбора(ПараметрВыбораКД.Имя, ПараметрВыбораКД.Значение));
	КонецЦикла;
	
	///////////////////////////////////////////////////////////////////
	// Список значений.
	
	Если ТипЗнч(ДоступнаяНастройкаКД.ДоступныеЗначения) = Тип("СписокЗначений") Тогда
		НастройкаВарианта.ЗначенияДляВыбора = ДоступнаяНастройкаКД.ДоступныеЗначения;
		НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями = НастройкаВарианта.ЗначенияДляВыбора.Количество() > 0;
	Иначе
		НастройкиСохраненныеРанее = Информация.ДополнительныеНастройкиЭлементов[НастройкаВарианта.ИдентификаторЭлемента];
		Ограничивать = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиСохраненныеРанее, "ОграничиватьВыборУказаннымиЗначениями");
		Если НастройкиСохраненныеРанее <> Неопределено И Ограничивать = Ложь Тогда
			СтарыеЗначенияДляВыбора = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиСохраненныеРанее, "ЗначенияДляВыбора");
			Если ТипЗнч(СтарыеЗначенияДляВыбора) = Тип("СписокЗначений") Тогда
				НастройкаВарианта.ЗначенияДляВыбора.ТипЗначения = НастройкаВарианта.ОписаниеТипов;
				Для Каждого СтарыйЭлементСписка Из СтарыеЗначенияДляВыбора Цикл
					Если Не НастройкаВарианта.ОписаниеТипов.СодержитТип(ТипЗнч(СтарыйЭлементСписка.Значение)) Тогда
						Продолжить;
					КонецЕсли;
					Если НастройкаВарианта.ЗначенияДляВыбора.НайтиПоЗначению(СтарыйЭлементСписка.Значение) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					ЭлементПриемник = НастройкаВарианта.ЗначенияДляВыбора.Добавить();
					ЭлементПриемник.Значение = СтарыйЭлементСписка.Значение;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииПараметровВыбора(Информация, НастройкаВарианта)
	ПредставлениеДо = Строка(НастройкаВарианта.ЗначенияДляВыбора);
	КоличествоДо = НастройкаВарианта.ЗначенияДляВыбора.Количество();
	
	// Механизмы расширения.
	// Глобальные настройки вывода типов.
	ОтчетыПереопределяемый.ПриОпределенииПараметровВыбора(Неопределено, НастройкаВарианта);
	// Локальное переопределение для отчета.
	Если Информация.НастройкиОтчета.События.ПриОпределенииПараметровВыбора Тогда
		ОтчетОбъект(Информация.ОтчетОбъектИлиПолноеИмя).ПриОпределенииПараметровВыбора(Неопределено, НастройкаВарианта);
	КонецЕсли;
	
	// Автоматическое заполнение.
	Если НастройкаВарианта.ЗапросЗначенийВыбора.Текст <> "" Тогда
		ДобавляемыеЗначения = НастройкаВарианта.ЗапросЗначенийВыбора.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
		Для Каждого ЗначениеВФорме Из ДобавляемыеЗначения Цикл
			ОтчетыКлиентСервер.ДобавитьУникальноеЗначениеВСписок(НастройкаВарианта.ЗначенияДляВыбора, ЗначениеВФорме, Неопределено, Ложь);
		КонецЦикла;
		НастройкаВарианта.ЗначенияДляВыбора.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
	КонецЕсли;
	
	// Удаление значений, выбор которых запрещен.
	Если НастройкаВарианта.ВводСписком
		И НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями
		И ТипЗнч(НастройкаВарианта.Значение) = Тип("СписокЗначений")
		И ТипЗнч(НастройкаВарианта.ЗначенияДляВыбора) = Тип("СписокЗначений") Тогда
		Список = НастройкаВарианта.Значение;
		Количество = Список.Количество();
		Для Номер = 1 По Количество Цикл
			ОбратныйИндекс = Количество - Номер;
			Значение = Список[ОбратныйИндекс].Значение;
			Если НастройкаВарианта.ЗначенияДляВыбора.НайтиПоЗначению(Значение) = Неопределено Тогда
				Список.Удалить(ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если КоличествоДо <> НастройкаВарианта.ЗначенияДляВыбора.Количество()
		Или ПредставлениеДо <> Строка(НастройкаВарианта.ЗначенияДляВыбора) Тогда
		НастройкаВарианта.СписокЗначенийПереопределен = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьСвязиОтВедущих(Информация)
	Связи = Информация.Связи;
	
	// Регистрация связи параметров выбора (динамическая связь, отключаемая флажком Использование).
	Найденные = Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ЕстьПодчиненные, ЕстьВедущие", Истина, Истина));
	Для Каждого СвязьПоМетаданным Из Найденные Цикл
		Для Каждого Ведущий Из СвязьПоМетаданным.Ведущие Цикл
			Для Каждого Подчиненный Из СвязьПоМетаданным.Подчиненные Цикл
				Если Ведущий.ВыводРазрешен Тогда // Отключаемая связь.
					ОписаниеСвязи = Новый Структура;
					ОписаниеСвязи.Вставить("ТипСвязи",                "ПоМетаданным");
					ОписаниеСвязи.Вставить("Ведущий",                 Ведущий);
					ОписаниеСвязи.Вставить("Подчиненный",             Подчиненный);
					ОписаниеСвязи.Вставить("ВедущийТип",              СвязьПоМетаданным.ВедущийТип);
					ОписаниеСвязи.Вставить("ПодчиненныйТип",          СвязьПоМетаданным.ПодчиненныйТип);
					ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметра", СвязьПоМетаданным.ПодчиненныйРеквизит);
					Информация.ОтключаемыеСвязи.Добавить(ОписаниеСвязи);
					Подчиненный.СвязиПоМетаданным.Добавить(ОписаниеСвязи);
				Иначе // Фиксированный параметр выбора.
					Подчиненный.ПараметрыВыбора.Добавить(Новый ПараметрВыбора(СвязьПоМетаданным.ПодчиненныйРеквизит, Ведущий.Значение));
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Связи по типу.
	Для Каждого СвязьПоТипу Из Связи.ПоТипу Цикл
		Ведущий = Информация.Поиск.НастройкиВариантаПоПолюКД.Получить(СвязьПоТипу.ВедущийПолеКД);
		Если Ведущий = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Подчиненный = СвязьПоТипу.Подчиненный;
		Если Ведущий.ВыводРазрешен Тогда // Отключаемая связь.
			ОписаниеСвязи = Новый Структура;
			ОписаниеСвязи.Вставить("ТипСвязи",                "ПоТипу");
			ОписаниеСвязи.Вставить("Ведущий",                 Ведущий);
			ОписаниеСвязи.Вставить("Подчиненный",             Подчиненный);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметра", СвязьПоТипу.ПодчиненныйИмяПараметра);
			Информация.ОтключаемыеСвязи.Добавить(ОписаниеСвязи);
			Подчиненный.СвязьПоТипу = ОписаниеСвязи;
		Иначе // Фиксированное ограничения типа.
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(Ведущий.Значение));
			Подчиненный.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
		КонецЕсли;
	КонецЦикла;
	
	// Связи параметров выбора.
	Для Каждого СвязьПараметровВыбора Из Связи.ПараметровВыбора Цикл
		Ведущий     = СвязьПараметровВыбора.Ведущий;
		Подчиненный = СвязьПараметровВыбора.Подчиненный;
		Если Ведущий = Неопределено Тогда
			ЛучшийВариант = 99;
			Найденные = Информация.НастройкиВарианта.Строки.НайтиСтроки(Новый Структура("ПолеКД", СвязьПараметровВыбора.ВедущийПолеКД), Истина);
			Для Каждого ПотенциальныйРодитель Из Найденные Цикл
				Если ПотенциальныйРодитель.Тип <> "ЭлементОтбора"
					И ПотенциальныйРодитель.Тип <> "ЗначениеПараметраНастроек" Тогда
					Продолжить;
				КонецЕсли;
				Если ПотенциальныйРодитель.Родитель = Подчиненный.Родитель Тогда // Элементы в одной группе.
					Если Не ПустаяСтрока(ПотенциальныйРодитель.ИдентификаторЭлемента) Тогда // Ведущий выведен в пользовательские.
						Ведущий = ПотенциальныйРодитель;
						ЛучшийВариант = 0;
						Прервать; // Самый лучший вариант.
					Иначе
						Ведущий = ПотенциальныйРодитель;
						ЛучшийВариант = 1;
					КонецЕсли;
				ИначеЕсли ЛучшийВариант > 2 И ПотенциальныйРодитель.Владелец = Подчиненный.Владелец Тогда // Элементы в одной коллекции.
					Если Не ПустаяСтрока(ПотенциальныйРодитель.ИдентификаторЭлемента) Тогда // Ведущий выведен в пользовательские.
						Если ЛучшийВариант > 2 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 2;
						КонецЕсли;
					Иначе
						Если ЛучшийВариант > 3 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 3;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЛучшийВариант > 4 И ПотенциальныйРодитель.СтрокаДерева = Подчиненный.СтрокаДерева Тогда // Элементы в одном узле.
					Если Не ПустаяСтрока(ПотенциальныйРодитель.ИдентификаторЭлемента) Тогда // Ведущий выведен в пользовательские.
						Если ЛучшийВариант > 4 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 4;
						КонецЕсли;
					Иначе
						Если ЛучшийВариант > 5 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 5;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЛучшийВариант > 6 Тогда
					Ведущий = ПотенциальныйРодитель;
					ЛучшийВариант = 6;
				КонецЕсли;
			КонецЦикла;
			Если Ведущий = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если Ведущий.ВыводРазрешен Тогда // Отключаемая связь.
			ОписаниеСвязи = Новый Структура;
			ОписаниеСвязи.Вставить("ТипСвязи",      "ПараметровВыбора");
			ОписаниеСвязи.Вставить("Ведущий",       Ведущий);
			ОписаниеСвязи.Вставить("Подчиненный",   Подчиненный);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметра", СвязьПараметровВыбора.ПодчиненныйИмяПараметра);
			ОписаниеСвязи.Вставить("ПодчиненныйДействие",     СвязьПараметровВыбора.Действие);
			Информация.ОтключаемыеСвязи.Добавить(ОписаниеСвязи);
			Подчиненный.СвязиПараметровВыбора.Добавить(ОписаниеСвязи);
		Иначе // Фиксированный параметр выбора.
			Если ТипЗнч(Ведущий.Значение) = Тип("ПолеКомпоновкиДанных") Тогда
				Продолжить; // Расширенная работа с отборами по полю компоновки данных не поддерживается.
			КонецЕсли;
			Попытка
				ЗначениеЗаполнено = ЗначениеЗаполнено(Ведущий.Значение);
			Исключение
				ЗначениеЗаполнено = Истина;
			КонецПопытки;
			Если Не ЗначениеЗаполнено Тогда
				Продолжить;
			КонецЕсли;
			Подчиненный.ПараметрыВыбора.Добавить(Новый ПараметрВыбора(СвязьПараметровВыбора.ПодчиненныйИмяПараметра, Ведущий.Значение));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Определяет свойства параметров вывода, влияющих на отображение заголовка, параметров данных и отборов:
//  * Выводить заголовок;
//  * Заголовок;
//  * Выводить параметры данных;
//  * Выводить отбор;
//
// Параметры:
//  Контекст - Структура - сведения о варианте отчета и о идентификаторе его метаданных.
//  Настройки - НастройкиКомпоновкиДанных - настройки, чьи параметры вывода устанавливаются.
//
Процедура ИнициализироватьПредопределенныеПараметрыВывода(Контекст, Настройки) Экспорт 
	Если Настройки = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыВывода = Настройки.ПараметрыВывода.Элементы;
	
	// Параметр Заголовок всегда доступен и только в форме настроек отчета.
	Объект = ПараметрыВывода.Найти("TITLE");
	Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	Объект.ИдентификаторПользовательскойНастройки = "";
	
	УстановитьЗаголовокОтчетаПоУмолчанию(Объект, Контекст);
	
	// Параметр ВыводитьЗаголовок всегда недоступный. Свойства зависят от параметра Заголовок.
	СвязанныйОбъект = ПараметрыВывода.Найти("TITLEOUTPUT");
	СвязанныйОбъект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	СвязанныйОбъект.ИдентификаторПользовательскойНастройки = "";
	СвязанныйОбъект.Использование = Истина;
	
	Если Объект.Использование Тогда 
		СвязанныйОбъект.Значение = ТипВыводаТекстаКомпоновкиДанных.Авто;
	Иначе
		СвязанныйОбъект.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
	КонецЕсли;
	
	// Параметр ВыводитьПараметры всегда доступен и только в форме настроек отчета.
	Объект = ПараметрыВывода.Найти("DATAPARAMETERSOUTPUT");
	Объект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	Объект.ИдентификаторПользовательскойНастройки = "";
	Объект.Использование = Истина;
	
	Если Объект.Значение <> ТипВыводаТекстаКомпоновкиДанных.НеВыводить Тогда 
		Объект.Значение = ТипВыводаТекстаКомпоновкиДанных.Авто;
	КонецЕсли;
	
	// Параметр ВыводитьОтбор всегда недоступный. Значения свойств те же, что и у параметра ВыводитьПараметрыДанных.
	СвязанныйОбъект = ПараметрыВывода.Найти("FILTEROUTPUT");
	СвязанныйОбъект.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	СвязанныйОбъект.ИдентификаторПользовательскойНастройки = "";
	СвязанныйОбъект.Использование = Истина;
	
	Если СвязанныйОбъект.Значение <> ТипВыводаТекстаКомпоновкиДанных.НеВыводить Тогда 
		СвязанныйОбъект.Значение = ТипВыводаТекстаКомпоновкиДанных.Авто;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьЗаголовокОтчетаПоУмолчанию(Заголовок, Контекст)
	Если ЗначениеЗаполнено(Заголовок.Значение) Тогда 
		Возврат;
	КонецЕсли;
	
	ИдентификаторОтчета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст, "ОтчетСсылка");
	Если ИдентификаторОтчета = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЭтоТипДополнительныйОтчетИлиОбработка = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		ЭтоТипДополнительныйОтчетИлиОбработка = МодульДополнительныеОтчетыИОбработки.ЭтоТипДополнительныйОтчетИлиОбработка(
			ТипЗнч(ИдентификаторОтчета));
	КонецЕсли;
	
	Если ТипЗнч(ИдентификаторОтчета) = Тип("Строка")
		Или ЭтоТипДополнительныйОтчетИлиОбработка Тогда 
		Заголовок.Значение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст, "Наименование", "");
		Возврат;
	КонецЕсли;
	
	Вариант = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст, "ВариантСсылка");
	Если ЗначениеЗаполнено(Вариант) Тогда 
		Заголовок.Значение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Вариант, "Наименование");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Заголовок.Значение)
		И Заголовок.Значение <> "Основной" Тогда 
		Возврат;
	КонецЕсли;
	
	МетаданныеОтчета = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ИдентификаторОтчета, Ложь);
	Если ТипЗнч(МетаданныеОтчета) = Тип("ОбъектМетаданных") Тогда 
		Заголовок.Значение = МетаданныеОтчета.Представление();
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьФиксированныеОтборы(СтруктураОтборов, НастройкиКД, НастройкиОтчета) Экспорт
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных")
		Или СтруктураОтборов = Неопределено
		Или СтруктураОтборов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ПараметрыКД = НастройкиКД.ПараметрыДанных;
	ОтборыКД = НастройкиКД.Отбор;
	Недоступный = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Для Каждого КлючИЗначение Из СтруктураОтборов Цикл
		Имя = КлючИЗначение.Ключ;
		Значение = КлючИЗначение.Значение;
		Если ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда
			Значение = Новый Массив(Значение);
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("Массив") Тогда
			Список = Новый СписокЗначений;
			Список.ЗагрузитьЗначения(Значение);
			Значение = Список;
		КонецЕсли;
		ПараметрКД = ПараметрыКД.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(Имя));
		Если ТипЗнч(ПараметрКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ПараметрКД.ИдентификаторПользовательскойНастройки = "";
			ПараметрКД.Использование    = Истина;
			ПараметрКД.РежимОтображения = Недоступный;
			ПараметрКД.Значение         = Значение;
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("Структура") Тогда
			ВидСравненияКД = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				Значение, "ВидСравнения", ВидСравненияКомпоновкиДанных.Равно);
			Значение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Значение, "ПравоеЗначение");
		ИначеЕсли ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборыКД, Имя, Значение, ВидСравненияКД, , Истина, Недоступный, "");
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Пользовательские настройки

Функция ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, СтрокаДерева, НастройкаВарианта)
	ПользовательскаяНастройкаКД = СвойстваНастройки.ПользовательскаяНастройкаКД;
	
	РежимОтображения = ПользовательскаяНастройкаКД.РежимОтображения;
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Возврат СвойстваНастройки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СвойстваНастройки.Идентификатор) Тогда
		Возврат СвойстваНастройки;
	КонецЕсли;
	СвойстваНастройки.ИдентификаторЭлемента = ПривестиИдентификаторКИмени(СвойстваНастройки.Идентификатор);
	
	Если НастройкаВарианта <> Неопределено Тогда
		Если НастройкаВарианта.Владелец <> Неопределено Тогда
			СвойстваНастройки.УзелКД = НастройкаВарианта.Владелец.ЭлементКД;
		КонецЕсли;
		СвойстваНастройки.СтрокаДерева         = НастройкаВарианта.СтрокаДерева;
		СвойстваНастройки.НастройкаВариантаКД  = НастройкаВарианта.ЭлементКД;
		СвойстваНастройки.НастройкаВарианта    = НастройкаВарианта;
		СвойстваНастройки.Подтип               = НастройкаВарианта.Подтип;
		СвойстваНастройки.ПолеКД               = НастройкаВарианта.ПолеКД;
		СвойстваНастройки.ДоступнаяНастройкаКД = НастройкаВарианта.ДоступнаяНастройкаКД;
		СвойстваНастройки.ИнформацияОТипах     = НастройкаВарианта.ИнформацияОТипах;
		СвойстваНастройки.ОписаниеТипов        = НастройкаВарианта.ОписаниеТипов;
		Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто Тогда
			РежимОтображения = СвойстваНастройки.НастройкаВариантаКД.РежимОтображения;
		КонецЕсли;
	Иначе
		СвойстваНастройки.УзелКД              = СтрокаДерева.УзелКД;
		СвойстваНастройки.СтрокаДерева        = СтрокаДерева;
		СвойстваНастройки.Тип                 = СтрокаДерева.Тип;
		СвойстваНастройки.Подтип              = СтрокаДерева.Подтип;
		СвойстваНастройки.НастройкаВариантаКД = СвойстваНастройки.УзелКД;
		Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто Тогда
			РежимОтображения = СвойстваНастройки.УзелКД.РежимОтображения;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ Тогда
		СвойстваНастройки.Быстрая = Истина;
		Информация.ЕстьБыстрыеНастройки = Истина;
	ИначеЕсли РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный Тогда
		СвойстваНастройки.Обычная = Истина;
		Информация.ЕстьОбычныеНастройки = Истина;
	ИначеЕсли Информация.ТолькоПользовательские Тогда
		Возврат СвойстваНастройки;
	КонецЕсли;
	
	// Определение доступной настройки.
	Если СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
		СвойстваНастройки.ДоступнаяНастройкаКД = Информация.НастройкиКД.ДоступныеОбъекты.Элементы.Найти(СвойстваНастройки.СтрокаДерева.УзелКД.ИдентификаторОбъекта);
	КонецЕсли;
	
	Если Информация.ТолькоПользовательские Тогда
		Если Информация.ТолькоБыстрые Тогда
			СвойстваНастройки.ВыводРазрешен = СвойстваНастройки.Быстрая;
		Иначе
			СвойстваНастройки.ВыводРазрешен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	СвойстваНастройки.ВыводитьФлажок = Истина;
	СвойстваНастройки.ВыводитьТолькоФлажок = Ложь;
	
	ЗаполнитьПредставлениеИСостояниеНастройки(
		СвойстваНастройки,
		СвойстваНастройки.НастройкаВариантаКД,
		СвойстваНастройки.ПользовательскаяНастройкаКД,
		СвойстваНастройки.ДоступнаяНастройкаКД);
	
	Если СвойстваНастройки.Состояние = "ПометкаУдаления" Тогда
		Информация.ЕстьНесуществующиеПоля = Истина;
		СвойстваНастройки.ВыводРазрешен = Ложь;
	КонецЕсли;
	
	Если СвойстваНастройки.Тип = "ГруппаЭлементовОтбора"
		Или СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта"
		Или СвойстваНастройки.Тип = "Группировка"
		Или СвойстваНастройки.Тип = "Таблица"
		Или СвойстваНастройки.Тип = "ГруппировкаТаблицы"
		Или СвойстваНастройки.Тип = "Диаграмма"
		Или СвойстваНастройки.Тип = "ГруппировкаДиаграммы"
		Или СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
		
		СвойстваНастройки.ВыводитьТолькоФлажок = Истина;
		
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
		Или СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		
		Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
			СвойстваНастройки.Значение = ПользовательскаяНастройкаКД.Значение;
		Иначе
			СвойстваНастройки.Значение = ПользовательскаяНастройкаКД.ПравоеЗначение;
		КонецЕсли;
		
		// Определение типа значения настройки.
		Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
			Если СвойстваНастройки.ДоступнаяНастройкаКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда Тогда
				СвойстваНастройки.ВыводитьФлажок = Ложь;
				ПользовательскаяНастройкаКД.Использование = Истина;
			КонецЕсли;
			Если СвойстваНастройки.ДоступнаяНастройкаКД.ДоступенСписокЗначений Тогда
				СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
			СвойстваНастройки.ВидСравнения = ПользовательскаяНастройкаКД.ВидСравнения;
		КонецЕсли;
		
		Если СвойстваНастройки.ИнформацияОТипах.СодержитТипПериод
			И СвойстваНастройки.ИнформацияОТипах.КоличествоТипов = 1 Тогда
			
			СвойстваНастройки.ТипЭлементов = "СтандартныйПериод";
			
		ИначеЕсли Не СвойстваНастройки.ВыводитьФлажок
			И СвойстваНастройки.ИнформацияОТипах.СодержитТипБулево
			И СвойстваНастройки.ИнформацияОТипах.КоличествоТипов = 1 Тогда
			
			СвойстваНастройки.ТипЭлементов = "ТолькоФлажокЗначения";
			
		ИначеЕсли СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
			
			СвойстваНастройки.ТипЭлементов = "УсловиеВРежимеПросмотра";
			
		ИначеЕсли СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
			
			СвойстваНастройки.ВводСписком = Истина;
			СвойстваНастройки.ТипЭлементов = "СписокСПодбором";
			СвойстваНастройки.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
			
		Иначе
			
			СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
			Если СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
				Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
				СвойстваНастройки.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СвойстваНастройки.ВыборГруппИЭлементов = Неопределено Тогда
			СвойстваНастройки.ВыборГруппИЭлементов = НастройкаВарианта.ВыборГруппИЭлементов;
		КонецЕсли;
		
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля"
		Или СвойстваНастройки.Тип = "Порядок"
		Или СвойстваНастройки.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
		Или СвойстваНастройки.Тип = "КоллекцияЭлементовСтруктурыДиаграммы"
		Или СвойстваНастройки.Тип = "Отбор"
		Или СвойстваНастройки.Тип = "УсловноеОформление"
		Или СвойстваНастройки.Тип = "СтруктураНастроек" Тогда
		
		СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
		СвойстваНастройки.ВыводитьФлажок = Ложь;
		
	Иначе
		
		СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
		
	КонецЕсли;
	
	Если СвойстваНастройки.ВыводитьТолькоФлажок Тогда
		СвойстваНастройки.ТипЭлементов = "";
	ИначеЕсли СвойстваНастройки.Быстрая И СвойстваНастройки.ТипЭлементов = "СписокСПодбором" Тогда
		СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
	КонецЕсли;
	
	Возврат СвойстваНастройки;
КонецФункции

Процедура ЗаполнитьПредставлениеИСостояниеНастройки(СвойстваНастройки, НастройкаВариантаКД, ПользовательскаяНастройкаКД, ДоступнаяНастройкаКД)
	Если ПользовательскаяНастройкаКД = Неопределено Тогда
		ПользовательскаяНастройкаКД = НастройкаВариантаКД;
	КонецЕсли;
	
	СтруктураПредставлений = Новый Структура("Представление, ПредставлениеПользовательскойНастройки", "", "");
	ЗаполнитьЗначенияСвойств(СтруктураПредставлений, НастройкаВариантаКД);
	
	СвойстваНастройки.ВыводитьТолькоФлажок = ЗначениеЗаполнено(СтруктураПредставлений.Представление);
	
	Если ЗначениеЗаполнено(СтруктураПредставлений.ПредставлениеПользовательскойНастройки) Тогда
		Представление = СтруктураПредставлений.ПредставлениеПользовательскойНастройки;
	ИначеЕсли ЗначениеЗаполнено(СтруктураПредставлений.Представление) И СтруктураПредставлений.Представление <> "1" Тогда
		Представление = СтруктураПредставлений.Представление;
	Иначе
		Представление = "";
	КонецЕсли;
	СвойстваНастройки.Представление = СокрЛП(Представление);
	
	// Представление "По умолчанию".
	Если ДоступнаяНастройкаКД <> Неопределено И ЗначениеЗаполнено(ДоступнаяНастройкаКД.Заголовок) Тогда
		ПредставлениеПоУмолчанию = ДоступнаяНастройкаКД.Заголовок;
	ИначеЕсли ЗначениеЗаполнено(СвойстваНастройки.Подтип) Тогда
		Если СвойстваНастройки.Подтип = "ДиаграммаСерии" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Серии'");
		ИначеЕсли СвойстваНастройки.Подтип = "ДиаграммаТочки" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Точки'");
		ИначеЕсли СвойстваНастройки.Подтип = "ТаблицаСтроки" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Строки'");
		ИначеЕсли СвойстваНастройки.Подтип = "ТаблицаКолонки" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Колонки'");
		ИначеЕсли СвойстваНастройки.Подтип = "Отчет" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Отчет'");
		Иначе
			ПредставлениеПоУмолчанию = Строка(СвойстваНастройки.Подтип);
		КонецЕсли;
	
	// Параметры и отборы.
	ИначеЕсли СвойстваНастройки.Тип = "Отбор" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Отбор'");
	ИначеЕсли СвойстваНастройки.Тип = "ГруппаЭлементовОтбора" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.ТипГруппы);
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.ЛевоеЗначение);
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.Параметр);
	
	// Сортировки.
	ИначеЕсли СвойстваНастройки.Тип = "Порядок" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Сортировка'");
	ИначеЕсли СвойстваНастройки.Тип = "АвтоЭлементПорядка" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Авто (сортировки родителя)'");
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементПорядка" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.Поле);
	
	// Выбранные поля.
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Поля'");
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранноеПоле" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.Поле);
	ИначеЕсли СвойстваНастройки.Тип = "АвтоВыбранноеПоле" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Авто (поля родителя)'");
	ИначеЕсли СвойстваНастройки.Тип = "ГруппаВыбранныхПолей" Тогда
		ПредставлениеПоУмолчанию = НастройкаВариантаКД.Заголовок;
		Если НастройкаВариантаКД.Расположение <> РасположениеПоляКомпоновкиДанных.Авто Тогда
			ПредставлениеПоУмолчанию = ПредставлениеПоУмолчанию + " (" + Строка(НастройкаВариантаКД.Расположение) + ")";
		КонецЕсли;
	
	// Оформление.
	ИначеЕсли СвойстваНастройки.Тип = "УсловноеОформление" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Оформление'");
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
		ПредставлениеПоУмолчанию = ОтчетыКлиентСервер.ПредставлениеЭлементаУсловногоОформления(
			ПользовательскаяНастройкаКД,
			НастройкаВариантаКД,
			СвойстваНастройки.Состояние);
	
	// Структура.
	ИначеЕсли СвойстваНастройки.Тип = "Группировка"
		Или СвойстваНастройки.Тип = "ГруппировкаТаблицы"
		Или СвойстваНастройки.Тип = "ГруппировкаДиаграммы" Тогда
		ПредставлениеПоУмолчанию = СокрЛП(Строка(НастройкаВариантаКД.ПоляГруппировки));
		Если ПустаяСтрока(ПредставлениеПоУмолчанию) Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = '<Детальные записи>'");
		Иначе
			ДоступныеПоляКД = НастройкаВариантаКД.ПоляГруппировки.ДоступныеПоляПолейГруппировок;
			Для Каждого ПолеГруппировкиКД Из НастройкаВариантаКД.ПоляГруппировки.Элементы Цикл
				Если ТипЗнч(ПолеГруппировкиКД) = Тип("ПолеГруппировкиКомпоновкиДанных")
					И ДоступныеПоляКД.НайтиПоле(ПолеГруппировкиКД.Поле) = Неопределено Тогда
					СвойстваНастройки.Состояние = "ПометкаУдаления";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли СвойстваНастройки.Тип = "Таблица" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Таблица'");
	ИначеЕсли СвойстваНастройки.Тип = "Диаграмма" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Диаграмма'");
	ИначеЕсли СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
		ПредставлениеПоУмолчанию = Строка(ПользовательскаяНастройкаКД);
		Если ПустаяСтрока(ПредставлениеПоУмолчанию) Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Вложенная группировка'");
		КонецЕсли;
	ИначеЕсли СвойстваНастройки.Тип = "СтруктураНастроек" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Структура'");
	Иначе
		ПредставлениеПоУмолчанию = Строка(СвойстваНастройки.Тип);
	КонецЕсли;
	СвойстваНастройки.ПредставлениеПоУмолчанию = СокрЛП(ПредставлениеПоУмолчанию);
	
	Если СвойстваНастройки.ДоступнаяНастройкаКД = Неопределено
		И (СвойстваНастройки.Тип = "ЭлементОтбора"
			Или СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
			Или СвойстваНастройки.Тип = "ЭлементПорядка"
			Или СвойстваНастройки.Тип = "ВыбранноеПоле")Тогда
		СвойстваНастройки.Состояние = "ПометкаУдаления";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СвойстваНастройки.Представление) Тогда
		СвойстваНастройки.Представление = СвойстваНастройки.ПредставлениеПоУмолчанию;
	КонецЕсли;
	
КонецПроцедуры

// Собирает статистику по количеству пользовательских настроек в разрезе режимов отображения.
//
// Параметры:
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - актуальный компоновщик.
//
// Возвращаемое значение:
//   Структура - количество пользовательских настроек в разрезе режимов отображения:
//             - БыстрогоДоступа - количество настроек с режимом отображения БыстрыйДоступ или Авто;
//             - Обычных - количество настроек с режимом отображения Обычный;
//             - Итог - общее количество доступных настроек.
//
Функция КоличествоДоступныхНастроек(КомпоновщикНастроек) Экспорт 
	ДоступныеНастройки = Новый Структура;
	ДоступныеНастройки.Вставить("БыстрогоДоступа", 0);
	ДоступныеНастройки.Вставить("Обычных", 0);
	ДоступныеНастройки.Вставить("Итог", 0);
	
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	Для Каждого ЭлементПользовательскойНастройки Из ПользовательскиеНастройки.Элементы Цикл 
		ЭлементНастройки = ОтчетыКлиентСервер.ПолучитьОбъектПоПользовательскомуИдентификатору(
			КомпоновщикНастроек.Настройки,
			ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки,,
			ПользовательскиеНастройки);
		
		РежимОтображения = ?(ЭлементНастройки = Неопределено,
			ЭлементПользовательскойНастройки.РежимОтображения, ЭлементНастройки.РежимОтображения);
		
		Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто
			Или РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ Тогда 
			ДоступныеНастройки.БыстрогоДоступа = ДоступныеНастройки.БыстрогоДоступа + 1;
		ИначеЕсли РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный Тогда 
			ДоступныеНастройки.Обычных = ДоступныеНастройки.Обычных + 1;
		КонецЕсли;
	КонецЦикла;
	
	ДоступныеНастройки.Итог = ДоступныеНастройки.БыстрогоДоступа + ДоступныеНастройки.Обычных;
	
	Возврат ДоступныеНастройки;
КонецФункции

Функция СвойстваЭлементаПользовательскихНастроек(КомпоновщикНастроек, ЭлементПользовательскойНастройки, ЭлементНастройки, ОписаниеНастройки)
	Свойства = ПалитраСвойствЭлементаПользовательскихНастроек();
	
	Свойства.ПользовательскаяНастройкаКД = ЭлементПользовательскойНастройки;
	Свойства.ЭлементКД = ЭлементНастройки;
	Свойства.ДоступнаяНастройкаКД = ОписаниеНастройки;
	
	Свойства.Идентификатор = ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки;
	Свойства.ИдентификаторКД = КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьИдентификаторПоОбъекту(
		ЭлементПользовательскойНастройки);
	Свойства.ИдентификаторЭлемента = СтрЗаменить(
		ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки, "-", "");
	
	ТипЭлементаНастройки = ТипЗнч(ЭлементНастройки);
	Если ТипЭлементаНастройки = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
		Свойства.ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(ЭлементНастройки.Параметр));
		Свойства.Значение = ЭлементНастройки.Значение;
	ИначеЕсли ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
		Свойства.ПолеКД = ЭлементНастройки.ЛевоеЗначение;
		Свойства.Значение = ЭлементНастройки.ПравоеЗначение;
	КонецЕсли;
	
	Свойства.Тип = ТипНастройкиСтрокой(ТипЭлементаНастройки);
	
	Если ОписаниеНастройки = Неопределено Тогда 
		Возврат Свойства;
	КонецЕсли;
	
	Свойства.ОписаниеТипов = ОписаниеНастройки.ТипЗначения;
	
	Если ОписаниеНастройки.ДоступныеЗначения <> Неопределено Тогда 
		Свойства.ЗначенияДляВыбора = ОписаниеНастройки.ДоступныеЗначения;
	КонецЕсли;
	
	Возврат Свойства;
КонецФункции

Функция ПалитраСвойствЭлементаПользовательскихНастроек()
	Свойства = Новый Структура;
	Свойства.Вставить("БыстрыйВыбор", Ложь);
	Свойства.Вставить("ВводСписком", Ложь);
	Свойства.Вставить("ВидСравнения", ВидСравненияКомпоновкиДанных.Равно);
	Свойства.Вставить("Владелец", Неопределено);
	Свойства.Вставить("ВыборГруппИЭлементов", ГруппыИЭлементы.Авто);
	Свойства.Вставить("ВыводРазрешен", Истина);
	Свойства.Вставить("ВыводитьВГруппеОсновныхНастроек", Ложь);
	Свойства.Вставить("ВыводитьТолькоФлажок", Ложь);
	Свойства.Вставить("ВыводитьФлажок", Истина);
	Свойства.Вставить("Глобальная", Истина);
	Свойства.Вставить("ДоступнаяНастройкаКД", Неопределено);
	Свойства.Вставить("ЗапросЗначенийВыбора", Новый Запрос);
	Свойства.Вставить("Значение", Неопределено);
	Свойства.Вставить("ЗначенияДляВыбора", Новый СписокЗначений);
	Свойства.Вставить("Идентификатор", "");
	Свойства.Вставить("ИдентификаторКД", Неопределено);
	Свойства.Вставить("ИдентификаторЭлемента", "");
	Свойства.Вставить("ИмяКоллекции", "");
	Свойства.Вставить("ИнформацияОТипах", Новый Структура);
	Свойства.Вставить("ОграничениеТипа", Неопределено);
	Свойства.Вставить("ОграничиватьВыборУказаннымиЗначениями", Ложь);
	Свойства.Вставить("ОписаниеТипов", Новый ОписаниеТипов("Неопределено"));
	Свойства.Вставить("ОтмеченныеЗначения", Неопределено);
	Свойства.Вставить("ПараметрыВыбора", Новый Массив);
	Свойства.Вставить("Подтип", "");
	Свойства.Вставить("ПолеКД", Неопределено);
	Свойства.Вставить("ПользовательскаяНастройка", Неопределено);
	Свойства.Вставить("ПользовательскаяНастройкаКД", Неопределено);
	Свойства.Вставить("Представление", "");
	Свойства.Вставить("ПредставлениеПоУмолчанию", "");
	Свойства.Вставить("Родитель", Неопределено);
	Свойства.Вставить("СвязиПараметровВыбора", Новый Массив);
	Свойства.Вставить("СвязиПоМетаданным", Новый Массив);
	Свойства.Вставить("СвязьПоТипу", Неопределено);
	Свойства.Вставить("СобытиеПриИзменении", Ложь);
	Свойства.Вставить("Состояние", "");
	Свойства.Вставить("СписокЗначенийПереопределен", Ложь);
	Свойства.Вставить("СтрокаДерева", Неопределено);
	Свойства.Вставить("Строки", Неопределено);
	Свойства.Вставить("Тип", "");
	Свойства.Вставить("ФормаВыбора", "");
	Свойства.Вставить("Ширина", 0);
	Свойства.Вставить("ЭлементКД", Неопределено);
	
	Возврат Свойства;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные

// Создает и возвращает экземпляр отчета по полному имени объекта метаданных.
//
// Параметры:
//  ПолноеИмя - Строка - полное имя объекта метаданных. Пример: "Отчет.БизнесПроцессы".
//
// Возвращаемое значение:
//  ОтчетОбъект - экземпляр отчета.
//
Функция ОтчетОбъект(Идентификатор) Экспорт
	ПолноеИмя = Идентификатор;
	
	Если ТипЗнч(Идентификатор) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
		ПолноеИмя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Идентификатор, "ПолноеИмя");
	КонецЕсли;
	
	ОписаниеОбъекта = СтрРазделить(ПолноеИмя, ".");
	
	Если ОписаниеОбъекта.Количество() >= 2 Тогда
		Вид = ВРег(ОписаниеОбъекта[0]);
		Имя = ОписаниеОбъекта[1];
	Иначе
		ВызватьИсключение СтрЗаменить(НСтр("ru = 'Некорректное полное имя отчета ""%1"".'"), "%1", ПолноеИмя);
	КонецЕсли;
	
	Если Вид = "ОТЧЕТ" Тогда
		Возврат Отчеты[Имя].Создать();
	ИначеЕсли Вид = "ВНЕШНИЙОТЧЕТ" Тогда
		Возврат ВнешниеОтчеты.Создать(Имя); // АПК:553 Для внешних отчетов, не подключенных к подсистеме "Дополнительные отчеты и обработки". Вызов безопасен, так как проверки безопасности для внешнего отчета выполнены ранее при подключении.
	Иначе
		ВызватьИсключение СтрЗаменить(НСтр("ru = '""%1"" не является отчетом.'"), "%1", ПолноеИмя);
	КонецЕсли;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обновление элементов формы пользовательских настроек

Функция СвойстваЭлементовФормыНастроек(ТипФормы, КомпоновщикНастроек, ДополнительныеПараметры) 
	#Область ПодготовкаСтруктуры
	
	СвойстваЭлементов = Новый Структура("Группы, Поля");
	СвойстваЭлементов.Группы = Новый Структура;
	
	ОписаниеСтроки = Новый ОписаниеТипов("Строка");
	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	
	Поля = Новый ТаблицаЗначений;
	Поля.Колонки.Добавить("ИндексНастройки", ОписаниеЧисла);
	Поля.Колонки.Добавить("ИдентификаторНастройки", ОписаниеСтроки);
	Поля.Колонки.Добавить("Настройки");
	Поля.Колонки.Добавить("ЭлементНастройки");
	Поля.Колонки.Добавить("ОписаниеНастройки");
	Поля.Колонки.Добавить("Представление", ОписаниеСтроки);
	Поля.Колонки.Добавить("ИдентификаторГруппы", ОписаниеСтроки);
	Поля.Колонки.Добавить("ПоложениеЗаголовка", Новый ОписаниеТипов("ПоложениеЗаголовкаЭлементаФормы"));
	Поля.Колонки.Добавить("РастягиватьПоГоризонтали");
	Поля.Колонки.Добавить("Ширина", ОписаниеЧисла);
	
	ДоступныеРежимы = Новый Массив;
	ДоступныеРежимы.Добавить(РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	Если ТипФормы = ТипФормыОтчета.Настройка Тогда 
		ДоступныеРежимы.Добавить(РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
	КонецЕсли;
	
	НедоступныеЭлементыСтруктуры = Новый Соответствие;
	НедоступныеЭлементыСтруктуры.Вставить(Тип("ГруппировкаКомпоновкиДанных"), ТипФормыОтчета.Настройка);
	НедоступныеЭлементыСтруктуры.Вставить(Тип("ГруппировкаТаблицыКомпоновкиДанных"), ТипФормыОтчета.Настройка);
	НедоступныеЭлементыСтруктуры.Вставить(Тип("ГруппировкаДиаграммыКомпоновкиДанных"), ТипФормыОтчета.Настройка);
	НедоступныеЭлементыСтруктуры.Вставить(Тип("ТаблицаКомпоновкиДанных"), ТипФормыОтчета.Настройка);
	НедоступныеЭлементыСтруктуры.Вставить(Тип("ДиаграммаКомпоновкиДанных"), ТипФормыОтчета.Настройка);
	
	СинхронизируемыеСвойства = Новый Соответствие;
	СинхронизируемыеСвойства.Вставить(Тип("ЭлементОтбораКомпоновкиДанных"), "ЛевоеЗначение, Представление");
	СинхронизируемыеСвойства.Вставить(Тип("ЭлементУсловногоОформленияКомпоновкиДанных"), "Представление");
	
	#КонецОбласти
	
	#Область ЗаполнениеСтруктуры
	
	Сведения = СведенияОПользовательскихНастройках(КомпоновщикНастроек.Настройки);
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	Для Каждого ЭлементПользовательскойНастройки Из ПользовательскиеНастройки.Элементы Цикл 
		НайденныеСведения = Сведения[ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки];
		ЭлементНастройки = НайденныеСведения.ЭлементНастройки;
		
		Если ЭлементНастройки = Неопределено
			Или НедоступныеЭлементыСтруктуры.Получить(ТипЗнч(ЭлементНастройки)) = ТипФормы
			Или ДоступныеРежимы.Найти(ЭлементНастройки.РежимОтображения) = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ТипЭлемента = ТипЗнч(ЭлементНастройки);
		Если ТипЭлемента = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда 
			Представление = ОтчетыКлиентСервер.ПредставлениеЭлементаУсловногоОформления(
				ЭлементНастройки, Неопределено, "");
			
			Если Не ЗначениеЗаполнено(ЭлементНастройки.Представление) Тогда 
				ЭлементНастройки.Представление = Представление;
			ИначеЕсли Не ЗначениеЗаполнено(ЭлементНастройки.ПредставлениеПользовательскойНастройки)
				И ЭлементНастройки.Представление <> Представление Тогда 
				
				ЭлементНастройки.ПредставлениеПользовательскойНастройки = ЭлементНастройки.Представление;
				ЭлементНастройки.Представление = Представление;
			КонецЕсли;
		КонецЕсли;
		
		Свойства = СинхронизируемыеСвойства[ТипЭлемента];
		Если Свойства <> Неопределено Тогда 
			ЗаполнитьЗначенияСвойств(ЭлементПользовательскойНастройки, ЭлементНастройки, Свойства);
		КонецЕсли;
		
		Поле = Поля.Добавить();
		Поле.ИдентификаторНастройки = ЭлементПользовательскойНастройки.ИдентификаторПользовательскойНастройки;
		Поле.ИндексНастройки = ПользовательскиеНастройки.Элементы.Индекс(ЭлементПользовательскойНастройки);
		Поле.Настройки = НайденныеСведения.Настройки;
		Поле.ЭлементНастройки = ЭлементНастройки;
		Поле.ОписаниеНастройки = НайденныеСведения.ОписаниеНастройки;
		Поле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		
		Если НедоступныеЭлементыСтруктуры.Получить(ТипЗнч(ЭлементНастройки)) <> Неопределено Тогда 
			Представление = ЭлементНастройки.ПараметрыВывода.Элементы.Найти("TITLE");
			Если Представление <> Неопределено
				И ЗначениеЗаполнено(Представление.Значение) Тогда 
				Поле.Представление = Представление.Значение;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипФормы = ТипФормыОтчета.Настройка
			И ТипЗнч(ЭлементНастройки) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда 
			Поле.ИдентификаторГруппы = "Дополнительно";
		КонецЕсли;
	КонецЦикла;
	
	Если Поля.Найти("Дополнительно", "ИдентификаторГруппы") <> Неопределено Тогда 
		СвойстваЭлементов.Группы.Вставить("Дополнительно", СвойстваГруппыЭлементовФормы());
	КонецЕсли;
	
	Поля.Сортировать("ИндексНастройки");
	СвойстваЭлементов.Поля = Поля;
	
	Если ДополнительныеПараметры.События.ПриОпределенииСвойствЭлементовФормыНастроек Тогда 
		ОтчетОбъект(ДополнительныеПараметры.ПолноеИмя).ПриОпределенииСвойствЭлементовФормыНастроек(
			ТипФормы, СвойстваЭлементов, ПользовательскиеНастройки.Элементы);
	КонецЕсли;
	
	#КонецОбласти
	
	Возврат СвойстваЭлементов;
КонецФункции

// Получение сведений об основных настройках, включенных в состав пользовательских.

Функция СведенияОПользовательскихНастройках(Настройки)
	Сведения = Новый Соответствие;
	ПолучитьСведенияОГруппировке(Настройки, Сведения);
	
	Возврат Сведения;
КонецФункции

Процедура ПолучитьСведенияОГруппировке(Группировка, Сведения)
	ТипГруппировки = ТипЗнч(Группировка);
	Если ТипГруппировки <> Тип("НастройкиКомпоновкиДанных")
		И ТипГруппировки <> Тип("ГруппировкаКомпоновкиДанных")
		И ТипГруппировки <> Тип("ГруппировкаТаблицыКомпоновкиДанных")
		И ТипГруппировки <> Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда 
		Возврат;
	КонецЕсли;
	
	Если ТипГруппировки <> Тип("НастройкиКомпоновкиДанных")
		И ЗначениеЗаполнено(Группировка.ИдентификаторПользовательскойНастройки) Тогда 
		
		ВидыСведений = ВидыСведений();
		ВидыСведений.Настройки = Группировка;
		ВидыСведений.ЭлементНастройки = Группировка;
		
		Сведения.Вставить(Группировка.ИдентификаторПользовательскойНастройки, ВидыСведений);
	КонецЕсли;
	
	ПолучитьСведенияОСвойствахЭлементаНастроек(Группировка, Сведения);
КонецПроцедуры

Процедура ПолучитьСведенияОТаблице(Таблица, Сведения)
	Если ТипЗнч(Таблица) <> Тип("ТаблицаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Таблица.ИдентификаторПользовательскойНастройки) Тогда 
		ВидыСведений = ВидыСведений();
		ВидыСведений.Настройки = Таблица;
		ВидыСведений.ЭлементНастройки = Таблица;
		
		Сведения.Вставить(Таблица.ИдентификаторПользовательскойНастройки, ВидыСведений);
	КонецЕсли;
	
	ПолучитьСведенияОСвойствахЭлементаНастроек(Таблица, Сведения);
	ПолучитьСведенияОКоллекции(Таблица, Таблица.Строки, Сведения);
	ПолучитьСведенияОКоллекции(Таблица, Таблица.Колонки, Сведения);
КонецПроцедуры

Процедура ПолучитьСведенияОДиаграмме(Диаграмма, Сведения)
	Если ТипЗнч(Диаграмма) <> Тип("ДиаграммаКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Диаграмма.ИдентификаторПользовательскойНастройки) Тогда 
		ВидыСведений = ВидыСведений();
		ВидыСведений.Настройки = Диаграмма;
		ВидыСведений.ЭлементНастройки = Диаграмма;
		
		Сведения.Вставить(Диаграмма.ИдентификаторПользовательскойНастройки, ВидыСведений);
	КонецЕсли;
	
	ПолучитьСведенияОСвойствахЭлементаНастроек(Диаграмма, Сведения);
	ПолучитьСведенияОКоллекции(Диаграмма, Диаграмма.Серии, Сведения);
	ПолучитьСведенияОКоллекции(Диаграмма, Диаграмма.Точки, Сведения);
КонецПроцедуры

Процедура ПолучитьСведенияОКоллекции(ЭлементНастроек, Коллекция, Сведения)
	ТипКоллекции = ТипЗнч(Коллекция);
	Если ТипКоллекции <> Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных")
		И ТипКоллекции <> Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных")
		И ТипКоллекции <> Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Коллекция.ИдентификаторПользовательскойНастройки) Тогда 
		ВидыСведений = ВидыСведений();
		ВидыСведений.Настройки = ЭлементНастроек;
		ВидыСведений.ЭлементНастройки = Коллекция;
		
		Сведения.Вставить(Коллекция.ИдентификаторПользовательскойНастройки, ВидыСведений);
	КонецЕсли;
	
	Для Каждого Элемент Из Коллекция Цикл 
		Настройки = Элемент;
		Если ТипЗнч(Элемент) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
			Если ЗначениеЗаполнено(Элемент.ИдентификаторПользовательскойНастройки) Тогда 
				ВидыСведений = ВидыСведений();
				ВидыСведений.Настройки = Элемент;
				ВидыСведений.ЭлементНастройки = Элемент;
				
				Сведения.Вставить(Элемент.ИдентификаторПользовательскойНастройки, ВидыСведений);
			КонецЕсли;
			
			Настройки = Элемент.Настройки;
		КонецЕсли;

		ПолучитьСведенияОГруппировке(Настройки, Сведения);
		ПолучитьСведенияОТаблице(Настройки, Сведения);
		ПолучитьСведенияОДиаграмме(Настройки, Сведения);
	КонецЦикла;
КонецПроцедуры

Процедура ПолучитьСведенияОСвойствахЭлементаНастроек(ЭлементНастроек, Сведения)
	ИдентификаторыСвойств = СтрРазделить("Выбор, ПараметрыВывода, УсловноеОформление", ", ", Ложь);
	ДоступныеСвойства = Новый Структура("Выбор, Отбор, Порядок, УсловноеОформление, Структура");
	
	ТипЭлементаНастроек = ТипЗнч(ЭлементНастроек);
	Если ТипЭлементаНастроек <> Тип("ТаблицаКомпоновкиДанных")
		И ТипЭлементаНастроек <> Тип("ДиаграммаКомпоновкиДанных") Тогда 
		
		ИдентификаторыСвойств.Добавить("Отбор");
		ИдентификаторыСвойств.Добавить("Порядок");
		ИдентификаторыСвойств.Добавить("Структура");
		
		Если ТипЭлементаНастроек = Тип("НастройкиКомпоновкиДанных") Тогда 
			ИдентификаторыСвойств.Добавить("ПараметрыДанных");
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Идентификатор Из ИдентификаторыСвойств Цикл 
		Свойство = ЭлементНастроек[Идентификатор];
		
		Если ДоступныеСвойства.Свойство(Идентификатор)
			И ЗначениеЗаполнено(Свойство.ИдентификаторПользовательскойНастройки) Тогда 
			
			ВидыСведений = ВидыСведений();
			ВидыСведений.Настройки = ЭлементНастроек;
			ВидыСведений.ЭлементНастройки = Свойство;
			
			Сведения.Вставить(Свойство.ИдентификаторПользовательскойНастройки, ВидыСведений);
		КонецЕсли;
		
		ПолучитьСведенияОЭлементахСвойстваНастроек(ЭлементНастроек, Свойство, Идентификатор, Сведения);
		ПолучитьСведенияОКоллекции(ЭлементНастроек, Свойство, Сведения);
	КонецЦикла;
КонецПроцедуры

Процедура ПолучитьСведенияОЭлементахСвойстваНастроек(Настройки, Свойство, ИдентификаторСвойства, Сведения)
	СвойстваСЭлементами = Новый Структура("Отбор, ПараметрыДанных, ПараметрыВывода, УсловноеОформление");
	Если Не СвойстваСЭлементами.Свойство(ИдентификаторСвойства) Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из Свойство.Элементы Цикл 
		ТипЭлемента = ТипЗнч(Элемент);
		
		Если ЗначениеЗаполнено(Элемент.ИдентификаторПользовательскойНастройки) Тогда 
			Описание = Неопределено;
			Если ТипЭлемента = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
				
				ДоступныеПоля = Настройки[ИдентификаторСвойства].ДоступныеПоляОтбора;
				Если ДоступныеПоля <> Неопределено Тогда 
					Описание = ДоступныеПоля.НайтиПоле(Элемент.ЛевоеЗначение);
				КонецЕсли;
				
			ИначеЕсли ТипЭлемента = Тип("ЗначениеПараметраКомпоновкиДанных")
				Или ТипЭлемента = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
				
				ДоступныеПараметры = Настройки[ИдентификаторСвойства].ДоступныеПараметры;
				Если ДоступныеПараметры <> Неопределено Тогда 
					Описание = ДоступныеПараметры.НайтиПараметр(Элемент.Параметр);
				КонецЕсли;
			КонецЕсли;
			
			ВидыСведений = ВидыСведений();
			ВидыСведений.Настройки = Настройки;
			ВидыСведений.ЭлементНастройки = Элемент;
			ВидыСведений.ОписаниеНастройки = Описание;
			
			Сведения.Вставить(Элемент.ИдентификаторПользовательскойНастройки, ВидыСведений);
		КонецЕсли;
		
		Если ТипЭлемента = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда 
			ПолучитьСведенияОЭлементахСвойстваНастроек(Настройки, Элемент, ИдентификаторСвойства, Сведения);
		ИначеЕсли ТипЭлемента = Тип("ЗначениеПараметраКомпоновкиДанных")
			Или ТипЭлемента = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
			ПолучитьСведенияОЗначенияхВложенныхПараметров(
				Настройки, Элемент.ЗначенияВложенныхПараметров, ИдентификаторСвойства, Сведения);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПолучитьСведенияОЗначенияхВложенныхПараметров(Настройки, ЗначенияПараметров, ИдентификаторСвойства, Сведения)
	Для Каждого ЗначениеПараметра Из ЗначенияПараметров Цикл 
		Если ЗначениеЗаполнено(ЗначениеПараметра.ИдентификаторПользовательскойНастройки) Тогда 
			ВидыСведений = ВидыСведений();
			ВидыСведений.Настройки = Настройки;
			ВидыСведений.ЭлементНастройки = ЗначениеПараметра;
			ВидыСведений.ОписаниеНастройки =
				Настройки[ИдентификаторСвойства].ДоступныеПараметры.НайтиПараметр(ЗначениеПараметра.Параметр);
			
			Сведения.Вставить(ЗначениеПараметра.ИдентификаторПользовательскойНастройки, ВидыСведений);
		КонецЕсли;
		
		ПолучитьСведенияОЗначенияхВложенныхПараметров(
			Настройки, ЗначениеПараметра.ЗначенияВложенныхПараметров, ИдентификаторСвойства, Сведения);
	КонецЦикла;
КонецПроцедуры

Функция ВидыСведений()
	Возврат Новый Структура("Настройки, ЭлементНастройки, ОписаниеНастройки");
КонецФункции

// Перегруппировка элементов формы, связанных с пользовательскими настройками.

Функция ИменаРеквизитовЭлементовНастроек(Форма, ВидыЭлементов)
	ИменаРеквизитовПредопределенных = Новый Структура;
	ИменаРеквизитовСгенерированных = Новый Структура;
	
	Для Каждого ВидЭлемента Из ВидыЭлементов Цикл 
		ИменаРеквизитовПредопределенных.Вставить(ВидЭлемента, Новый Массив);
		ИменаРеквизитовСгенерированных.Вставить(ВидЭлемента, Новый Массив);
	КонецЦикла;
	
	Реквизиты = Форма.ПолучитьРеквизиты();
	Для Каждого Реквизит Из Реквизиты Цикл 
		Для Каждого ВидЭлемента Из ВидыЭлементов Цикл 
			Если СтрНачинаетсяС(Реквизит.Имя, ВидЭлемента)
				И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(Реквизит.Имя, ВидЭлемента, "")) Тогда 
				ИменаРеквизитовПредопределенных[ВидЭлемента].Добавить(Реквизит.Имя);
			КонецЕсли;
			
			Если СтрНачинаетсяС(Реквизит.Имя, "КомпоновщикНастроекПользовательскиеНастройкиЭлемент")
				И СтрЗаканчиваетсяНа(Реквизит.Имя, ВидЭлемента) Тогда 
				ИменаРеквизитовСгенерированных[ВидЭлемента].Добавить(Реквизит.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ИменаРеквизитов = Новый Структура;
	ИменаРеквизитов.Вставить("Предопределенных", ИменаРеквизитовПредопределенных);
	ИменаРеквизитов.Вставить("Сгенерированных", ИменаРеквизитовСгенерированных);
	
	Возврат ИменаРеквизитов;
КонецФункции

Процедура ПодготовитьФормуКПерегруппировкеЭлементов(Форма, УзелИерархииЭлементов, ИменаРеквизитов, ВидыСтилизованныхЭлементов)
	Элементы = Форма.Элементы;
	
	// Перегруппировка предопределенных элементов формы.
	СвойстваПредопределенныхЭлементов = СтрРазделить("Отступ, Подбор, ВставитьИзБуфера", ", ", Ложь);
	СвойстваПредопределенныхЭлементов.Добавить("");
	
	Для Каждого ВидЭлемента Из ВидыСтилизованныхЭлементов Цикл 
		ИменаПредопределенныхРеквизитов = ИменаРеквизитов.Предопределенных[ВидЭлемента];
		Для Каждого ИмяРеквизита Из ИменаПредопределенныхРеквизитов Цикл 
			Для Каждого Свойство Из СвойстваПредопределенныхЭлементов Цикл 
				НайденныйЭлемент = Элементы.Найти(ИмяРеквизита + Свойство);
				Если НайденныйЭлемент <> Неопределено Тогда 
					Элементы.Переместить(НайденныйЭлемент, Элементы.ПредопределенныеЭлементыНастроек);
					НайденныйЭлемент.Видимость = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Удаление динамических элементов формы.
	УзлыИерархииЭлементов = Новый Массив;
	УзлыИерархииЭлементов.Добавить(УзелИерархииЭлементов);
	
	НайденныйУзел = Элементы.Найти("Дополнительно");
	Если НайденныйУзел <> Неопределено Тогда 
		УзлыИерархииЭлементов.Добавить(НайденныйУзел);
	КонецЕсли;
	
	Исключения = Новый Массив;
	
	НайденныйУзел = Элементы.Найти("ПредопределенныеНастройки");
	Если НайденныйУзел <> Неопределено Тогда 
		Исключения.Добавить(НайденныйУзел);
	КонецЕсли;
	
	Для Каждого ТекущийУзел Из УзлыИерархииЭлементов Цикл 
		ИерархияЭлементов = ТекущийУзел.ПодчиненныеЭлементы;
		ИндексЭлемента = ИерархияЭлементов.Количество() - 1;
		Пока ИндексЭлемента >= 0 Цикл 
			ЭлементИерархии = ИерархияЭлементов[ИндексЭлемента];
			Если Исключения.Найти(ЭлементИерархии) = Неопределено Тогда 
				Элементы.Удалить(ЭлементИерархии);
			КонецЕсли;
			ИндексЭлемента = ИндексЭлемента - 1;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ПерегруппироватьЭлементыФормыНастроек(Форма, Знач УзелИерархииЭлементов, СвойстваЭлементов, ИменаРеквизитов, ВидыСтилизованныхЭлементов)
	ОписаниеНастроек = СвойстваЭлементов.Поля.Скопировать(,
		"ИндексНастройки, ИдентификаторНастройки, Настройки, ЭлементНастройки, ОписаниеНастройки");
	
	ЭлементыНастроек = ЭлементыФормыНастроек(Форма, ОписаниеНастроек, ИменаРеквизитов);
	УстановитьСвойстваЭлементовФормыНастроек(Форма, ЭлементыНастроек, СвойстваЭлементов);
	
	Если Форма.ТипФормыОтчета <> ТипФормыОтчета.Настройка Тогда 
		ЭлементыНастроек.ЗаполнитьЗначения(Ложь, "ЭтоСписок");
	КонецЕсли;
	
	ВынестиСписокВОтдельнуюГруппу(ЭлементыНастроек, СвойстваЭлементов);
	
	ИдентификаторыГрупп = СвойстваЭлементов.Поля.Скопировать();
	ИдентификаторыГрупп.Свернуть("ИдентификаторГруппы");
	ИдентификаторыГрупп = ИдентификаторыГрупп.ВыгрузитьКолонку("ИдентификаторГруппы");
	
	Элементы = Форма.Элементы;
	
	Если ИдентификаторыГрупп.Количество() = 1 Тогда 
		УзелИерархииЭлементов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Иначе
		УзелИерархииЭлементов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	КонецЕсли;
	
	НомерГруппы = 0;
	Для Каждого ИдентификаторГруппы Из ИдентификаторыГрупп Цикл 
		НомерГруппы = НомерГруппы + 1;
		
		СвойстваГруппы = Неопределено;
		Если Не ЗначениеЗаполнено(ИдентификаторГруппы)
			Или Не СвойстваЭлементов.Группы.Свойство(ИдентификаторГруппы, СвойстваГруппы) Тогда 
			СвойстваГруппы = СвойстваГруппыЭлементовФормы();
		КонецЕсли;
		
		НайденныйУзелИерархии = Элементы.Найти(ИдентификаторГруппы);
		Если НайденныйУзелИерархии <> Неопределено Тогда 
			УзелИерархииЭлементов = НайденныйУзелИерархии;
			НомерГруппы = 1;
		КонецЕсли;
		
		ИмяГруппы = УзелИерархииЭлементов.Имя + "Строка" + НомерГруппы;
		Группа = ?(ИдентификаторыГрупп.Количество() = 1, УзелИерархииЭлементов, Элементы.Найти(ИмяГруппы));
		
		Если Группа = Неопределено Тогда 
			Группа = ГруппаЭлементовФормыНастроек(Элементы, УзелИерархииЭлементов, ИмяГруппы);
			Группа.Заголовок = "Строка " + НомерГруппы;
			ЗаполнитьЗначенияСвойств(Группа, СвойстваГруппы,, "Группировка");
			Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		КонецЕсли;
		
		ПоискПолейГруппы = Новый Структура("ИдентификаторГруппы", ИдентификаторГруппы);
		СвойстваПолейГруппы = СвойстваЭлементов.Поля.НайтиСтроки(ПоискПолейГруппы);
		ЭлементыНастроекГруппы = ЭлементыНастроекГруппы(ЭлементыНастроек, СвойстваПолейГруппы);
		
		ПодготовитьЭлементыФормыНастроекКРаспределению(ЭлементыНастроекГруппы, СвойстваГруппы.Группировка);
		РаспределитьЭлементыФормыНастроек(Форма.Элементы, Группа, ЭлементыНастроекГруппы);
	КонецЦикла;
	
	ВывестиСтилизованныеЭлементыФормыНастроек(Форма, ЭлементыНастроек, ОписаниеНастроек, ИменаРеквизитов, ВидыСтилизованныхЭлементов);
КонецПроцедуры

// Поиск созданных системой элементов формы настроек и их подготовка к распределению

Функция ЭлементыФормыНастроек(Форма, ОписаниеНастроек, ИменаРеквизитов)
	Элементы = Форма.Элементы;
	
	ЭлементыНастроек = ПалитраКоллекцииЭлементовНастроек();
	НайтиЭлементыФормыНастроек(Форма, Элементы.Временная, ЭлементыНастроек, ОписаниеНастроек);
	
	СводныеСведения = ЭлементыНастроек.Скопировать();
	СводныеСведения.Свернуть("ИндексНастройки", "КонтрольнаяСумма");
	НеполныеЭлементы = СводныеСведения.НайтиСтроки(Новый Структура("КонтрольнаяСумма", 1));
	
	Поиск = Новый Структура("ИндексНастройки, СвойствоНастройки");
	ОбщиеСвойства = "ЭтоПериод, ЭтоФлажок, ЭтоСписок, ТипЗначения, ФормаВыбора, ДоступныеЗначения";
	
	Для Каждого Запись Из НеполныеЭлементы Цикл 
		Элемент = ЭлементыНастроек.Найти(Запись.ИндексНастройки, "ИндексНастройки");
		Элемент.Поле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		
		ИсходноеСвойство = "Значение";
		СвязанноеСвойство = "Использование";
		Если СтрЗаканчиваетсяНа(Элемент.Поле.Имя, СвязанноеСвойство) Тогда 
			ИсходноеСвойство = "Использование";
			СвязанноеСвойство = "Значение";
		КонецЕсли;
		
		ИмяДополнительногоЭлемента = СтрЗаменить(Элемент.Поле.Имя, Элемент.СвойствоНастройки, СвязанноеСвойство);
		
		ГруппаЭлемента = Элемент.Поле.Родитель;
		Если Элементы.Найти(ИмяДополнительногоЭлемента) <> Неопределено
			Или ГруппаЭлемента.ПодчиненныеЭлементы.Найти(ИмяДополнительногоЭлемента) <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		ДополнительныйЭлемент = Элементы.Добавить(ИмяДополнительногоЭлемента, Тип("ДекорацияФормы"), ГруппаЭлемента);
		ДополнительныйЭлемент.Заголовок = Элемент.Поле.Заголовок;
		ДополнительныйЭлемент.АвтоМаксимальнаяВысота = Ложь;
		
		ДополнительнаяЗапись = ЭлементыНастроек.Добавить();
		ДополнительнаяЗапись.Поле = ДополнительныйЭлемент;
		ДополнительнаяЗапись.ИндексНастройки = Запись.ИндексНастройки;
		ДополнительнаяЗапись.СвойствоНастройки = СвязанноеСвойство;
		ДополнительнаяЗапись.КонтрольнаяСумма = 1;
		
		Поиск.ИндексНастройки = Запись.ИндексНастройки;
		Поиск.СвойствоНастройки = ИсходноеСвойство;
		СвязанныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
		ЗаполнитьЗначенияСвойств(ДополнительнаяЗапись, СвязанныеЭлементы[0], ОбщиеСвойства);
	КонецЦикла;
	
	НайтиЗначенияКакФлажки(Форма, ЭлементыНастроек, ИменаРеквизитов);
	
	ЭлементыНастроек.Сортировать("ИндексНастройки");
	
	Возврат ЭлементыНастроек;
КонецФункции

Функция ПалитраКоллекцииЭлементовНастроек()
	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	ОписаниеСтроки = Новый ОписаниеТипов("Строка");
	ОписаниеПризнака = Новый ОписаниеТипов("Булево");
	
	ЭлементыНастроек = Новый ТаблицаЗначений;
	ЭлементыНастроек.Колонки.Добавить("Приоритет", ОписаниеЧисла);
	ЭлементыНастроек.Колонки.Добавить("ИндексНастройки", ОписаниеЧисла);
	ЭлементыНастроек.Колонки.Добавить("СвойствоНастройки", ОписаниеСтроки);
	ЭлементыНастроек.Колонки.Добавить("Поле");
	ЭлементыНастроек.Колонки.Добавить("ЭтоПериод", ОписаниеПризнака);
	ЭлементыНастроек.Колонки.Добавить("ЭтоСписок", ОписаниеПризнака);
	ЭлементыНастроек.Колонки.Добавить("ЭтоФлажок", ОписаниеПризнака);
	ЭлементыНастроек.Колонки.Добавить("ЭтоЗначениеКакФлажок", ОписаниеПризнака);
	ЭлементыНастроек.Колонки.Добавить("ТипЗначения");
	ЭлементыНастроек.Колонки.Добавить("ФормаВыбора", ОписаниеСтроки);
	ЭлементыНастроек.Колонки.Добавить("ДоступныеЗначения");
	ЭлементыНастроек.Колонки.Добавить("КонтрольнаяСумма", ОписаниеЧисла);
	ЭлементыНастроек.Колонки.Добавить("НомерКолонки", ОписаниеЧисла);
	ЭлементыНастроек.Колонки.Добавить("НомерГруппы", ОписаниеЧисла);
	
	Возврат ЭлементыНастроек;
КонецФункции

Процедура НайтиЭлементыФормыНастроек(Форма, ГруппаЭлементов, ЭлементыНастроек, ОписаниеНастроек)
	ПользовательскиеНастройки = Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	
	ОсновныеСвойства = Новый Структура("Использование, Значение");
	Для Каждого Элемент Из ГруппаЭлементов.ПодчиненныеЭлементы Цикл 
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда 
			НайтиЭлементыФормыНастроек(Форма, Элемент, ЭлементыНастроек, ОписаниеНастроек);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда 
			СвойствоНастройки = Неопределено;
			ИндексНастройки = ОтчетыКлиентСервер.ИндексЭлементаНастройкиПоПути(Элемент.Имя, СвойствоНастройки);
			
			ОписаниеНастройкиЭлемента = ОписаниеНастроек.Найти(ИндексНастройки, "ИндексНастройки");
			Если ОписаниеНастройкиЭлемента = Неопределено Тогда 
				Продолжить;
			КонецЕсли;
			
			Запись = ЭлементыНастроек.Добавить();
			Запись.ИндексНастройки = ИндексНастройки;
			Запись.СвойствоНастройки = СвойствоНастройки;
			Запись.Поле = Элемент;
			
			ЭлементНастройки = ОписаниеНастройкиЭлемента.ЭлементНастройки;
			ОписаниеНастройки = ОписаниеНастройкиЭлемента.ОписаниеНастройки;
			
			Если ОписаниеНастройки <> Неопределено Тогда 
				ЗаполнитьЗначенияСвойств(Запись, ОписаниеНастройки, "ТипЗначения, ФормаВыбора, ДоступныеЗначения");
			КонецЕсли;
			
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
				Запись.ЭтоПериод = ТипЗнч(ЭлементНастройки.Значение) = Тип("СтандартныйПериод");
				Запись.ЭтоСписок = ОписаниеНастройки <> Неопределено И ОписаниеНастройки.ДоступенСписокЗначений;
			ИначеЕсли ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
				Запись.ЭтоПериод = ТипЗнч(ЭлементНастройки.ПравоеЗначение) = Тип("СтандартныйПериод");
				Запись.ЭтоФлажок = ЗначениеЗаполнено(ЭлементНастройки.Представление);
				
				ЭлементПользовательскойНастройки = ПользовательскиеНастройки.Найти(
					ОписаниеНастройкиЭлемента.ИдентификаторНастройки);
				
				Запись.ЭтоСписок = Не Запись.ЭтоФлажок
					И ОтчетыКлиентСервер.ЭтоВидСравненияСписка(ЭлементПользовательскойНастройки.ВидСравнения);
			ИначеЕсли ТипЗнч(ЭлементНастройки) = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда 
				Запись.ЭтоФлажок = ЗначениеЗаполнено(ЭлементНастройки.Представление)
					Или ЗначениеЗаполнено(ЭлементНастройки.ПредставлениеПользовательскойНастройки);
			КонецЕсли;
			
			Если ОсновныеСвойства.Свойство(Запись.СвойствоНастройки) Тогда 
				Запись.КонтрольнаяСумма = 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура НайтиЗначенияКакФлажки(Форма, ЭлементыНастроек, ИменаРеквизитов)
	Поиск = Новый Структура;
	Поиск.Вставить("ТипЗначения", Новый ОписаниеТипов("Булево"));
	НайденныеЭлементы = ЭлементыНастроек.Скопировать(Поиск);
	НайденныеЭлементы.Свернуть("ИндексНастройки, ТипЗначения", "КонтрольнаяСумма");
	
	НайденныеЭлементы = НайденныеЭлементы.НайтиСтроки(Новый Структура("КонтрольнаяСумма", 2));
	Если НайденныеЭлементы.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Поиск = Новый Структура("СвойствоНастройки, ИндексНастройки");
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Поиск.СвойствоНастройки = "Использование";
		Поиск.ИндексНастройки = Элемент.ИндексНастройки;
		
		ЭлементФлажка = ЭлементыНастроек.НайтиСтроки(Поиск);
		Если ЭлементФлажка.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ЭлементФлажка = ЭлементФлажка[0];
		Если ТипЗнч(ЭлементФлажка.Поле) <> Тип("ДекорацияФормы") Тогда 
			Продолжить;
		КонецЕсли;
		
		Поиск.СвойствоНастройки = "Значение";
		
		ЭлементЗначения = ЭлементыНастроек.НайтиСтроки(Поиск);
		Если ЭлементЗначения.Количество() = 0 Тогда 
			Продолжить;
		КонецЕсли;
		
		ЭлементФлажка.СвойствоНастройки = "Значение";
		ЭлементФлажка.ЭтоФлажок = Истина;
		ЭлементФлажка.ЭтоЗначениеКакФлажок = Истина;
		
		ЭлементЗначения = ЭлементЗначения[0];
		ЭлементЗначения.СвойствоНастройки = "Использование";
		ЭлементЗначения.ЭтоФлажок = Истина;
		ЭлементЗначения.ЭтоЗначениеКакФлажок = Истина;
		ЭлементЗначения.Поле.Видимость = Ложь;
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьСвойстваЭлементовФормыНастроек(Форма, ЭлементыНастроек, СвойстваЭлементов)
	КомпоновщикНастроек = Форма.Отчет.КомпоновщикНастроек;
	
	#Область УстановкаСвойствЭлементовИспользование
	
	Исключения = Новый Массив;
	Исключения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
	Исключения.Добавить(ВидСравненияКомпоновкиДанных.Содержит);
	Исключения.Добавить(ВидСравненияКомпоновкиДанных.Заполнено);
	Исключения.Добавить(ВидСравненияКомпоновкиДанных.Подобно);
	Исключения.Добавить(ВидСравненияКомпоновкиДанных.ВСписке);
	Исключения.Добавить(ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
	
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Новый Структура("СвойствоНастройки", "Использование"));
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Поле = Элемент.Поле;
		СвойстваПоля = СвойстваЭлементов.Поля.Найти(Элемент.ИндексНастройки, "ИндексНастройки");
		
		Если ЗначениеЗаполнено(СвойстваПоля.Представление) Тогда 
			Поле.Заголовок = СвойстваПоля.Представление;
		КонецЕсли;
		
		Если ТипЗнч(Поле) = Тип("ПолеФормы") Тогда 
			Поле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			Поле.УстановитьДействие("ПриИзменении", "Подключаемый_ЭлементНастройки_ПриИзменении");
		ИначеЕсли ТипЗнч(Поле) = Тип("ДекорацияФормы") Тогда 
			Поле.Видимость = (СвойстваПоля.ПоложениеЗаголовка <> ПоложениеЗаголовкаЭлементаФормы.Нет);
		КонецЕсли;
		
		Если СтрДлина(Поле.Заголовок) > 40 Тогда
			Поле.ВысотаЗаголовка = 2;
		КонецЕсли;
		
		ЭлементНастройки = СвойстваПоля.ЭлементНастройки;
		Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
			Поле.Заголовок = Элемент.Поле.Заголовок + ":";
		ИначеЕсли ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда 
			Если Исключения.Найти(ЭлементНастройки.ВидСравнения) <> Неопределено Тогда 
				Поле.Заголовок = Элемент.Поле.Заголовок + ":";
			ИначеЕсли Не ЗначениеЗаполнено(ЭлементНастройки.Представление) Тогда 
				Поле.Заголовок = Элемент.Поле.Заголовок + " (" + НРег(ЭлементНастройки.ВидСравнения) + "):";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	#КонецОбласти
	
	#Область УстановкаСвойствЭлементовЭлементыНастроек
	
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Новый Структура("СвойствоНастройки", "ВидСравнения"));
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Элемент.Поле.Видимость = Ложь;
	КонецЦикла;
	
	#КонецОбласти
	
	#Область УстановкаСвойствЭлементовЗначение
	
	ПараметрыПодбора = Новый Соответствие;
	РасширенноеОписаниеТипов = Новый Соответствие;
	
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Новый Структура("СвойствоНастройки", "Значение"));
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Поле = Элемент.Поле;
		
		Если ТипЗнч(Поле) = Тип("ДекорацияФормы") Тогда 
			Поле.Заголовок = "     ";
			
			Поиск = Новый Структура("ИндексНастройки, СвойствоНастройки", Элемент.ИндексНастройки, "Использование");
			НайденныеСвязанныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
			
			СвязанноеПоле = НайденныеСвязанныеЭлементы[0].Поле;
			Если СтрЗаканчиваетсяНа(СвязанноеПоле.Заголовок, ":") Тогда 
				СвязанноеПоле.Заголовок = Лев(СвязанноеПоле.Заголовок, СтрДлина(СвязанноеПоле.Заголовок) - 1);
			КонецЕсли;
		Иначе // Поле ввода.
			СвойстваПоля = СвойстваЭлементов.Поля.Найти(Элемент.ИндексНастройки, "ИндексНастройки");
			ЗаполнитьЗначенияСвойств(Поле, СвойстваПоля,, "ПоложениеЗаголовка");
			
			Если Элемент.ЭтоФлажок Тогда 
				Элемент.Поле.Видимость = Ложь;
				Продолжить;
			КонецЕсли;
			
			Поле.УстановитьДействие("ПриИзменении", "Подключаемый_ЭлементНастройки_ПриИзменении");
			Если Элемент.ЭтоСписок Тогда
				Поле.УстановитьДействие("НачалоВыбора", "Подключаемый_ЭлементНастройки_НачалоВыбора");
			КонецЕсли;
			
			Поле.ФормаВыбора = Элемент.ФормаВыбора;
			Если ЗначениеЗаполнено(Поле.ФормаВыбора) Тогда 
				ПараметрыПодбора.Вставить(Элемент.ИндексНастройки, Поле.ФормаВыбора);
			КонецЕсли;
			
			Результат = ОтчетыКлиентСервер.ДополнитьСписок(
				Поле.СписокВыбора, Элемент.ДоступныеЗначения, Ложь, Истина);
			Поле.РежимВыбораИзСписка = Не Элемент.ЭтоСписок И Результат <> Неопределено И Результат.Всего > 0;
			
			Если Элемент.ТипЗначения = Неопределено Тогда 
				Продолжить;
			КонецЕсли;
			
			РасширенноеОписаниеТипа = РасширенноеОписаниеТипов(Элемент.ТипЗначения, Истина, ПараметрыПодбора);
			РасширенноеОписаниеТипов.Вставить(Элемент.ИндексНастройки, РасширенноеОписаниеТипа);
			
			Поле.ДоступныеТипы = РасширенноеОписаниеТипа.ОписаниеТиповДляФормы;
			Поле.ОграничениеТипа = РасширенноеОписаниеТипа.ОписаниеТиповДляФормы;
			
			Если СтрДлина(Поле.Заголовок) > 40 Тогда
				Поле.ВысотаЗаголовка = 2;
			КонецЕсли;
			
			Если Поле.РастягиватьПоГоризонтали = Неопределено Тогда
				Поле.РастягиватьПоГоризонтали = Истина;
				Поле.АвтоМаксимальнаяШирина = Ложь;
				Поле.МаксимальнаяШирина = 40;
			КонецЕсли;
			
			Если РасширенноеОписаниеТипа.КоличествоТипов = 1 Тогда 
				Если РасширенноеОписаниеТипа.СодержитТипЧисло Тогда 
					Поле.КнопкаВыбора = Истина;
					Если Поле.РастягиватьПоГоризонтали = Истина Тогда
						Поле.РастягиватьПоГоризонтали = Ложь;
					КонецЕсли;
				ИначеЕсли РасширенноеОписаниеТипа.СодержитТипДата Тогда 
					Поле.МаксимальнаяШирина = 25;
				ИначеЕсли РасширенноеОписаниеТипа.СодержитТипБулево Тогда 
					Поле.МаксимальнаяШирина = 5;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДополнительныеСвойства = КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("ПараметрыПодбора", ПараметрыПодбора);
	ДополнительныеСвойства.Вставить("РасширенноеОписаниеТипов", РасширенноеОписаниеТипов);
	
	#КонецОбласти
КонецПроцедуры

Процедура ВынестиСписокВОтдельнуюГруппу(ЭлементыНастроек, СвойстваЭлементов)
	Поиск = Новый Структура("ЭтоСписок", Истина);
	Статистика = ЭлементыНастроек.Скопировать(Поиск);
	Статистика.Свернуть("ИндексНастройки");
	
	Если Статистика.Количество() <> 1 Тогда 
		Возврат;
	КонецЕсли;
	
	ИндексНастройки = ЭлементыНастроек.НайтиСтроки(Поиск)[0].ИндексНастройки;
	СвойстваПоля = СвойстваЭлементов.Поля.Найти(ИндексНастройки, "ИндексНастройки");
	Если ЗначениеЗаполнено(СвойстваПоля.ИдентификаторГруппы) Тогда 
		Возврат;
	КонецЕсли;
	
	ИдентификаторГруппы = "_" + СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
	СвойстваПоля.ИдентификаторГруппы = ИдентификаторГруппы;
	СвойстваЭлементов.Группы.Вставить(ИдентификаторГруппы, СвойстваГруппыЭлементовФормы());
КонецПроцедуры

Функция ЭлементыНастроекГруппы(ЭлементыНастроек, СвойстваПолейГруппы)
	ЭлементыНастроекГруппы = ЭлементыНастроек.СкопироватьКолонки();
	
	Поиск = Новый Структура("ИндексНастройки");
	Для Каждого Свойства Из СвойстваПолейГруппы Цикл 
		Поиск.ИндексНастройки = Свойства.ИндексНастройки;
		НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
		Для Каждого Элемент Из НайденныеЭлементы Цикл 
			ЗаполнитьЗначенияСвойств(ЭлементыНастроекГруппы.Добавить(), Элемент);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ЭлементыНастроекГруппы;
КонецФункции

// Распределение элементов формы настроек в иерархии

Процедура ПодготовитьЭлементыФормыНастроекКРаспределению(ЭлементыНастроек, Группировка)
	#Область ПередПодготовкой
	
	Статистика = ЭлементыНастроек.Скопировать();
	Статистика.Свернуть("ИндексНастройки");
	
	КоличествоКолонок = 1;
	Если Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно Тогда 
		КоличествоКолонок = Мин(2, Статистика.Количество());
	ИначеЕсли Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда Тогда 
		КоличествоКолонок = Статистика.Количество();
	КонецЕсли;
	КоличествоКолонок = Макс(1, КоличествоКолонок);
	
	#КонецОбласти
	
	#Область УстановкаПриоритета
	
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Новый Структура("ЭтоПериод", Истина));
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Элемент.Приоритет = -1;
	КонецЦикла;
	
	ЭлементыНастроек.Сортировать("Приоритет, ИндексНастройки");
	
	#КонецОбласти
	
	#Область УстановкаНомеровКолонок
	
	Статистика = ЭлементыНастроек.Скопировать();
	Статистика.Свернуть("Приоритет, ИндексНастройки");
	
	КоличествоЭлементов = Статистика.Количество();
	Индекс = 0;
	ГраницаСвойств = КоличествоЭлементов - 1;
	
	Шаг = КоличествоЭлементов / КоличествоКолонок;
	ГраницаРазрыва = ?(КоличествоЭлементов % КоличествоКолонок = 0, Шаг - 1, Цел(Шаг));
	Шаг = ?(ГраницаРазрыва = 0, 1, Цел(Шаг));
	
	Поиск = Новый Структура("ИндексНастройки");
	Для НомерКолонки = 1 По КоличествоКолонок Цикл 
		Пока Индекс <= ГраницаРазрыва Цикл 
			Поиск.ИндексНастройки = Статистика[Индекс].ИндексНастройки;
			НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
			Для Каждого Элемент Из НайденныеЭлементы Цикл 
				Элемент.НомерКолонки = НомерКолонки;
			КонецЦикла;
			Индекс = Индекс + 1;
		КонецЦикла;
		
		ГраницаРазрыва = ГраницаРазрыва + Шаг;
		Если ГраницаРазрыва > ГраницаСвойств Тогда 
			ГраницаРазрыва = ГраницаСвойств;
		КонецЕсли;
	КонецЦикла;
	
	РаспределитьСпискиПоКолонкамПропорционально(ЭлементыНастроек, КоличествоКолонок);
	
	#КонецОбласти
	
	#Область УстановкаНомеровГрупп
	
	ВариантыПоиска = Новый Массив;
	ВариантыПоиска.Добавить(Новый Структура("НомерГруппы, ЭтоФлажок, ЭтоСписок", 0, Ложь, Ложь));
	ВариантыПоиска.Добавить(Новый Структура("НомерГруппы, ЭтоФлажок, ЭтоСписок", 0, Истина, Ложь));
	ВариантыПоиска.Добавить(Новый Структура("НомерГруппы, ЭтоФлажок, ЭтоСписок", 0, Ложь, Истина));
	
	НомерГруппы = 1;
	Для Каждого Поиск Из ВариантыПоиска Цикл 
		НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
		
		ПредыдущийИндекс = Неопределено;
		Для Каждого Элемент Из НайденныеЭлементы Цикл 
			Если Элемент.ЭтоФлажок Или Элемент.ЭтоСписок Тогда 
				Индекс = Элемент.ИндексНастройки;
			Иначе
				Индекс = ЭлементыНастроек.Индекс(Элемент);
			КонецЕсли;
			
			Если ПредыдущийИндекс = Неопределено Тогда 
				ПредыдущийИндекс = Индекс;
			КонецЕсли;
			
			Если ((Элемент.ЭтоФлажок Или Элемент.ЭтоСписок) И Индекс <> ПредыдущийИндекс)
				Или (Не Элемент.ЭтоФлажок И Не Элемент.ЭтоСписок И Индекс > ПредыдущийИндекс + 1) Тогда 
				НомерГруппы = НомерГруппы + 1;
			КонецЕсли;
			
			Элемент.НомерГруппы = НомерГруппы;
			ПредыдущийИндекс = Индекс;
		КонецЦикла;
		
		НомерГруппы = НомерГруппы + 1;
	КонецЦикла;
	
	#КонецОбласти
КонецПроцедуры

Процедура РаспределитьСпискиПоКолонкамПропорционально(ЭлементыНастроек, КоличествоКолонок)
	Если КоличествоКолонок <> 2
		Или ЭлементыНастроек.Найти(Истина, "ЭтоСписок") = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	
	Статистика = ЭлементыНастроек.Скопировать();
	Статистика.Свернуть("ИндексНастройки, ЭтоСписок, НомерКолонки");
	Статистика.Колонки.Добавить("Списков", ОписаниеЧисла);
	
	Для Каждого Элемент Из Статистика Цикл 
		Элемент.Списков = Число(Элемент.ЭтоСписок);
	КонецЦикла;
	
	Статистика.Свернуть("НомерКолонки", "Списков");
	
	Списков = Статистика.Итог("Списков");
	Если Списков = 1 Тогда 
		Возврат;
	КонецЕсли;
	
	Статистика.Сортировать("Списков");
	
	Среднее = Окр(Списков / Статистика.Количество(), 0, РежимОкругления.Окр15как10);
	Приемник = Статистика[0];
	Источник = Статистика[Статистика.Количество() - 1];
	
	Отклонение = Среднее - Приемник.Списков;
	Если Отклонение = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Поиск = Новый Структура("ЭтоСписок, НомерКолонки", Истина, Источник.НомерКолонки);
	ЭлементыИсточника = ЭлементыНастроек.Скопировать(Поиск);
	ЭлементыИсточника.Свернуть("ИндексНастройки");
	Если Приемник.НомерКолонки > Источник.НомерКолонки Тогда 
		ЭлементыИсточника.Сортировать("ИндексНастройки Убыв");
	КонецЕсли;
	
	Отклонение = Мин(Отклонение, ЭлементыИсточника.Количество());
	Поиск = Новый Структура("ИндексНастройки");
	
	Индекс = 0;
	Пока Отклонение > 0 Цикл 
		Поиск.ИндексНастройки = ЭлементыИсточника[Индекс].ИндексНастройки;
		СвязанныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
		Для Каждого Элемент Из СвязанныеЭлементы Цикл 
			Элемент.НомерКолонки = Приемник.НомерКолонки;
		КонецЦикла;
		
		Индекс = Индекс + 1;
		Отклонение = Отклонение - 1;
	КонецЦикла;
КонецПроцедуры

Процедура РаспределитьЭлементыФормыНастроек(Элементы, Знач Группа, ЭлементыНастроек)
	КоличествоКолонок = 0;
	Если ЭлементыНастроек.Количество() > 0 Тогда 
		КоличествоКолонок = ЭлементыНастроек[ЭлементыНастроек.Количество() - 1].НомерКолонки;
	КонецЕсли;
	
	Для НомерКолонки = 1 По КоличествоКолонок Цикл 
		ПризнакиЭлементов = ЭлементыНастроек.Скопировать(Новый Структура("НомерКолонки", НомерКолонки));
		ПризнакиЭлементов.Свернуть("ЭтоФлажок, ЭтоСписок, НомерГруппы");
		
		ТолькоПоляВвода = ПризнакиЭлементов.Найти(Истина, "ЭтоФлажок") = Неопределено
			И ПризнакиЭлементов.Найти(Истина, "ЭтоСписок") = Неопределено;
		
		ИмяКолонки = Группа.Имя + "Колонка" + НомерКолонки;
		Колонка = ?(КоличествоКолонок = 1, Группа, Элементы.Найти(ИмяКолонки));
		Если Колонка = Неопределено Тогда 
			Колонка = ГруппаЭлементовФормыНастроек(Элементы, Группа, ИмяКолонки);
			Колонка.Заголовок = "Колонка " + НомерКолонки;
			
			Если ТолькоПоляВвода Тогда 
				Колонка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
			КонецЕсли;
		КонецЕсли;
		
		Если ТолькоПоляВвода Тогда 
			РаспределитьЭлементыФормыНастроекПоСвойствам(Элементы, Колонка, ЭлементыНастроек, НомерКолонки);
			Продолжить;
		КонецЕсли;
		
		НомерСтроки = 0;
		Для Каждого Признаки Из ПризнакиЭлементов Цикл 
			НомерСтроки = НомерСтроки + 1;
			Родитель = ИерархияЭлементовФормыНастроек(Элементы, Колонка, Признаки, НомерСтроки, НомерКолонки);
			
			РаспределитьЭлементыФормыНастроекПоСвойствам(Элементы, Родитель, ЭлементыНастроек, НомерКолонки, Признаки.НомерГруппы);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура РаспределитьЭлементыФормыНастроекПоСвойствам(Элементы, Родитель, ЭлементыНастроек, НомерКолонки, НомерГруппы = Неопределено)
	СвойстваНастроек = СтрРазделить("Использование, ВидСравнения, Значение", ", ", Ложь);
	Для Каждого СвойствоНастройки Из СвойстваНастроек Цикл 
		ИмяГруппы = Родитель.Имя + СвойствоНастройки;
		Группа = ГруппаЭлементовФормыНастроек(Элементы, Родитель, ИмяГруппы);
		Группа.Заголовок = СвойствоНастройки;
		Группа.Видимость = (СвойствоНастройки <> "ВидСравнения");
		
		Поиск = Новый Структура("СвойствоНастройки, НомерКолонки", СвойствоНастройки, НомерКолонки);
		Если НомерГруппы <> Неопределено Тогда 
			Поиск.Вставить("НомерГруппы", НомерГруппы);
		КонецЕсли;
		
		НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
		Для Каждого Элемент Из НайденныеЭлементы Цикл 
			Группа.Объединенная = СвойствоНастройки = "Использование" И Элемент.ЭтоСписок;
			Элементы.Переместить(Элемент.Поле, Группа);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Функция ИерархияЭлементовФормыНастроек(Элементы, Родитель, Признаки, НомерСтроки, НомерКолонки)
	ИмяСтроки = Родитель.Имя + "Строка" + НомерСтроки;
	Строка = ГруппаЭлементовФормыНастроек(Элементы, Родитель, ИмяСтроки);
	Строка.Заголовок = "Строка " + НомерКолонки + "." + НомерСтроки;
	
	Если Не Признаки.ЭтоСписок Тогда 
		Строка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	КонецЕсли;
	
	Если Признаки.ЭтоФлажок Или Признаки.ЭтоСписок Тогда 
		Возврат Строка;
	КонецЕсли;
	
	ИмяКолонки = Строка.Имя + "Колонка1";
	Колонка = ГруппаЭлементовФормыНастроек(Элементы, Строка, ИмяКолонки);
	Колонка.Заголовок = "Колонка " + НомерКолонки + "." + НомерСтроки + ".1";
	Колонка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Возврат Колонка;
КонецФункции

Функция ГруппаЭлементовФормыНастроек(Элементы, Родитель, ИмяГруппы)
	Группа = Элементы.Найти(ИмяГруппы);
	Если Группа <> Неопределено Тогда 
		Возврат Группа;
	КонецЕсли;
	
	Группа = Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), Родитель);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Возврат Группа;
КонецФункции

// Вывод стилизованных элементов формы настроек.

Процедура ВывестиСтилизованныеЭлементыФормыНастроек(Форма, ЭлементыНастроек, ОписаниеНастроек, ИменаРеквизитов, ВидыЭлементов)
	// Изменение реквизитов.
	ПутьКДаннымЭлементов = Новый Структура("ПоИмени, ПоИндексу", Новый Соответствие, Новый Соответствие);
	
	РеквизитыДобавляемые = РеквизитыДобавляемыеЭлементовНастроек(ЭлементыНастроек, ВидыЭлементов, ИменаРеквизитов, ПутьКДаннымЭлементов);
	РеквизитыУдаляемые = РеквизитыУдаляемыеЭлементовНастроек(ВидыЭлементов, ИменаРеквизитов, ПутьКДаннымЭлементов);
	
	Форма.ИзменитьРеквизиты(РеквизитыДобавляемые, РеквизитыУдаляемые);
	УдалитьКомандыЭлементовНастроек(Форма, РеквизитыУдаляемые);
	
	Форма.ПутьКДаннымЭлементов = ПутьКДаннымЭлементов;
	
	// Изменение элементов.
	ВывестиПериодыНастроек(Форма, ЭлементыНастроек, ИменаРеквизитов);
	ВывестиСпискиНастроек(Форма, ЭлементыНастроек, ОписаниеНастроек, ИменаРеквизитов);
	ВывестиЗначенияКакПоляФлажков(Форма, ЭлементыНастроек, ИменаРеквизитов);
КонецПроцедуры

Функция РеквизитыДобавляемыеЭлементовНастроек(ЭлементыНастроек, ВидыЭлементов, ИменаРеквизитов, ПутьКДаннымЭлементов)
	РеквизитыДобавляемые = Новый Массив;
	
	ТипыЭлементов = Новый Структура;
	ТипыЭлементов.Вставить("Период", Новый ОписаниеТипов("СтандартныйПериод"));
	ТипыЭлементов.Вставить("Список", Новый ОписаниеТипов("СписокЗначений"));
	ТипыЭлементов.Вставить("Флажок", Новый ОписаниеТипов("Булево"));
	
	ПризнакиВидовЭлементов = Новый Структура("Период, Список, Флажок", "ЭтоПериод", "ЭтоСписок", "ЭтоЗначениеКакФлажок");
	СвойстваВидовЭлементов = Новый Структура("Период, Список, Флажок", "Значение", "Значение", "Использование");
	
	Для Каждого ВидЭлемента Из ВидыЭлементов Цикл 
		Признак = ПризнакиВидовЭлементов[ВидЭлемента];
		
		Сгенерированные = ИменаРеквизитов.Сгенерированных[ВидЭлемента];
		Предопределенные = ИменаРеквизитов.Предопределенных[ВидЭлемента];
		
		ИндексПредопределенных = -1;
		ГраницаПредопределенных = Предопределенные.ВГраница();
		
		Поиск = Новый Структура;
		Поиск.Вставить(Признак, Истина);
		Поиск.Вставить("СвойствоНастройки", "Значение");
		
		НайденныеЭлементы = ЭлементыНастроек.Скопировать(Поиск);
		Для Каждого Элемент Из НайденныеЭлементы Цикл 
			Если ГраницаПредопределенных >= НайденныеЭлементы.Индекс(Элемент) Тогда 
				ИндексПредопределенных = ИндексПредопределенных + 1;
				ПутьКДаннымЭлементов.ПоИмени.Вставить(Предопределенные[ИндексПредопределенных], Элемент.ИндексНастройки);
				ПутьКДаннымЭлементов.ПоИндексу.Вставить(Элемент.ИндексНастройки, Предопределенные[ИндексПредопределенных]);
				Продолжить;
			КонецЕсли;
			
			ЗаголовокЭлемента = Элемент.Поле.Заголовок;
			ШаблонИмениЭлемента = СтрЗаменить(Элемент.Поле.Имя, СвойстваВидовЭлементов[ВидЭлемента], "%1");
			
			ИмяРеквизита = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, ВидЭлемента);
			Если Сгенерированные.Найти(ИмяРеквизита) = Неопределено Тогда 
				ТипЭлемента = Элемент.ТипЗначения;
				ТипыЭлементов.Свойство(ВидЭлемента, ТипЭлемента);
				
				РеквизитыДобавляемые.Добавить(Новый РеквизитФормы(ИмяРеквизита, ТипЭлемента,, ЗаголовокЭлемента));
			КонецЕсли;
			
			ПутьКДаннымЭлементов.ПоИмени.Вставить(ИмяРеквизита, Элемент.ИндексНастройки);
			ПутьКДаннымЭлементов.ПоИндексу.Вставить(Элемент.ИндексНастройки, ИмяРеквизита);
		КонецЦикла;
	КонецЦикла;
	
	Возврат РеквизитыДобавляемые;
КонецФункции

Функция РеквизитыУдаляемыеЭлементовНастроек(ВидыЭлементов, ИменаРеквизитов, ПутьКДаннымЭлементов)
	РеквизитыУдаляемые = Новый Массив;
	
	Для Каждого ВидЭлемента Из ВидыЭлементов Цикл 
		Сгенерированные = ИменаРеквизитов.Сгенерированных[ВидЭлемента];
		Для Каждого ИмяРеквизита Из Сгенерированные Цикл 
			Если ПутьКДаннымЭлементов.ПоИмени[ИмяРеквизита] = Неопределено Тогда 
				РеквизитыУдаляемые.Добавить(ИмяРеквизита);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат РеквизитыУдаляемые;
КонецФункции

Процедура УдалитьКомандыЭлементовНастроек(Форма, РеквизитыУдаляемые)
	СуффиксыКоманд = СтрРазделить("ВыбратьПериод, Подбор, ВставитьИзБуфера", ", ", ЛОЖЬ);
	
	Для Каждого ИмяРеквизита Из РеквизитыУдаляемые Цикл 
		Для Каждого Суффикс Из СуффиксыКоманд Цикл 
			Команда = Форма.Команды.Найти(ИмяРеквизита + Суффикс);
			Если Команда <> Неопределено Тогда 
				Форма.Команды.Удалить(Команда);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Вывод периодов элементов формы настроек

Процедура ВывестиПериодыНастроек(Форма, ЭлементыНастроек, ИменаРеквизитов)
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Новый Структура("ЭтоПериод, СвойствоНастройки", Истина, "Значение"));
	Если НайденныеЭлементы.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	ИменаРеквизитовПредопределенных = ИменаРеквизитов.Предопределенных.Период;
	
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		СвязанныеЭлементы = ЭлементыНастроек.НайтиСтроки(Новый Структура("ИндексНастройки", Элемент.ИндексНастройки));
		Для Каждого СвязанныйЭлемент Из СвязанныеЭлементы Цикл 
			СвязанныйЭлемент.Поле.Видимость = (СвязанныйЭлемент.СвойствоНастройки = "Использование");
		КонецЦикла;
		
		ИнициализироватьПериод(Форма, Элемент.ИндексНастройки);
		
		Поле = Элемент.Поле;
		Родитель = Поле.Родитель;
		
		СледующийЭлемент = Неопределено;
		ИндексЭлемента = Родитель.ПодчиненныеЭлементы.Индекс(Поле);
		Если Родитель.ПодчиненныеЭлементы.Количество() > ИндексЭлемента + 1 Тогда 
			СледующийЭлемент = Родитель.ПодчиненныеЭлементы.Получить(ИндексЭлемента + 1);
		КонецЕсли;
		
		ИмяРеквизита = Форма.ПутьКДаннымЭлементов.ПоИндексу[Элемент.ИндексНастройки];
		Если ИменаРеквизитовПредопределенных.Найти(ИмяРеквизита) <> Неопределено Тогда 
			НайденныйЭлемент = Элементы.Найти(ИмяРеквизита);
			Элементы.Переместить(НайденныйЭлемент, Родитель, СледующийЭлемент);
			НайденныйЭлемент.Видимость = Истина;
			Продолжить;
		КонецЕсли;
		
		ШаблонИмениЭлемента = СтрЗаменить(Поле.Имя, "Значение", "%1%2");
		
		Группа = ГруппаЭлементовПериода(Элементы, Родитель, СледующийЭлемент, ШаблонИмениЭлемента, Поле.Заголовок);
		ДобавитьЭлементПериода(Элементы, Группа, ШаблонИмениЭлемента, "ДатаНачала", Поле.Заголовок);
		
		ИмяЭлемента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Разделитель");
		Разделитель = Элементы.Найти(ИмяЭлемента);
		Если Разделитель = Неопределено Тогда 
			Разделитель = Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияФормы"), Группа);
		КонецЕсли;
		Разделитель.Вид = ВидДекорацииФормы.Надпись;
		Разделитель.Заголовок = Символ(8211); // Среднее тире (en dash).
		
		ДобавитьЭлементПериода(Элементы, Группа, ШаблонИмениЭлемента, "ДатаОкончания", Поле.Заголовок);
		ДобавитьКомандуВыбораПериода(Форма, Группа, ШаблонИмениЭлемента);
	КонецЦикла;
КонецПроцедуры

Функция ГруппаЭлементовПериода(Элементы, Родитель, СледующийЭлемент, ШаблонИмени, Заголовок)
	ИмяЭлемента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмени, "", "Период");
	
	Группа = Элементы.Найти(ИмяЭлемента);
	Если Группа = Неопределено Тогда 
		Группа = Элементы.Добавить(ИмяЭлемента, Тип("ГруппаФормы"), Родитель);
	КонецЕсли;
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Группа.Заголовок = Заголовок;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.РазрешитьИзменениеСостава = Ложь;
	
	Если СледующийЭлемент <> Неопределено Тогда 
		Элементы.Переместить(Группа, Родитель, СледующийЭлемент);
	КонецЕсли;
	
	Возврат Группа;
КонецФункции

Процедура ДобавитьЭлементПериода(Элементы, Группа, ШаблонИмени, Свойство, ЗаголовокЭлементаНастройки)
	ИмяЭлемента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмени, "", Свойство);
	
	ШаблонЗаголовка = "%1 (дата %2)";
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовка, ЗаголовокЭлементаНастройки, ?(СтрЗаканчиваетсяНа(Свойство, "Начала"), "начала", "окончания"));
	
	Элемент = Элементы.Найти(ИмяЭлемента);
	Если Элемент = Неопределено Тогда 
		Элемент = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), Группа);
	КонецЕсли;
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмени, "Период.", Свойство);
	Элемент.Ширина = 9;
	Элемент.РастягиватьПоГоризонтали = Ложь;
	Элемент.КнопкаВыбора = Истина;
	Элемент.КнопкаОткрытия = Ложь;
	Элемент.КнопкаОчистки = Ложь;
	Элемент.КнопкаРегулирования = Ложь;
	Элемент.РедактированиеТекста = Истина;
	Элемент.Заголовок = Заголовок;
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_Период_ПриИзменении");
КонецПроцедуры

Процедура ДобавитьКомандуВыбораПериода(Форма, Группа, ШаблонИмени)
	ИмяЭлемента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмени, "", "ВыбратьПериод");
	
	Команда = Форма.Команды.Найти(ИмяЭлемента);
	Если Команда = Неопределено Тогда 
		Команда = Форма.Команды.Добавить(ИмяЭлемента);
	КонецЕсли;
	Команда.Действие = "Подключаемый_ВыбратьПериод";
	Команда.Заголовок = НСтр("ru = 'Выбрать период...'");
	Команда.Подсказка = Команда.Заголовок;
	Команда.Отображение = ОтображениеКнопки.Картинка;
	Команда.Картинка = БиблиотекаКартинок.Выбрать;
	
	Кнопка = Форма.Элементы.Найти(ИмяЭлемента);
	Если Кнопка = Неопределено Тогда 
		Кнопка = Форма.Элементы.Добавить(ИмяЭлемента, Тип("КнопкаФормы"), Группа);
	КонецЕсли;
	Кнопка.ИмяКоманды = ИмяЭлемента;
КонецПроцедуры

Процедура ИнициализироватьПериод(Форма, Индекс)
	ПользовательскиеНастройки = Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	ЭлементНастройки = ПользовательскиеНастройки.Элементы[Индекс];
	
	Путь = Форма.ПутьКДаннымЭлементов.ПоИндексу[Индекс];
	Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
		Форма[Путь] = ЭлементНастройки.Значение;
	Иначе // Элемент отбора.
		Форма[Путь] = ЭлементНастройки.ПравоеЗначение;
	КонецЕсли;
КонецПроцедуры

// Вывод списков элементов формы настроек

Процедура ВывестиСпискиНастроек(Форма, ЭлементыНастроек, ОписаниеНастроек, ИменаРеквизитов)
	Поиск = Новый Структура("ЭтоСписок, СвойствоНастройки", Истина, "Значение");
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
	Если НайденныеЭлементы.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Элемент.Поле.Видимость = Ложь;
		Описание = ОписаниеНастроек.Найти(Элемент.ИндексНастройки, "ИндексНастройки");
		
		ИмяСписка = Форма.ПутьКДаннымЭлементов.ПоИндексу[Элемент.ИндексНастройки];
		
		ДобавитьЭлементыСписка(Форма, Элемент, Описание, ИмяСписка, ИменаРеквизитов);
		ДобавитьКомандыСписка(Форма, Элемент, ЭлементыНастроек, ИмяСписка);
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьЭлементыСписка(Форма, ЭлементНастройки, ОписаниеЭлементаНастройки, ИмяСписка, ИменаРеквизитов)
	Элементы = Форма.Элементы; 
	Поле = ЭлементНастройки.Поле;
	
	ИменаРеквизитовПредопределенных = ИменаРеквизитов.Предопределенных.Список;
	
	Если ИменаРеквизитовПредопределенных.Найти(ИмяСписка) = Неопределено Тогда 
		Список = Элементы.Добавить(ИмяСписка, Тип("ТаблицаФормы"), Поле.Родитель);
		Список.ПутьКДанным = ИмяСписка;
		Список.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		Список.Высота = 3;
		Список.УстановитьДействие("ПриИзменении", "Подключаемый_Список_ПриИзменении");
		Список.УстановитьДействие("ОбработкаВыбора", "Подключаемый_Список_ОбработкаВыбора");
		
		ПоляСписка = Элементы.Добавить(Список.Имя + "Колонки", Тип("ГруппаФормы"), Список);
		ПоляСписка.Вид = ВидГруппыФормы.ГруппаКолонок;
		ПоляСписка.Группировка = ГруппировкаКолонок.ВЯчейке;
		ПоляСписка.Заголовок = "Поля";
		ПоляСписка.ОтображатьЗаголовок = Ложь;
		
		ПолеПометки = Элементы.Добавить(ИмяСписка + "Пометка", Тип("ПолеФормы"), ПоляСписка);
		ПолеПометки.Вид = ВидПоляФормы.ПолеФлажка;
		ПолеПометки.ПутьКДанным = ИмяСписка + ".Пометка";
		
		ПолеЗначения = Элементы.Добавить(ИмяСписка + "Значение", Тип("ПолеФормы"), ПоляСписка);
		ПолеЗначения.Вид = ВидПоляФормы.ПолеВвода;
		ПолеЗначения.ПутьКДанным = ИмяСписка + ".Значение";
		ПолеЗначения.УстановитьДействие("ПриИзменении", "Подключаемый_ЭлементСписка_ПриИзменении");
		ПолеЗначения.УстановитьДействие("НачалоВыбора", "Подключаемый_ЭлементСписка_НачалоВыбора");
	Иначе
		Список = Элементы.Найти(ИмяСписка);
		Список.Видимость = Истина;
		
		ПоляСписка = Элементы.Найти(Список.Имя + "Колонки");
		ПолеЗначения = Элементы.Найти(Список.Имя + "Значение");
		
		Элементы.Переместить(Список, Поле.Родитель);
	КонецЕсли;
	
	Список.Заголовок = Поле.Заголовок;
	
	Свойства = "ДоступныеТипы, ОграничениеТипа, АвтоОтметкаНезаполненного, СвязиПараметровВыбора, СвязьПоТипу";
	ЗаполнитьЗначенияСвойств(ПолеЗначения, Поле, Свойства);
	
	ОтчетыКлиентСервер.ДополнитьСписок(ПолеЗначения.СписокВыбора, Поле.СписокВыбора, Ложь, Истина);
	
	ПараметрыРедактирования = Новый Структура("БыстрыйВыбор, ВыборГруппИЭлементов");
	Если ОписаниеЭлементаНастройки.ОписаниеНастройки <> Неопределено Тогда 
		ЗаполнитьЗначенияСвойств(ПараметрыРедактирования, ОписаниеЭлементаНастройки.ОписаниеНастройки);
	КонецЕсли;
	
	ПолеЗначения.БыстрыйВыбор = ПараметрыРедактирования.БыстрыйВыбор;
	
	Условие = ОтчетыКлиентСервер.УсловиеЭлементаНастройки(
		ОписаниеЭлементаНастройки.ЭлементНастройки, ОписаниеЭлементаНастройки.ОписаниеНастройки);
	ПолеЗначения.ВыборГруппИЭлементов = ОтчетыКлиентСервер.ЗначениеТипаГруппыИЭлементы(
		ПараметрыРедактирования.ВыборГруппИЭлементов, Условие);
	
	ПолеЗначения.ПараметрыВыбора = ОтчетыКлиентСервер.ПараметрыВыбора(
		ОписаниеЭлементаНастройки.Настройки,
		Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы,
		ОписаниеЭлементаНастройки.ЭлементНастройки);
	
	ИнициализироватьСписок(Форма, ЭлементНастройки.ИндексНастройки, ПолеЗначения, ОписаниеЭлементаНастройки.ЭлементНастройки);
	
	Если Поле.СписокВыбора.Количество() > 0 Тогда 
		Список.ИзменятьСоставСтрок = Ложь;
		ПолеЗначения.ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьКомандыСписка(Форма, ЭлементНастройки, ЭлементыНастроек, ИмяСписка)
	Элементы = Форма.Элементы; 
	
	Поиск = Новый Структура("СвойствоНастройки, ИндексНастройки", "Использование");
	Поиск.ИндексНастройки = ЭлементНастройки.ИндексНастройки;
	
	ПолеЗаголовка = ЭлементыНастроек.НайтиСтроки(Поиск)[0].Поле;
	ГруппаЗаголовка = ПолеЗаголовка.Родитель;
	ГруппаЗаголовка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ГруппаСписка = ГруппаЗаголовка.Родитель;
	ГруппаСписка.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	
	Если Не Элементы[ИмяСписка].ИзменятьСоставСтрок Тогда 
		Возврат;
	КонецЕсли;
	
	ИмяЭлемента = ИмяСписка + "Отступ";
	Отступ = Элементы.Найти(ИмяЭлемента);
	Если Отступ = Неопределено Тогда 
		Отступ = Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияФормы"), ГруппаЗаголовка);
	ИначеЕсли Отступ.Родитель <> ГруппаЗаголовка Тогда 
		Элементы.Переместить(Отступ, ГруппаЗаголовка);
	КонецЕсли;
	Отступ.Вид = ВидДекорацииФормы.Надпись;
	Отступ.Заголовок = "     ";
	Отступ.РастягиватьПоГоризонтали = Истина;
	Отступ.АвтоМаксимальнаяШирина = Ложь;
	Отступ.Видимость = Истина;
	
	ИмяКоманды = ИмяСписка + "Подбор";
	ЗаголовокКоманды = НСтр("ru = 'Подбор'");
	ДобавитьКомандуСписка(Форма, ГруппаЗаголовка, ИмяКоманды, ЗаголовокКоманды, "Подключаемый_Список_Подбор");
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла") Тогда 
		Возврат;
	КонецЕсли;
	
	ИмяКоманды = ИмяСписка + "ВставитьИзБуфера";
	ЗаголовокКоманды = НСтр("ru = 'Вставить из буфера обмена...'");
	ДобавитьКомандуСписка(Форма, ГруппаЗаголовка, ИмяКоманды, ЗаголовокКоманды,
		"Подключаемый_Список_ВставитьИзБуфера", БиблиотекаКартинок.ВставитьИзБуфераОбмена);
КонецПроцедуры

Процедура ДобавитьКомандуСписка(Форма, Родитель, ИмяКоманды, Заголовок, Действие, Картинка = Неопределено)
	Команда = Форма.Команды.Найти(ИмяКоманды);
	Если Команда = Неопределено Тогда 
		Команда = Форма.Команды.Добавить(ИмяКоманды);
	КонецЕсли;
	Команда.Действие = Действие;
	Команда.Заголовок = Заголовок;
	Команда.Подсказка = Заголовок;
	
	Если Картинка = Неопределено Тогда 
		Команда.Отображение = ОтображениеКнопки.Текст;
	Иначе
		Команда.Отображение = ОтображениеКнопки.Картинка;
		Команда.Картинка = БиблиотекаКартинок.ВставитьИзБуфераОбмена;
	КонецЕсли;
	
	Кнопка = Форма.Элементы.Найти(ИмяКоманды);
	Если Кнопка = Неопределено Тогда 
		Кнопка = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Родитель);
	ИначеЕсли Кнопка.Родитель <> Родитель Тогда 
		Форма.Элементы.Переместить(Кнопка, Родитель);
	КонецЕсли;
	Кнопка.ИмяКоманды = ИмяКоманды;
	Кнопка.Вид = ВидКнопкиФормы.Гиперссылка;
	Кнопка.Видимость = Истина;
КонецПроцедуры

Процедура ИнициализироватьСписок(Форма, Индекс, Поле, ЭлементНастройки)
	ПользовательскиеНастройки = Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	ЭлементНастройки = ПользовательскиеНастройки.Элементы[Индекс];
	
	Путь = Форма.ПутьКДаннымЭлементов.ПоИндексу[Индекс];
	Список = Форма[Путь];
	Список.ТипЗначения = Поле.ДоступныеТипы;
	Список.Очистить();
	
	ИмяПоляЗначения = "ПравоеЗначение";
	Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
		ИмяПоляЗначения = "Значение";
	КонецЕсли;
	
	ВыбранныеЗначения = ОтчетыКлиентСервер.ЗначенияСписком(ЭлементНастройки[ИмяПоляЗначения], Истина);
	Если ВыбранныеЗначения.Количество() > 0 Тогда 
		ЭлементНастройки[ИмяПоляЗначения] = ВыбранныеЗначения;
	Иначе
		ВыбранныеЗначения = ОтчетыКлиентСервер.ЗначенияСписком(ЭлементНастройки[ИмяПоляЗначения], Истина);
		Если ВыбранныеЗначения.Количество() > 0 Тогда 
			// При выполнении метода СоздатьЭлементыФормыПользовательскихНастроек()
			// сбрасывается значение пользовательской настройки.
			ЭлементНастройки[ИмяПоляЗначения] = ВыбранныеЗначения;
		КонецЕсли;
	КонецЕсли;
	
	ДоступныеЗначения = Новый СписокЗначений;
	Если Поле.БыстрыйВыбор = Истина Тогда 
		ПараметрыСписка = Новый Структура("ПараметрыВыбора, ОписаниеТипов, ВыборГруппИЭлементов, Отбор");
		ЗаполнитьЗначенияСвойств(ПараметрыСписка, Поле);
		ПараметрыСписка.ОписаниеТипов = Поле.ДоступныеТипы;
		ПараметрыСписка.Отбор = Новый Структура;
		
		ДоступныеЗначения = ЗначенияДляВыбора(ПараметрыСписка);
	КонецЕсли;
	
	Если ДоступныеЗначения.Количество() = 0 Тогда 
		ДоступныеЗначения = Поле.СписокВыбора;
	КонецЕсли;
	
	ОтчетыКлиентСервер.ДополнитьСписок(Список, ДоступныеЗначения, Ложь, Истина);
	ОтчетыКлиентСервер.ДополнитьСписок(Список, ВыбранныеЗначения, Ложь, Истина);
	
	Для Каждого ЭлементСписка Из Список Цикл 
		Если Не ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда 
			Продолжить;
		КонецЕсли;
		
		НайденныйЭлемент = ДоступныеЗначения.НайтиПоЗначению(ЭлементСписка.Значение);
		Если НайденныйЭлемент <> Неопределено Тогда 
			ЭлементСписка.Представление = НайденныйЭлемент.Представление;
		КонецЕсли;
		
		НайденныйЭлемент = ВыбранныеЗначения.НайтиПоЗначению(ЭлементСписка.Значение);
		ЭлементСписка.Пометка = (НайденныйЭлемент <> Неопределено);
	КонецЦикла;
	
	ПолеСписка = Форма.Элементы[Путь];
	ПолеСписка.ЦветТекста = ?(ЭлементНастройки.Использование, Новый Цвет, ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
КонецПроцедуры

// Вывод значений как поля флажков.

Процедура ВывестиЗначенияКакПоляФлажков(Форма, ЭлементыНастроек, ИменаРеквизитов)
	Поиск = Новый Структура("ЭтоЗначениеКакФлажок, СвойствоНастройки", Истина, "Использование");
	НайденныеЭлементы = ЭлементыНастроек.НайтиСтроки(Поиск);
	Если НайденныеЭлементы.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	ИменаРеквизитовПредопределенных = ИменаРеквизитов.Предопределенных.Флажок;
	
	Для Каждого Элемент Из НайденныеЭлементы Цикл 
		Поле = Элемент.Поле;
		
		ИмяРеквизита = Форма.ПутьКДаннымЭлементов.ПоИндексу[Элемент.ИндексНастройки];
		Если ИменаРеквизитовПредопределенных.Найти(ИмяРеквизита) = Неопределено Тогда 
			ПолеФлажка = Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Поле.Родитель);
			ПолеФлажка.Вид = ВидПоляФормы.ПолеФлажка;
			ПолеФлажка.ПутьКДанным = ИмяРеквизита;
			ПолеФлажка.УстановитьДействие("ПриИзменении", "Подключаемый_ЭлементНастройки_ПриИзменении");
		Иначе
			ПолеФлажка = Элементы.Найти(ИмяРеквизита);
			Элементы.Переместить(ПолеФлажка, Поле.Родитель);
			ПолеФлажка.Видимость = Истина;
		КонецЕсли;
		
		ПолеФлажка.Заголовок = Поле.Заголовок;
		ПолеФлажка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		Элемент.Поле = ПолеФлажка;
		
		ИнициализироватьФлажок(Форма, Элемент.ИндексНастройки);
	КонецЦикла;
КонецПроцедуры

Процедура ИнициализироватьФлажок(Форма, Индекс)
	ПользовательскиеНастройки = Форма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	ЭлементНастройки = ПользовательскиеНастройки.Элементы[Индекс];
	
	Путь = Форма.ПутьКДаннымЭлементов.ПоИндексу[Индекс];
	Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
		Форма[Путь] = ЭлементНастройки.Значение;
	Иначе // Элемент отбора.
		Форма[Путь] = ЭлементНастройки.ПравоеЗначение;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Сохранение состояния формы

Функция ЗапомнитьВыделенныеСтроки(Форма, ИмяТаблицы, КлючевыеКолонки) Экспорт
	ТаблицаРеквизит = Форма[ИмяТаблицы];
	ТаблицаЭлемент = Форма.Элементы[ИмяТаблицы];
	
	Результат = Новый Структура;
	Результат.Вставить("Выделенные", Новый Массив);
	Результат.Вставить("Текущая", Неопределено);
	
	ТекущаяИдентификатор = ТаблицаЭлемент.ТекущаяСтрока;
	Если ТекущаяИдентификатор <> Неопределено Тогда
		СтрокаТаблицы = ТаблицаРеквизит.НайтиПоИдентификатору(ТекущаяИдентификатор);
		Если СтрокаТаблицы <> Неопределено Тогда
			ДанныеСтроки = Новый Структура(КлючевыеКолонки);
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);
			Результат.Текущая = ДанныеСтроки;
		КонецЕсли;
	КонецЕсли;
	
	ВыделенныеСтроки = ТаблицаЭлемент.ВыделенныеСтроки;
	Если ВыделенныеСтроки <> Неопределено Тогда
		Для Каждого ВыделеннаяИдентификатор Из ВыделенныеСтроки Цикл
			Если ВыделеннаяИдентификатор = ТекущаяИдентификатор Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицы = ТаблицаРеквизит.НайтиПоИдентификатору(ВыделеннаяИдентификатор);
			Если СтрокаТаблицы <> Неопределено Тогда
				ДанныеСтроки = Новый Структура(КлючевыеКолонки);
				ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);
				Результат.Выделенные.Добавить(ДанныеСтроки);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ВосстановитьВыделенныеСтроки(Форма, ИмяТаблицы, СтрокиТаблицы) Экспорт
	ТаблицаРеквизит = Форма[ИмяТаблицы];
	ТаблицаЭлемент = Форма.Элементы[ИмяТаблицы];
	
	ТаблицаЭлемент.ВыделенныеСтроки.Очистить();
	
	Если СтрокиТаблицы.Текущая <> Неопределено Тогда
		Найденные = ОтчетыКлиентСервер.НайтиСтрокиТаблицы(ТаблицаРеквизит, СтрокиТаблицы.Текущая);
		Если Найденные <> Неопределено И Найденные.Количество() > 0 Тогда
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				Если СтрокаТаблицы <> Неопределено Тогда
					Идентификатор = СтрокаТаблицы.ПолучитьИдентификатор();
					ТаблицаЭлемент.ТекущаяСтрока = Идентификатор;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ДанныеСтроки Из СтрокиТаблицы.Выделенные Цикл
		Найденные = ОтчетыКлиентСервер.НайтиСтрокиТаблицы(ТаблицаРеквизит, ДанныеСтроки);
		Если Найденные <> Неопределено И Найденные.Количество() > 0 Тогда
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				Если СтрокаТаблицы <> Неопределено Тогда
					ТаблицаЭлемент.ВыделенныеСтроки.Добавить(СтрокаТаблицы.ПолучитьИдентификатор());
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Отчет пустой

// Проверяет наличие внешних наборов данных.
//
// Параметры:
//   НаборыДанных - НаборыДанныхМакетаКомпоновкиДанных - Коллекция проверяемых наборов данных.
//
// Возвращаемое значение: 
//   Булево - Истина, если есть внешние наборы данных.
//
Функция ЕстьВнешнийНаборДанных(НаборыДанных)
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
		
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъектМакетаКомпоновкиДанных") Тогда
			
			Возврат Истина;
			
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеМакетаКомпоновкиДанных") Тогда
			
			Если ЕстьВнешнийНаборДанных(НаборДанных.Элементы) Тогда
				
				Возврат Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Параметры выбора

Функция ЗначенияДляВыбора(ПараметрыНастройки, ТипИлиТипы = Неопределено) Экспорт
	ПараметрыПолученияДанныхВыбора = Новый Структура("Отбор, ВыборГруппИЭлементов");
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияДанныхВыбора, ПараметрыНастройки);
	ДополнитьСтруктуруИзПараметровВыбора(ПараметрыПолученияДанныхВыбора, ПараметрыНастройки.ПараметрыВыбора);
	
	ЗначенияДляВыбора = Новый СписокЗначений;
	Если ТипЗнч(ТипИлиТипы) = Тип("Тип") Тогда
		Типы = Новый Массив;
		Типы.Добавить(ТипИлиТипы);
	ИначеЕсли ТипЗнч(ТипИлиТипы) = Тип("Массив") Тогда
		Типы = ТипИлиТипы;
	Иначе
		Типы = ПараметрыНастройки.ОписаниеТипов.Типы();
	КонецЕсли;
	
	Для Каждого Тип Из Типы Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		Если ОбъектМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя());
		
		СписокВыбора = Менеджер.ПолучитьДанныеВыбора(ПараметрыПолученияДанныхВыбора);
		Для Каждого ЭлементСписка Из СписокВыбора Цикл
			ЗначениеДляВыбора = ЗначенияДляВыбора.Добавить();
			ЗаполнитьЗначенияСвойств(ЗначениеДляВыбора, ЭлементСписка);
			
			// Для перечислений значения возвращаются в виде структуры со свойством Значение.
			ЗначениеПеречисления = Неопределено;
			Если ТипЗнч(ЗначениеДляВыбора.Значение) = Тип("Структура") 
				И ЗначениеДляВыбора.Значение.Свойство("Значение", ЗначениеПеречисления) Тогда
				ЗначениеДляВыбора.Значение = ЗначениеПеречисления;
			КонецЕсли;	
				
		КонецЦикла;
	КонецЦикла;
	Возврат ЗначенияДляВыбора;
КонецФункции

Процедура ДополнитьСтруктуруИзПараметровВыбора(Структура, МассивПараметровВыбора)
	Для Каждого ПараметрВыбора Из МассивПараметровВыбора Цикл
		ТекущаяСтруктура = Структура;
		МассивСтрок = СтрРазделить(ПараметрВыбора.Имя, ".");
		Количество = МассивСтрок.Количество();
		Если Количество > 1 Тогда
			Для Индекс = 0 По Количество-2 Цикл
				Ключ = МассивСтрок[Индекс];
				Если ТекущаяСтруктура.Свойство(Ключ) И ТипЗнч(ТекущаяСтруктура[Ключ]) = Тип("Структура") Тогда
					ТекущаяСтруктура = ТекущаяСтруктура[Ключ];
				Иначе
					ТекущаяСтруктура.Вставить(Ключ, Новый Структура);
					ТекущаяСтруктура = ТекущаяСтруктура[Ключ];
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Ключ = МассивСтрок[Количество-1];
		ТекущаяСтруктура.Вставить(Ключ, ПараметрВыбора.Значение);
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Схема компоновки данных

// Добавляет выбранное поле компоновки данных.
//
// Параметры:
//   Куда - КомпоновщикНастроекКомпоновкиДанных, НастройкиКомпоновкиДанных, ВыбранныеПоляКомпоновкиДанных -
//       Коллекция в которую требуется добавить выбранное поле.
//   ИмяИлиПолеКД - Строка, ПолеКомпоновкиДанных - Имя поля.
//   Заголовок    - Строка - Необязательный. Представление поля.
//
// Возвращаемое значение:
//   ВыбранноеПолеКомпоновкиДанных - Добавленное выбранное поле.
//
Функция ДобавитьВыбранноеПоле(Куда, ИмяИлиПолеКД, Заголовок = "") Экспорт
	
	Если ТипЗнч(Куда) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Настройки.Выбор;
	ИначеЕсли ТипЗнч(Куда) = Тип("НастройкиКомпоновкиДанных") Тогда
		ВыбранныеПоляКД = Куда.Выбор;
	Иначе
		ВыбранныеПоляКД = Куда;
	КонецЕсли;
	
	Если ТипЗнч(ИмяИлиПолеКД) = Тип("Строка") Тогда
		ПолеКД = Новый ПолеКомпоновкиДанных(ИмяИлиПолеКД);
	Иначе
		ПолеКД = ИмяИлиПолеКД;
	КонецЕсли;
	
	ВыбранноеПолеКД = ВыбранныеПоляКД.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПолеКД.Поле = ПолеКД;
	Если Заголовок <> "" Тогда
		ВыбранноеПолеКД.Заголовок = Заголовок;
	КонецЕсли;
	
	Возврат ВыбранноеПолеКД;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Прочее

Функция РасширенноеОписаниеТипов(ИсходноеОписаниеТипов, ПривестиКФорме, ПараметрыПодбора = Неопределено) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("СодержитТипТип",        Ложь);
	Результат.Вставить("СодержитТипДата",       Ложь);
	Результат.Вставить("СодержитТипБулево",     Ложь);
	Результат.Вставить("СодержитТипСтрока",     Ложь);
	Результат.Вставить("СодержитТипЧисло",      Ложь);
	Результат.Вставить("СодержитТипПериод",     Ложь);
	Результат.Вставить("СодержитТипУИД",        Ложь);
	Результат.Вставить("СодержитТипХранилище",  Ложь);
	Результат.Вставить("СодержитОбъектныеТипы", Ложь);
	Результат.Вставить("ОграниченнойДлины",     Истина);
	
	Результат.Вставить("КоличествоТипов",            0);
	Результат.Вставить("КоличествоПримитивныхТипов", 0);
	Результат.Вставить("ОбъектныеТипы", Новый Массив);
	
	Если ПривестиКФорме Тогда
		ДобавляемыеТипы = Новый Массив;
		ВычитаемыеТипы = Новый Массив;
		Результат.Вставить("ОписаниеТиповИсходное", ИсходноеОписаниеТипов);
		Результат.Вставить("ОписаниеТиповДляФормы", ИсходноеОписаниеТипов);
	КонецЕсли;
	
	Если ИсходноеОписаниеТипов = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	МассивТипов = ИсходноеОписаниеТипов.Типы();
	Для Каждого Тип Из МассивТипов Цикл
		Если Тип = Тип("Null") Тогда 
			ВычитаемыеТипы.Добавить(Тип);
			Продолжить;
		КонецЕсли;
		
		Если Тип = Тип("ПолеКомпоновкиДанных") Тогда
			Если ПривестиКФорме Тогда
				ВычитаемыеТипы.Добавить(Тип);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		МетаданныеНастройки = Метаданные.НайтиПоТипу(Тип);
		Если МетаданныеНастройки <> Неопределено Тогда 
			Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(МетаданныеНастройки) Тогда
				Если ТипЗнч(ПараметрыПодбора) = Тип("Соответствие") Тогда 
					ПараметрыПодбора.Вставить(Тип, МетаданныеНастройки.ПолноеИмя() + ".ФормаВыбора");
				КонецЕсли;
			Иначе // Объект недоступен.
				Если ПривестиКФорме Тогда
					ВычитаемыеТипы.Добавить(Тип);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Результат.КоличествоТипов = Результат.КоличествоТипов + 1;
		
		Если Тип = Тип("Тип") Тогда
			Результат.СодержитТипТип = Истина;
		ИначеЕсли Тип = Тип("Дата") Тогда
			Результат.СодержитТипДата = Истина;
			Результат.КоличествоПримитивныхТипов = Результат.КоличествоПримитивныхТипов + 1;
		ИначеЕсли Тип = Тип("Булево") Тогда
			Результат.СодержитТипБулево = Истина;
			Результат.КоличествоПримитивныхТипов = Результат.КоличествоПримитивныхТипов + 1;
		ИначеЕсли Тип = Тип("Число") Тогда
			Результат.СодержитТипЧисло = Истина;
			Результат.КоличествоПримитивныхТипов = Результат.КоличествоПримитивныхТипов + 1;
		ИначеЕсли Тип = Тип("СтандартныйПериод") Тогда
			Результат.СодержитТипПериод = Истина;
		ИначеЕсли Тип = Тип("Строка") Тогда
			Результат.СодержитТипСтрока = Истина;
			Результат.КоличествоПримитивныхТипов = Результат.КоличествоПримитивныхТипов + 1;
			Если ИсходноеОписаниеТипов.КвалификаторыСтроки.Длина = 0
				И ИсходноеОписаниеТипов.КвалификаторыСтроки.ДопустимаяДлина = ДопустимаяДлина.Переменная Тогда
				Результат.ОграниченнойДлины = Ложь;
			КонецЕсли;
		ИначеЕсли Тип = Тип("УникальныйИдентификатор") Тогда
			Результат.СодержитТипУИД = Истина;
		ИначеЕсли Тип = Тип("ХранилищеЗначения") Тогда
			Результат.СодержитТипХранилище = Истина;
		Иначе
			Результат.СодержитОбъектныеТипы = Истина;
			Результат.ОбъектныеТипы.Добавить(Тип);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПривестиКФорме
		И (ДобавляемыеТипы.Количество() > 0 Или ВычитаемыеТипы.Количество() > 0) Тогда
		Результат.ОписаниеТиповДляФормы = Новый ОписаниеТипов(ИсходноеОписаниеТипов, ДобавляемыеТипы, ВычитаемыеТипы);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ТипНастройкиСтрокой(Тип)
	Если Тип = Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат "Настройки";
	ИначеЕсли Тип = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат "НастройкиВложенногоОбъекта";
	
	ИначеЕсли Тип = Тип("ОтборКомпоновкиДанных") Тогда
		Возврат "Отбор";
	ИначеЕсли Тип = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		Возврат "ЭлементОтбора";
	ИначеЕсли Тип = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		Возврат "ГруппаЭлементовОтбора";
	
	ИначеЕсли Тип = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		Возврат "ЗначениеПараметраНастроек";
	
	ИначеЕсли Тип = Тип("ГруппировкаКомпоновкиДанных") Тогда
		Возврат "Группировка";
	ИначеЕсли Тип = Тип("ПоляГруппировкиКомпоновкиДанных") Тогда
		Возврат "ПоляГруппировки";
	ИначеЕсли Тип = Тип("КоллекцияПолейГруппировкиКомпоновкиДанных") Тогда
		Возврат "КоллекцияПолейГруппировки";
	ИначеЕсли Тип = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат "ПолеГруппировки";
	ИначеЕсли Тип = Тип("АвтоПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат "АвтоПолеГруппировки";
	
	ИначеЕсли Тип = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
		Возврат "ВыбранныеПоля";
	ИначеЕсли Тип = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат "ВыбранноеПоле";
	ИначеЕсли Тип = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		Возврат "ГруппаВыбранныхПолей";
	ИначеЕсли Тип = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
		Возврат "АвтоВыбранноеПоле";
	
	ИначеЕсли Тип = Тип("ПорядокКомпоновкиДанных") Тогда
		Возврат "Порядок";
	ИначеЕсли Тип = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат "ЭлементПорядка";
	ИначеЕсли Тип = Тип("АвтоЭлементПорядкаКомпоновкиДанных") Тогда
		Возврат "АвтоЭлементПорядка";
	
	ИначеЕсли Тип = Тип("УсловноеОформлениеКомпоновкиДанных") Тогда
		Возврат "УсловноеОформление";
	ИначеЕсли Тип = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
		Возврат "ЭлементУсловногоОформления";
	
	ИначеЕсли Тип = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
		Возврат "СтруктураНастроек";
	ИначеЕсли Тип = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		Возврат "КоллекцияЭлементовСтруктурыНастроек";
	
	ИначеЕсли Тип = Тип("ТаблицаКомпоновкиДанных") Тогда
		Возврат "Таблица";
	ИначеЕсли Тип = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Возврат "ГруппировкаТаблицы";
	ИначеЕсли Тип = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		Возврат "КоллекцияЭлементовСтруктурыТаблицы";
	
	ИначеЕсли Тип = Тип("ДиаграммаКомпоновкиДанных") Тогда
		Возврат "Диаграмма";
	ИначеЕсли Тип = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		Возврат "ГруппировкаДиаграммы";
	ИначеЕсли Тип = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		Возврат "КоллекцияЭлементовСтруктурыДиаграммы";
	
	ИначеЕсли Тип = Тип("ЗначенияПараметровДанныхКомпоновкиДанных") Тогда
		Возврат "ЗначенияПараметровДанных";
	
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

Функция ЗначениеВМассив(Значение) Экспорт
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Возврат Значение;
	Иначе
		Массив = Новый Массив;
		Массив.Добавить(Значение);
		Возврат Массив;
	КонецЕсли;
КонецФункции

Функция ПривестиИдентификаторКИмени(Идентификатор) Экспорт
	Возврат СтрЗаменить(СтрЗаменить(Строка(Идентификатор), "-", ""), ".", "_");
КонецФункции

// Приводит значение типа ИспользованиеГруппИЭлементов к типу ГруппыИЭлементы.
//  Для других типов возвращает значение Авто.
//
Функция ПривестиЗначениеКТипуГруппыИЭлементы(ИсходноеЗначение, ЗначениеПоУмолчанию = Неопределено)
	Тип = ТипЗнч(ИсходноеЗначение);
	Если Тип = Тип("ГруппыИЭлементы") Тогда
		Возврат ИсходноеЗначение;
	ИначеЕсли Тип = Тип("ИспользованиеГруппИЭлементов") Тогда
		Если ИсходноеЗначение = ИспользованиеГруппИЭлементов.Элементы Тогда
			Возврат ГруппыИЭлементы.Элементы;
		ИначеЕсли ИсходноеЗначение = ИспользованиеГруппИЭлементов.ГруппыИЭлементы Тогда
			Возврат ГруппыИЭлементы.ГруппыИЭлементы;
		ИначеЕсли ИсходноеЗначение = ИспользованиеГруппИЭлементов.Группы Тогда
			Возврат ГруппыИЭлементы.Группы;
		КонецЕсли;
	ИначеЕсли Тип = Тип("ВидСравненияКомпоновкиДанных") Тогда
		Если ИсходноеЗначение = ВидСравненияКомпоновкиДанных.ВСписке
			Или ИсходноеЗначение = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
			Или ИсходноеЗначение = ВидСравненияКомпоновкиДанных.НеВСписке
			Или ИсходноеЗначение = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
			// Виды сравнения ВСпискеПоИерархии (В группе из списка) и НеВСпискеПоИерархии (Не в группе из списка)
			// следует понимать как "В списке или в группах" и "Не в списке и не в группах".
			// - Тогда более понятно почему для них используется "ГруппыИЭлементы", а не "Группы".
			Возврат ГруппыИЭлементы.ГруппыИЭлементы;
		ИначеЕсли ИсходноеЗначение = ВидСравненияКомпоновкиДанных.ВИерархии
			Или ИсходноеЗначение = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
			Возврат ГруппыИЭлементы.Группы;
		КонецЕсли;
	КонецЕсли;
	Возврат ?(ЗначениеПоУмолчанию = Неопределено, ГруппыИЭлементы.Авто, ЗначениеПоУмолчанию);
КонецФункции

// Находит элемент компоновки данных по полному пути.
//
// Параметры:
//   НастройкиКД - НастройкиКомпоновкиДанных - Корневой узел настроек, в который вложен искомый элемент.
//   ПолныйПутьКЭлементу - Строка - полный путь к элементу. Может быть получена в функции ПолныйПутьКЭлементу().
//
// Возвращаемое значение:
//   ЭлементКД - Произвольный - Найденный узел настроек.
//
Функция ЭлементНастроекПоПолномуПути(Знач Настройки, Знач ПолныйПутьКЭлементу) Экспорт
	Индексы = СтрРазделить(ПолныйПутьКЭлементу, "/", Ложь);
	ЭлементНастроек = Настройки;
	
	Для Каждого Индекс Из Индексы Цикл
		Если Индекс = "Строки" Тогда
			ЭлементНастроек = ЭлементНастроек.Строки;
		ИначеЕсли Индекс = "Колонки" Тогда
			ЭлементНастроек = ЭлементНастроек.Колонки;
		ИначеЕсли Индекс = "Серии" Тогда
			ЭлементНастроек = ЭлементНастроек.Серии;
		ИначеЕсли Индекс = "Точки" Тогда
			ЭлементНастроек = ЭлементНастроек.Точки;
		ИначеЕсли Индекс = "Структура" Тогда
			ЭлементНастроек = ЭлементНастроек.Структура;
		ИначеЕсли Индекс = "Настройки" Тогда
			ЭлементНастроек = ЭлементНастроек.Настройки;
		Иначе
			ЭлементНастроек = ЭлементНастроек[Число(Индекс)];
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЭлементНастроек;
КонецФункции

#КонецОбласти
