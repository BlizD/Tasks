////////////////////////////////////////////////////////////////////////////////
// Методы работы с СКД из формы отчета (сервер).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Инициализирует компоновщик на основании схемы и загружает новые настройки.
//   Для вызова из обработчика события ПередЗагрузкойНастроекВКомпоновщик() формы отчета, расположенного в модуле объекта отчета.
//
// Параметры:
//   ОтчетОбъект - ОтчетОбъект.*, ВнешнийОтчетОбъект.* - Отчет, для которого необходимо подключить схему.
//   Контекст - Произвольный - Передается "как есть" из одноименного параметра события ПередЗагрузкойНастроекВКомпоновщик.
//   СхемаКД - СхемаКомпоновкиДанных - Схема, которую необходимо подключить.
//   КлючСхемы - Строка - Идентификатор новой схемы, который будет записан в дополнительные свойства пользовательских настроек.
//
Процедура ПодключитьСхему(ОтчетОбъект, Контекст, СхемаКД, КлючСхемы) Экспорт
	СобытиеИзФормыОтчета = (ТипЗнч(Контекст) = Тип("УправляемаяФорма"));
	
	ОтчетОбъект.СхемаКомпоновкиДанных = СхемаКД;
	Если СобытиеИзФормыОтчета Тогда
		НастройкиОтчета = Контекст.НастройкиОтчета;
		АдресСхемы = НастройкиОтчета.АдресСхемы;
		НастройкиОтчета.Вставить("СхемаМодифицирована", Истина);
	Иначе
		АдресСхемыЗаполнен = (ТипЗнч(Контекст.АдресСхемы) = Тип("Строка") И ЭтоАдресВременногоХранилища(Контекст.АдресСхемы));
		Если Не АдресСхемыЗаполнен Тогда
			ИдентификаторФормы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст, "ИдентификаторФормы");
			Если ТипЗнч(ИдентификаторФормы) = Тип("УникальныйИдентификатор") Тогда
				АдресСхемыЗаполнен = Истина;
				Контекст.АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКД, ИдентификаторФормы);
			КонецЕсли;
		КонецЕсли;
		Если АдресСхемыЗаполнен Тогда
			АдресСхемы = Контекст.АдресСхемы;
		Иначе
			АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКД);
		КонецЕсли;
		Контекст.СхемаМодифицирована = Истина;
	КонецЕсли;
	ПоместитьВоВременноеХранилище(СхемаКД, АдресСхемы);
	
	ОтчетОбъект.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	
	Если СобытиеИзФормыОтчета Тогда
		ЗначениеВДанныеФормы(ОтчетОбъект, Контекст.Отчет);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует описание периода для целей сбора статистики.
//   Получаемая информация не содержит конкретных дат, поэтому может считаться анонимной.
//
// Параметры:
//   СтандартныйПериод - СтандартныйПериод - Анализируемый период.
//
// Возвращаемое значение:
//   Структура - Описание периода.
//       * ВидПериода - Строка - Классификация периода. Например: "Год", "СНачалаГода" или "ДоКонцаГода".
//       * СдвигПериода - Число - Количество шагов, необходимых для получения периода относительно текущей даты.
//           Размер шага определяется видом периода.
//
Функция АнализПериода(СтандартныйПериод) Экспорт
	Если СтандартныйПериод.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		Возврат АнализПроизвольногоПериода(СтандартныйПериод.ДатаНачала, СтандартныйПериод.ДатаОкончания);
	Иначе
		Возврат АнализСтандартногоПериода(СтандартныйПериод.Вариант);
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Анализ

Функция РасширеннаяИнформацияОНастройках(КомпоновщикНастроекКД, НастройкиОтчета, ОтчетОбъектИлиПолноеИмя, УсловияВывода = Неопределено) Экспорт
	НастройкиКД = КомпоновщикНастроекКД.Настройки;
	ПользовательскиеНастройкиКД = КомпоновщикНастроекКД.ПользовательскиеНастройки;
	
	ДополнительныеНастройкиЭлементов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПользовательскиеНастройкиКД.ДополнительныеСвойства, "ЭлементыФормы");
	Если ДополнительныеНастройкиЭлементов = Неопределено Тогда
		ДополнительныеНастройкиЭлементов = Новый Соответствие;
	КонецЕсли;
	
	Информация = Новый Структура;
	Информация.Вставить("ТолькоПользовательские", Ложь);
	Информация.Вставить("ТолькоБыстрые", Ложь);
	Информация.Вставить("ИдентификаторТекущегоУзлаКД", Неопределено);
	Если УсловияВывода <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Информация, УсловияВывода);
	КонецЕсли;
	
	Информация.Вставить("НастройкиКД", НастройкиКД);
	
	Информация.Вставить("НастройкиОтчета",           НастройкиОтчета);
	Информация.Вставить("ОтчетОбъектИлиПолноеИмя",   ОтчетОбъектИлиПолноеИмя);
	Информация.Вставить("ДеревоВарианта",            ДеревоВарианта());
	Информация.Вставить("НастройкиВарианта",         ТаблицаНастроекВарианта());
	Информация.Вставить("ПользовательскиеНастройки", ТаблицаПользовательскихНастроек());
	
	Информация.Вставить("ОтключаемыеСвязи", Новый Массив);
	Информация.Вставить("Связи", Новый Структура);
	Информация.Связи.Вставить("ПоТипу",             ТаблицаСвязейПоТипу());
	Информация.Связи.Вставить("ПараметровВыбора",   ТаблицаСвязейПараметровВыбора());
	Информация.Связи.Вставить("ОбъектовМетаданных", ТаблицаСвязейОбъектовМетаданных(НастройкиОтчета, Информация.ОтчетОбъектИлиПолноеИмя));
	
	Информация.Вставить("ДополнительныеНастройкиЭлементов",   ДополнительныеНастройкиЭлементов);
	Информация.Вставить("СоответствиеИменОбъектовМетаданных", Новый Соответствие);
	Информация.Вставить("Поиск", Новый Структура);
	Информация.Поиск.Вставить("НастройкиВариантаПоПолюКД", Новый Соответствие);
	Информация.Поиск.Вставить("ПользовательскиеНастройки", Новый Соответствие);
	Информация.Вставить("ЕстьБыстрыеНастройки", Ложь);
	Информация.Вставить("ЕстьОбычныеНастройки", Ложь);
	
	Информация.Вставить("ЕстьВложенныеОтчеты", Ложь);
	Информация.Вставить("ЕстьВложенныеОтборы", Ложь);
	Информация.Вставить("ЕстьВложенноеОформление", Ложь);
	Информация.Вставить("ЕстьВложенныеПоля", Ложь);
	Информация.Вставить("ЕстьВложенныеСортировки", Ложь);
	
	Для Каждого ПользовательскаяНастройкаКД Из ПользовательскиеНастройкиКД.Элементы Цикл
		СвойстваНастройки = Информация.ПользовательскиеНастройки.Добавить();
		СвойстваНастройки.ПользовательскаяНастройкаКД = ПользовательскаяНастройкаКД;
		СвойстваНастройки.Идентификатор               = ПользовательскаяНастройкаКД.ИдентификаторПользовательскойНастройки;
		СвойстваНастройки.ИндексВКоллекции = ПользовательскиеНастройкиКД.Элементы.Индекс(ПользовательскаяНастройкаКД);
		СвойстваНастройки.ИдентификаторКД  = ПользовательскиеНастройкиКД.ПолучитьИдентификаторПоОбъекту(ПользовательскаяНастройкаКД);
		СвойстваНастройки.Тип              = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(ПользовательскаяНастройкаКД));
		Информация.Поиск.ПользовательскиеНастройки.Вставить(СвойстваНастройки.Идентификатор, СвойстваНастройки);
	КонецЦикла;
	
	СтрокаДерева = ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, НастройкиКД, Информация.ДеревоВарианта.Строки, "Отчет");
	СтрокаДерева.Глобальная = Истина;
	Информация.Вставить("ДеревоВариантаКорневаяСтрока", СтрокаДерева);
	Если Информация.ИдентификаторТекущегоУзлаКД = Неопределено Тогда
		Информация.ИдентификаторТекущегоУзлаКД = СтрокаДерева.ИдентификаторКД;
		Если Не Информация.ТолькоПользовательские Тогда
			СтрокаДерева.ВыводРазрешен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ЗарегистрироватьНастройкиВарианта(НастройкиКД, Информация);
	
	ЗарегистрироватьСвязиОтВедущих(Информация);
	
	Возврат Информация;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Дерево варианта

Функция ДеревоВарианта()
	Результат = Новый ДеревоЗначений;
	
	// Узлы СКД.
	Результат.Колонки.Добавить("УзелКД");
	Результат.Колонки.Добавить("ДоступнаяНастройкаКД");
	Результат.Колонки.Добавить("ПользовательскаяНастройкаКД");
	
	// Прикладная структура.
	Результат.Колонки.Добавить("ПользовательскаяНастройка");
	
	// Поиск этой настройки в узле.
	Результат.Колонки.Добавить("ИдентификаторКД");
	
	// Связь с узлами СКД.
	Результат.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	
	// Тип настройки.
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ЕстьСтруктура", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ЕстьПоляИОформление", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Глобальная", Новый ОписаниеТипов("Булево"));
	
	// Содержимое настройки.
	Результат.Колонки.Добавить("СодержитОтборы", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СодержитПоля", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СодержитСортировки", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СодержитУсловноеОформление", Новый ОписаниеТипов("Булево"));
	
	// Вывод.
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ПредставлениеПоУмолчанию", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Заголовок", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьТолькоФлажок", Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
КонецФункции

Функция ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, УзелКД, НаборСтрокДерева, Подтип = "")
	СтрокаДерева = НаборСтрокДерева.Добавить();
	СтрокаДерева.УзелКД = УзелКД;
	СтрокаДерева.Тип = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(УзелКД));
	СтрокаДерева.Подтип = Подтип;
	Если СтрокаДерева.Тип <> "Настройки" Тогда
		СтрокаДерева.Идентификатор = УзелКД.ИдентификаторПользовательскойНастройки;
	КонецЕсли;
	
	СтрокаДерева.ИдентификаторКД = НастройкиКД.ПолучитьИдентификаторПоОбъекту(УзелКД);
	СтрокаДерева.Глобальная = (Подтип = "Отчет");
	
	Если СтрокаДерева.Тип = "Настройки" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "Группировка"
		Или СтрокаДерева.Тип = "ГруппировкаДиаграммы"
		Или СтрокаДерева.Тип = "ГруппировкаТаблицы" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "Таблица" Тогда
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "Диаграмма" Тогда
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.Тип = "НастройкиВложенногоОбъекта" Тогда
		СтрокаДерева.ДоступнаяНастройкаКД = НастройкиКД.ДоступныеОбъекты.Элементы.Найти(УзелКД.ИдентификаторОбъекта);
	ИначеЕсли СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
		Или СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыДиаграммы" Тогда
		// см. далее.
	Иначе
		Возврат СтрокаДерева;
	КонецЕсли;
	
	ЗаполнитьПредставлениеНастройки(
		СтрокаДерева,
		СтрокаДерева.УзелКД,
		Неопределено,
		СтрокаДерева.ДоступнаяНастройкаКД);
	
	Если СтрокаДерева.ЕстьПоляИОформление Тогда
		СтрокаДерева.Заголовок = ЗаголовокИзПараметровВывода(УзелКД.ПараметрыВывода);
		СтрокаДерева.СодержитПоля               = НастройкиКД.НаличиеВыбораУЭлемента(УзелКД);
		СтрокаДерева.СодержитУсловноеОформление = НастройкиКД.НаличиеУсловногоОформленияУЭлемента(УзелКД);
	КонецЕсли;
	
	Если Не Информация.ТолькоПользовательские Тогда
		СтрокаДерева.ВыводРазрешен = (СтрокаДерева.ИдентификаторКД = Информация.ИдентификаторТекущегоУзлаКД);
	КонецЕсли;
	
	Если ТипЗнч(СтрокаДерева.Идентификатор) = Тип("Строка") И Не ПустаяСтрока(СтрокаДерева.Идентификатор) Тогда
		СвойстваНастройки = Информация.Поиск.ПользовательскиеНастройки.Получить(СтрокаДерева.Идентификатор);
		Если СвойстваНастройки <> Неопределено Тогда
			СтрокаДерева.ПользовательскаяНастройка   = СвойстваНастройки;
			СтрокаДерева.ПользовательскаяНастройкаКД = СвойстваНастройки.ПользовательскаяНастройкаКД;
			ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, СтрокаДерева, Неопределено);
			Если ЗначениеЗаполнено(СтрокаДерева.Заголовок) Тогда
				СвойстваНастройки.Представление = СтрокаДерева.Заголовок;
			КонецЕсли;
			Если Информация.ТолькоПользовательские Тогда
				СтрокаДерева.ВыводРазрешен = СвойстваНастройки.ВыводРазрешен;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаДерева.ЕстьСтруктура Тогда
		Для Каждого ВложенныйЭлемент Из УзелКД.Структура Цикл
			ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, ВложенныйЭлемент, СтрокаДерева.Строки);
		КонецЦикла;
		СтрокаДерева.СодержитОтборы     = НастройкиКД.НаличиеОтбораУЭлемента(УзелКД);
		СтрокаДерева.СодержитСортировки = НастройкиКД.НаличиеПорядкаУЭлемента(УзелКД);
	КонецЕсли;
	
	Если СтрокаДерева.Тип = "Таблица" Тогда
		ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, УзелКД.Строки, СтрокаДерева.Строки, "ТаблицаСтроки");
		ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, УзелКД.Колонки, СтрокаДерева.Строки, "ТаблицаКолонки");
	ИначеЕсли СтрокаДерева.Тип = "Диаграмма" Тогда
		ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, УзелКД.Точки, СтрокаДерева.Строки, "ДиаграммаТочки");
		ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, УзелКД.Серии, СтрокаДерева.Строки, "ДиаграммаСерии");
	ИначеЕсли СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
		Или СтрокаДерева.Тип = "КоллекцияЭлементовСтруктурыДиаграммы" Тогда
		Для Каждого ВложенныйЭлемент Из УзелКД Цикл
			ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, ВложенныйЭлемент, СтрокаДерева.Строки);
		КонецЦикла;
	ИначеЕсли СтрокаДерева.Тип = "НастройкиВложенногоОбъекта" Тогда
		Информация.ЕстьВложенныеОтчеты = Истина;
		ДеревоВариантаЗарегистрироватьУзел(Информация, НастройкиКД, УзелКД.Настройки, СтрокаДерева.Строки);
	КонецЕсли;
	
	Если Не СтрокаДерева.Глобальная Тогда
		Если СтрокаДерева.СодержитПоля Тогда
			Информация.ЕстьВложенныеПоля = Истина;
		КонецЕсли;
		Если СтрокаДерева.СодержитУсловноеОформление Тогда
			Информация.ЕстьВложенноеОформление = Истина;
		КонецЕсли;
		Если СтрокаДерева.СодержитОтборы Тогда
			Информация.ЕстьВложенныеОтборы = Истина;
		КонецЕсли;
		Если СтрокаДерева.СодержитСортировки Тогда
			Информация.ЕстьВложенныеСортировки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрокаДерева;
КонецФункции

Функция ЗаголовокИзПараметровВывода(ПараметрыВывода)
	ВыводитьЗаголовокКД = ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьЗаголовок"));
	Если ВыводитьЗаголовокКД = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	Если ВыводитьЗаголовокКД.Использование = Истина
		И ВыводитьЗаголовокКД.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить Тогда
		Возврат "";
	КонецЕсли;
	// В значении Авто считается что заголовок выводится.
	// Когда параметр ВыводитьЗаголовок отключен это эквивалент значения Авто.
	ЗаголовокКД = ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Заголовок"));
	Если ЗаголовокКД = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	Возврат ЗаголовокКД.Значение;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Настройки варианта

Функция ТаблицаНастроекВарианта()
	Результат = Новый ДеревоЗначений;
	
	// Узлы СКД.
	Результат.Колонки.Добавить("ЭлементКД");
	Результат.Колонки.Добавить("ДоступнаяНастройкаКД");
	Результат.Колонки.Добавить("ПользовательскаяНастройкаКД");
	
	// Прикладная структура.
	Результат.Колонки.Добавить("СтрокаДерева");
	Результат.Колонки.Добавить("ПользовательскаяНастройка");
	Результат.Колонки.Добавить("Владелец");
	Результат.Колонки.Добавить("Глобальная", Новый ОписаниеТипов("Булево"));
	
	// Поиск этой настройки в узле.
	Результат.Колонки.Добавить("ИмяКоллекции", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИдентификаторКД");
	
	// Связь с узлами СКД.
	Результат.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИдентификаторЭлемента", Новый ОписаниеТипов("Строка"));
	
	// Описание настройки.
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ПолеКД");
	Результат.Колонки.Добавить("Значение");
	Результат.Колонки.Добавить("ВидСравнения");
	Результат.Колонки.Добавить("ВводСписком", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ИнформацияОТипах");
	Результат.Колонки.Добавить("ФормаВыбора", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ОтмеченныеЗначения");
	Результат.Колонки.Добавить("ПараметрыВыбора");
	
	Результат.Колонки.Добавить("СвязьПоТипу");
	Результат.Колонки.Добавить("СвязиПараметровВыбора");
	Результат.Колонки.Добавить("СвязиПоМетаданным");
	Результат.Колонки.Добавить("ОграничениеТипа");
	
	// API
	Результат.Колонки.Добавить("ОписаниеТипов");
	Результат.Колонки.Добавить("ЗначенияДляВыбора");
	Результат.Колонки.Добавить("ЗапросЗначенийВыбора");
	Результат.Колонки.Добавить("СписокЗначенийПереопределен",           Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ОграничиватьВыборУказаннымиЗначениями", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("СобытиеПриИзменении", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Ширина", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("ВыводитьВГруппеОсновныхНастроек", Новый ОписаниеТипов("Булево"));
	
	// Вывод.
	Результат.Колонки.Добавить("ПредставлениеПоУмолчанию", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьФлажок", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыборГруппИЭлементов");
	Результат.Колонки.Добавить("ВыводитьТолькоФлажок", Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
КонецФункции

Функция ТаблицаПользовательскихНастроек()
	Результат = Новый ТаблицаЗначений;
	
	// Узлы СКД.
	Результат.Колонки.Добавить("УзелКД");
	Результат.Колонки.Добавить("НастройкаВариантаКД");
	Результат.Колонки.Добавить("ПользовательскаяНастройкаКД");
	Результат.Колонки.Добавить("ДоступнаяНастройкаКД");
	
	// Прикладная структура.
	Результат.Колонки.Добавить("СтрокаДерева");
	Результат.Колонки.Добавить("НастройкаВарианта");
	
	// Поиск этой настройки в узле.
	Результат.Колонки.Добавить("ИдентификаторКД");
	Результат.Колонки.Добавить("ИндексВКоллекции", Новый ОписаниеТипов("Число"));
	
	// Связь с узлами СКД.
	Результат.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИдентификаторЭлемента", Новый ОписаниеТипов("Строка"));
	
	// Описание настройки.
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	
	Результат.Колонки.Добавить("ПолеКД");
	Результат.Колонки.Добавить("Значение");
	Результат.Колонки.Добавить("ВидСравнения");
	Результат.Колонки.Добавить("ВводСписком", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ИнформацияОТипах");
	
	Результат.Колонки.Добавить("ОтмеченныеЗначения");
	Результат.Колонки.Добавить("ПараметрыВыбора");
	
	// API
	Результат.Колонки.Добавить("ОписаниеТипов");
	Результат.Колонки.Добавить("ЗначенияДляВыбора");
	Результат.Колонки.Добавить("ЗапросЗначенийВыбора");
	Результат.Колонки.Добавить("ОграничиватьВыборУказаннымиЗначениями", Новый ОписаниеТипов("Булево"));
	
	// Вывод.
	Результат.Колонки.Добавить("ПредставлениеПоУмолчанию", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Быстрая", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Обычная", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьФлажок", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ВыводитьТолькоФлажок", Новый ОписаниеТипов("Булево"));
	
	Результат.Колонки.Добавить("ТипЭлементов", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВыборГруппИЭлементов");
	
	// Дополнительные свойства.
	Результат.Колонки.Добавить("Дополнительно", Новый ОписаниеТипов("Структура"));
	
	Возврат Результат;
КонецФункции

Функция ТаблицаСвязейПоТипу()
	// Связи из СКД.
	ТаблицаСвязейПоТипу = Новый ТаблицаЗначений;
	ТаблицаСвязейПоТипу.Колонки.Добавить("Ведущий");
	ТаблицаСвязейПоТипу.Колонки.Добавить("ВедущийПолеКД");
	ТаблицаСвязейПоТипу.Колонки.Добавить("Подчиненный");
	ТаблицаСвязейПоТипу.Колонки.Добавить("ПодчиненныйИмяПараметра");
	
	Возврат ТаблицаСвязейПоТипу;
КонецФункции

Функция ТаблицаСвязейПараметровВыбора()
	ТаблицаСвязейПараметровВыбора = Новый ТаблицаЗначений;
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("Ведущий");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("ВедущийПолеКД");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("Подчиненный");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("ПодчиненныйИмяПараметра");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("Действие");
	
	Возврат ТаблицаСвязейПараметровВыбора;
КонецФункции

Функция ТаблицаСвязейОбъектовМетаданных(НастройкиОтчета, ОтчетОбъектИлиПолноеИмя)
	// Связи из метаданных.
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ВедущийТип",          Новый ОписаниеТипов("Тип"));
	Результат.Колонки.Добавить("ПодчиненныйТип",      Новый ОписаниеТипов("Тип"));
	Результат.Колонки.Добавить("ПодчиненныйРеквизит", Новый ОписаниеТипов("Строка"));
	
	// Механизмы расширения.
	ОтчетыПереопределяемый.ДополнитьСвязиОбъектовМетаданных(Результат); // Глобальные связи...
	Если НастройкиОтчета.События.ДополнитьСвязиОбъектовМетаданных Тогда // ... можно переопределить локально для отчета.
		ОтчетОбъект(ОтчетОбъектИлиПолноеИмя).ДополнитьСвязиОбъектовМетаданных(Результат);
	КонецЕсли;
	
	Результат.Колонки.Добавить("ЕстьВедущие",     Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ЕстьПодчиненные", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Ведущие",     Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("Подчиненные", Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("ВедущийПолноеИмя",     Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ПодчиненныйПолноеИмя", Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;
КонецФункции

Процедура ЗарегистрироватьНастройкиВарианта(НастройкиКД, Информация)
	ДеревоВарианта = Информация.ДеревоВарианта;
	НастройкиВарианта = Информация.НастройкиВарианта;
	
	Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьСтруктура", Истина), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		
		// Настройки, свойство Отбор
		// Группировка, свойство Отбор
		// ГруппировкаТаблицы, свойство Отбор.
		// ГруппировкаДиаграммы, свойство Отбор.
		
		// Настройки, свойство Отбор.Элементы.
		// Группировка, свойство Отбор.Элементы
		// ГруппировкаТаблицы, свойство Отбор.Элементы
		// ГруппировкаДиаграммы, свойство Отбор.Элементы.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Отбор");
		
		// Настройки, свойство Порядок.
		// Группировка, свойство Порядок
		// ГруппировкаТаблицы, свойство Порядок.
		// ГруппировкаДиаграммы, свойство Порядок.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Порядок");
		
		// Настройки, свойство Структура.
		// Группировка, свойство Структура.
		// ГруппировкаТаблицы, свойство Структура.
		// ГруппировкаДиаграммы, свойство Структура.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Структура");
		
	КонецЦикла;
	
	Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьПоляИОформление", Истина), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		
		// Настройки, свойство Выбор
		// Таблица, свойство Выбор
		// Диаграмма, свойство Выбор
		// Группировка, свойство Выбор
		// ГруппировкаДиаграммы, свойство Выбор.
		// ГруппировкаТаблицы, свойство Выбор.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "Выбор");
		
		// Настройки, свойство УсловноеОформление.
		// Таблица, свойство УсловноеОформление.
		// Диаграмма, свойство УсловноеОформление.
		// Группировка, свойство УсловноеОформление.
		// ГруппировкаДиаграммы, свойство УсловноеОформление.
		// ГруппировкаТаблицы, свойство УсловноеОформление.
		
		// Настройки, свойство УсловноеОформление.Элементы.
		// Таблица, свойство УсловноеОформление.Элементы.
		// Диаграмма, свойство УсловноеОформление.Элементы.
		// Группировка, свойство УсловноеОформление.Элементы
		// ГруппировкаДиаграммы, свойство УсловноеОформление.Элементы
		// ГруппировкаТаблицы, свойство УсловноеОформление.Элементы.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "УсловноеОформление");
		
		// Настройки, свойство ПараметрыВывода
		// Таблица, свойство ПараметрыВывода
		// Диаграмма, свойство ПараметрыВывода
		// Группировка, свойство ПараметрыВывода
		// ГруппировкаДиаграммы, свойство ПараметрыВывода
		// ГруппировкаТаблицы, свойство ПараметрыВывода.
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "ПараметрыВывода");
		
	КонецЦикла;
	
	Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("Тип", "Настройки"), Истина);
	Для Каждого СтрокаДерева Из Найденные Цикл
		
		// Настройки, свойство ПараметрыДанных, метод НайтиЗначениеПараметра().
		
		ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, "ПараметрыДанных");
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьУзелНастроек(НастройкиКД, Информация, СтрокаДерева, ИмяКоллекции, НаборЭлементов = Неопределено, Родитель = Неопределено, Владелец = Неопределено)
	УзелКД = СтрокаДерева.УзелКД[ИмяКоллекции];
	
	Владелец = Информация.НастройкиВарианта.Строки.Добавить();
	Владелец.СтрокаДерева = СтрокаДерева;
	Если ИмяКоллекции <> "ПараметрыДанных" И ИмяКоллекции <> "ПараметрыВывода" Тогда
		Владелец.Идентификатор = УзелКД.ИдентификаторПользовательскойНастройки;
	КонецЕсли;
	Владелец.Тип           = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(УзелКД));
	Владелец.ИмяКоллекции  = ИмяКоллекции;
	Владелец.Глобальная    = СтрокаДерева.Глобальная;
	Владелец.ЭлементКД     = УзелКД;
	Владелец.ВыводРазрешен = Не Информация.ТолькоПользовательские И СтрокаДерева.ВыводРазрешен;
	
	Если ТипЗнч(Владелец.Идентификатор) = Тип("Строка") И Не ПустаяСтрока(Владелец.Идентификатор) Тогда
		СвойстваНастройки = Информация.Поиск.ПользовательскиеНастройки.Получить(Владелец.Идентификатор);
		Если СвойстваНастройки <> Неопределено Тогда
			Владелец.ПользовательскаяНастройка = СвойстваНастройки;
			ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, Неопределено, Владелец);
			СвойстваНастройки.ПараметрыВыбора = Новый Массив;
			Владелец.ПараметрыВыбора          = Новый Массив;
			Владелец.СвязиПараметровВыбора    = Новый Массив;
			Владелец.СвязиПоМетаданным        = Новый Массив;
			Если Информация.ТолькоПользовательские Тогда
				Владелец.ВыводРазрешен = СвойстваНастройки.ВыводРазрешен;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Владелец.ВыводРазрешен Тогда
		Если Владелец.ПользовательскаяНастройка = Неопределено Тогда
			ЗаполнитьПредставлениеНастройки(
				Владелец,
				Владелец.ЭлементКД,
				Неопределено,
				Неопределено);
		Иначе
			ЗаполнитьЗначенияСвойств(Владелец, Владелец.ПользовательскаяНастройка, "Представление, ВыводитьТолькоФлажок");
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяКоллекции = "Отбор"
		Или ИмяКоллекции = "ПараметрыДанных"
		Или ИмяКоллекции = "ПараметрыВывода"
		Или ИмяКоллекции = "УсловноеОформление" Тогда
		ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, УзелКД.Элементы, Владелец, Владелец);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, НаборЭлементов, Владелец, Родитель)
	Для Каждого ЭлементКД Из НаборЭлементов Цикл
		НастройкаВарианта = Родитель.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НастройкаВарианта, Владелец, "СтрокаДерева, ИмяКоллекции, Глобальная");
		НастройкаВарианта.Идентификатор = ЭлементКД.ИдентификаторПользовательскойНастройки;
		НастройкаВарианта.Тип = ОтчетыКлиентСервер.ТипНастройкиСтрокой(ТипЗнч(ЭлементКД));
		НастройкаВарианта.ИдентификаторКД = УзелКД.ПолучитьИдентификаторПоОбъекту(ЭлементКД);
		НастройкаВарианта.Владелец = Владелец;
		НастройкаВарианта.ЭлементКД = ЭлементКД;
		НастройкаВарианта.ВыводРазрешен = Не Информация.ТолькоПользовательские И Владелец.ВыводРазрешен;
		НастройкаВарианта.ВыводитьФлажок = Истина;
		
		Если НастройкаВарианта.Тип = "ВыбранныеПоля"
			Или НастройкаВарианта.Тип = "Порядок"
			Или НастройкаВарианта.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
			Или НастройкаВарианта.Тип = "КоллекцияЭлементовСтруктурыДиаграммы"
			Или НастройкаВарианта.Тип = "Отбор"
			Или НастройкаВарианта.Тип = "УсловноеОформление"
			Или НастройкаВарианта.Тип = "СтруктураНастроек" Тогда
			НастройкаВарианта.ВыводитьФлажок = Ложь;
		КонецЕсли;
		
		Если НастройкаВарианта.Тип = "ЭлементОтбора"
			Или НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
			ЗарегистрироватьПоле(Информация, УзелКД, ЭлементКД, НастройкаВарианта);
			Если НастройкаВарианта.ДоступнаяНастройкаКД = Неопределено Тогда
				НастройкаВарианта.ВыводРазрешен = Ложь;
				Продолжить;
			КонецЕсли;
			ЗарегистрироватьТипыИСвязи(Информация, УзелКД, ЭлементКД, НастройкаВарианта);
		КонецЕсли;
		
		СвойстваНастройки = Неопределено;
		Если ТипЗнч(НастройкаВарианта.Идентификатор) = Тип("Строка") И Не ПустаяСтрока(НастройкаВарианта.Идентификатор) Тогда
			СвойстваНастройки = Информация.Поиск.ПользовательскиеНастройки.Получить(НастройкаВарианта.Идентификатор);
		КонецЕсли;
		Если СвойстваНастройки <> Неопределено Тогда
			НастройкаВарианта.ПользовательскаяНастройка = СвойстваНастройки;
			ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, Неопределено, НастройкаВарианта);
			Если Информация.ТолькоПользовательские Тогда
				НастройкаВарианта.ВыводРазрешен = СвойстваНастройки.ВыводРазрешен;
				НастройкаВарианта.Значение      = СвойстваНастройки.Значение;
				НастройкаВарианта.ВидСравнения  = СвойстваНастройки.ВидСравнения;
			КонецЕсли;
		КонецЕсли;
		
		Если НастройкаВарианта.ВыводРазрешен Тогда
			ЗаполнитьПредставлениеНастройки(
				НастройкаВарианта,
				НастройкаВарианта.ЭлементКД,
				Неопределено,
				НастройкаВарианта.ДоступнаяНастройкаКД);
			Если НастройкаВарианта.Тип = "ЭлементОтбора"
				Или НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
				ПриОпределенииПараметровВыбора(Информация, НастройкаВарианта);
			КонецЕсли;
		КонецЕсли;
		
		Если НастройкаВарианта.Тип = "ГруппаЭлементовОтбора" Тогда
			НастройкаВарианта.Значение = ЭлементКД.ТипГруппы;
			ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, ЭлементКД.Элементы, Владелец, НастройкаВарианта);
		ИначеЕсли НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
			ЗарегистрироватьЭлементыНастроек(Информация, УзелКД, ЭлементКД.ЗначенияВложенныхПараметров, Владелец, НастройкаВарианта);
		КонецЕсли;
		
		Если СвойстваНастройки <> Неопределено Тогда
			СвойстваНастройки.ЗначенияДляВыбора = НастройкаВарианта.ЗначенияДляВыбора;
			СвойстваНастройки.ПараметрыВыбора   = НастройкаВарианта.ПараметрыВыбора;
			СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями = НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗарегистрироватьПоле(Информация, УзелКД, ЭлементКД, НастройкаВарианта)
	Если ПустаяСтрока(НастройкаВарианта.Идентификатор) Тогда
		Идентификатор = Строка(НастройкаВарианта.СтрокаДерева.ИдентификаторКД);
		Если Не ПустаяСтрока(Идентификатор) Тогда
			Идентификатор = Идентификатор + "_";
		КонецЕсли;
		НастройкаВарианта.Идентификатор = Идентификатор + НастройкаВарианта.ИмяКоллекции + "_" + Строка(НастройкаВарианта.ИдентификаторКД);
	КонецЕсли;
	НастройкаВарианта.ИдентификаторЭлемента = ОтчетыКлиентСервер.ПривестиИдентификаторКИмени(НастройкаВарианта.Идентификатор);
	
	Если НастройкаВарианта.Тип = "ЗначениеПараметраНастроек" Тогда
		ДоступныеПараметры = УзелКД.ДоступныеПараметры;
		Если ДоступныеПараметры = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ДоступнаяНастройкаКД = ДоступныеПараметры.НайтиПараметр(ЭлементКД.Параметр);
		Если ДоступнаяНастройкаКД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		// ДоступнаяНастройкаКД имеет тип ДоступныйПараметрКомпоновкиДанных:
		//   Видимость - Булево - Видимость параметра при редактировании значений.
		//   Параметр - ПараметрКомпоновкиДанных - Имя параметра.
		//   Значение - Произвольный - Начальное значение.
		//   ДоступенСписокЗначений - Булево - Можно ли указывать несколько значений.
		//   ДоступныеЗначения - СписокЗначений, Неопределено - Значения, доступные для выбора.
		//   БыстрыйВыбор, ВыборГруппИЭлементов, ЗапрещатьНезаполненныеЗначения,
		//   Использование, Маска, СвязьПоТипу, ФормаВыбора, ФорматРедактирования.
		Если Не ДоступнаяНастройкаКД.Видимость Тогда
			НастройкаВарианта.ВыводРазрешен = Ложь;
		КонецЕсли;
		НастройкаВарианта.ДоступнаяНастройкаКД = ДоступнаяНастройкаКД;
		НастройкаВарианта.ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(ЭлементКД.Параметр));
		НастройкаВарианта.Значение = ЭлементКД.Значение;
		Если ДоступнаяНастройкаКД.ДоступенСписокЗначений Тогда
			НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		Если ДоступнаяНастройкаКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда Тогда
			НастройкаВарианта.ВыводитьФлажок = Ложь;
		КонецЕсли;
	Иначе
		ДоступныеПоляОтбора = УзелКД.ДоступныеПоляОтбора;
		Если ДоступныеПоляОтбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ДоступнаяНастройкаКД = ДоступныеПоляОтбора.НайтиПоле(ЭлементКД.ЛевоеЗначение);
		Если ДоступнаяНастройкаКД = Неопределено Тогда
			Возврат;
		КонецЕсли;
		// ДоступнаяНастройкаКД имеет тип ДоступноеПолеОтбораКомпоновкиДанных.
		НастройкаВарианта.ДоступнаяНастройкаКД = ДоступнаяНастройкаКД;
		НастройкаВарианта.ПолеКД       = ЭлементКД.ЛевоеЗначение;
		НастройкаВарианта.Значение     = ЭлементКД.ПравоеЗначение;
		НастройкаВарианта.ВидСравнения = ЭлементКД.ВидСравнения;
	КонецЕсли;
	
	Если НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		НастройкаВарианта.ВводСписком = Истина;
		НастройкаВарианта.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	ИначеЕсли НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
		Или НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
		НастройкаВарианта.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
	Иначе
		НастройкаВарианта.ВыборГруппИЭлементов = ОтчетыКлиентСервер.ПривестиЗначениеКТипуГруппыИЭлементы(ДоступнаяНастройкаКД.ВыборГруппИЭлементов);
	КонецЕсли;
	
	НастройкаВарианта.ОписаниеТипов = ДоступнаяНастройкаКД.ТипЗначения;
	НастройкаВарианта.ФормаВыбора = ДоступнаяНастройкаКД.ФормаВыбора;
	
	Если Информация.Поиск.НастройкиВариантаПоПолюКД.Получить(НастройкаВарианта.ПолеКД) = Неопределено Тогда
		Информация.Поиск.НастройкиВариантаПоПолюКД.Вставить(НастройкаВарианта.ПолеКД, НастройкаВарианта);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьТипыИСвязи(Информация, УзелКД, ЭлементКД, НастройкаВарианта)
	
	///////////////////////////////////////////////////////////////////
	// Информация о типах.
	
	НастройкаВарианта.СвязиПоМетаданным     = Новый Массив;
	НастройкаВарианта.СвязиПараметровВыбора = Новый Массив;
	НастройкаВарианта.ПараметрыВыбора       = Новый Массив;
	
	Если НастройкаВарианта.ВводСписком Тогда
		НастройкаВарианта.ОтмеченныеЗначения = ОтчетыКлиентСервер.ЗначенияСписком(НастройкаВарианта.Значение);
	КонецЕсли;
	НастройкаВарианта.ЗапросЗначенийВыбора = Новый Запрос;
	
	ШаблонЗапросаПеречислений = "ВЫБРАТЬ Ссылка ИЗ &ИмяПеречисления";
	ТекстЗапросаЗначений = "";
	
	ИнформацияОТипах = ОтчетыКлиентСервер.АнализТипов(НастройкаВарианта.ОписаниеТипов, Истина);
	ИнформацияОТипах.Вставить("СодержитСсылочныеТипы", Ложь);
	ИнформацияОТипах.Вставить("КоличествоПеречислений",         0);
	ИнформацияОТипах.Вставить("КоличествоПрочихСсылочныхТипов", 0);
	ИнформацияОТипах.Вставить("Перечисления",        Новый Массив);
	ИнформацияОТипах.Вставить("ПрочиеСсылочныеТипы", Новый Массив);
	Для Каждого Тип Из ИнформацияОТипах.ОбъектныеТипы Цикл
		ПолноеИмя = Информация.СоответствиеИменОбъектовМетаданных.Получить(Тип);
		Если ПолноеИмя = Неопределено Тогда // Регистрация имени объекта метаданных.
			ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
			Если ОбъектМетаданных = Неопределено Тогда
				ПолноеИмя = -1;
			Иначе
				ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
			КонецЕсли;
			Информация.СоответствиеИменОбъектовМетаданных.Вставить(Тип, ПолноеИмя);
		КонецЕсли;
		Если ПолноеИмя = -1 Тогда
			Продолжить;
		КонецЕсли;
		
		ИнформацияОТипах.СодержитСсылочныеТипы = Истина;
		
		Если ВРег(Лев(ПолноеИмя, 13)) = "ПЕРЕЧИСЛЕНИЕ." Тогда
			ИнформацияОТипах.Перечисления.Добавить(ПолноеИмя);
			ИнформацияОТипах.КоличествоПеречислений = ИнформацияОТипах.КоличествоПеречислений + 1;
			Если ТекстЗапросаЗначений <> "" Тогда
				ТекстЗапросаЗначений = ТекстЗапросаЗначений + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
			КонецЕсли;
			ТекстЗапросаЗначений = ТекстЗапросаЗначений + СтрЗаменить(ШаблонЗапросаПеречислений, "&ИмяПеречисления", ПолноеИмя);
		Иначе
			ИнформацияОТипах.ПрочиеСсылочныеТипы.Добавить(ПолноеИмя);
			ИнформацияОТипах.КоличествоПрочихСсылочныхТипов = ИнформацияОТипах.КоличествоПрочихСсылочныхТипов + 1;
		КонецЕсли;
		
		// Поиск типа в глобальных связях среди подчиненных.
		Найденные = Информация.Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ПодчиненныйТип", Тип));
		Для Каждого СвязьПоМетаданным Из Найденные Цикл // Регистрация настройки как подчиненной.
			СвязьПоМетаданным.ЕстьПодчиненные = Истина;
			СвязьПоМетаданным.Подчиненные.Добавить(НастройкаВарианта);
		КонецЦикла;
		
		// Поиск типа в глобальных связях среди ведущих.
		Если НастройкаВарианта.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			// Поле может быть ведущим если имеет вид сравнения "Равно".
			Найденные = Информация.Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ВедущийТип", Тип));
			Для Каждого СвязьПоМетаданным Из Найденные Цикл // Регистрация настройки как ведущей.
				СвязьПоМетаданным.ЕстьВедущие = Истина;
				СвязьПоМетаданным.Ведущие.Добавить(НастройкаВарианта);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	НастройкаВарианта.ИнформацияОТипах = ИнформацияОТипах;
	НастройкаВарианта.ОписаниеТипов = ИнформацияОТипах.ОписаниеТиповДляФормы;
	
	///////////////////////////////////////////////////////////////////
	// Информация о связях и параметрах выбора.
	
	ДоступнаяНастройкаКД = НастройкаВарианта.ДоступнаяНастройкаКД;
	
	Если ЗначениеЗаполнено(ДоступнаяНастройкаКД.СвязьПоТипу) Тогда
		СтрокаСвязи = Информация.Связи.ПоТипу.Добавить();
		СтрокаСвязи.Подчиненный   = НастройкаВарианта;
		СтрокаСвязи.ВедущийПолеКД = ДоступнаяНастройкаКД.СвязьПоТипу.Поле;
		СтрокаСвязи.ПодчиненныйИмяПараметра = ДоступнаяНастройкаКД.СвязьПоТипу.ЭлементСвязи;
	КонецЕсли;
	
	Для Каждого СтрокаСвязи Из ДоступнаяНастройкаКД.ПолучитьСвязиПараметровВыбора() Цикл
		Если ПустаяСтрока(Строка(СтрокаСвязи.Поле)) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаСвязиПараметров = Информация.Связи.ПараметровВыбора.Добавить();
		СтрокаСвязиПараметров.Подчиненный             = НастройкаВарианта;
		СтрокаСвязиПараметров.ПодчиненныйИмяПараметра = СтрокаСвязи.Имя;
		СтрокаСвязиПараметров.ВедущийПолеКД           = СтрокаСвязи.Поле;
		СтрокаСвязиПараметров.Действие                = СтрокаСвязи.ИзменениеЗначения;
	КонецЦикла;
	
	Для Каждого ПараметрВыбораКД Из ДоступнаяНастройкаКД.ПолучитьПараметрыВыбора() Цикл
		НастройкаВарианта.ПараметрыВыбора.Добавить(Новый ПараметрВыбора(ПараметрВыбораКД.Имя, ПараметрВыбораКД.Значение));
	КонецЦикла;
	
	///////////////////////////////////////////////////////////////////
	// Список значений.
	
	Если ТипЗнч(ДоступнаяНастройкаКД.ДоступныеЗначения) = Тип("СписокЗначений") Тогда
		НастройкаВарианта.ЗначенияДляВыбора = ДоступнаяНастройкаКД.ДоступныеЗначения;
		НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями = НастройкаВарианта.ЗначенияДляВыбора.Количество() > 0;
	Иначе
		Если ИнформацияОТипах.КоличествоПеречислений = ИнформацияОТипах.КоличествоТипов Тогда
			НастройкаВарианта.ЗапросЗначенийВыбора.Текст = ТекстЗапросаЗначений;
			НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями = Истина; // Только перечисления.
		КонецЕсли;
		
		НастройкаВарианта.ЗначенияДляВыбора = Новый СписокЗначений;
		НастройкиСохраненныеРанее = Информация.ДополнительныеНастройкиЭлементов[НастройкаВарианта.ИдентификаторЭлемента];
		Ограничивать = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиСохраненныеРанее, "ОграничиватьВыборУказаннымиЗначениями");
		Если НастройкиСохраненныеРанее <> Неопределено И Ограничивать = Ложь Тогда
			СтарыеЗначенияДляВыбора = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиСохраненныеРанее, "ЗначенияДляВыбора");
			Если ТипЗнч(СтарыеЗначенияДляВыбора) = Тип("СписокЗначений") Тогда
				НастройкаВарианта.ЗначенияДляВыбора.ТипЗначения = НастройкаВарианта.ОписаниеТипов;
				Для Каждого СтарыйЭлементСписка Из СтарыеЗначенияДляВыбора Цикл
					Если Не НастройкаВарианта.ОписаниеТипов.СодержитТип(ТипЗнч(СтарыйЭлементСписка.Значение)) Тогда
						Продолжить;
					КонецЕсли;
					Если НастройкаВарианта.ЗначенияДляВыбора.НайтиПоЗначению(СтарыйЭлементСписка.Значение) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					ЭлементПриемник = НастройкаВарианта.ЗначенияДляВыбора.Добавить();
					ЭлементПриемник.Значение = СтарыйЭлементСписка.Значение;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииПараметровВыбора(Информация, НастройкаВарианта)
	ПредставлениеДо = Строка(НастройкаВарианта.ЗначенияДляВыбора);
	КоличествоДо = НастройкаВарианта.ЗначенияДляВыбора.Количество();
	
	// Механизмы расширения.
	// Глобальные настройки вывода типов.
	ОтчетыПереопределяемый.ПриОпределенииПараметровВыбора(Неопределено, НастройкаВарианта);
	// Локальное переопределение для отчета.
	Если Информация.НастройкиОтчета.События.ПриОпределенииПараметровВыбора Тогда
		ОтчетОбъект(Информация.ОтчетОбъектИлиПолноеИмя).ПриОпределенииПараметровВыбора(Неопределено, НастройкаВарианта);
	КонецЕсли;
	
	// Автоматическое заполнение.
	Если НастройкаВарианта.ЗапросЗначенийВыбора.Текст <> "" Тогда
		ДобавляемыеЗначения = НастройкаВарианта.ЗапросЗначенийВыбора.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
		Для Каждого ЗначениеВФорме Из ДобавляемыеЗначения Цикл
			ОтчетыКлиентСервер.ДобавитьУникальноеЗначениеВСписок(НастройкаВарианта.ЗначенияДляВыбора, ЗначениеВФорме, Неопределено, Ложь);
		КонецЦикла;
		НастройкаВарианта.ЗначенияДляВыбора.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
	КонецЕсли;
	
	// Удаление значений, выбор которых запрещен.
	Если НастройкаВарианта.ВводСписком
		И НастройкаВарианта.ОграничиватьВыборУказаннымиЗначениями
		И ТипЗнч(НастройкаВарианта.Значение) = Тип("СписокЗначений")
		И ТипЗнч(НастройкаВарианта.ЗначенияДляВыбора) = Тип("СписокЗначений") Тогда
		Список = НастройкаВарианта.Значение;
		Количество = Список.Количество();
		Для Номер = 1 По Количество Цикл
			ОбратныйИндекс = Количество - Номер;
			Значение = Список[ОбратныйИндекс].Значение;
			Если НастройкаВарианта.ЗначенияДляВыбора.НайтиПоЗначению(Значение) = Неопределено Тогда
				Список.Удалить(ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если КоличествоДо <> НастройкаВарианта.ЗначенияДляВыбора.Количество()
		Или ПредставлениеДо <> Строка(НастройкаВарианта.ЗначенияДляВыбора) Тогда
		НастройкаВарианта.СписокЗначенийПереопределен = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ЗарегистрироватьСвязиОтВедущих(Информация)
	Связи = Информация.Связи;
	
	// Регистрация связи параметров выбора (динамическая связь, отключаемая флажком Использование).
	Найденные = Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ЕстьПодчиненные, ЕстьВедущие", Истина, Истина));
	Для Каждого СвязьПоМетаданным Из Найденные Цикл
		Для Каждого Ведущий Из СвязьПоМетаданным.Ведущие Цикл
			Для Каждого Подчиненный Из СвязьПоМетаданным.Подчиненные Цикл
				Если Ведущий.ВыводРазрешен Тогда // Отключаемая связь.
					ОписаниеСвязи = Новый Структура;
					ОписаниеСвязи.Вставить("ТипСвязи",                "ПоМетаданным");
					ОписаниеСвязи.Вставить("Ведущий",                 Ведущий);
					ОписаниеСвязи.Вставить("Подчиненный",             Подчиненный);
					ОписаниеСвязи.Вставить("ВедущийТип",              СвязьПоМетаданным.ВедущийТип);
					ОписаниеСвязи.Вставить("ПодчиненныйТип",          СвязьПоМетаданным.ПодчиненныйТип);
					ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметра", СвязьПоМетаданным.ПодчиненныйРеквизит);
					Информация.ОтключаемыеСвязи.Добавить(ОписаниеСвязи);
					Подчиненный.СвязиПоМетаданным.Добавить(ОписаниеСвязи);
				Иначе // Фиксированный параметр выбора.
					Подчиненный.ПараметрыВыбора.Добавить(Новый ПараметрВыбора(СвязьПоМетаданным.ПодчиненныйРеквизит, Ведущий.Значение));
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Связи по типу.
	Для Каждого СвязьПоТипу Из Связи.ПоТипу Цикл
		Ведущий = Информация.Поиск.НастройкиВариантаПоПолюКД.Получить(СвязьПоТипу.ВедущийПолеКД);
		Если Ведущий = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Подчиненный = СвязьПоТипу.Подчиненный;
		Если Ведущий.ВыводРазрешен Тогда // Отключаемая связь.
			ОписаниеСвязи = Новый Структура;
			ОписаниеСвязи.Вставить("ТипСвязи",                "ПоТипу");
			ОписаниеСвязи.Вставить("Ведущий",                 Ведущий);
			ОписаниеСвязи.Вставить("Подчиненный",             Подчиненный);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметра", СвязьПоТипу.ПодчиненныйИмяПараметра);
			Информация.ОтключаемыеСвязи.Добавить(ОписаниеСвязи);
			Подчиненный.СвязьПоТипу = ОписаниеСвязи;
		Иначе // Фиксированное ограничения типа.
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(Ведущий.Значение));
			Подчиненный.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
		КонецЕсли;
	КонецЦикла;
	
	// Связи параметров выбора.
	Для Каждого СвязьПараметровВыбора Из Связи.ПараметровВыбора Цикл
		Ведущий     = СвязьПараметровВыбора.Ведущий;
		Подчиненный = СвязьПараметровВыбора.Подчиненный;
		Если Ведущий = Неопределено Тогда
			ЛучшийВариант = 99;
			Найденные = Информация.НастройкиВарианта.Строки.НайтиСтроки(Новый Структура("ПолеКД", СвязьПараметровВыбора.ВедущийПолеКД), Истина);
			Для Каждого ПотенциальныйРодитель Из Найденные Цикл
				Если ПотенциальныйРодитель.Родитель = Подчиненный.Родитель Тогда // Элементы в одной группе.
					Если Не ПустаяСтрока(ПотенциальныйРодитель.ИдентификаторЭлемента) Тогда // Ведущий выведен в пользовательские.
						Ведущий = ПотенциальныйРодитель;
						ЛучшийВариант = 0;
						Прервать; // Самый лучший вариант.
					Иначе
						Ведущий = ПотенциальныйРодитель;
						ЛучшийВариант = 1;
					КонецЕсли;
				ИначеЕсли ЛучшийВариант > 2 И ПотенциальныйРодитель.Владелец = Подчиненный.Владелец Тогда // Элементы в одной коллекции.
					Если Не ПустаяСтрока(ПотенциальныйРодитель.ИдентификаторЭлемента) Тогда // Ведущий выведен в пользовательские.
						Если ЛучшийВариант > 2 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 2;
						КонецЕсли;
					Иначе
						Если ЛучшийВариант > 3 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 3;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЛучшийВариант > 4 И ПотенциальныйРодитель.СтрокаДерева = Подчиненный.СтрокаДерева Тогда // Элементы в одном узле.
					Если Не ПустаяСтрока(ПотенциальныйРодитель.ИдентификаторЭлемента) Тогда // Ведущий выведен в пользовательские.
						Если ЛучшийВариант > 4 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 4;
						КонецЕсли;
					Иначе
						Если ЛучшийВариант > 5 Тогда
							Ведущий = ПотенциальныйРодитель;
							ЛучшийВариант = 5;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЛучшийВариант > 6 Тогда
					Ведущий = ПотенциальныйРодитель;
					ЛучшийВариант = 6;
				КонецЕсли;
			КонецЦикла;
			Если Ведущий = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если Ведущий.ВыводРазрешен Тогда // Отключаемая связь.
			ОписаниеСвязи = Новый Структура;
			ОписаниеСвязи.Вставить("ТипСвязи",      "ПараметровВыбора");
			ОписаниеСвязи.Вставить("Ведущий",       Ведущий);
			ОписаниеСвязи.Вставить("Подчиненный",   Подчиненный);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметра", СвязьПараметровВыбора.ПодчиненныйИмяПараметра);
			ОписаниеСвязи.Вставить("ПодчиненныйДействие",     СвязьПараметровВыбора.Действие);
			Информация.ОтключаемыеСвязи.Добавить(ОписаниеСвязи);
			Подчиненный.СвязиПараметровВыбора.Добавить(ОписаниеСвязи);
		Иначе // Фиксированный параметр выбора.
			Если ТипЗнч(Ведущий.Значение) = Тип("ПолеКомпоновкиДанных") Тогда
				Продолжить; // Расширенная работа с отборами по полю компоновки данных не поддерживается.
			КонецЕсли;
			Попытка
				ЗначениеЗаполнено = ЗначениеЗаполнено(Ведущий.Значение);
			Исключение
				ЗначениеЗаполнено = Истина;
			КонецПопытки;
			Если Не ЗначениеЗаполнено Тогда
				Продолжить;
			КонецЕсли;
			Подчиненный.ПараметрыВыбора.Добавить(Новый ПараметрВыбора(СвязьПараметровВыбора.ПодчиненныйИмяПараметра, Ведущий.Значение));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьФиксированныеОтборы(СтруктураОтборов, НастройкиКД, НастройкиОтчета) Экспорт
	ПараметрыКД = НастройкиКД.ПараметрыДанных;
	ОтборыКД = НастройкиКД.Отбор;
	Недоступный = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	Для Каждого КлючИЗначение Из СтруктураОтборов Цикл
		Имя = КлючИЗначение.Ключ;
		Значение = КлючИЗначение.Значение;
		Если ТипЗнч(Значение) = Тип("ФиксированныйМассив") Тогда
			Значение = Новый Массив(Значение);
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("Массив") Тогда
			Список = Новый СписокЗначений;
			Список.ЗагрузитьЗначения(Значение);
			Значение = Список;
		КонецЕсли;
		ПараметрКД = ПараметрыКД.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(Имя));
		Если ТипЗнч(ПараметрКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ПараметрКД.ИдентификаторПользовательскойНастройки = "";
			ПараметрКД.Использование    = Истина;
			ПараметрКД.РежимОтображения = Недоступный;
			ПараметрКД.Значение         = Значение;
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Значение) = Тип("СписокЗначений") Тогда
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			ВидСравненияКД = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборыКД, Имя, Значение, ВидСравненияКД, , Истина, Недоступный, "");
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Пользовательские настройки

Функция ЗарегистрироватьПользовательскуюНастройку(Информация, СвойстваНастройки, СтрокаДерева, НастройкаВарианта)
	ПользовательскаяНастройкаКД = СвойстваНастройки.ПользовательскаяНастройкаКД;
	
	РежимОтображения = ПользовательскаяНастройкаКД.РежимОтображения;
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
		Возврат СвойстваНастройки;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СвойстваНастройки.Идентификатор) Тогда
		Возврат СвойстваНастройки;
	КонецЕсли;
	СвойстваНастройки.ИдентификаторЭлемента = ОтчетыКлиентСервер.ПривестиИдентификаторКИмени(СвойстваНастройки.Идентификатор);
	
	Если НастройкаВарианта <> Неопределено Тогда
		Если НастройкаВарианта.Владелец <> Неопределено Тогда
			СвойстваНастройки.УзелКД = НастройкаВарианта.Владелец.ЭлементКД;
		КонецЕсли;
		СвойстваНастройки.СтрокаДерева         = НастройкаВарианта.СтрокаДерева;
		СвойстваНастройки.НастройкаВариантаКД  = НастройкаВарианта.ЭлементКД;
		СвойстваНастройки.НастройкаВарианта    = НастройкаВарианта;
		СвойстваНастройки.Подтип               = НастройкаВарианта.Подтип;
		СвойстваНастройки.ПолеКД               = НастройкаВарианта.ПолеКД;
		СвойстваНастройки.ДоступнаяНастройкаКД = НастройкаВарианта.ДоступнаяНастройкаКД;
		СвойстваНастройки.ИнформацияОТипах     = НастройкаВарианта.ИнформацияОТипах;
		СвойстваНастройки.ОписаниеТипов        = НастройкаВарианта.ОписаниеТипов;
		Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто Тогда
			РежимОтображения = СвойстваНастройки.НастройкаВариантаКД.РежимОтображения;
		КонецЕсли;
	Иначе
		СвойстваНастройки.УзелКД              = СтрокаДерева.УзелКД;
		СвойстваНастройки.СтрокаДерева        = СтрокаДерева;
		СвойстваНастройки.Тип                 = СтрокаДерева.Тип;
		СвойстваНастройки.Подтип              = СтрокаДерева.Подтип;
		СвойстваНастройки.НастройкаВариантаКД = СвойстваНастройки.УзелКД;
		Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Авто Тогда
			РежимОтображения = СвойстваНастройки.УзелКД.РежимОтображения;
		КонецЕсли;
	КонецЕсли;
	
	Если РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ Тогда
		СвойстваНастройки.Быстрая = Истина;
		Информация.ЕстьБыстрыеНастройки = Истина;
	ИначеЕсли РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный Тогда
		СвойстваНастройки.Обычная = Истина;
		Информация.ЕстьОбычныеНастройки = Истина;
	ИначеЕсли Информация.ТолькоПользовательские Тогда
		Возврат СвойстваНастройки;
	КонецЕсли;
	
	// Определение доступной настройки.
	Если СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
		СвойстваНастройки.ДоступнаяНастройкаКД = Информация.НастройкиКД.ДоступныеОбъекты.Элементы.Найти(СвойстваНастройки.СтрокаДерева.УзелКД.ИдентификаторОбъекта);
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
		Или СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		Если СвойстваНастройки.ДоступнаяНастройкаКД = Неопределено Тогда
			Возврат СвойстваНастройки; // Имя поля изменилось или поле было удалено.
		КонецЕсли;
	КонецЕсли;
	
	Если Информация.ТолькоПользовательские Тогда
		Если Информация.ТолькоБыстрые Тогда
			СвойстваНастройки.ВыводРазрешен = СвойстваНастройки.Быстрая;
		Иначе
			СвойстваНастройки.ВыводРазрешен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	СвойстваНастройки.ВыводитьФлажок = Истина;
	СвойстваНастройки.ВыводитьТолькоФлажок = Ложь;
	
	ЗаполнитьПредставлениеНастройки(
		СвойстваНастройки,
		СвойстваНастройки.НастройкаВариантаКД,
		СвойстваНастройки.ПользовательскаяНастройкаКД,
		СвойстваНастройки.ДоступнаяНастройкаКД);
	
	Если СвойстваНастройки.Тип = "ГруппаЭлементовОтбора"
		Или СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта"
		Или СвойстваНастройки.Тип = "Группировка"
		Или СвойстваНастройки.Тип = "Таблица"
		Или СвойстваНастройки.Тип = "ГруппировкаТаблицы"
		Или СвойстваНастройки.Тип = "Диаграмма"
		Или СвойстваНастройки.Тип = "ГруппировкаДиаграммы"
		Или СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
		
		СвойстваНастройки.ВыводитьТолькоФлажок = Истина;
		
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
		Или СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		
		Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
			СвойстваНастройки.Значение = ПользовательскаяНастройкаКД.Значение;
		Иначе
			СвойстваНастройки.Значение = ПользовательскаяНастройкаКД.ПравоеЗначение;
		КонецЕсли;
		
		// Определение типа значения настройки.
		Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
			Если СвойстваНастройки.ДоступнаяНастройкаКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда Тогда
				СвойстваНастройки.ВыводитьФлажок = Ложь;
				ПользовательскаяНастройкаКД.Использование = Истина;
			КонецЕсли;
			Если СвойстваНастройки.ДоступнаяНастройкаКД.ДоступенСписокЗначений Тогда
				СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Иначе
				СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
			СвойстваНастройки.ВидСравнения = ПользовательскаяНастройкаКД.ВидСравнения;
		КонецЕсли;
		
		Если СвойстваНастройки.ИнформацияОТипах.СодержитТипПериод
			И СвойстваНастройки.ИнформацияОТипах.КоличествоТипов = 1 Тогда
			
			СвойстваНастройки.ТипЭлементов = "СтандартныйПериод";
			
		ИначеЕсли Не СвойстваНастройки.ВыводитьФлажок
			И СвойстваНастройки.ИнформацияОТипах.СодержитТипБулево
			И СвойстваНастройки.ИнформацияОТипах.КоличествоТипов = 1 Тогда
			
			СвойстваНастройки.ТипЭлементов = "ТолькоФлажокЗначения";
			
		ИначеЕсли СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
			
			СвойстваНастройки.ТипЭлементов = "УсловиеВРежимеПросмотра";
			
		ИначеЕсли СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
			Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
			
			СвойстваНастройки.ВводСписком = Истина;
			СвойстваНастройки.ТипЭлементов = "СписокСПодбором";
			СвойстваНастройки.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
			
		Иначе
			
			СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
			Если СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
				Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
				СвойстваНастройки.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СвойстваНастройки.ВыборГруппИЭлементов = Неопределено Тогда
			СвойстваНастройки.ВыборГруппИЭлементов = НастройкаВарианта.ВыборГруппИЭлементов;
		КонецЕсли;
		
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля"
		Или СвойстваНастройки.Тип = "Порядок"
		Или СвойстваНастройки.Тип = "КоллекцияЭлементовСтруктурыТаблицы"
		Или СвойстваНастройки.Тип = "КоллекцияЭлементовСтруктурыДиаграммы"
		Или СвойстваНастройки.Тип = "Отбор"
		Или СвойстваНастройки.Тип = "УсловноеОформление"
		Или СвойстваНастройки.Тип = "СтруктураНастроек" Тогда
		
		СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
		СвойстваНастройки.ВыводитьФлажок = Ложь;
		
	Иначе
		
		СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
		
	КонецЕсли;
	
	Если СвойстваНастройки.ВыводитьТолькоФлажок Тогда
		СвойстваНастройки.ТипЭлементов = "";
	ИначеЕсли СвойстваНастройки.Быстрая И СвойстваНастройки.ТипЭлементов = "СписокСПодбором" Тогда
		СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
	КонецЕсли;
	
	Возврат СвойстваНастройки;
КонецФункции

Процедура ЗаполнитьПредставлениеНастройки(СвойстваНастройки, НастройкаВариантаКД, ПользовательскаяНастройкаКД, ДоступнаяНастройкаКД)
	Если ПользовательскаяНастройкаКД = Неопределено Тогда
		ПользовательскаяНастройкаКД = НастройкаВариантаКД;
	КонецЕсли;
	
	СтруктураПредставлений = Новый Структура("Представление, ПредставлениеПользовательскойНастройки", "", "");
	ЗаполнитьЗначенияСвойств(СтруктураПредставлений, НастройкаВариантаКД);
	
	СвойстваНастройки.ВыводитьТолькоФлажок = ЗначениеЗаполнено(СтруктураПредставлений.Представление);
	
	Если ЗначениеЗаполнено(СтруктураПредставлений.ПредставлениеПользовательскойНастройки) Тогда
		Представление = СтруктураПредставлений.ПредставлениеПользовательскойНастройки;
	ИначеЕсли ЗначениеЗаполнено(СтруктураПредставлений.Представление) И СтруктураПредставлений.Представление <> "1" Тогда
		Представление = СтруктураПредставлений.Представление;
	Иначе
		Представление = "";
	КонецЕсли;
	СвойстваНастройки.Представление = СокрЛП(Представление);
	
	// Представление "По умолчанию".
	Если ДоступнаяНастройкаКД <> Неопределено И ЗначениеЗаполнено(ДоступнаяНастройкаКД.Заголовок) Тогда
		ПредставлениеПоУмолчанию = ДоступнаяНастройкаКД.Заголовок;
	ИначеЕсли ЗначениеЗаполнено(СвойстваНастройки.Подтип) Тогда
		Если СвойстваНастройки.Подтип = "ДиаграммаСерии" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Серии'");
		ИначеЕсли СвойстваНастройки.Подтип = "ДиаграммаТочки" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Точки'");
		ИначеЕсли СвойстваНастройки.Подтип = "ТаблицаСтроки" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Строки'");
		ИначеЕсли СвойстваНастройки.Подтип = "ТаблицаКолонки" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Колонки'");
		ИначеЕсли СвойстваНастройки.Подтип = "Отчет" Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Отчет'");
		Иначе
			ПредставлениеПоУмолчанию = Строка(СвойстваНастройки.Подтип);
		КонецЕсли;
	ИначеЕсли СвойстваНастройки.Тип = "Отбор" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Отбор'");
	ИначеЕсли СвойстваНастройки.Тип = "ГруппаЭлементовОтбора" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.ТипГруппы);
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.ЛевоеЗначение);
	ИначеЕсли СвойстваНастройки.Тип = "Порядок" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Сортировка'");
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Поля'");
	ИначеЕсли СвойстваНастройки.Тип = "УсловноеОформление" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Оформление'");
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
		ПредставлениеПоУмолчанию = ОтчетыКлиентСервер.ПредставлениеЭлементаУсловногоОформления(ПользовательскаяНастройкаКД);
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
		ПредставлениеПоУмолчанию = Строка(НастройкаВариантаКД.Параметр);
	ИначеЕсли СвойстваНастройки.Тип = "Группировка"
		Или СвойстваНастройки.Тип = "ГруппировкаТаблицы"
		Или СвойстваНастройки.Тип = "ГруппировкаДиаграммы" Тогда
		ПредставлениеПоУмолчанию = СокрЛП(Строка(НастройкаВариантаКД.ПоляГруппировки));
		Если ПустаяСтрока(ПредставлениеПоУмолчанию) Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = '<Детальные записи>'");
		КонецЕсли;
	ИначеЕсли СвойстваНастройки.Тип = "Таблица" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Таблица'");
	ИначеЕсли СвойстваНастройки.Тип = "Диаграмма" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Диаграмма'");
	ИначеЕсли СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
		ПредставлениеПоУмолчанию = Строка(ПользовательскаяНастройкаКД);
		Если ПустаяСтрока(ПредставлениеПоУмолчанию) Тогда
			ПредставлениеПоУмолчанию = НСтр("ru = 'Вложенная группировка'");
		КонецЕсли;
	ИначеЕсли СвойстваНастройки.Тип = "СтруктураНастроек" Тогда
		ПредставлениеПоУмолчанию = НСтр("ru = 'Структура'");
	Иначе
		ПредставлениеПоУмолчанию = Строка(СвойстваНастройки.Тип);
	КонецЕсли;
	СвойстваНастройки.ПредставлениеПоУмолчанию = СокрЛП(ПредставлениеПоУмолчанию);
	
	Если Не ЗначениеЗаполнено(СвойстваНастройки.Представление) Тогда
		СвойстваНастройки.Представление = СвойстваНастройки.ПредставлениеПоУмолчанию;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные

Функция ОписанияТиповСовпадают(ОписаниеТипов1, ОписаниеТипов2) Экспорт
	Если ОписаниеТипов1 = Неопределено Или ОписаниеТипов2 = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ОписаниеТипов1 = ОписаниеТипов2
		Или ОбщегоНазначения.ЗначениеВСтрокуXML(ОписаниеТипов1) = ОбщегоНазначения.ЗначениеВСтрокуXML(ОписаниеТипов2);
КонецФункции

Функция ОтчетОбъект(ОтчетОбъектИлиПолноеИмя) Экспорт
	Если ТипЗнч(ОтчетОбъектИлиПолноеИмя) = Тип("Строка") Тогда
		ОтчетОбъектИлиПолноеИмя = ОбщегоНазначения.ОбъектПоПолномуИмени(ОтчетОбъектИлиПолноеИмя);
	КонецЕсли;
	Возврат ОтчетОбъектИлиПолноеИмя;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вывод

Процедура ВывестиЭлементыНастройки(Форма, Элементы, СвойстваНастройки, ГруппаВывода, Прочее) Экспорт
	ЭлементВывода = Новый Структура("Размер, ИмяЭлемента1, ИмяЭлемента2");
	ЭлементВывода.Размер = 1;
	
	ШаблонИмениЭлемента = СвойстваНастройки.Тип + "_%1_" + СвойстваНастройки.ИдентификаторЭлемента;
	
	// Группа требуется для вывода некоторых типов полей.
	Если СвойстваНастройки.ТипЭлементов = "СтандартныйПериод"
		Или СвойстваНастройки.ТипЭлементов = "СписокСПодбором" Тогда
		ГруппаИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Группа");
		
		Группа = Элементы.Добавить(ГруппаИмя, Тип("ГруппаФормы"), Элементы.НеОтсортированное);
		Группа.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.Отображение         = ОтображениеОбычнойГруппы.Нет;
		Группа.Заголовок           = СвойстваНастройки.Представление;
		Группа.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	
	// Условие.
	ВыводитьУсловие = (СвойстваНастройки.Тип = "ЭлементОтбора"
		И СвойстваНастройки.ТипЭлементов <> "СтандартныйПериод"
		И СвойстваНастройки.ТипЭлементов <> "ТолькоФлажокЗначения"
		И Не СвойстваНастройки.ВыводитьТолькоФлажок);
	
	Если ВыводитьУсловие Тогда
		Прочее.ЕстьОтборыСУсловиями = Истина;
		
		КомандаВидСравненияИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "ВидСравнения");
		КомандаВидСравнения = Форма.Команды.Добавить(КомандаВидСравненияИмя);
		КомандаВидСравнения.Действие    = "Подключаемый_ИзменитьВидСравнения";
		КомандаВидСравнения.Заголовок   = НСтр("ru = 'Изменить условие отбора...'");
		КомандаВидСравнения.Подсказка   = КомандаВидСравнения.Заголовок; // Для платформы.
		КомандаВидСравнения.Отображение = ОтображениеКнопки.Текст;
		КомандаВидСравнения.Картинка    = БиблиотекаКартинок.ВидСравнения;
	КонецЕсли;
	
	// Флажок использования.
	Если СвойстваНастройки.ВыводитьФлажок Тогда
		ФлажокИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Использование");
		
		Если СвойстваНастройки.ТипЭлементов = "СписокСПодбором" Тогда
			ГруппаДляФлажка = Группа;
			ЭлементВывода.ИмяЭлемента1 = ГруппаИмя;
		Иначе
			ГруппаДляФлажка = Элементы.НеОтсортированное;
			ЭлементВывода.ИмяЭлемента1 = ФлажокИмя;
		КонецЕсли;
		
		ЗаголовокФлажка = СвойстваНастройки.Представление;
		Если ВыводитьУсловие
			И СвойстваНастройки.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно
			И СвойстваНастройки.ВидСравнения <> ВидСравненияКомпоновкиДанных.ВСписке
			И СвойстваНастройки.ВидСравнения <> ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
			И СвойстваНастройки.ВидСравнения <> ВидСравненияКомпоновкиДанных.Содержит
			И СвойстваНастройки.ТипЭлементов <> "УсловиеВРежимеПросмотра" Тогда
			ЗаголовокФлажка = ЗаголовокФлажка + " (" + НРег(Строка(СвойстваНастройки.ВидСравнения)) + ")";
		КонецЕсли;
		Если Не СвойстваНастройки.ВыводитьТолькоФлажок Тогда
			ЗаголовокФлажка = ЗаголовокФлажка + ":";
		КонецЕсли;
		
		Флажок = Элементы.Добавить(ФлажокИмя, Тип("ПолеФормы"), ГруппаДляФлажка);
		Флажок.Вид         = ВидПоляФормы.ПолеФлажка;
		Флажок.Заголовок   = ЗаголовокФлажка;
		Флажок.ПутьКДанным = Прочее.ПутьККомпоновщику + ".ПользовательскиеНастройки[" + СвойстваНастройки.ИндексВКоллекции + "].Использование";
		Флажок.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		Флажок.УстановитьДействие("ПриИзменении", "Подключаемый_ФлажокИспользование_ПриИзменении");
		
		Если ВыводитьУсловие Тогда
			КнопкаИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "ВидСравнения_Использование");
			КнопкаВидСравнения = Элементы.Добавить(КнопкаИмя, Тип("КнопкаФормы"), Флажок.КонтекстноеМеню);
			КнопкаВидСравнения.ИмяКоманды = КомандаВидСравненияИмя;
		КонецЕсли;
	КонецЕсли;
	
	Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
		Или СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		
		Если СвойстваНастройки.ВводСписком Тогда
			СвойстваНастройки.ОтмеченныеЗначения = ОтчетыКлиентСервер.ЗначенияСписком(СвойстваНастройки.Значение);
		КонецЕсли;
		
		// Сохранение параметров выбора настройки в дополнительных свойствах пользовательских настроек.
		НастройкиЭлемента = Новый Структура("Представление, ВыводитьФлажок,
		|ВводСписком, ОписаниеТипов, ПараметрыВыбора, ЗначенияДляВыбора,
		|ОграничиватьВыборУказаннымиЗначениями, ВыборГруппИЭлементов, ФормаВыбора");
		НастройкиЭлемента.ФормаВыбора = СвойстваНастройки.ДоступнаяНастройкаКД.ФормаВыбора;
		ЗаполнитьЗначенияСвойств(НастройкиЭлемента, СвойстваНастройки);
		Прочее.ДополнительныеНастройкиЭлементов.Вставить(СвойстваНастройки.ИдентификаторЭлемента, НастройкиЭлемента);
	КонецЕсли;
	
	// Поля для значений.
	Если СвойстваНастройки.ТипЭлементов <> "" Тогда
		
		ИнформацияОТипах = СвойстваНастройки.ИнформацияОТипах;
		
		////////////////////////////////////////////////////////////////////////////////
		// ВЫВОД.
		
		ЗначениеИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Значение");
		
		Если СвойстваНастройки.ТипЭлементов = "ТолькоФлажокЗначения" Тогда
			
			БыстрыеНастройкиДобавитьРеквизит(Прочее.ПараметрыЗаполнения, ЗначениеИмя, СвойстваНастройки.ОписаниеТипов);
			
			ЭлементВывода.ИмяЭлемента1 = ЗначениеИмя;
			
			ПолеФлажка = Элементы.Добавить(ЗначениеИмя, Тип("ПолеФормы"), Группа);
			ПолеФлажка.Вид                = ВидПоляФормы.ПолеФлажка;
			ПолеФлажка.Заголовок          = СвойстваНастройки.Представление;
			ПолеФлажка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			ПолеФлажка.УстановитьДействие("ПриИзменении", "Подключаемый_ФлажокЗначения_ПриИзменении");
			
			Если ВыводитьУсловие Тогда
				КнопкаВидСравнения = Элементы.Добавить(КомандаВидСравненияИмя, Тип("КнопкаФормы"), ПолеФлажка.КонтекстноеМеню);
				КнопкаВидСравнения.ИмяКоманды = КомандаВидСравненияИмя;
			КонецЕсли;
			
			Прочее.ДобавленныеПоляВвода.Вставить(ЗначениеИмя, СвойстваНастройки.Значение);
			
		ИначеЕсли СвойстваНастройки.ТипЭлементов = "УсловиеВРежимеПросмотра" Тогда
			
			ЭлементВывода.ИмяЭлемента2 = ЗначениеИмя;
			
			ПолеВвода = Элементы.Добавить(ЗначениеИмя, Тип("ПолеФормы"), Группа);
			ПолеВвода.Вид                = ВидПоляФормы.ПолеВвода;
			ПолеВвода.Заголовок          = СвойстваНастройки.Представление;
			ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ПолеВвода.ТолькоПросмотр     = Истина;
			ПолеВвода.ПутьКДанным = Прочее.ПутьККомпоновщику + ".ПользовательскиеНастройки[" + СвойстваНастройки.ИндексВКоллекции + "].ВидСравнения";
			ПолеВвода.УстановитьДействие("НачалоВыбора", "Подключаемый_ВидСравнения_НачалоВыбора");
			
			Если ВыводитьУсловие Тогда
				КнопкаВидСравнения = Элементы.Добавить(КомандаВидСравненияИмя, Тип("КнопкаФормы"), ПолеВвода.КонтекстноеМеню);
				КнопкаВидСравнения.ИмяКоманды = КомандаВидСравненияИмя;
			КонецЕсли;
			
		ИначеЕсли СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком" Тогда
			
			Прочее.ИменаОсновныхРеквизитовФормы.Вставить(СвойстваНастройки.ИдентификаторЭлемента, ЗначениеИмя);
			Прочее.ИменаЭлементовДляУстановкиСвязей.Вставить(СвойстваНастройки.ИдентификаторЭлемента, ЗначениеИмя);
			
			ЭлементВывода.ИмяЭлемента2 = ЗначениеИмя;
			
			ПолеВвода = Элементы.Добавить(ЗначениеИмя, Тип("ПолеФормы"), Группа);
			ПолеВвода.Вид                = ВидПоляФормы.ПолеВвода;
			ПолеВвода.Заголовок          = СвойстваНастройки.Представление;
			ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ПолеВвода.УстановитьДействие("ПриИзменении", "Подключаемый_ПолеВвода_ПриИзменении");
			
			// Связь с СКД.
			ПолеВвода.ПутьКДанным = Прочее.ПутьККомпоновщику + ".ПользовательскиеНастройки[" + СвойстваНастройки.ИндексВКоллекции + "].Значение";
			
			Если СвойстваНастройки.ВводСписком Тогда
				ПолеВвода.УстановитьДействие("НачалоВыбора", "Подключаемый_СписокКомпоновщика_НачалоВыбора");
			ИначеЕсли СвойстваНастройки.ДоступнаяНастройкаКД <> Неопределено
				И СвойстваНастройки.ДоступнаяНастройкаКД.БыстрыйВыбор <> Истина Тогда
				// Когда есть отключаемые связи "ПоМетаданным" или "ПараметровВыбора" от подчиненного,
				// тогда для выбора используется прикладная логика.
				Если СвойстваНастройки.НастройкаВарианта.СвязиПараметровВыбора.Количество() > 0
					Или СвойстваНастройки.НастройкаВарианта.СвязиПоМетаданным.Количество() > 0 Тогда
					ПолеВвода.УстановитьДействие("НачалоВыбора", "Подключаемый_ЗначениеКомпоновщика_НачалоВыбора");
				КонецЕсли;
			КонецЕсли;
			
			Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
				Или СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
				
				ЗаполнитьЗначенияСвойств(ПолеВвода, СвойстваНастройки.ДоступнаяНастройкаКД, "БыстрыйВыбор, Маска, ФормаВыбора, ФорматРедактирования");
				
				Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
					ПолеВвода.АвтоОтметкаНезаполненного = СвойстваНастройки.ДоступнаяНастройкаКД.ЗапрещатьНезаполненныеЗначения;
				КонецЕсли;
				
				ПолеВвода.ВыборГруппИЭлементов = СвойстваНастройки.ВыборГруппИЭлементов;
				
				// Поля ввода следующих типов не растягиваются по горизонтали и не имеют кнопки очистки:
				//     Дата, Булево, Число, Тип.
				ПолеВвода.КнопкаОткрытия           = Ложь;
				ПолеВвода.КнопкаРегулирования      = Ложь;
				ПолеВвода.КнопкаОчистки            = ИнформацияОТипах.СодержитОбъектныеТипы;
				ПолеВвода.РастягиватьПоГоризонтали = ИнформацияОТипах.СодержитОбъектныеТипы;
				
				Если Не СвойстваНастройки.ВводСписком Тогда
					Для Каждого ЭлементСпискаВФорме Из СвойстваНастройки.ЗначенияДляВыбора Цикл
						ЗаполнитьЗначенияСвойств(ПолеВвода.СписокВыбора.Добавить(), ЭлементСпискаВФорме);
					КонецЦикла;
					Если СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями Тогда
						ПолеВвода.РежимВыбораИзСписка      = Истина;
						ПолеВвода.КнопкаСоздания           = Ложь;
						ПолеВвода.КнопкаВыбора             = Ложь;
						ПолеВвода.КнопкаВыпадающегоСписка  = Истина;
						ПолеВвода.РастягиватьПоГоризонтали = Истина;
					КонецЕсли;
					// Для платформы (переопределение доступных значений на клиенте).
					Если СвойстваНастройки.НастройкаВарианта.СписокЗначенийПереопределен Тогда
						РезультатКлиента = Прочее.ПараметрыЗаполнения.Результат;
						СвоиСпискиВыбора = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатКлиента, "СвоиСпискиВыбора");
						Если СвоиСпискиВыбора = Неопределено Тогда
							СвоиСпискиВыбора = Новый Массив;
							РезультатКлиента.Вставить("СвоиСпискиВыбора", СвоиСпискиВыбора);
						КонецЕсли;
						Если СвоиСпискиВыбора.Найти(СвойстваНастройки.ИдентификаторЭлемента) = Неопределено Тогда
							СвоиСпискиВыбора.Добавить(СвойстваНастройки.ИдентификаторЭлемента);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				// Условие.
				Если ВыводитьУсловие Тогда
					КнопкаВидСравнения = Элементы.Добавить(КомандаВидСравненияИмя, Тип("КнопкаФормы"), ПолеВвода.КонтекстноеМеню);
					КнопкаВидСравнения.ИмяКоманды = КомандаВидСравненияИмя;
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли СвойстваНастройки.ТипЭлементов = "СтандартныйПериод" Тогда
			
			Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			
			ЭлементВывода.Размер = 1;
			ЭлементВывода.ИмяЭлемента2 = ГруппаИмя;
			
			ПериодНачалоИмя    = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Начало");
			ПериодОкончаниеИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Окончание");
			ТиреИмя            = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Тире");
			КнопкаВыбораИмя    = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "КнопкаВыбора");
			
			// Реквизиты.
			БыстрыеНастройкиДобавитьРеквизит(Прочее.ПараметрыЗаполнения, ЗначениеИмя, "СтандартныйПериод");
			
			// Начало произвольного периода.
			ПериодНачало = Элементы.Добавить(ПериодНачалоИмя, Тип("ПолеФормы"), Группа);
			ПериодНачало.Вид    = ВидПоляФормы.ПолеВвода;
			ПериодНачало.Ширина = 9;
			ПериодНачало.РастягиватьПоГоризонтали = Ложь;
			ПериодНачало.КнопкаВыбора   = Истина;
			ПериодНачало.КнопкаОткрытия = Ложь;
			ПериодНачало.КнопкаОчистки  = Ложь;
			ПериодНачало.КнопкаРегулирования  = Ложь;
			ПериодНачало.РедактированиеТекста = Истина;
			ПериодНачало.ПоложениеЗаголовка   = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ПериодНачало.УстановитьДействие("ПриИзменении", "Подключаемый_СтандартныйПериод_НачалоПериода_ПриИзменении");
			
			Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
				ПериодНачало.АвтоОтметкаНезаполненного = СвойстваНастройки.ДоступнаяНастройкаКД.ЗапрещатьНезаполненныеЗначения;
			КонецЕсли;
			
			Тире = Элементы.Добавить(ТиреИмя, Тип("ДекорацияФормы"), Группа);
			Тире.Вид       = ВидДекорацииФормы.Надпись;
			Тире.Заголовок = Символ(8211); // Среднее тире (en dash).
			
			// Окончание произвольного периода.
			ПериодОкончание = Элементы.Добавить(ПериодОкончаниеИмя, Тип("ПолеФормы"), Группа);
			ПериодОкончание.Вид = ВидПоляФормы.ПолеВвода;
			ЗаполнитьЗначенияСвойств(ПериодОкончание, ПериодНачало, "РастягиватьПоГоризонтали, Ширина, ПоложениеЗаголовка, 
			|РедактированиеТекста, КнопкаВыбора, КнопкаОткрытия, КнопкаОчистки, КнопкаРегулирования, АвтоОтметкаНезаполненного");
			ПериодОкончание.УстановитьДействие("ПриИзменении", "Подключаемый_СтандартныйПериод_КонецПериода_ПриИзменении");
			
			// Кнопка выбора.
			КомандаВыбора = Форма.Команды.Добавить(КнопкаВыбораИмя);
			КомандаВыбора.Действие    = "Подключаемый_ВыбратьПериод";
			КомандаВыбора.Заголовок   = НСтр("ru = 'Выбрать период...'");
			КомандаВыбора.Подсказка   = КомандаВыбора.Заголовок; // Для платформы.
			КомандаВыбора.Отображение = ОтображениеКнопки.Картинка;
			КомандаВыбора.Картинка    = БиблиотекаКартинок.Выбрать;
			
			КнопкаВыбора = Элементы.Добавить(КнопкаВыбораИмя, Тип("КнопкаФормы"), Группа);
			КнопкаВыбора.ИмяКоманды = КнопкаВыбораИмя;
			
			Дополнительно = Новый Структура;
			Дополнительно.Вставить("ЗначениеИмя",        ЗначениеИмя);
			Дополнительно.Вставить("ПериодНачалоИмя",    ПериодНачалоИмя);
			Дополнительно.Вставить("ПериодОкончаниеИмя", ПериодОкончаниеИмя);
			СвойстваНастройки.Дополнительно = Дополнительно;
			Прочее.ДобавленныеСтандартныеПериоды.Добавить(СвойстваНастройки);
			
		ИначеЕсли СвойстваНастройки.ТипЭлементов = "СписокСПодбором" Тогда
			
			Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			
			ЭлементВывода.Размер = 5;
			ЭлементВывода.ИмяЭлемента1 = ГруппаИмя;
			
			ГруппаЗаголовокИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "ГруппаЗаголовка");
			ДекорацияИмя       = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Декорация");
			ТаблицаИмя              = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "СписокЗначений");
			ГруппаКолонокИмя        = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "ГруппаКолонок");
			КолонкаИспользованиеИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Колонка_Использование");
			КолонкаЗначениеИмя      = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Колонка_Значение");
			КоманднаяПанельИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "КоманднаяПанель");
			КнопкаПодборИмя    = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Подбор");
			КнопкаВставитьИмя  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "ВставитьИзБуфера");
			
			Прочее.ИменаОсновныхРеквизитовФормы.Вставить(СвойстваНастройки.ИдентификаторЭлемента, ТаблицаИмя);
			Прочее.ИменаЭлементовДляУстановкиСвязей.Вставить(СвойстваНастройки.ИдентификаторЭлемента, КолонкаЗначениеИмя);
			
			Если Не СвойстваНастройки.ВыводитьФлажок Или Не СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями Тогда
				
				// Группа-строка для заголовка и командной панели таблицы.
				ГруппаЗаголовокТаблицы = Элементы.Добавить(ГруппаЗаголовокИмя, Тип("ГруппаФормы"), Группа);
				ГруппаЗаголовокТаблицы.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				ГруппаЗаголовокТаблицы.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				ГруппаЗаголовокТаблицы.Отображение         = ОтображениеОбычнойГруппы.Нет;
				ГруппаЗаголовокТаблицы.ОтображатьЗаголовок = Ложь;
				
				// Флажок уже создан.
				Если СвойстваНастройки.ВыводитьФлажок Тогда
					Элементы.Переместить(Флажок, ГруппаЗаголовокТаблицы);
				КонецЕсли;
				
				// Заголовок / Пустая декорация.
				ПустаяДекорация = Элементы.Добавить(ДекорацияИмя, Тип("ДекорацияФормы"), ГруппаЗаголовокТаблицы);
				ПустаяДекорация.Вид                      = ВидДекорацииФормы.Надпись;
				ПустаяДекорация.Заголовок                = ?(СвойстваНастройки.ВыводитьФлажок, " ", СвойстваНастройки.Представление + ":");
				ПустаяДекорация.РастягиватьПоГоризонтали = Истина;
				
				// Кнопки.
				Если Не СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями Тогда
					Если ИнформацияОТипах.СодержитСсылочныеТипы Тогда
						КомандаПодбор = Форма.Команды.Добавить(КнопкаПодборИмя);
						КомандаПодбор.Действие  = "Подключаемый_СписокСПодбором_Подбор";
						КомандаПодбор.Заголовок = НСтр("ru = 'Подбор'");
					Иначе
						КомандаПодбор = Форма.Команды.Добавить(КнопкаПодборИмя);
						КомандаПодбор.Действие  = "Подключаемый_СписокСПодбором_Добавить";
						КомандаПодбор.Заголовок = НСтр("ru = 'Добавить'");
						КомандаПодбор.Картинка  = БиблиотекаКартинок.СоздатьЭлементСписка;
					КонецЕсли;
					
					КнопкаПодбор = Элементы.Добавить(КнопкаПодборИмя, Тип("КнопкаФормы"), ГруппаЗаголовокТаблицы);
					КнопкаПодбор.ИмяКоманды  = КнопкаПодборИмя;
					КнопкаПодбор.Вид         = ВидКнопкиФормы.Гиперссылка;
					КнопкаПодбор.Отображение = ОтображениеКнопки.Текст;
					
					Если СвойстваНастройки.ИнформацияОТипах.СодержитСсылочныеТипы И Прочее.ЕстьЗагрузкаДанныхИзФайла Тогда
						КомандаВставить = Форма.Команды.Добавить(КнопкаВставитьИмя);
						КомандаВставить.Действие  = "Подключаемый_СписокСПодбором_ВставитьИзБуфера";
						КомандаВставить.Заголовок = НСтр("ru = 'Вставить из буфера обмена...'");
						КомандаВставить.Подсказка = КомандаВставить.Заголовок; // Для платформы.
						КомандаВставить.Картинка  = БиблиотекаКартинок.ВставитьИзБуфераОбмена;
						
						КнопкаВставить = Элементы.Добавить(КнопкаВставитьИмя, Тип("КнопкаФормы"), ГруппаЗаголовокТаблицы);
						КнопкаВставить.ИмяКоманды  = КнопкаВставитьИмя;
						КнопкаВставить.Вид         = ВидКнопкиФормы.Гиперссылка;
						КнопкаВставить.Отображение = ОтображениеКнопки.Картинка;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			// Реквизит.
			БыстрыеНастройкиДобавитьРеквизит(Прочее.ПараметрыЗаполнения, ТаблицаИмя, "СписокЗначений");
			
			// Группа с отступом и таблицей.
			ГруппаСОтступом = Элементы.Добавить(ГруппаИмя + "Отступ", Тип("ГруппаФормы"), Группа);
			ГруппаСОтступом.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаСОтступом.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			ГруппаСОтступом.Отображение         = ОтображениеОбычнойГруппы.Нет;
			ГруппаСОтступом.Заголовок           = СвойстваНастройки.Представление;
			ГруппаСОтступом.ОтображатьЗаголовок = Ложь;
			
			// Декорация отступа.
			ПустаяДекорация = Элементы.Добавить(ДекорацияИмя + "Отступ", Тип("ДекорацияФормы"), ГруппаСОтступом);
			ПустаяДекорация.Вид                      = ВидДекорацииФормы.Надпись;
			ПустаяДекорация.Заголовок                = "     ";
			ПустаяДекорация.РастягиватьПоГоризонтали = Ложь;
			
			// Таблица.
			ТаблицаФормы = Элементы.Добавить(ТаблицаИмя, Тип("ТаблицаФормы"), ГруппаСОтступом);
			ТаблицаФормы.Отображение               = ОтображениеТаблицы.Список;
			ТаблицаФормы.Заголовок                 = СвойстваНастройки.Представление;
			ТаблицаФормы.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ТаблицаФормы.ПоложениеКоманднойПанели  = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
			ТаблицаФормы.ВертикальныеЛинии         = Ложь;
			ТаблицаФормы.ГоризонтальныеЛинии       = Ложь;
			ТаблицаФормы.Шапка                     = Ложь;
			ТаблицаФормы.Подвал                    = Ложь;
			ТаблицаФормы.ИзменятьПорядокСтрок      = Истина;
			ТаблицаФормы.РастягиватьПоГоризонтали  = Истина;
			ТаблицаФормы.РастягиватьПоВертикали    = Истина;
			ТаблицаФормы.Высота                    = 3;
			
			Если СвойстваНастройки.ВыводитьФлажок Тогда
				Если Не СвойстваНастройки.ПользовательскаяНастройкаКД.Использование Тогда
					ТаблицаФормы.ЦветТекста = Форма.ЦветНеактивныхЗначенийТаблицы;
				КонецЕсли;
			КонецЕсли;
			
			// Группа колонок "в ячейке".
			ГруппаКолонок = Элементы.Добавить(ГруппаКолонокИмя, Тип("ГруппаФормы"), ТаблицаФормы);
			ГруппаКолонок.Вид         = ВидГруппыФормы.ГруппаКолонок;
			ГруппаКолонок.Группировка = ГруппировкаКолонок.ВЯчейке;
			
			// Колонка "Использование".
			КолонкаИспользованиеЭлемент = Элементы.Добавить(КолонкаИспользованиеИмя, Тип("ПолеФормы"), ГруппаКолонок);
			КолонкаИспользованиеЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
			
			// Колонка "Значение".
			КолонкаЗначениеЭлемент = Элементы.Добавить(КолонкаЗначениеИмя, Тип("ПолеФормы"), ГруппаКолонок);
			КолонкаЗначениеЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			
			ЗаполнитьЗначенияСвойств(КолонкаЗначениеЭлемент, СвойстваНастройки.ДоступнаяНастройкаКД, "БыстрыйВыбор, Маска, ФормаВыбора, ФорматРедактирования");
			
			КолонкаЗначениеЭлемент.ВыборГруппИЭлементов = СвойстваНастройки.ВыборГруппИЭлементов;
			
			Если СвойстваНастройки.ОграничиватьВыборУказаннымиЗначениями Тогда
				КолонкаЗначениеЭлемент.ТолькоПросмотр = Истина;
			КонецЕсли;
			
			// Заполнение имен объектов метаданных в разрезах типов и идентификаторов элементов (для предустановленных).
			// Используется при клике по кнопке "Подбор" для получения имени формы подбора.
			Если ЗначениеЗаполнено(КолонкаЗначениеЭлемент.ФормаВыбора) Тогда
				Прочее.СоответствиеИменОбъектовМетаданных.Вставить(СвойстваНастройки.ИдентификаторЭлемента, КолонкаЗначениеЭлемент.ФормаВыбора);
			КонецЕсли;
			
			// Фиксированные параметры выбора.
			Если СвойстваНастройки.ПараметрыВыбора.Количество() > 0 Тогда
				КолонкаЗначениеЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(СвойстваНастройки.ПараметрыВыбора);
			КонецЕсли;
			
			Если ВыводитьУсловие Тогда
				КнопкаВидСравнения = Элементы.Добавить(КомандаВидСравненияИмя, Тип("КнопкаФормы"), ТаблицаФормы.КонтекстноеМеню);
				КнопкаВидСравнения.ИмяКоманды  = КомандаВидСравненияИмя;
			КонецЕсли;
			
			Дополнительно = Новый Структура;
			Дополнительно.Вставить("ТаблицаИмя",              ТаблицаИмя);
			Дополнительно.Вставить("ИмяКолонкиЗначение",      КолонкаЗначениеИмя);
			Дополнительно.Вставить("ИмяКолонкиИспользование", КолонкаИспользованиеИмя);
			СвойстваНастройки.Дополнительно = Дополнительно;
			Прочее.ДобавленныеСпискиЗначений.Добавить(СвойстваНастройки);
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлементВывода.ИмяЭлемента1 = Неопределено Тогда
		ЗаголовокИмя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениЭлемента, "Заголовок");
		ПолеНадписи = Элементы.Добавить(ЗаголовокИмя, Тип("ДекорацияФормы"), Элементы.НеОтсортированное);
		ПолеНадписи.Вид       = ВидДекорацииФормы.Надпись;
		ПолеНадписи.Заголовок = СвойстваНастройки.Представление + ":";
		ЭлементВывода.ИмяЭлемента1 = ЗаголовокИмя;
	КонецЕсли;
	
	Если СвойстваНастройки.ТипЭлементов = "СтандартныйПериод" Тогда
		ГруппаВывода.Порядок.Вставить(0, ЭлементВывода);
	Иначе
		ГруппаВывода.Порядок.Добавить(ЭлементВывода);
	КонецЕсли;
	ГруппаВывода.Размер = ГруппаВывода.Размер + ЭлементВывода.Размер;
	
КонецПроцедуры

Процедура БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, РеквизитПолноеИмя, ТипРеквизита)
	Если ТипЗнч(ТипРеквизита) = Тип("ОписаниеТипов") Тогда
		ТипыДобавляемого = ТипРеквизита;
	ИначеЕсли ТипЗнч(ТипРеквизита) = Тип("Строка") Тогда
		ТипыДобавляемого = Новый ОписаниеТипов(ТипРеквизита);
	ИначеЕсли ТипЗнч(ТипРеквизита) = Тип("Массив") Тогда
		ТипыДобавляемого = Новый ОписаниеТипов(ТипРеквизита);
	ИначеЕсли ТипЗнч(ТипРеквизита) = Тип("Тип") Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипРеквизита);
		ТипыДобавляемого = Новый ОписаниеТипов(МассивТипов);
	Иначе
		Возврат;
	КонецЕсли;
	
	ТипыСуществующего = ПараметрыЗаполнения.Реквизиты.Существующие.Получить(РеквизитПолноеИмя);
	Если ОписанияТиповСовпадают(ТипыСуществующего, ТипыДобавляемого) Тогда
		ПараметрыЗаполнения.Реквизиты.Существующие.Удалить(РеквизитПолноеИмя);
	Иначе
		ПозицияТочки = СтрНайти(РеквизитПолноеИмя, ".");
		Если ПозицияТочки = 0 Тогда
			ПутьКРеквизиту = "";
			КраткоеИмяРеквизита = РеквизитПолноеИмя;
		Иначе
			ПутьКРеквизиту = Лев(РеквизитПолноеИмя, ПозицияТочки - 1);
			КраткоеИмяРеквизита = Сред(РеквизитПолноеИмя, ПозицияТочки + 1);
		КонецЕсли;
		
		ПараметрыЗаполнения.Реквизиты.Добавляемые.Добавить(Новый РеквизитФормы(КраткоеИмяРеквизита, ТипыДобавляемого, ПутьКРеквизиту));
		Если ТипыСуществующего <> Неопределено Тогда
			ПараметрыЗаполнения.Реквизиты.Удаляемые.Добавить(РеквизитПолноеИмя);
			ПараметрыЗаполнения.Реквизиты.Существующие.Удалить(РеквизитПолноеИмя);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ВывестиПоПорядку(Форма, ГруппаВывода, Родитель, КоличествоКолонок, ГибкаяБалансировка = Истина) Экспорт
	Элементы = Форма.Элементы;
	Если ГибкаяБалансировка Тогда
		Если ГруппаВывода.Размер <= 7 Тогда
			КоличествоКолонок = 1;
		КонецЕсли;
	КонецЕсли;
	
	РодительИмя = Родитель.Имя;
	
	НомерКолонки = 0;
	ОсталосьКолонок = КоличествоКолонок + 1;
	ОсталосьМестаВсего = ГруппаВывода.Размер;
	ОсталосьМестаВКолонке = 0;
	
	Для Каждого ЭлементВывода Из ГруппаВывода.Порядок Цикл
		Если ОсталосьКолонок > 0
			И ЭлементВывода.Размер > ОсталосьМестаВКолонке*4 Тогда // Текущий шаг больше оставшегося места.
			НомерКолонки = НомерКолонки + 1;
			ОсталосьКолонок = ОсталосьКолонок - 1;
			ОсталосьМестаВКолонке = ОсталосьМестаВсего/ОсталосьКолонок;
			
			КолонкаВерхнегоУровня = Элементы.Добавить(РодительИмя + НомерКолонки, Тип("ГруппаФормы"), Элементы[РодительИмя]);
			КолонкаВерхнегоУровня.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
			КолонкаВерхнегоУровня.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			КолонкаВерхнегоУровня.Отображение         = ОтображениеОбычнойГруппы.Нет;
			КолонкаВерхнегоУровня.ОтображатьЗаголовок = Ложь;
			
			НомерПодгруппы = 0;
			ТекущаяГруппа1 = Неопределено;
			ТекущаяГруппа2 = Неопределено;
		КонецЕсли;
		
		Если ЭлементВывода.ИмяЭлемента2 = Неопределено Тогда // Вывод в одну колонку.
			Если ТекущаяГруппа2 <> Неопределено Тогда
				ТекущаяГруппа2 = Неопределено;
			КонецЕсли;
			Элементы.Переместить(Элементы[ЭлементВывода.ИмяЭлемента1], КолонкаВерхнегоУровня);
		Иначе
			Если ТекущаяГруппа2 = Неопределено Тогда
				НомерПодгруппы = НомерПодгруппы + 1;
				
				Колонки = Элементы.Добавить(РодительИмя + НомерКолонки + "_" + НомерПодгруппы, Тип("ГруппаФормы"), КолонкаВерхнегоУровня);
				Колонки.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				Колонки.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				Колонки.Отображение         = ОтображениеОбычнойГруппы.Нет;
				Колонки.ОтображатьЗаголовок = Ложь;
				
				ТекущаяГруппа1 = Элементы.Добавить(РодительИмя + НомерКолонки + "_" + НомерПодгруппы + "_1", Тип("ГруппаФормы"), Колонки);
				ТекущаяГруппа1.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				ТекущаяГруппа1.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
				ТекущаяГруппа1.Отображение         = ОтображениеОбычнойГруппы.Нет;
				ТекущаяГруппа1.ОтображатьЗаголовок = Ложь;
				
				ТекущаяГруппа2 = Элементы.Добавить(РодительИмя + НомерКолонки + "_" + НомерПодгруппы + "_2", Тип("ГруппаФормы"), Колонки);
				ТекущаяГруппа2.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				ТекущаяГруппа2.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
				ТекущаяГруппа2.Отображение         = ОтображениеОбычнойГруппы.Нет;
				ТекущаяГруппа2.ОтображатьЗаголовок = Ложь;
			КонецЕсли;
			Элементы.Переместить(Элементы[ЭлементВывода.ИмяЭлемента1], ТекущаяГруппа1);
			Элементы.Переместить(Элементы[ЭлементВывода.ИмяЭлемента2], ТекущаяГруппа2);
		КонецЕсли;
		
		ОсталосьМестаВсего = ОсталосьМестаВсего - ЭлементВывода.Размер;
		ОсталосьМестаВКолонке = ОсталосьМестаВКолонке - ЭлементВывода.Размер;
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Сохранение состояния формы

Функция ЗапомнитьВыделенныеСтроки(Форма, ИмяТаблицы, КлючевыеКолонки) Экспорт
	ТаблицаРеквизит = Форма[ИмяТаблицы];
	ТаблицаЭлемент = Форма.Элементы[ИмяТаблицы];
	
	Результат = Новый Структура;
	Результат.Вставить("Выделенные", Новый Массив);
	Результат.Вставить("Текущая", Неопределено);
	
	ТекущаяИдентификатор = ТаблицаЭлемент.ТекущаяСтрока;
	Если ТекущаяИдентификатор <> Неопределено Тогда
		СтрокаТаблицы = ТаблицаРеквизит.НайтиПоИдентификатору(ТекущаяИдентификатор);
		Если СтрокаТаблицы <> Неопределено Тогда
			ДанныеСтроки = Новый Структура(КлючевыеКолонки);
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);
			Результат.Текущая = ДанныеСтроки;
		КонецЕсли;
	КонецЕсли;
	
	ВыделенныеСтроки = ТаблицаЭлемент.ВыделенныеСтроки;
	Если ВыделенныеСтроки <> Неопределено Тогда
		Для Каждого ВыделеннаяИдентификатор Из ВыделенныеСтроки Цикл
			Если ВыделеннаяИдентификатор = ТекущаяИдентификатор Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицы = ТаблицаРеквизит.НайтиПоИдентификатору(ВыделеннаяИдентификатор);
			Если СтрокаТаблицы <> Неопределено Тогда
				ДанныеСтроки = Новый Структура(КлючевыеКолонки);
				ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);
				Результат.Выделенные.Добавить(ДанныеСтроки);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ВосстановитьВыделенныеСтроки(Форма, ИмяТаблицы, СтрокиТаблицы) Экспорт
	ТаблицаРеквизит = Форма[ИмяТаблицы];
	ТаблицаЭлемент = Форма.Элементы[ИмяТаблицы];
	
	ТаблицаЭлемент.ВыделенныеСтроки.Очистить();
	
	Если СтрокиТаблицы.Текущая <> Неопределено Тогда
		Найденные = ОтчетыКлиентСервер.НайтиСтрокиТаблицы(ТаблицаРеквизит, СтрокиТаблицы.Текущая);
		Если Найденные <> Неопределено И Найденные.Количество() > 0 Тогда
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				Если СтрокаТаблицы <> Неопределено Тогда
					Идентификатор = СтрокаТаблицы.ПолучитьИдентификатор();
					ТаблицаЭлемент.ТекущаяСтрока = Идентификатор;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ДанныеСтроки Из СтрокиТаблицы.Выделенные Цикл
		Найденные = ОтчетыКлиентСервер.НайтиСтрокиТаблицы(ТаблицаРеквизит, ДанныеСтроки);
		Если Найденные <> Неопределено И Найденные.Количество() > 0 Тогда
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				Если СтрокаТаблицы <> Неопределено Тогда
					ТаблицаЭлемент.ВыделенныеСтроки.Добавить(СтрокаТаблицы.ПолучитьИдентификатор());
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Период

Функция АнализСтандартногоПериода(Знач Вариант)
	Результат = Новый Структура("ВидПериода, СдвигПериода");
	
	Если Вариант = ВариантСтандартногоПериода.ЭтотГод Тогда
		Результат.ВидПериода   = "Год";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлыйГод Тогда
		Результат.ВидПериода   = "Год";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующийГод Тогда
		Результат.ВидПериода   = "Год";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СНачалаЭтогоГода Тогда
		Результат.ВидПериода   = "СНачалаГода";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлыйГодДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаГода";
		Результат.СдвигПериода = -1
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующийГодДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаГода";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ДоКонцаЭтогоГода Тогда
		Результат.ВидПериода   = "ДоКонцаГода";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ЭтоПолугодие Тогда
		Результат.ВидПериода   = "Полугодие";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлоеПолугодие Тогда
		Результат.ВидПериода   = "Полугодие";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующееПолугодие Тогда
		Результат.ВидПериода   = "Полугодие";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СНачалаЭтогоПолугодия Тогда
		Результат.ВидПериода   = "СНачалаПолугодия";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлоеПолугодиеДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаПолугодия";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующееПолугодиеДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаПолугодия";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ДоКонцаЭтогоПолугодия Тогда
		Результат.ВидПериода   = "ДоКонцаПолугодия";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ЭтотКвартал Тогда
		Результат.ВидПериода   = "Квартал";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлыйКвартал Тогда
		Результат.ВидПериода   = "Квартал";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующийКвартал Тогда
		Результат.ВидПериода   = "Квартал";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СНачалаЭтогоКвартала Тогда
		Результат.ВидПериода   = "СНачалаКвартала";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлыйКварталДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаКвартала";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующийКварталДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаКвартала";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ДоКонцаЭтогоКвартала Тогда
		Результат.ВидПериода   = "ДоКонцаКвартала";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ЭтотМесяц Тогда
		Результат.ВидПериода   = "Месяц";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлыйМесяц Тогда
		Результат.ВидПериода   = "Месяц";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующийМесяц Тогда
		Результат.ВидПериода   = "Месяц";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.Месяц Тогда
		Результат.ВидПериода   = "ПоследнийМесяц";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СНачалаЭтогоМесяца Тогда
		Результат.ВидПериода   = "СНачалаМесяца";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлыйМесяцДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаМесяца";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующийМесяцДоТакойЖеДаты Тогда
		Результат.ВидПериода   = "СНачалаМесяца";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ДоКонцаЭтогоМесяца Тогда
		Результат.ВидПериода   = "ДоКонцаМесяца";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ЭтаДекада Тогда
		Результат.ВидПериода   = "Декада";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлаяДекада Тогда
		Результат.ВидПериода   = "Декада";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующаяДекада Тогда
		Результат.ВидПериода   = "Декада";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СНачалаЭтойДекады Тогда
		Результат.ВидПериода   = "СНачалаДекады";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлаяДекадаДоТакогоЖеНомераДня Тогда
		Результат.ВидПериода   = "СНачалаДекады";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующаяДекадаДоТакогоЖеНомераДня Тогда
		Результат.ВидПериода   = "СНачалаДекады";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ДоКонцаЭтойДекады Тогда
		Результат.ВидПериода   = "ДоКонцаДекады";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ЭтаНеделя Тогда
		Результат.ВидПериода   = "Неделя";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлаяНеделя Тогда
		Результат.ВидПериода   = "Неделя";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующаяНеделя Тогда
		Результат.ВидПериода   = "Неделя";
		Результат.СдвигПериода = 1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.Последние7Дней Тогда
		Результат.ВидПериода   = "Последние7Дней";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.Следующие7Дней Тогда
		Результат.ВидПериода   = "Следующие7Дней";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СНачалаЭтойНедели Тогда
		Результат.ВидПериода   = "СНачалаНедели";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ПрошлаяНеделяДоТакогоЖеДняНедели Тогда
		Результат.ВидПериода   = "СНачалаНедели";
		Результат.СдвигПериода = -1
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.СледующаяНеделяДоТакогоЖеДняНедели Тогда
		Результат.ВидПериода   = "СНачалаНедели";
		Результат.СдвигПериода = 1
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.ДоКонцаЭтойНедели Тогда
		Результат.ВидПериода   = "ДоКонцаНедели";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.Сегодня Тогда
		Результат.ВидПериода   = "День";
		Результат.СдвигПериода = 0;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.Вчера Тогда
		Результат.ВидПериода   = "День";
		Результат.СдвигПериода = -1;
		
	ИначеЕсли Вариант = ВариантСтандартногоПериода.Завтра Тогда
		Результат.ВидПериода   = "День";
		Результат.СдвигПериода = 1;
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция АнализПроизвольногоПериода(Знач НачалоПериода, Знач КонецПериода)
	Результат = Новый Структура("ВидПериода, СдвигПериода");
	
	НачалоПериода = НачалоДня(НачалоПериода);
	КонецПериода  = НачалоДня(КонецПериода);
	Сегодня       = НачалоДня(ТекущаяДатаСеанса());
	ДнейВПериоде    = (КонецПериода - НачалоПериода) / 86400 + 1;
	КонецСдвигВДнях = (КонецПериода - Сегодня) / 86400;
	
	Если ДнейВПериоде = 1 Тогда
		Результат.ВидПериода   = "День";
		Результат.СдвигПериода = КонецСдвигВДнях;
		Возврат Результат;
	КонецЕсли;
	
	Если ДнейВПериоде <= 7 Тогда
		НачалоДеньНедели  = ДеньНедели(НачалоПериода);
		КонецДеньНедели   = ДеньНедели(КонецПериода);
		ТекущийДеньНедели = ДеньНедели(Сегодня);
		Если НачалоДеньНедели = 1 И КонецДеньНедели = 7 Тогда
			Результат.ВидПериода = "Неделя";
			Результат.СдвигПериода = (НачалоНедели(КонецПериода) - НачалоНедели(Сегодня)) / 604800;
		ИначеЕсли НачалоДеньНедели = 1 И КонецДеньНедели = ТекущийДеньНедели Тогда
			Результат.ВидПериода = "СНачалаНедели";
			Результат.СдвигПериода = (НачалоНедели(КонецПериода) - НачалоНедели(Сегодня)) / 604800;
		ИначеЕсли НачалоДеньНедели = ТекущийДеньНедели И КонецДеньНедели = 7 Тогда
			Результат.ВидПериода = "ДоКонцаНедели";
			Результат.СдвигПериода = (НачалоНедели(КонецПериода) - НачалоНедели(Сегодня)) / 604800;
		ИначеЕсли КонецДеньНедели = ТекущийДеньНедели И ДнейВПериоде = 7 Тогда
			Результат.ВидПериода = "Последние7Дней";
			Результат.СдвигПериода = (НачалоНедели(КонецПериода) - НачалоНедели(Сегодня)) / 604800;
		ИначеЕсли НачалоДеньНедели = ТекущийДеньНедели И ДнейВПериоде = 7 Тогда
			Результат.ВидПериода = "Следующие7Дней";
			Результат.СдвигПериода = (НачалоНедели(НачалоПериода) - НачалоНедели(Сегодня)) / 604800;
		КонецЕсли;
		Если Результат.ВидПериода <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	НачалоНомерГода  = Год(НачалоПериода);
	КонецНомерГода   = Год(КонецПериода);
	ТекущийНомерГода = Год(Сегодня);
	
	НачалоНомерМесяца  = Месяц(НачалоПериода);
	КонецНомерМесяца   = Месяц(КонецПериода);
	ТекущийНомерМесяца = Месяц(Сегодня);
	
	КонецСдвигВГодах   = КонецНомерГода - ТекущийНомерГода;
	КонецСдвигВМесяцах = КонецСдвигВГодах*12 + КонецНомерМесяца - ТекущийНомерМесяца;
	
	НачалоДеньМесяца  = День(НачалоПериода);
	КонецДеньМесяца   = День(КонецПериода);
	ТекущийДеньМесяца = День(Сегодня);
	
	КонецЭтоПоследнийДеньМесяца = КонецДеньМесяца >= 27 И КонецДеньМесяца = День(КонецМесяца(КонецПериода));
	
	Если ДнейВПериоде <= 31 Тогда
		Если НачалоДеньМесяца = 1 И КонецЭтоПоследнийДеньМесяца Тогда
			Результат.ВидПериода   = "Месяц";
			Результат.СдвигПериода = КонецСдвигВМесяцах;
			Возврат Результат;
		ИначеЕсли НачалоДеньМесяца = 1 И КонецДеньМесяца = ТекущийДеньМесяца Тогда
			Результат.ВидПериода   = "СНачалаМесяца";
			Результат.СдвигПериода = КонецСдвигВМесяцах;
			Возврат Результат;
		ИначеЕсли НачалоДеньМесяца = ТекущийДеньМесяца И КонецЭтоПоследнийДеньМесяца Тогда
			Результат.ВидПериода   = "ДоКонцаМесяца";
			Результат.СдвигПериода = КонецСдвигВМесяцах;
			Возврат Результат;
		ИначеЕсли КонецДеньМесяца = ТекущийДеньМесяца И НачалоПериода = ДобавитьМесяц(КонецПериода + 86400, -1) Тогда
			Результат.ВидПериода   = "ПоследнийМесяц";
			Результат.СдвигПериода = КонецСдвигВМесяцах;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Если ДнейВПериоде <= 93 Тогда // Легкая оптимизация вычислений.
		НачалоНомерМесяцаВКвартале = (НачалоНомерМесяца-1)%3 + 1;
		КонецНомерМесяцаВКвартале  = (КонецНомерМесяца-1)%3 + 1;
		
		НачалоЭтоНачалоКвартала = (НачалоНомерМесяцаВКвартале = 1 И НачалоДеньМесяца = 1);
		КонецЭтоКонецКвартала   = (КонецНомерМесяцаВКвартале  = 3 И КонецЭтоПоследнийДеньМесяца);
		
		Если НачалоЭтоНачалоКвартала Или КонецЭтоКонецКвартала Тогда // Легкая оптимизация вычислений.
			
			ТекущийНомерМесяцаВКвартале = (ТекущийНомерМесяца-1)%3 + 1;
			
			Если НачалоЭтоНачалоКвартала И КонецЭтоКонецКвартала Тогда
				Результат.ВидПериода = "Квартал";
			ИначеЕсли НачалоЭтоНачалоКвартала
				И КонецНомерМесяцаВКвартале = ТекущийНомерМесяцаВКвартале
				И КонецДеньМесяца = ТекущийДеньМесяца Тогда
				Результат.ВидПериода = "СНачалаКвартала";
			ИначеЕсли КонецЭтоКонецКвартала
				И НачалоНомерМесяцаВКвартале = ТекущийНомерМесяцаВКвартале
				И НачалоДеньМесяца = ТекущийДеньМесяца Тогда
				Результат.ВидПериода = "ДоКонцаКвартала";
			КонецЕсли;
			
			Если Результат.ВидПериода <> Неопределено Тогда // Легкая оптимизация вычислений.
				КонецНомерКварталаВГоду   = Цел((КонецНомерМесяца-1)/3)+1;
				ТекущийНомерКварталаВГоду = Цел((ТекущийНомерМесяца-1)/3)+1;
				Результат.СдвигПериода = КонецСдвигВГодах*4 + КонецНомерКварталаВГоду - ТекущийНомерКварталаВГоду;
				Возврат Результат;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ДнейВПериоде <= 186 Тогда // Легкая оптимизация вычислений.
		НачалоНомерМесяцаВПолугодии = (НачалоНомерМесяца-1)%6 + 1;
		КонецНомерМесяцаВПолугодии  = (КонецНомерМесяца-1)%6 + 1;
		
		НачалоЭтоНачалоПолугодия = (НачалоНомерМесяцаВПолугодии = 1 И НачалоДеньМесяца = 1);
		КонецЭтоКонецПолугодия   = (КонецНомерМесяцаВПолугодии  = 6 И КонецЭтоПоследнийДеньМесяца);
		
		Если НачалоЭтоНачалоПолугодия Или КонецЭтоКонецПолугодия Тогда // Легкая оптимизация вычислений.
			
			ТекущийНомерМесяцаВПолугодии = (ТекущийНомерМесяца-1)%6 + 1;
			
			Если НачалоЭтоНачалоПолугодия И КонецЭтоКонецПолугодия Тогда
				Результат.ВидПериода = "Полугодие";
			ИначеЕсли НачалоЭтоНачалоПолугодия
				И КонецНомерМесяцаВПолугодии = ТекущийНомерМесяцаВПолугодии
				И КонецДеньМесяца = ТекущийДеньМесяца Тогда
				Результат.ВидПериода = "СНачалаПолугодия";
			ИначеЕсли КонецЭтоКонецПолугодия
				И НачалоНомерМесяцаВПолугодии = ТекущийНомерМесяцаВПолугодии
				И НачалоДеньМесяца = ТекущийДеньМесяца Тогда
				Результат.ВидПериода = "ДоКонцаПолугодия";
			КонецЕсли;
			
			Если Результат.ВидПериода <> Неопределено Тогда // Легкая оптимизация вычислений.
				КонецНомерПолугодияВГоду   = ?(КонецНомерМесяца   <= 6, 1, 2);
				ТекущийНомерПолугодияВГоду = ?(ТекущийНомерМесяца <= 6, 1, 2);
				Результат.СдвигПериода = КонецСдвигВГодах*2 + КонецНомерПолугодияВГоду - ТекущийНомерПолугодияВГоду;
				Возврат Результат;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ДнейВПериоде <= 366 Тогда // Легкая оптимизация вычислений.
		
		НачалоЭтоНачалоГода = (НачалоНомерМесяца = 1 И НачалоДеньМесяца = 1);
		КонецЭтоКонецГода   = (КонецНомерМесяца  = 12 И КонецЭтоПоследнийДеньМесяца);
		
		Если НачалоЭтоНачалоГода Или КонецЭтоКонецГода Тогда // Легкая оптимизация вычислений.
			
			Если НачалоЭтоНачалоГода И КонецЭтоКонецГода Тогда
				Результат.ВидПериода   = "Год";
				Результат.СдвигПериода = КонецСдвигВГодах;
				Возврат Результат;
			ИначеЕсли НачалоЭтоНачалоГода
				И КонецНомерМесяца = ТекущийНомерМесяца
				И КонецДеньМесяца  = ТекущийДеньМесяца Тогда
				Результат.ВидПериода   = "СНачалаГода";
				Результат.СдвигПериода = КонецСдвигВГодах;
				Возврат Результат;
			ИначеЕсли КонецЭтоКонецГода
				И НачалоНомерМесяца = ТекущийНомерМесяца
				И НачалоДеньМесяца  = ТекущийДеньМесяца Тогда
				Результат.ВидПериода   = "ДоКонцаГода";
				Результат.СдвигПериода = КонецСдвигВГодах;
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДнейВПериоде <= 11 Тогда
		НачалоЭтоНачалоДекады = (НачалоДеньМесяца = 1 Или НачалоДеньМесяца = 11 Или НачалоДеньМесяца = 21);
		КонецЭтоКонецДекады   = (КонецДеньМесяца = 10 Или КонецДеньМесяца = 20 Или КонецЭтоПоследнийДеньМесяца);
		Если НачалоЭтоНачалоДекады И КонецЭтоКонецДекады Тогда
			Результат.ВидПериода = "Декада";
		Иначе
			НачалоДеньДекады  = (НачалоДеньМесяца-1)%10 + 1; // % это остаток от деления.
			КонецДеньДекады   = (КонецДеньМесяца-1)%10 + 1;
			ТекущийДеньДекады = (ТекущийДеньМесяца-1)%10 + 1;
			Если НачалоЭтоНачалоДекады И КонецДеньДекады = ТекущийДеньДекады Тогда
				Результат.ВидПериода = "СНачалаДекады";
			ИначеЕсли НачалоДеньДекады = ТекущийДеньДекады И КонецЭтоКонецДекады Тогда
				Результат.ВидПериода = "ДоКонцаДекады";
			КонецЕсли;
		КонецЕсли;
		Если Результат.ВидПериода <> Неопределено Тогда
			КонецНомерДекадыВМесяце   = ?(КонецДеньМесяца   <= 10, 1, ?(КонецДеньМесяца   <= 20, 2, 3));
			ТекущийНомерДекадыВМесяце = ?(ТекущийДеньМесяца <= 10, 1, ?(ТекущийДеньМесяца <= 20, 2, 3));
			Результат.СдвигПериода = КонецСдвигВМесяцах*3 + КонецНомерДекадыВМесяце - ТекущийНомерДекадыВМесяце;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
