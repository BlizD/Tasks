////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИК ИНТЕРФЕЙСА РАЗРЕШЕНИЙ ДОПОЛНИТЕЛЬНЫХ ОТЧЕТОВ И ОБРАБОТОК
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает пространство имен текущей (используемой вызывающим кодом) версии интерфейса сообщений.
Функция Пакет() Экспорт
	
	Возврат "http://www.1c.ru/1cFresh/ApplicationExtensions/Permissions/" + Версия();
	
КонецФункции

// Возвращает текущую (используемую вызывающим кодом) версию интерфейса сообщений.
Функция Версия() Экспорт
	
	Возврат "1.0.0.1";
	
КонецФункции

// Возвращает название программного интерфейса сообщений.
Функция ПрограммныйИнтерфейс() Экспорт
	
	Возврат "ApplicationExtensionsPermissions";
	
КонецФункции

// Выполняет регистрацию поддерживаемых версий интерфейса сообщений.
//
// Параметры:
//  СтруктураПоддерживаемыхВерсий - структура:
//    Ключ - название программного интерфейса.
//    Значение - массив поддерживаемых версий.
//
Процедура ЗарегистрироватьИнтерфейс(Знач СтруктураПоддерживаемыхВерсий) Экспорт
	
	МассивВерсий = Новый Массив;
	МассивВерсий.Добавить("1.0.0.1");
	СтруктураПоддерживаемыхВерсий.Вставить(ПрограммныйИнтерфейс(), МассивВерсий);
	
КонецПроцедуры

// Выполняет регистрацию обработчиков сообщений в качестве обработчиков каналов обмена сообщениями.
//
// Параметры:
//  МассивОбработчиков - массив.
//
Процедура ОбработчикиКаналовСообщений(Знач МассивОбработчиков) Экспорт
	
КонецПроцедуры

// Возвращает идентификатор вида действия, соответствующего вызову метода конфигурации.
//
Функция ВидДействияВызовМетодаКонфигурации() Экспорт
	
	Возврат "МетодКонфигурации"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида действия, соответствующего вызову метода обработки.
//
Функция ВидДействияВызовМетодаОбработки() Экспорт
	
	Возврат "МетодОбработки"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида параметра, соответствующему ключу запуска.
//
Функция ВидПараметраКлючСессии() Экспорт
	
	Возврат "КлючСессии"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида параметра, соответствующему фиксированному значению.
//
Функция ВидПараметраЗначение() Экспорт
	
	Возврат "ФиксированноеЗначение"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида параметра, соответствующему сохраняемому значению.
//
Функция ВидПараметраСохраняемоеЗначение() Экспорт
	
	Возврат "СохраняемоеЗначение"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида параметра, соответствующему коллекции сохраняемых значений.
//
Функция ВидПараметраКоллекцияСохраняемыхЗначений() Экспорт
	
	Возврат "КоллекцияСохраняемыхЗначений"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида параметра, соответствующему параметру выполнения команды.
//
Функция ВидПараметраПараметрВыполненияКоманды() Экспорт
	
	Возврат "ПараметрВыполненияКоманды"; // Не локализуется
	
КонецФункции

// Возвращает идентификатор вида параметра, соответствующему коллекции объектов назначения.
//
Функция ВидПараметраОбъектыНазначения() Экспорт
	
	Возврат "ОбъектыНазначения"; // Не локализуется
	
КонецФункции

// Конструктор пустой таблицы значений, которая используется в качестве описания сценария,
// выполняемого в безопасном режиме.
//
// Возвращаемое значение: ТаблицаЗначений.
//
Функция НовыйСценарий() Экспорт
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("ВидДействия", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ИмяМетода", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Параметры", Новый ОписаниеТипов("ТаблицаЗначений"));
	Результат.Колонки.Добавить("СохранениеРезультата", Новый ОписаниеТипов("Строка"));
	
	Возврат Результат;
	
КонецФункции

// Добавляет в сценарий выполнения обработки в безопасном режиме этап сценария, содержащий
// выполнение метода конфигурации.
//
// Параметры:
//  Сценарий, ТаблицаЗначений, структура колонок должна соответствовать результату, возвращаемому.
//    Функцией НовыйСценарий(),
//  ИмяМетода - Строка, имя метода конфигурации, вызов которого предполагается на этапе выполнения
//    этапа сценария,
//  СохранениеРезультата - Строка, имя сохраняемого значения сценария, в котором будет сохранен
//    результат выполнения метода, переданного в качестве значения параметра ИмяМетода.
//
// Возвращаемое значение: СтрокаТаблицыЗначений.
//
Функция ДобавитьМетодКонфигурации(Сценарий, Знач ИмяМетода, Знач СохранениеРезультата = "") Экспорт
	
	Возврат ДобавитьЭтап(Сценарий, ВидДействияВызовМетодаКонфигурации(), ИмяМетода, СохранениеРезультата);
	
КонецФункции

// Добавляет в сценарий выполнения обработки в безопасном режиме этап сценария, содержащий
// выполнение метода обработки.
//
// Параметры:
//  Сценарий, ТаблицаЗначений, структура колонок должна соответствовать результату, возвращаемому.
//    Функцией НовыйСценарий(),
//  ИмяМетода - Строка, имя метода конфигурации, вызов которого предполагается на этапе выполнения
//    этапа сценария,
//  СохранениеРезультата - Строка, имя сохраняемого значения сценария, в котором будет сохранен
//    результат выполнения метода, переданного в качестве значения параметра ИмяМетода.
//
// Возвращаемое значение: СтрокаТаблицыЗначений.
//
Функция ДобавитьМетодОбработки(Сценарий, Знач ИмяМетода, Знач СохранениеРезультата = "") Экспорт
	
	Возврат ДобавитьЭтап(Сценарий, ВидДействияВызовМетодаОбработки(), ИмяМетода, СохранениеРезультата);
	
КонецФункции

// Конструктор пустой таблицы значений, которая используется в качестве описания
// параметров элементов сценария, выполняемого в безопасном режиме.
//
// Возвращаемое значение: ТаблицаЗначений.
//
Функция НоваяТаблицаПараметров() Экспорт
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("Вид", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Значение");
	
	Возврат Результат;
	
КонецФункции

// Добавляет в таблицу параметров ключ запуска текущей обработки.
//
// Параметры:
//  Этап - СтрокаТаблицыЗначений, возвращенная методами ДобавитьМетодКонфигурации
//    или ДобавитьМетодОбработки.
//
Процедура ДобавитьКлючСессии(Этап) Экспорт
	
	ДобавитьПараметр(Этап, ВидПараметраКлючСессии());
	
КонецПроцедуры

// Добавляет в таблицу параметров фиксированное значение.
//
// Параметры:
//  Этап - СтрокаТаблицыЗначений, возвращенная методами ДобавитьМетодКонфигурации.
//    или ДобавитьМетодОбработки,
//  Значение - Произвольный, фиксированное значение.
//
Процедура ДобавитьЗначение(Этап, Знач Значение) Экспорт
	
	ДобавитьПараметр(Этап, ВидПараметраЗначение(), Значение);
	
КонецПроцедуры

// Добавляет в таблицу параметров фиксированное значение.
//
// Параметры:
//  Этап - СтрокаТаблицыЗначений, возвращенная методами ДобавитьМетодКонфигурации.
//    или ДобавитьМетодОбработки,
//  СохраняемоеЗначение - Строка, имя переменной сохраняемого значения внутри сценария.
//
Процедура ДобавитьСохраняемоеЗначение(Этап, Знач СохраняемоеЗначение) Экспорт
	
	ДобавитьПараметр(Этап, ВидПараметраСохраняемоеЗначение(), СохраняемоеЗначение);
	
КонецПроцедуры

// Добавляет в таблицу параметров коллекцию сохраняемых значений.
//
// Параметры:
//  Этап - СтрокаТаблицыЗначений, возвращенная методами ДобавитьМетодКонфигурации
//    или ДобавитьМетодОбработки.
//
Процедура ДобавитьКоллекциюСохраняемыхЗначений(Этап) Экспорт
	
	ДобавитьПараметр(Этап, ВидПараметраКоллекцияСохраняемыхЗначений());
	
КонецПроцедуры

// Добавляет в таблицу параметров параметр выполнения команды.
//
// Параметры:
//  Этап - СтрокаТаблицыЗначений, возвращенная методами ДобавитьМетодКонфигурации.
//    или ДобавитьМетодОбработки,
//  ИмяПараметра - Строка, имя параметра выполнения команды.
//
Процедура ДобавитьПараметрВыполненияКоманды(Этап, Знач ИмяПараметра) Экспорт
	
	ДобавитьПараметр(Этап, ВидПараметраПараметрВыполненияКоманды(), ИмяПараметра);
	
КонецПроцедуры

// Добавляет в таблицу параметров коллекцию объектов назначения.
//
// Параметры:
//  Этап - СтрокаТаблицыЗначений, возвращенная методами ДобавитьМетодКонфигурации
//    или ДобавитьМетодОбработки.
//
Процедура ДобавитьОбъектыНазначения(Этап) Экспорт
	
	ДобавитьПараметр(Этап, ВидПараметраОбъектыНазначения());
	
КонецПроцедуры

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}CreateComObject
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипСозданиеCOMОбъекта(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "CreateComObject");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}CreateComObject
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеСозданиеCOMОбъекта(Знач ProgId, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипСозданиеCOMОбъекта(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.ProgId = ProgId;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}AttachAddin
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипПодключениеВнешнейКомпоненты(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "AttachAddin");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}AttachAddin
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПодключениеВнешнейКомпонентыИзОбщегоМакетаКонфигурации(Знач ИмяОбщегоМакета, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПодключениеВнешнейКомпоненты(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.TemplateName = "ОбщийМакет." + ИмяОбщегоМакета;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}AttachAddin
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПодключениеВнешнейКомпонентыИзМакетаКонфигурации(Знач ОбъектМетаданных, Знач ИмяМакета, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПодключениеВнешнейКомпоненты(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.TemplateName = ОбъектМетаданных.ПолноеИмя() + ".Макет" + ИмяМакета;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}GetFileFromExternalSoftware
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипПолучениеФайлаИзВнешнегоОбъекта(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "GetFileFromExternalSoftware");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}GetFileFromExternalSoftware
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПолучениеФайлаИзВнешнегоОбъекта(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПолучениеФайлаИзВнешнегоОбъекта(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}SendFileToExternalSoftware
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипПередачаФайлаВоВнешнийОбъект(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "SendFileToExternalSoftware");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}SendFileToExternalSoftware
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПередачаФайлаВоВнешнийОбъект(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПередачаФайлаВоВнешнийОбъект(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}GetFileFromInternet
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипПолучениеДанныхИзИнтернет(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "GetFileFromInternet");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}GetFileFromInternet
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПолучениеДанныхИзИнтернет(Знач Протокол, Знач Сервер, Знач Порт, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПолучениеДанныхИзИнтернет(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.Protocol = ВРег(Протокол);
	Разрешение.Host = Сервер;
	Разрешение.Port = Порт;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}SendFileToInternet
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипПередачаДанныхВИнтернет(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "SendFileToInternet");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}SendFileToInternet
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПередачаДанныхВИнтернет(Знач Протокол, Знач Сервер, Знач Порт, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПередачаДанныхВИнтернет(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.Protocol = ВРег(Протокол);
	Разрешение.Host = Сервер;
	Разрешение.Port = Порт;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}SoapConnect
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипWSСоединение(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "SoapConnect");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}SoapConnect
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеWSСоединение(Знач АдресWSDL, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипWSСоединение(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.WsdlDestination = АдресWSDL;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}DocumentPosting
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ТипПроведениеДокументов(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "DocumentPosting");
	
КонецФункции

// Возвращает объект {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}DocumentPosting
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ОбъектXDTO
//
Функция РазрешениеПроведениеДокументов(Знач ОбъектМетаданных, Знач РежимЗаписи, Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Тип = ТипПроведениеДокументов(ИспользуемыйПакет);
	Разрешение = ФабрикаXDTO.Создать(Тип);
	Разрешение.DocumentType = ОбъектМетаданных.ПолноеИмя();
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Разрешение.Action = "Posting";
	Иначе
		Разрешение.Action = "UndoPosting";
	КонецЕсли;
	
	Возврат Разрешение;
	
КонецФункции

// Возвращает тип {http://www.1c.ru/1cFresh/ApplicationExtensions/Core/a.b.c.d}InternalFileHandler
//
// Параметры:
//  ИспользуемыйПакет - строка, пространство имен версии интерфейса сообщений, для которой
//    получается тип сообщения.
//
// Возвращаемое значение:
//  ТипXDTO
//
Функция ПараметрПередаваемыйФайл(Знач ИспользуемыйПакет = Неопределено) Экспорт
	
	Возврат СоздатьТипСообщения(ИспользуемыйПакет, "InternalFileHandler");
	
КонецФункции

// Возвращает значение, соответствующее любому значению ограничителя (*) при
// регистрации разрешений, запрашиваемых дополнительной обработкой.
//
// Возвращаемое значение: Неопределено.
//
Функция ЛюбоеЗначение() Экспорт
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьТипСообщения(Знач ИспользуемыйПакет, Знач Тип)
		
	Если ИспользуемыйПакет = Неопределено Тогда
		ИспользуемыйПакет = Пакет();
	КонецЕсли;
	
	Возврат ФабрикаXDTO.Тип(ИспользуемыйПакет, Тип);
	
КонецФункции

Функция ДобавитьЭтап(Сценарий, Знач ВидЭтапа, Знач ИмяМетода, Знач СохранениеРезультата = "")
	
	Этап = Сценарий.Добавить();
	Этап.ВидДействия = ВидЭтапа;
	Этап.ИмяМетода = ИмяМетода;
	Этап.Параметры = НоваяТаблицаПараметров();
	Если Не ПустаяСтрока(СохранениеРезультата) Тогда
		Этап.СохранениеРезультата = СохранениеРезультата;
	КонецЕсли;
	
	Возврат Этап;
	
КонецФункции

Процедура ДобавитьПараметр(Этап, Знач ВидПараметра, Знач Значение = Неопределено)
	
	Параметр = Этап.Параметры.Добавить();
	Параметр.Вид = ВидПараметра;
	Если Значение <> Неопределено Тогда
		Параметр.Значение = Значение;
	КонецЕсли;
	
КонецПроцедуры

// Преобразовывает разрешения из формата 2.1.3 в формат 2.2.2.
//
Функция ПреобразоватьРазрешенияВерсии_2_1_3_ВРазрешенияВерсии_2_2_2(Знач ДополнительныйОтчетИлиОбработка, Знач Разрешения) Экспорт
	
	Результат = Новый Массив();
	
	// Если у обработки есть команды, которые являются сценарием - добавляем права на работу с каталогом временных
	// файлов.
	
	ОтборСценариев = Новый Структура("ВариантЗапуска", Перечисления.СпособыВызоваДополнительныхОбработок.СценарийВБезопасномРежиме);
	ЕстьСценарии = ДополнительныйОтчетИлиОбработка.Команды.НайтиСтроки(ОтборСценариев).Количество() > 0;
	Если ЕстьСценарии Тогда
		Результат.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаВременныхФайлов(Истина, Истина));
	КонецЕсли;
	
	// Преобразуем разрешения в нотации "расширения" безопасного режима.
	Для Каждого Разрешение Из Разрешения Цикл
		
		Если Разрешение.Тип() = ТипПолучениеДанныхИзИнтернет(Пакет()) Тогда
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
					Разрешение.Protocol,
					Разрешение.Host,
					Разрешение.Port));
			
		ИначеЕсли Разрешение.Тип() = ТипПередачаДанныхВИнтернет(Пакет()) Тогда
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
					Разрешение.Protocol,
					Разрешение.Host,
					Разрешение.Port));
			
		ИначеЕсли Разрешение.Тип() = ТипWSСоединение(Пакет()) Тогда
			
			СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(Разрешение.WsdlDestination);
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаИспользованиеИнтернетРесурса(
					СтруктураURI.Схема,
					СтруктураURI.ИмяСервера,
					СтруктураURI.Порт));
			
		ИначеЕсли Разрешение.Тип() = ТипСозданиеCOMОбъекта(Пакет()) Тогда
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаСозданиеCOMКласса(
					Разрешение.ProgId,
					ИдентификаторCOMКлассаВРежимеОбратнойСовместимости(Разрешение.ProgId)));
			
		ИначеЕсли Разрешение.Тип() = ТипПодключениеВнешнейКомпоненты(Пакет()) Тогда
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаИспользованиеВнешнейКомпоненты(
					Разрешение.TemplateName));
			
		ИначеЕсли Разрешение.Тип() = ТипПолучениеФайлаИзВнешнегоОбъекта(Пакет()) Тогда
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаВременныхФайлов(Истина, Истина));
			
		ИначеЕсли Разрешение.Тип() = ТипПередачаФайлаВоВнешнийОбъект(Пакет()) Тогда
			
			Результат.Добавить(
				РаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаВременныхФайлов(Истина, Истина));
			
		ИначеЕсли Разрешение.Тип() = ТипПроведениеДокументов(Пакет()) Тогда
			
			Результат.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеПривилегированногоРежима());
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторCOMКлассаВРежимеОбратнойСовместимости(Знач ProgId)
	
	ПоддерживаемыеИдентификаторы = ИдентификаторыCOMКлассовВРежимеОбратнойСовместимости();
	CLSID = ПоддерживаемыеИдентификаторы.Получить(ProgId);
	
	Если CLSID = Неопределено Тогда
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Разрешение на использование COM-класса ""%1"" не может быть предоставлено дополнительной обработке,
				|работающей в режиме обратной совместимости с механизмом разрешений, реализованным в версии БСП 2.1.3.
				|Для использования COM-класса требуется переработать дополнительную обработку для работы без режима обратной совместимости.'"),
			ProgId);
		
	Иначе
		
		Возврат CLSID;
		
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторыCOMКлассовВРежимеОбратнойСовместимости()
	
	Результат = Новый Соответствие();
	
	// V83.ComConnector
	Результат.Вставить(ОбщегоНазначенияКлиентСервер.ИмяCOMСоединителя(), ОбщегоНазначения.ИдентификаторCOMСоединителя(ОбщегоНазначенияКлиентСервер.ИмяCOMСоединителя()));
	// Word.Application
	Результат.Вставить("Word.Application", "000209FF-0000-0000-C000-000000000046");
	// Excel.Application
	Результат.Вставить("Excel.Application", "00024500-0000-0000-C000-000000000046");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
