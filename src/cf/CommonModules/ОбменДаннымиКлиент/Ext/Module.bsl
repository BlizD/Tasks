////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен данными"
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура-обработчик закрытия формы настройки узлов плана обмена.
//
// Параметры:
//  Форма - управляемая форма, из которой вызвана процедура.
// 
Процедура ФормаНастройкиУзловКомандаЗакрытьФорму(Форма) Экспорт
	
	Если Не Форма.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Модифицированность = Ложь;
	ЗаполнитьДанныеСтруктуры(Форма);
	Форма.Закрыть(Форма.Контекст);
	
КонецПроцедуры

// Процедура-обработчик закрытия формы настройки узла плана обмена.
//
// Параметры:
//  Форма - управляемая форма, из которой вызвана процедура.
// 
Процедура ФормаНастройкиУзлаКомандаЗакрытьФорму(Форма) Экспорт
	
	ПриЗакрытииФормыНастройкиУзлаПланаОбмена(Форма, "НастройкаОтборовНаУзле");
	
КонецПроцедуры

// Процедура-обработчик закрытия формы настройки значений по умолчанию узла плана обмена.
//
// Параметры:
//  Форма - управляемая форма, из которой вызвана процедура.
// 
Процедура ФормаНастройкиЗначенийПоУмолчаниюКомандаЗакрытьФорму(Форма) Экспорт
	
	ПриЗакрытииФормыНастройкиУзлаПланаОбмена(Форма, "ЗначенияПоУмолчаниюНаУзле");
	
КонецПроцедуры

// Процедура-обработчик закрытия формы настройки узла плана обмена.
//
// Параметры:
//  Отказ - флаг отказа от закрытия формы.
//  Форма - управляемая форма, из которой вызвана процедура.
// 
Процедура ФормаНастройкиПередЗакрытием(Отказ, Форма) Экспорт
	
	Если Форма.Модифицированность Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Закрыть форму без сохранения изменений?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ФормаНастройкиПередЗакрытиемЗавершение", ЭтотОбъект, Форма);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму помощника настройки обмена данными для заданного плана обмена.
//
// Параметры:
//  ИмяПланаОбмена - Строка - имя плана обмена, как объекта метаданных, для которого необходимо открыть помощник.
//  НастройкаОбменаССервисом - Булево - Признак того, что настраивается обмен с приложением в модели сервиса.
// 
Процедура ОткрытьПомощникНастройкиОбменаДанными(Знач ИмяПланаОбмена, НастройкаОбменаССервисом = Ложь) Экспорт
	
	ДополнительнаяНастройка = "";
	
	Если СтрНайти(ИмяПланаОбмена, "КорреспондентВМоделиСервиса") > 0 Тогда
		
		ИмяПланаОбмена = СтрЗаменить(ИмяПланаОбмена, "КорреспондентВМоделиСервиса", "");
		
		НастройкаОбменаССервисом = Истина;
		
	КонецЕсли;
	
	Если СтрНайти(ИмяПланаОбмена, "ИдентификаторНастройки") > 0 Тогда
		
		ЧислоСимволовИмяПланаОбмена = СтрНайти(ИмяПланаОбмена, "ИдентификаторНастройки") - 1;
		
		ДополнительнаяНастройка = Прав(ИмяПланаОбмена, СтрДлина(ИмяПланаОбмена) - ЧислоСимволовИмяПланаОбмена - 22);
		ИмяПланаОбмена          = Лев(ИмяПланаОбмена, ЧислоСимволовИмяПланаОбмена);
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ИмяПланаОбмена", ИмяПланаОбмена);
	ПараметрыФормы.Вставить("ДополнительнаяНастройка", ДополнительнаяНастройка);
	
	Если НастройкаОбменаССервисом Тогда
		ПараметрыФормы.Вставить("НастройкаОбменаССервисом");
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПомощникСозданияОбменаДанными.Форма.Форма", ПараметрыФормы, , ИмяПланаОбмена + НастройкаОбменаССервисом, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Обработчик начала выбора элемента для формы задания настроек узла базы-корреспондента при настройке обмена через
// внешнее соединение.
//
// Параметры:
//	ИмяРеквизита - Строка - Имя реквизита формы.
//	ИмяТаблицы - Строка - Полное имя объекта метаданных.
//	Владелец - УправляемаяФорма - Форма выбора элементов базы-корреспондента.
//	СтандартнаяОбработка - Булево - Признак выполнения стандартной (системной) обработки события.
//	ПараметрыВнешнегоСоединения - Структура - параметры внешнего соединения.
//	ПараметрыВыбора - Структура - Структура параметров выбора.
//
Процедура ОбработчикВыбораЭлементовБазыКорреспондентаНачалоВыбора(Знач ИмяРеквизита, Знач ИмяТаблицы, Знач Владелец,
	Знач СтандартнаяОбработка, Знач ПараметрыВнешнегоСоединения, Знач ПараметрыВыбора=Неопределено) Экспорт
	
	ИмяРеквизитаИдентификатора = ИмяРеквизита + "_Ключ";
	
	НачальноеЗначениеВыбора = Неопределено;
	ВыборГруппИЭлементов    = Неопределено;
	
	ТипВладельца = ТипЗнч(Владелец);
	Если ТипВладельца=Тип("ТаблицаФормы") Тогда
		ТекущиеДанные = Владелец.ТекущиеДанные;
		Если ТекущиеДанные<>Неопределено Тогда
			НачальноеЗначениеВыбора = ТекущиеДанные[ИмяРеквизитаИдентификатора];
		КонецЕсли;
		
	ИначеЕсли ТипВладельца=Тип("УправляемаяФорма") Тогда
		НачальноеЗначениеВыбора = Владелец[ИмяРеквизитаИдентификатора];
		
	КонецЕсли;
	
	Если ПараметрыВыбора<>Неопределено Тогда
		Если ПараметрыВыбора.Свойство("ВыборГруппИЭлементов") Тогда
			ВыборГруппИЭлементов = ПараметрыВыбора.ВыборГруппИЭлементов;
		КонецЕсли;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыВнешнегоСоединения",        ПараметрыВнешнегоСоединения);
	ПараметрыФормы.Вставить("ПолноеИмяТаблицыБазыКорреспондента", ИмяТаблицы);
	ПараметрыФормы.Вставить("НачальноеЗначениеВыбора",            НачальноеЗначениеВыбора);
	ПараметрыФормы.Вставить("ИмяРеквизита",                       ИмяРеквизита);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов",               ВыборГруппИЭлементов);
	
	ОткрытьФорму("ОбщаяФорма.ВыборОбъектовИнформационнойБазыКорреспондента", ПараметрыФормы, Владелец);
	
КонецПроцедуры

// Обработчик подбора элементов для формы задания настроек узла базы-корреспондента при настройке обмена через внешнее
// соединение.
//
// Параметры:
//	ИмяРеквизита - Строка - Имя реквизита формы.
//	ИмяТаблицы - Строка - Полное имя объекта метаданных.
//	Владелец - УправляемаяФорма - Форма выбора элементов базы-корреспондента.
//	ПараметрыВнешнегоСоединения - Структура - параметры внешнего соединения.
//	ПараметрыВыбора - Структура - Структура параметров выбора.
//
Процедура ОбработчикВыбораЭлементовБазыКорреспондентаПодбор(Знач ИмяРеквизита, Знач ИмяТаблицы, Знач Владелец,
	Знач ПараметрыВнешнегоСоединения, Знач ПараметрыВыбора=Неопределено) Экспорт
	
	ИмяРеквизитаИдентификатора = ИмяРеквизита + "_Ключ";
	
	НачальноеЗначениеВыбора = Неопределено;
	ВыборГруппИЭлементов    = Неопределено;
	
	ТекущиеДанные = Владелец.ТекущиеДанные;
	Если ТекущиеДанные<>Неопределено Тогда
		НачальноеЗначениеВыбора = ТекущиеДанные[ИмяРеквизитаИдентификатора];
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ПараметрыВыбора<>Неопределено Тогда
		Если ПараметрыВыбора.Свойство("ВыборГруппИЭлементов") Тогда
			ВыборГруппИЭлементов = ПараметрыВыбора.ВыборГруппИЭлементов;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыВнешнегоСоединения",        ПараметрыВнешнегоСоединения);
	ПараметрыФормы.Вставить("ПолноеИмяТаблицыБазыКорреспондента", ИмяТаблицы);
	ПараметрыФормы.Вставить("НачальноеЗначениеВыбора",            НачальноеЗначениеВыбора);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",                 Ложь);
	ПараметрыФормы.Вставить("ИмяРеквизита",                       ИмяРеквизита);
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов",               ВыборГруппИЭлементов);
	
	ОткрытьФорму("ОбщаяФорма.ВыборОбъектовИнформационнойБазыКорреспондента", ПараметрыФормы, Владелец);
КонецПроцедуры

// Обработчик обработки выбора элемента для формы задания настроек узла базы-корреспондента при настройке обмена через
// внешнее соединение.
//
// Параметры:
//	Элемент - УправляемаяФорма, ТаблицаФормы - Элемент для обработки выбора.
//	ВыбранноеЗначение - см. описание параметра ВыбранноеЗначение события ОбработкаВыбора.
//	ДанныеФормыКоллекция - ДанныеФормыКоллекция - Для режима подбора из списка.
//
Процедура ОбработчикВыбораЭлементовБазыКорреспондентаОбработкаВыбора(Знач Элемент, Знач ВыбранноеЗначение, Знач ДанныеФормыКоллекция=Неопределено) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаИдентификатора = ВыбранноеЗначение.ИмяРеквизита + "_Ключ";
	ИмяРеквизитаПредставления  = ВыбранноеЗначение.ИмяРеквизита;
	
	ТипЭлемента = ТипЗнч(Элемент);
	Если ТипЭлемента=Тип("ТаблицаФормы") Тогда
		
		Если ВыбранноеЗначение.РежимПодбора Тогда
			Если ДанныеФормыКоллекция<>Неопределено Тогда
				Фильтр = Новый Структура(ИмяРеквизитаИдентификатора, ВыбранноеЗначение.Идентификатор);
				СуществующиеСтроки = ДанныеФормыКоллекция.НайтиСтроки(Фильтр);
				Если СуществующиеСтроки.Количество() > 0 Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Элемент.ДобавитьСтроку();
		КонецЕсли;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные<>Неопределено Тогда
			ТекущиеДанные[ИмяРеквизитаИдентификатора] = ВыбранноеЗначение.Идентификатор;
			ТекущиеДанные[ИмяРеквизитаПредставления]  = ВыбранноеЗначение.Представление;
		КонецЕсли;
		
	ИначеЕсли ТипЭлемента=Тип("УправляемаяФорма") Тогда
		Элемент[ИмяРеквизитаИдентификатора] = ВыбранноеЗначение.Идентификатор;
		Элемент[ИмяРеквизитаПредставления]  = ВыбранноеЗначение.Представление;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет установку флага "Использовать" для всех строк таблицы.
//
// Таблица - ТаблицаЗначений - проверяемая таблица.
//
Функция ВТаблицеОтмеченыВсеЭлементы(Таблица) Экспорт
	
	Для Каждого Элемент Из Таблица Цикл
		
		Если Элемент.Использовать = Ложь Тогда
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Открывает модально журнал регистрации с отбором по событиям выгрузки или загрузки данных для заданного узла плана
// обмена.
//
Процедура ПерейтиВЖурналРегистрацииСобытийДанныхМодально(УзелИнформационнойБазы, Владелец, ДействиеПриОбмене) Экспорт
	
	// вызов сервера
	ПараметрыФормы = ОбменДаннымиВызовСервера.ПолучитьСтруктуруДанныхОтбораЖурналаРегистрации(УзелИнформационнойБазы, ДействиеПриОбмене);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, Владелец);
	
КонецПроцедуры

// Возвращает имя формы сообщения о неудачном обновлении при ошибке в ПРО при выполнении обновления информационной базы.
// 
// Возвращаемое значение:
//  Строка - имя формы сообщения о неудачном обновлении.
//
Функция ИмяФормыСообщенияОНеудачномОбновлении() Экспорт
	
	Возврат "РегистрСведений.ПравилаДляОбменаДанными.Форма.СообщениеОНеудачномОбновлении";
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики событий подсистем БСП.

// Процедура-обработчик запуска клиентского сеанса приложения.
// Если выполняется запуск ИБ для подчиненного узла РИБ,
// с необходимостью повтора загрузки сообщения обмена, тогда
// пользователю предлагается принять решение о повторной загрузке
// или продолжении без загрузки.
// 
Процедура ПередНачаломРаботыСистемы(Параметры) Экспорт
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	
	Если НЕ ПараметрыКлиента.Свойство("ПовторитьЗагрузкуСообщенияОбменаДаннымиПередЗапуском") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ИнтерактивнаяОбработка = Новый ОписаниеОповещения(
		"ИнтерактивнаяОбработкаПовторитьЗагрузкуСообщенияОбменаДаннымиПередЗапуском", ЭтотОбъект);
	
КонецПроцедуры

// Процедура-обработчик запуска клиентского сеанса приложения.
// Если выполняется первый запуск ИБ для подчиненного узла РИБ,
// то открывается форма помощника создания обмена данными.
// 
Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если ПараметрыРаботыКлиента.Свойство("ОткрытьПомощникСозданияОбменаДаннымиДляНастройкиПодчиненногоУзла") Тогда
		
		Для Каждого Окно Из ПолучитьОкна() Цикл
			Если Окно.Основное Тогда
				Окно.Активизировать();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ПараметрыФормы = Новый Структура("ИмяПланаОбмена, ЭтоПродолжениеНастройкиВПодчиненномУзлеРИБ", ПараметрыРаботыКлиента.ИмяПланаОбменаРИБ, Истина);
		ОткрытьФорму("Обработка.ПомощникСозданияОбменаДанными.Форма.Форма", ПараметрыФормы, , , , , , РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если НЕ ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных ИЛИ ПараметрыРаботыКлиента.РазделениеВключено Тогда
		Возврат;
	КонецЕсли;
		
	Если Не ПараметрыРаботыКлиента.Свойство("ОткрытьПомощникСозданияОбменаДаннымиДляНастройкиПодчиненногоУзла")
		И ПараметрыРаботыКлиента.Свойство("ПроверитьНеобходимостьОбновленияКонфигурацииПодчиненногоУзла") Тогда
		
		ПодключитьОбработчикОжидания("ПроверитьНеобходимостьОбновленияКонфигурацииПодчиненногоУзлаПриЗапуске", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Экспортные служебные функции-свойства.

// Возвращает максимально допустимое количество полей,
// которые отображаются в помощнике сопоставления объектов ИБ.
//
// Возвращаемое значение:
//     Число - максимальное количество полей для сопоставления.
//
Функция МаксимальноеКоличествоПолейСопоставленияОбъектов() Экспорт
	
	Возврат 5;
	
КонецФункции

// Возвращает структуру статусов выполнения загрузки данных.
//
Функция СтраницыСтатусаЗагрузкиДанных() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("Неопределено", "СтатусЗагрузкиНеопределено");
	Структура.Вставить("Ошибка",       "СтатусЗагрузкиОшибка");
	Структура.Вставить("Успех",        "СтатусЗагрузкиУспех");
	Структура.Вставить("Выполнение",   "СтатусЗагрузкиВыполнение");
	
	Структура.Вставить("Предупреждение_СообщениеОбменаБылоРанееПринято", "СтатусЗагрузкиПредупреждение");
	Структура.Вставить("ВыполненоСПредупреждениями",                     "СтатусЗагрузкиПредупреждение");
	Структура.Вставить("Ошибка_ТранспортСообщения",                      "СтатусЗагрузкиОшибка");
	
	Возврат Структура;
КонецФункции

// Возвращает структуру статусов выполнения выгрузки данных.
//
Функция СтраницыСтатусаВыгрузкиДанных() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("Неопределено", "СтатусВыгрузкиНеопределено");
	Структура.Вставить("Ошибка",       "СтатусВыгрузкиОшибка");
	Структура.Вставить("Успех",        "СтатусВыгрузкиУспех");
	Структура.Вставить("Выполнение",   "СтатусВыгрузкиВыполнение");
	
	Структура.Вставить("Предупреждение_СообщениеОбменаБылоРанееПринято", "СтатусВыгрузкиПредупреждение");
	Структура.Вставить("ВыполненоСПредупреждениями",                     "СтатусВыгрузкиПредупреждение");
	Структура.Вставить("Ошибка_ТранспортСообщения",                      "СтатусВыгрузкиОшибка");
	
	Возврат Структура;
КонецФункции

// Возвращает структуру с наименованием гиперссылки поля загрузки данных.
//
Функция ЗаголовкиГиперссылокЗагрузкиДанных() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("Неопределено",               НСтр("ru = 'Получение данных не выполнялось'"));
	Структура.Вставить("Ошибка",                     НСтр("ru = 'Не удалось получить данные'"));
	Структура.Вставить("ВыполненоСПредупреждениями", НСтр("ru = 'Данные получены с предупреждениями'"));
	Структура.Вставить("Успех",                      НСтр("ru = 'Данные успешно получены'"));
	Структура.Вставить("Выполнение",                 НСтр("ru = 'Выполняется получение данных...'"));
	
	Структура.Вставить("Предупреждение_СообщениеОбменаБылоРанееПринято", НСтр("ru = 'Нет новых данных для получения'"));
	Структура.Вставить("Ошибка_ТранспортСообщения",                      НСтр("ru = 'Не удалось получить данные'"));
	
	Возврат Структура;
КонецФункции

// Возвращает структуру с наименованием гиперссылки поля выгрузки данных.
//
Функция ЗаголовкиГиперссылокВыгрузкиДанных() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("Неопределено", НСтр("ru = 'Отправка данных не выполнялась'"));
	Структура.Вставить("Ошибка",       НСтр("ru = 'Не удалось отправить данные'"));
	Структура.Вставить("Успех",        НСтр("ru = 'Данные успешно отправлены'"));
	Структура.Вставить("Выполнение",   НСтр("ru = 'Выполняется отправка данных...'"));
	
	Структура.Вставить("Предупреждение_СообщениеОбменаБылоРанееПринято", НСтр("ru = 'Данные отправлены с предупреждениями'"));
	Структура.Вставить("ВыполненоСПредупреждениями",                     НСтр("ru = 'Данные отправлены с предупреждениями'"));
	Структура.Вставить("Ошибка_ТранспортСообщения",                      НСтр("ru = 'Не удалось отправить данные'"));
	
	Возврат Структура;
КонецФункции

// Открывает форму или гиперссылку с подробным описанием синхронизации данных.
//
Процедура ОткрытьПодробноеОписаниеСинхронизации(СсылкаНаПодробноеОписание) Экспорт
	
	Если ВРег(Лев(СсылкаНаПодробноеОписание, 4)) = "HTTP" Тогда
		
		ПерейтиПоНавигационнойСсылке(СсылкаНаПодробноеОписание);
		
	Иначе
		
		ОткрытьФорму(СсылкаНаПодробноеОписание);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму для ввода параметров прокси сервера.
//
Процедура ОткрытьФормуПараметровПроксиСервера() Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернетаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПолучениеФайловИзИнтернетаКлиент");
		МодульПолучениеФайловИзИнтернетаКлиент.ОткрытьФормуПараметровПроксиСервера();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Экспортные служебные процедуры и функции.

// Только для внутреннего использования.
//
Процедура ИнтерактивнаяОбработкаПовторитьЗагрузкуСообщенияОбменаДаннымиПередЗапуском(Параметры, Неопределен) Экспорт
	
	Форма = ОткрытьФорму(
		"РегистрСведений.НастройкиТранспортаОбмена.Форма.ПовторнаяСинхронизацияДанныхПередЗапуском", , , , , ,
		Новый ОписаниеОповещения(
			"ПослеЗакрытияФормыПовторнаяСинхронизацияДанныхПередЗапуском", ЭтотОбъект, Параметры));
	
	Если Форма = Неопределено Тогда
		ПослеЗакрытияФормыПовторнаяСинхронизацияДанныхПередЗапуском("Продолжить", Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры.
// ИнтерактивнаяОбработкаПовторитьЗагрузкуСообщенияОбменаДаннымиПередЗапуском.
//
Процедура ПослеЗакрытияФормыПовторнаяСинхронизацияДанныхПередЗапуском(Результат, Параметры) Экспорт
	
	Если Результат <> "Продолжить" Тогда
		Параметры.Отказ = Истина;
	Иначе
		Параметры.ПолученныеПараметрыКлиента.Вставить(
			"ПовторитьЗагрузкуСообщенияОбменаДаннымиПередЗапуском");
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработкаПродолжения);
	
КонецПроцедуры

// Только для внутреннего использования. Продолжение процедуры.
// ФормаНастройкиПередЗакрытием.
//
Процедура ФормаНастройкиПередЗакрытиемЗавершение(Ответ, Форма) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Модифицированность = Ложь;
	Форма.Закрыть();
	
	// Очищаем кэш повторного использования для сброса возможных COM соединений.
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

// Открывает файл в ассоциированном приложении операционной системы.
//
// Параметры:
//     Объект               - Произвольный - Объект из которого по имени свойства будет получено имя файла для открытия.
//     ИмяСвойства          - Строка       - Имя свойства объекта из которого будет получено имя файла для открытия.
//     СтандартнаяОбработка - Булево       - Флаг стандартной обработки, устанавливается в Ложь.
//
Процедура ОбработчикОткрытияФайлаИлиКаталога(Объект, ИмяСвойства, СтандартнаяОбработка = Ложь) Экспорт
	СтандартнаяОбработка = Ложь;
	
	ПолноеИмяФайла = Объект[ИмяСвойства];
	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикОткрытияФайлаИлиКаталогаПослеНачалаЗапускаПриложения", ЭтотОбъект);
	НачатьЗапускПриложения(Оповещение, ПолноеИмяФайла);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
Процедура ОбработчикОткрытияФайлаИлиКаталогаПослеНачалаЗапускаПриложения(КодВозврата, ДополнительныеПараметры) Экспорт
	// Обработка не требуется.
КонецПроцедуры

// Открывает диалог для выбора файлового каталога, запрашивая установку расширения для работы с файлами.
//
// Параметры:
//     Объект                - Произвольный       - Объект, в котором будет установлено выбираемое свойство.
//     ИмяСвойства           - Строка             - Имя свойства с именем файла, устанавливаемого в объекте. Источник
//                                                  начального значения.
//     СтандартнаяОбработка  - Булево             - Флаг стандартной обработки, устанавливается в Ложь.
//     ПараметрыДиалога      - Структура          - Необязательные дополнительные параметры диалога выбора каталога.
//     ОповещениеЗавершения  - ОписаниеОповещения - Необязательное оповещение, которое будет вызвано со следующими
//                                                  параметрами:
//                                 Результат               - Строка - Выбранное значение (массив строк, если был
//                                                                    множественный выбор);
//                                 ДополнительныеПараметры - Неопределено.
//
Процедура ОбработчикВыбораФайловогоКаталога(Объект, Знач ИмяСвойства, СтандартнаяОбработка = Ложь, Знач ПараметрыДиалога = Неопределено, ОповещениеЗавершения = Неопределено) Экспорт
	СтандартнаяОбработка = Ложь;
	
	УмолчанияДиалога = Новый Структура;
	УмолчанияДиалога.Вставить("Заголовок", НСтр("ru = 'Укажите каталог'") );
	
	УстановитьЗначенияСтруктурыПоУмолчанию(ПараметрыДиалога, УмолчанияДиалога);
	
	ТекстПредупреждения = НСтр("ru = 'Для данной операции необходимо установить расширение для веб-клиента 1С:Предприятие.'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект",               Объект);
	ДополнительныеПараметры.Вставить("ИмяСвойства",          ИмяСвойства);
	ДополнительныеПараметры.Вставить("ПараметрыДиалога",     ПараметрыДиалога);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикВыбораФайловогоКаталогаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Оповещение, ТекстПредупреждения, Ложь);
КонецПроцедуры

// Обработчик немодального завершения диалога выбора каталога.
//
Процедура ОбработчикВыбораФайловогоКаталогаЗавершение(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСвойства = ДополнительныеПараметры.ИмяСвойства;
	Объект      = ДополнительныеПараметры.Объект;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ЗаполнитьЗначенияСвойств(Диалог, ДополнительныеПараметры.ПараметрыДиалога);
	
	Диалог.Каталог = Объект[ИмяСвойства];
	Если Диалог.Выбрать() Тогда
		Объект[ИмяСвойства] = Диалог.Каталог;
		
		Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
			Результат = ?(Диалог.МножественныйВыбор, Диалог.ВыбранныеФайлы, Диалог.Каталог);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Открывает диалог для выбора файла, запрашивая установку расширения для работы с файлами.
//
// Параметры:
//     Объект                - Произвольный       - Объект, в котором будет установлено выбираемое свойство.
//     ИмяСвойства           - Строка             - Имя свойства с именем файла, устанавливаемого в объекте. Источник
//                                                  начального значения.
//     СтандартнаяОбработка  - Булево             - Флаг стандартной обработки, устанавливается в Ложь.
//     ПараметрыДиалога      - Структура          - Необязательные дополнительные параметры диалога выбора файла.
//     ОповещениеЗавершения  - ОписаниеОповещения - Необязательное оповещение, которое будет вызвано со следующими
//                                                  параметрами:
//                                 Результат               - Строка - Выбранное значение (массив строк, если был
//                                                                    множественный выбор);
//                                 ДополнительныеПараметры - Неопределено.
//
//
Процедура ОбработчикВыбораФайла(Объект, Знач ИмяСвойства, СтандартнаяОбработка = Ложь, Знач ПараметрыДиалога = Неопределено, ОповещениеЗавершения = Неопределено) Экспорт
	СтандартнаяОбработка = Ложь;
	
	УмолчанияДиалога = Новый Структура;
	УмолчанияДиалога.Вставить("Режим",                       РежимДиалогаВыбораФайла.Открытие);
	УмолчанияДиалога.Вставить("ПроверятьСуществованиеФайла", Истина);
	УмолчанияДиалога.Вставить("Заголовок",                   НСтр("ru = 'Выберите файл'"));
	УмолчанияДиалога.Вставить("МножественныйВыбор",          Ложь);
	УмолчанияДиалога.Вставить("ПредварительныйПросмотр",     Ложь);
	
	УстановитьЗначенияСтруктурыПоУмолчанию(ПараметрыДиалога, УмолчанияДиалога);
	
	ТекстПредупреждения = НСтр("ru = 'Для данной операции необходимо установить расширение для веб-клиента 1С:Предприятие.'");
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикВыбораФайлаЗавершение", ЭтотОбъект, Новый Структура);
	Оповещение.ДополнительныеПараметры.Вставить("Объект",               Объект);
	Оповещение.ДополнительныеПараметры.Вставить("ИмяСвойства",          ИмяСвойства);
	Оповещение.ДополнительныеПараметры.Вставить("ПараметрыДиалога",     ПараметрыДиалога);
	Оповещение.ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Оповещение, ТекстПредупреждения, Ложь);
КонецПроцедуры

// Обработчик немодального завершения диалога выбора файла.
//
Процедура ОбработчикВыбораФайлаЗавершение(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСвойства = ДополнительныеПараметры.ИмяСвойства;
	Объект      = ДополнительныеПараметры.Объект;
	
	ПараметрыДиалогаВыбора = ДополнительныеПараметры.ПараметрыДиалога;
	Диалог = Новый ДиалогВыбораФайла(ПараметрыДиалогаВыбора.Режим);
	ЗаполнитьЗначенияСвойств(Диалог, ПараметрыДиалогаВыбора);
	
	Диалог.ПолноеИмяФайла = Объект[ИмяСвойства];
	Если Диалог.Выбрать() Тогда
		Объект[ИмяСвойства] = Диалог.ПолноеИмяФайла;
		
		Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда
			Результат = ?(Диалог.МножественныйВыбор, Диалог.ВыбранныеФайлы, Диалог.ПолноеИмяФайла);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, Результат);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Передает файлы на сервер, запрашивая установку расширения для работы с файлами.
//
// Параметры:
//     ОповещениеЗавершения - ОписаниеОповещения - Экспортная процедура, которая будет вызвана следующими параметрами:
//                                Результат               - Массив - Содержит структуры с полями Имя, Хранение,
//                                                                   ОписаниеОшибки для каждого файла.
//                                ДополнительныеПараметры - Неопределено.
//
//     ИменаФайлов          - Массив                  - Массив имен файлов для передачи.
//     ИдентификаторФормы   - УникальныйИдентификатор - Параметр для хранилища.
//     ТекстПредупреждения  - Строка                  - Текст предупреждения о необходимости установки расширения для
//                                                      работы с файлами.
//
Процедура ПередатьФайлыНаСервер(ОповещениеЗавершения, Знач ИменаФайлов, Знач ИдентификаторФормы = Неопределено, Знач ТекстПредупреждения = Неопределено) Экспорт
	
	ДанныеФайлов = Новый Массив;
	ЕстьПустые   = Ложь;
	
	Для Каждого ИмяФайла Из ИменаФайлов Цикл
		ОписаниеФайла = Новый Структура("Имя, Хранение, ОписаниеОшибки", ИмяФайла);
		Если ПустаяСтрока(ИмяФайла) Тогда
			ЕстьПустые = Истина;
			ОписаниеФайла.ОписаниеОшибки = НСтр("ru = 'Файл не выбран.'");
		КонецЕсли;
		ДанныеФайлов.Добавить(ОписаниеФайла);
	КонецЦикла;
	
	Если ЕстьПустые Тогда
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, ДанныеФайлов);
		Возврат;
	КонецЕсли;

	Оповещение= Новый ОписаниеОповещения("ПередатьФайлНаСерверЗавершениеПодключенияРасширения", ЭтотОбъект, Новый Структура);
	Оповещение.ДополнительныеПараметры.Вставить("Оповещение",         ОповещениеЗавершения);
	Оповещение.ДополнительныеПараметры.Вставить("ДанныеФайлов",       ДанныеФайлов);
	Оповещение.ДополнительныеПараметры.Вставить("ИдентификаторФормы", ИдентификаторФормы);
	
	Если ТекстПредупреждения = Неопределено Тогда
		Если ИменаФайлов.Количество() = 1 Тогда
			ТекстПредупреждения = НСтр("ru = 'Для передачи файла на сервер необходимо установить расширение для веб-клиента 1С:Предприятие.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Для передачи файлов на сервер необходимо установить расширение для веб-клиента 1С:Предприятие.'");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(Оповещение, ТекстПредупреждения, Ложь);
КонецПроцедуры

// Обработчик немодального завершения передачи файлов на сервер.
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширения(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	// Расширение успешно установлено.
	
	ПараметрыЗаполненияСпискаДляПередачи = Новый Структура;
	ПараметрыЗаполненияСпискаДляПередачи.Вставить("ДанныеФайлов", ДополнительныеПараметры.ДанныеФайлов);
	ПараметрыЗаполненияСпискаДляПередачи.Вставить("СписокДляПередачи", Новый Массив);
	ПараметрыЗаполненияСпискаДляПередачи.Вставить("БылиОшибки", Ложь);
	ПараметрыЗаполненияСпискаДляПередачи.Вставить("НомерПозиции", 0);
	ДополнительныеПараметры.Вставить("ПараметрыЗаполненияСпискаДляПередачи", ПараметрыЗаполненияСпискаДляПередачи);
	
	ПередатьФайлНаСерверЗавершениеПодключенияРасширенияЗаполнитьСписокДляПередачи(ДополнительныеПараметры);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширенияЗаполнитьСписокДляПередачи(ДополнительныеПараметры)
	
	ПараметрыЗаполненияСпискаДляПередачи = ДополнительныеПараметры.ПараметрыЗаполненияСпискаДляПередачи;
	Путь = ПараметрыЗаполненияСпискаДляПередачи.ДанныеФайлов[ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции].Имя;
	Оповещение = Новый ОписаниеОповещения(
		"ПередатьФайлНаСерверЗавершениеПодключенияРасширенияПослеИнициализацииФайла",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ПроверяемыйФайл = Новый Файл();
	ПроверяемыйФайл.НачатьИнициализацию(Оповещение, Путь);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширенияПослеИнициализацииФайла(Файл, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ПараметрыЗаполненияСпискаДляПередачи.Вставить("Файл", Файл);
	Оповещение = Новый ОписаниеОповещения(
		"ПередатьФайлНаСерверЗавершениеПодключенияРасширенияПослеПроверкиСуществованияФайла",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	Файл.НачатьПроверкуСуществования(Оповещение);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширенияПослеПроверкиСуществованияФайла(Существует, ДополнительныеПараметры) Экспорт
	
	ПараметрыЗаполненияСпискаДляПередачи = ДополнительныеПараметры.ПараметрыЗаполненияСпискаДляПередачи;
	Если Не Существует Тогда
		ПараметрыЗаполненияСпискаДляПередачи.БылиОшибки = Истина;
		Элемент = ПараметрыЗаполненияСпискаДляПередачи.ДанныеФайлов[ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции];
		Элемент.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файл ""%1"" не существует или к нему нет доступа'"), Элемент.Имя);
			
		// Переходим к следующему файлу, либо завершаем заполнение списка для передачи.
		Если ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции = ПараметрыЗаполненияСпискаДляПередачи.ДанныеФайлов.ВГраница() Тогда
			ПередатьФайлНаСерверЗавершениеПодключенияРасширенияДействияПослеЗаполненияСпискаПередачи(ДополнительныеПараметры);
		Иначе
			ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции = ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции + 1;
			ПередатьФайлНаСерверЗавершениеПодключенияРасширенияЗаполнитьСписокДляПередачи(ДополнительныеПараметры);
		КонецЕсли;
		
	Иначе
		
		Оповещение = Новый ОписаниеОповещения(
			"ПередатьФайлНаСерверЗавершениеПодключенияРасширенияПослеПроверкиНаКаталог",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ДополнительныеПараметры.ПараметрыЗаполненияСпискаДляПередачи.Файл.НачатьПроверкуЭтоКаталог(Оповещение);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширенияПослеПроверкиНаКаталог(ЭтоКаталог, ДополнительныеПараметры) Экспорт
	
	ПараметрыЗаполненияСпискаДляПередачи = ДополнительныеПараметры.ПараметрыЗаполненияСпискаДляПередачи;
	Элемент = ПараметрыЗаполненияСпискаДляПередачи.ДанныеФайлов[ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции];
	
	Если ЭтоКаталог Тогда
		ПараметрыЗаполненияСпискаДляПередачи.БылиОшибки = Истина;
		Элемент.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Файл ""%1"" не существует или к нему нет доступа'"), Элемент.Имя);
	Иначе
		ПараметрыЗаполненияСпискаДляПередачи.СписокДляПередачи.Добавить(Новый ОписаниеПередаваемогоФайла(Элемент.Имя));
	КонецЕсли;
	
	// Переходим к следующему файлу, либо завершаем заполнение списка для передачи.
	Если ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции = ПараметрыЗаполненияСпискаДляПередачи.ДанныеФайлов.ВГраница() Тогда
		ПередатьФайлНаСерверЗавершениеПодключенияРасширенияДействияПослеЗаполненияСпискаПередачи(ДополнительныеПараметры);
	Иначе
		ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции = ПараметрыЗаполненияСпискаДляПередачи.НомерПозиции + 1;
		ПередатьФайлНаСерверЗавершениеПодключенияРасширенияЗаполнитьСписокДляПередачи(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширенияДействияПослеЗаполненияСпискаПередачи(ДополнительныеПараметры)
	
	ПараметрыЗаполненияСпискаДляПередачи = ДополнительныеПараметры.ПараметрыЗаполненияСпискаДляПередачи;
	
	Если ПараметрыЗаполненияСпискаДляПередачи.БылиОшибки Тогда
		
		// Оповещаем исходного вызывающего.
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.ДанныеФайлов);
		
	Иначе
		ПомещенныеФайлы = Новый Массив;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередатьФайлНаСерверЗавершениеПодключенияРасширенияДействияПослеПомещенияФайлов",
			ЭтотОбъект, ДополнительныеПараметры);
		
		НачатьПомещениеФайлов(
			ОписаниеОповещения,
			ПараметрыЗаполненияСпискаДляПередачи.СписокДляПередачи,,
			Ложь,
			ДополнительныеПараметры.ИдентификаторФормы);
		
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПередатьФайлНаСерверЗавершениеПодключенияРасширенияДействияПослеПомещенияФайлов(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	Для Индекс = 0 По ПомещенныеФайлы.ВГраница() Цикл
		ДополнительныеПараметры.ДанныеФайлов[Индекс].Хранение = ПомещенныеФайлы[Индекс].Хранение;
	КонецЦикла;
	
	// Оповещаем исходного вызывающего.
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ДополнительныеПараметры.ДанныеФайлов);
	
КонецПроцедуры

// Передает файл на сервер интерактивно, без расширения для работы с файлами.
//
// Параметры:
//     ОповещениеЗавершения - ОписаниеОповещения - Экспортная процедура, которая будет вызвано со следующими
//                                                 параметрами:
//                                Результат               - Структура с полями Имя, Хранение, ОписаниеОшибки.
//                                ДополнительныеПараметры - Неопределено.
//
//     ПараметрыДиалога     - Структура                       - Необязательные дополнительные параметры диалога выбора
//                                                              файлов.
//     ИдентификаторФормы   - Строка, УникальныйИдентификатор - Параметр для хранилища.
//
Процедура ВыбратьИПередатьФайлНаСервер(ОповещениеЗавершения, Знач ПараметрыДиалога = Неопределено, Знач ИдентификаторФормы = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	ДополнительныеПараметры.Вставить("ПараметрыДиалога", ПараметрыДиалога);
	ДополнительныеПараметры.Вставить("ИдентификаторФормы", ИдентификаторФормы);
	
	ОповещениеПодключенияРасширенияРаботыСФайлами = Новый ОписаниеОповещения(
		"ВыбратьИПередатьФайлНаСерверПослеПодключенияРасширенияРаботыСФайлами",
		ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСФайлами(ОповещениеПодключенияРасширенияРаботыСФайлами);

КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ВыбратьИПередатьФайлНаСерверПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДополнительныеПараметры) Экспорт
	
	Результат  = Новый Структура("Имя, Хранение, ОписаниеОшибки");
	ДополнительныеПараметры.Вставить("Результат", Результат);
	Оповещение = Новый ОписаниеОповещения("ВыбратьИПередатьФайлыНаСерверЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если Не Подключено Тогда
		// У нас нет расширения, используем диалог выбора из НачатьПомещениеФайла.
		НачатьПомещениеФайла(Оповещение, , , Истина, ДополнительныеПараметры.ИдентификаторФормы);
		Возврат;
	КонецЕсли;
	
	// У нас есть расширение, используем диалог.
	УмолчанияДиалога = Новый Структура;
	УмолчанияДиалога.Вставить("ПроверятьСуществованиеФайла", Истина);
	УмолчанияДиалога.Вставить("Заголовок",                   НСтр("ru = 'Выберите файл'"));
	УмолчанияДиалога.Вставить("МножественныйВыбор",          Ложь);
	УмолчанияДиалога.Вставить("ПредварительныйПросмотр",     Ложь);
	
	УстановитьЗначенияСтруктурыПоУмолчанию(ДополнительныеПараметры.ПараметрыДиалога, УмолчанияДиалога);
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ЗаполнитьЗначенияСвойств(ДиалогВыбора, ДополнительныеПараметры.ПараметрыДиалога);
	
	ОписаниеОповещенияДиалогаВыбора = Новый ОписаниеОповещения("ВыбратьИПередатьФайлНаСерверПослеВыбораВДиалоге", ЭтотОбъект, ДополнительныеПараметры);
	ДиалогВыбора.Показать(ОписаниеОповещенияДиалогаВыбора);

КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ВыбратьИПередатьФайлНаСерверПослеВыбораВДиалоге(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено
		И ВыбранныеФайлы.Количество() = 1 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВыбратьИПередатьФайлыНаСерверЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		НачатьПомещениеФайла(Оповещение, , ВыбранныеФайлы[0], Ложь, ДополнительныеПараметры.ИдентификаторФормы);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик немодального завершения выбора и передачи файлов на сервер.
//
Процедура ВыбратьИПередатьФайлыНаСерверЗавершение(Знач Успешно, Знач Адрес, Знач ВыбранноеИмяФайла, Знач ДополнительныеПараметры) Экспорт
	Если Не Успешно Тогда
		Возврат;
	КонецЕсли;
	
	// Оповещаем исходного вызывающего.
	Результат = ДополнительныеПараметры.Результат;
	Результат.Имя      = ВыбранноеИмяФайла;
	Результат.Хранение = Адрес;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, Результат);
КонецПроцедуры

// Запускает получение файла с сервера интерактивно, без расширения для работы с файлами.
//
// Параметры:
//     ПолучаемыйФайл   - Структура - Описание файла для получения. Содержит свойства Имя и Хранение.
//     ПараметрыДиалога - Структура - Необязательные дополнительные параметры диалога выбора файлов.
//
Процедура ВыбратьИСохранитьФайлНаКлиенте(Знач ПолучаемыйФайл, Знач ПараметрыДиалога = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолучаемыйФайл", ПолучаемыйФайл);
	ДополнительныеПараметры.Вставить("ПараметрыДиалога", ПараметрыДиалога);
	
	ОповещениеПодключенияРасширенияРаботыСФайлами = Новый ОписаниеОповещения(
		"ВыбратьИСохранитьФайлНаКлиентеПослеПодключенияРасширенияРаботыСФайлами",
		ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПодключениеРасширенияРаботыСФайлами(ОповещениеПодключенияРасширенияРаботыСФайлами);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ВыбратьИСохранитьФайлНаКлиентеПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		// У нас нет расширения, используем диалог выбора из ПолучитьФайл.
		ПолучитьФайл(ДополнительныеПараметры.ПолучаемыйФайл.Хранение, ДополнительныеПараметры.ПолучаемыйФайл.Имя, Истина);
		Возврат;
	КонецЕсли;
	
	// Доступно расширение, используем диалог.
	УмолчанияДиалога = Новый Структура;
	УмолчанияДиалога.Вставить("Заголовок",               НСтр("ru = 'Выберите файл для сохранения'"));
	УмолчанияДиалога.Вставить("МножественныйВыбор",      Ложь);
	УмолчанияДиалога.Вставить("ПредварительныйПросмотр", Ложь);
	
	УстановитьЗначенияСтруктурыПоУмолчанию(ДополнительныеПараметры.ПараметрыДиалога, УмолчанияДиалога);
	
	ДиалогСохранения = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ЗаполнитьЗначенияСвойств(ДиалогСохранения, ДополнительныеПараметры.ПараметрыДиалога);
	
	Получаемые = Новый Массив;
	Получаемые.Добавить( Новый ОписаниеПередаваемогоФайла(ДополнительныеПараметры.ПолучаемыйФайл.Имя,
		ДополнительныеПараметры.ПолучаемыйФайл.Хранение) );
	
	ОписаниеОповещенияПолученияФайлов = Новый ОписаниеОповещения(
		"ВыбратьИСохранитьФайлНаКлиентеПослеПолученияФайлов",
		ЭтотОбъект, ДополнительныеПараметры);
	
	НачатьПолучениеФайлов(ОписаниеОповещенияПолученияФайлов, Получаемые, ДиалогСохранения, Истина);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ВыбратьИСохранитьФайлНаКлиентеПослеПолученияФайлов(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	// Обработка не требуется.
КонецПроцедуры

// Добавляет поля в целевую структуру, если их там нет.
//
// Параметры:
//     Результат           - Структура - Целевая структура.
//     ЗначенияПоУмолчанию - Структура - Значения по умолчанию.
//
Процедура УстановитьЗначенияСтруктурыПоУмолчанию(Результат, Знач ЗначенияПоУмолчанию)
	
	Если Результат = Неопределено Тогда
		Результат = Новый Структура;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ЗначенияПоУмолчанию Цикл
		ИмяСвойства = КлючЗначение.Ключ;
		Если Не Результат.Свойство(ИмяСвойства) Тогда
			Результат.Вставить(ИмяСвойства, КлючЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Открывает форму записи регистра сведений по заданному отбору.
Процедура ОткрытьФормуЗаписиРегистраСведенийПоОтбору(
												Отбор,
												ЗначенияЗаполнения,
												Знач ИмяРегистра,
												ФормаВладелец,
												Знач ИмяФормы = "",
												ПараметрыФормы = Неопределено,
												ОповещениеОЗакрытии = Неопределено) Экспорт
	
	Перем КлючЗаписи;
	
	НаборЗаписейПустой = ОбменДаннымиВызовСервера.НаборЗаписейРегистраПустой(Отбор, ИмяРегистра);
	
	Если Не НаборЗаписейПустой Тогда
		// Создаем через тип, так как находимся на клиенте.
		
		ТипЗначения = Тип("РегистрСведенийКлючЗаписи." + ИмяРегистра);
		Параметры = Новый Массив(1);
		Параметры[0] = Отбор;
		
		КлючЗаписи = Новый(ТипЗначения, Параметры);
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Ключ",               КлючЗаписи);
	ПараметрыЗаписи.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Если ПараметрыФормы <> Неопределено Тогда
		
		Для Каждого Элемент Из ПараметрыФормы Цикл
			
			ПараметрыЗаписи.Вставить(Элемент.Ключ, Элемент.Значение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяФормы) Тогда
		
		ПолноеИмяФормы = "РегистрСведений.[ИмяРегистра].ФормаЗаписи";
		ПолноеИмяФормы = СтрЗаменить(ПолноеИмяФормы, "[ИмяРегистра]", ИмяРегистра);
		
	Иначе
		
		ПолноеИмяФормы = "РегистрСведений.[ИмяРегистра].Форма.[ИмяФормы]";
		ПолноеИмяФормы = СтрЗаменить(ПолноеИмяФормы, "[ИмяРегистра]", ИмяРегистра);
		ПолноеИмяФормы = СтрЗаменить(ПолноеИмяФормы, "[ИмяФормы]", ИмяФормы);
		
	КонецЕсли;
	
	// открываем форму записи РС
	Если ОповещениеОЗакрытии <> Неопределено Тогда
		ОткрытьФорму(ПолноеИмяФормы, ПараметрыЗаписи, ФормаВладелец, , , , ОповещениеОЗакрытии);
	Иначе
		ОткрытьФорму(ПолноеИмяФормы, ПараметрыЗаписи, ФормаВладелец);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму загрузки правил конвертации и регистрации единым файлом.
//
Процедура ЗагрузитьПравилаСинхронизацииДанных(Знач ИмяПланаОбмена) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяПланаОбмена", ИмяПланаОбмена);
	
	ОткрытьФорму("РегистрСведений.ПравилаДляОбменаДанными.Форма.ЗагрузитьПравилаСинхронизацииДанных", ПараметрыФормы,, ИмяПланаОбмена);
	
КонецПроцедуры

// Открывает журнал регистрации с отбором по событиям выгрузки или загрузки данных для заданного узла плана обмена.
// 
Процедура ПерейтиВЖурналРегистрацииСобытийДанных(УзелИнформационнойБазы, ПараметрыВыполненияКоманды, ДействиеПриОбменеСтрокой) Экспорт
	
	СобытиеЖурналаРегистрации = ОбменДаннымиВызовСервера.ПолучитьКлючСообщенияЖурналаРегистрацииПоСтрокеДействия(УзелИнформационнойБазы, ДействиеПриОбменеСтрокой);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", СобытиеЖурналаРегистрации);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

// Открывает форму выполнения обмена данными для заданного узла плана обмена.
//
// Параметры:
//  УзелИнформационнойБазы - ПланОбменаСсылка - узел плана обмена, для которого необходимо открыть форму;
//  Владелец               - форма-владелец для открываемой формы;
// 
Процедура ВыполнитьОбменДаннымиОбработкаКоманды(УзелИнформационнойБазы, Владелец,
		АдресДляВосстановленияПароляУчетнойЗаписи = "", Знач АвтоматическаяСинхронизация = Неопределено) Экспорт
	
	Если АвтоматическаяСинхронизация = Неопределено Тогда
		АвтоматическаяСинхронизация = (ОбменДаннымиВызовСервера.ВариантОбменаДанными(УзелИнформационнойБазы) = "Синхронизация");
	КонецЕсли;
	
	Если АвтоматическаяСинхронизация Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("УзелИнформационнойБазы", УзелИнформационнойБазы);
		ПараметрыФормы.Вставить("АдресДляВосстановленияПароляУчетнойЗаписи", АдресДляВосстановленияПароляУчетнойЗаписи);
		
		ОткрытьФорму("Обработка.ВыполнениеОбменаДанными.Форма.Форма", ПараметрыФормы, Владелец, УзелИнформационнойБазы);
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("УзелИнформационнойБазы", УзелИнформационнойБазы);
		ПараметрыФормы.Вставить("РасширенныйРежимДополненияВыгрузки", Истина);
		
		ИмяФормы = "Обработка.ПомощникИнтерактивногоОбменаДанными.Форма";
		ОткрытьФорму(ИмяФормы, ПараметрыФормы, Владелец, УзелИнформационнойБазы);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму интерактивного выполнения обмена данными для заданного узла плана обмена.
//
// Параметры:
//  УзелИнформационнойБазы - ПланОбменаСсылка - узел плана обмена, для которого необходимо открыть форму;
//  Владелец               - форма-владелец для открываемой формы;
//
Процедура ОткрытьПомощникСопоставленияОбъектовОбработкаКоманды(УзелИнформационнойБазы, Владелец) Экспорт
	
	// Открываем форму помощника сопоставления объектов;
	// в качестве параметра формы задаем узел информационной базы;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УзелИнформационнойБазы", УзелИнформационнойБазы);
	ПараметрыФормы.Вставить("РасширенныйРежимДополненияВыгрузки", Истина);
	
	ИмяФормы = "Обработка.ПомощникИнтерактивногоОбменаДанными.Форма";
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, Владелец, УзелИнформационнойБазы, , , ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Открывает форму списка сценариев выполнения обмена данными для заданного узла плана обмена.
//
// Параметры:
//  УзелИнформационнойБазы - ПланОбменаСсылка - узел плана обмена, для которого необходимо открыть форму;
//  Владелец               - форма-владелец для открываемой формы;
//
Процедура ОбработкаКомандыНастроитьРасписаниеВыполненияОбмена(УзелИнформационнойБазы, Владелец) Экспорт
	
	ПараметрыФормы = Новый Структура("УзелИнформационнойБазы", УзелИнформационнойБазы);
	
	ОткрытьФорму("Справочник.СценарииОбменовДанными.Форма.НастройкаРасписанияОбменовДанными", ПараметрыФормы, Владелец);
	
КонецПроцедуры

// Оповещает все открытые динамические списки о необходимости обновить отображаемые данные.
//
Процедура ОбновитьВсеОткрытыеДинамическиеСписки() Экспорт
	
	Типы = ОбменДаннымиВызовСервера.ВсеСсылочныеТипыКонфигурации();
	
	Для Каждого Тип Из Типы Цикл
		
		ОповеститьОбИзменении(Тип);
		
	КонецЦикла;
	
КонецПроцедуры

// Открывает форму монитора зарегистрированных к отправке данных.
//
Процедура ОткрытьСоставОтправляемыхДанных(Знач УзелИнформационнойБазы) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УзелОбмена", УзелИнформационнойБазы);
	ПараметрыФормы.Вставить("ЗапрещеноВыбиратьУзелОбмена", Истина);
	
	// Служебные данные, которые нельзя править при доступе к обработке через команду.
	ПараметрыФормы.Вставить("ИменаСкрываемыхМетаданных", Новый СписокЗначений);
	ПараметрыФормы.ИменаСкрываемыхМетаданных.Добавить("РегистрСведений.СоответствияОбъектовИнформационныхБаз");
	
	НеВыгружатьПоПравилам = ОбменДаннымиВызовСервера.ИменаМетаданныхНеВыгружаемыхОбъектовУзла(УзелИнформационнойБазы);
	Для Каждого ИмяМетаданных Из НеВыгружатьПоПравилам Цикл
		ПараметрыФормы.ИменаСкрываемыхМетаданных.Добавить(ИмяМетаданных);
	КонецЦикла;
	
	ОткрытьФорму("Обработка.РегистрацияИзмененийДляОбменаДанными.Форма", ПараметрыФормы,, УзелИнформационнойБазы);
КонецПроцедуры

// Выполняет удаление настройки синхронизации данных.
//
Процедура УдалитьНастройкуСинхронизации(Знач УзелИнформационнойБазы) Экспорт
	
	Если ОбменДаннымиВызовСервера.УзелЯвляетсяГлавным(УзелИнформационнойБазы) Тогда
		ТекстПредупреждения = НСтр("ru = 'Отключение информационной базы от главного узла можно выполнить
			|с помощью параметра запуска конфигуратора /ResetMasterNode.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе
		ТекстВопроса = НСтр("ru = 'Удалить настройку синхронизации данных?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьНастройкуСинхронизацииЗавершение", ЭтотОбъект, УзелИнформационнойБазы);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения
//
Процедура УдалитьНастройкуСинхронизацииЗавершение(Ответ, УзелИнформационнойБазы) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеУдаленияРазрешений", ЭтотОбъект, УзелИнформационнойБазы);
		Запросы = ОбменДаннымиВызовСервера.ЗапросНаОчисткуРазрешенийИспользованияВнешнихРесурсов(УзелИнформационнойБазы);
		РаботаВБезопасномРежимеКлиент.ПрименитьЗапросыНаИспользованиеВнешнихРесурсов(Запросы, Неопределено, ОповещениеОЗакрытии);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеУдаленияРазрешений(Результат, УзелИнформационнойБазы)Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		
		ОбменДаннымиВызовСервера.УдалитьНастройкуСинхронизации(УзелИнформационнойБазы);
		Оповестить("Запись_УзелПланаОбмена");
		ЗакрытьФормы("ФормаУзла");
		
	КонецЕсли;
	
КонецПроцедуры

// Закрывает все открытые формы, в имени которых встречается указанная подстрока,
// и у которых не установлен флаг модифицированности.
//
Процедура ЗакрытьФормы(Знач ИмяФормы)
	
	Окна = ПолучитьОкна();
	
	Если Окна <> Неопределено Тогда
		
		Для Каждого Окно Из Окна Цикл
			
			Если Не Окно.Основное Тогда
				
				Форма = Окно.ПолучитьСодержимое();
				
				Если ТипЗнч(Форма) = Тип("УправляемаяФорма")
					И Не Форма.Модифицированность
					И СтрНайти(Форма.ИмяФормы, ИмяФормы) <> 0 Тогда
					
					Форма.Закрыть();
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Регистрирует обработчик для открытия новой формы сразу после закрытия текущей.
// 
Процедура ОткрытьФормуПослеЗакрытияТекущей(ТекущаяФорма, Знач ИмяФормы, Знач Параметры = Неопределено, Знач ПараметрыОткрытия = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяФормы",          ИмяФормы);
	ДополнительныеПараметры.Вставить("Параметры",         Параметры);
	ДополнительныеПараметры.Вставить("ПараметрыОткрытия", ПараметрыОткрытия);
	
	ДополнительныеПараметры.Вставить("ПредыдущееОповещениеОЗакрытии",  ТекущаяФорма.ОписаниеОповещенияОЗакрытии);
	
	ТекущаяФорма.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОткрытияФормыПослеЗакрытияТекущей", ЭтотОбъект, ДополнительныеПараметры);
КонецПроцедуры

// Отложенное открытие
Процедура ОбработчикОткрытияФормыПослеЗакрытияТекущей(Знач РезультатЗакрытия, Знач ДополнительныеПараметры) Экспорт
	
	ПараметрыОткрытия = Новый Структура("Владелец, Уникальность, Окно, НавигационнаяСсылка, ОписаниеОповещенияОЗакрытии, РежимОткрытияОкна");
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ДополнительныеПараметры.ПараметрыОткрытия);
	ОткрытьФорму(ДополнительныеПараметры.ИмяФормы, ДополнительныеПараметры.Параметры,
		ПараметрыОткрытия.Владелец, ПараметрыОткрытия.Уникальность, ПараметрыОткрытия.Окно, 
		ПараметрыОткрытия.НавигационнаяСсылка, ПараметрыОткрытия.ОписаниеОповещенияОЗакрытии, ПараметрыОткрытия.РежимОткрытияОкна);
	
	Если ДополнительныеПараметры.ПредыдущееОповещениеОЗакрытии <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ПредыдущееОповещениеОЗакрытии, РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов из других подсистем.

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов в другие подсистемы.

// Обновляет конфигурацию базы данных.
//
Процедура УстановитьОбновлениеКонфигурации(ЗавершениеРаботыСистемы = Ложь) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ОбновлениеКонфигурации") Тогда
		МодульОбновлениеКонфигурацииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбновлениеКонфигурацииКлиент");
		МодульОбновлениеКонфигурацииКлиент.УстановитьОбновлениеКонфигурации(ЗавершениеРаботыСистемы);
	Иначе
		ОткрытьФорму("ОбщаяФорма.ДополнительноеОписание", Новый Структура("Заголовок,ИмяМакета",
		НСтр("ru = 'Установка обновления'"), "ИнструкцияКакВыполнитьУстановкуОбновленияВручную"));
	КонецЕсли;
	
КонецПроцедуры

// Используется для открытия формы группового изменения объектов.
//
// Параметры:
//  Список - ТаблицаФормы - элемент формы списка, содержащий ссылки на изменяемые объекты.
//
Процедура ПриИзмененииВыделенныхОбъектов(Список) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов") Тогда
		МодульГрупповоеИзменениеОбъектовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ГрупповоеИзменениеОбъектовКлиент");
		МодульГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Список);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события открытия инструкции по восстановлению/изменению пароля синхронизации данных
// с автономным рабочим местом.
//
Процедура ПриОткрытииИнструкцииКакИзменитьПарольСинхронизацииДанных(Знач АдресДляВосстановленияПароляУчетнойЗаписи) Экспорт
	
	Если ПустаяСтрока(АдресДляВосстановленияПароляУчетнойЗаписи) Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Адрес для восстановления пароля учетной записи не задан.'"));
		
	Иначе
		
		ПерейтиПоНавигационнойСсылке(АдресДляВосстановленияПароляУчетнойЗаписи);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает отчет о версии или о сравнении версий.
//
// Параметры:
//	Ссылка - Ссылка на объект
//	СравниваемыеВерсии - Массив - Содержит массив сравниваемых версий,
//	если версия одна, то открывается отчет о версии.
//
Процедура ПриОткрытииФормыОтчетаПоВерсии(Ссылка, СравниваемыеВерсии) Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ВерсионированиеОбъектовКлиент");
		МодульВерсионированиеОбъектовКлиент.ПриОткрытииФормыОтчетаПоВерсии(Ссылка, СравниваемыеВерсии);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Локальные служебные процедуры и функции.

Процедура ПриЗакрытииФормыНастройкиУзлаПланаОбмена(Форма, ИмяРеквизитаФормы)
	
	Если Не Форма.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого НастройкаОтбора Из Форма[ИмяРеквизитаФормы] Цикл
		
		Если ТипЗнч(Форма[НастройкаОтбора.Ключ]) = Тип("ДанныеФормыКоллекция") Тогда
			
			СтруктураТабличнойЧасти = Форма[ИмяРеквизитаФормы][НастройкаОтбора.Ключ];
			
			Для Каждого Элемент Из СтруктураТабличнойЧасти Цикл
				
				СтруктураТабличнойЧасти[Элемент.Ключ].Очистить();
				
				Для Каждого СтрокаКоллекции Из Форма[НастройкаОтбора.Ключ] Цикл
					
					СтруктураТабличнойЧасти[Элемент.Ключ].Добавить(СтрокаКоллекции[Элемент.Ключ]);
					
				КонецЦикла;
				
			КонецЦикла;
			
		Иначе
			
			Форма[ИмяРеквизитаФормы][НастройкаОтбора.Ключ] = Форма[НастройкаОтбора.Ключ];
			
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.Модифицированность = Ложь;
	Форма.Закрыть(Форма[ИмяРеквизитаФормы]);
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС ИНТЕРАКТИВНОГО ДОПОЛНЕНИЯ ВЫГРУЗКИ
//

// Обработка диалогов интерактивного дополнения.
//
// Параметры:
//     ДополнениеВыгрузки           - Структура, ДанныеФормыСтруктура - настройки выгрузки.
//     Владелец, Уникальность, Окно - параметры открытия окна формы.
//
// Возвращаемое значение:
//     Открытая форма
//
Функция ОткрытьФормуДополненияВыгрузкиСценарийУзла(Знач ДополнениеВыгрузки, Знач Владелец=Неопределено, Знач Уникальность=Неопределено, Знач Окно=Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина);
	ПараметрыФормы.Вставить("УзелИнформационнойБазы", ДополнениеВыгрузки.УзелИнформационнойБазы);
	ПараметрыФормы.Вставить("ПериодОтбора",           ДополнениеВыгрузки.ПериодОтбораСценарияУзла);
	ПараметрыФормы.Вставить("Отбор",                  ДополнениеВыгрузки.ДополнительнаяРегистрацияСценарияУзла);

	Возврат ОткрытьФорму(ДополнениеВыгрузки.ПараметрыСценарияДополнения.ВариантДополнительно.ИмяФормыОтбора,
		ПараметрыФормы, Владелец, Уникальность, Окно);
КонецФункции

// Обработка диалогов интерактивного дополнения.
//
// Параметры:
//     ДополнениеВыгрузки           - Структура, ДанныеФормыСтруктура - настройки выгрузки.
//     Владелец, Уникальность, Окно - параметры открытия окна формы.
//
// Возвращаемое значение:
//     Открытая форма
//
Функция ОткрытьФормуДополненияВыгрузкиВсеДокументы(Знач ДополнениеВыгрузки, Знач Владелец=Неопределено, Знач Уникальность=Неопределено, Знач Окно=Неопределено) Экспорт
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru='Добавление документов для отправки'") );
	ПараметрыФормы.Вставить("ДействиеВыбора", 1);
	
	ПараметрыФормы.Вставить("ВыборПериода", Истина);
	ПараметрыФормы.Вставить("ПериодДанных", ДополнениеВыгрузки.ПериодОтбораВсехДокументов);
	
	ПараметрыФормы.Вставить("АдресКомпоновщикаНастроек", ДополнениеВыгрузки.АдресКомпоновщикаВсехДокументов);
	
	ПараметрыФормы.Вставить("АдресХранилищаФормы", ДополнениеВыгрузки.АдресХранилищаФормы);
	
	Возврат ОткрытьФорму("Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.РедактированиеПериодаИОтбора", 
		ПараметрыФормы, Владелец, Уникальность, Окно);
КонецФункции

// Обработка диалогов интерактивного дополнения.
//
// Параметры:
//     ДополнениеВыгрузки           - Структура, ДанныеФормыСтруктура - настройки выгрузки.
//     Владелец, Уникальность, Окно - параметры открытия окна формы.
//
// Возвращаемое значение:
//     Открытая форма
//
Функция ОткрытьФормуДополненияВыгрузкиДетальныйОтбор(Знач ДополнениеВыгрузки, Знач Владелец=Неопределено, Знач Уникальность=Неопределено, Знач Окно=Неопределено) Экспорт
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ДействиеВыбора", 2);
	ПараметрыФормы.Вставить("НастройкиОбъекта", ДополнениеВыгрузки);
	
	ПараметрыФормы.Вставить("ОткрытаПоСценарию", Истина);
	Возврат ОткрытьФорму("Обработка.ИнтерактивноеИзменениеВыгрузки.Форма", 
		ПараметрыФормы, Владелец, Уникальность, Окно);
КонецФункции

// Обработка диалогов интерактивного дополнения.
//
// Параметры:
//     ДополнениеВыгрузки           - Структура, ДанныеФормыСтруктура - настройки выгрузки.
//     Владелец, Уникальность, Окно - параметры открытия окна формы.
//
// Возвращаемое значение:
//     Открытая форма
//
Функция ОткрытьФормуДополненияВыгрузкиСоставДанных(Знач ДополнениеВыгрузки, Знач Владелец=Неопределено, Знач Уникальность=Неопределено, Знач Окно=Неопределено) Экспорт
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НастройкиОбъекта", ДополнениеВыгрузки);
	Если ДополнениеВыгрузки.ВариантВыгрузки=3 Тогда
		ПараметрыФормы.Вставить("УпрощенныйРежим", Истина);
	КонецЕсли;
	
	Возврат ОткрытьФорму("Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.СоставВыгрузки",
		ПараметрыФормы, Владелец, Уникальность, Окно);
КонецФункции

// Обработка диалогов интерактивного дополнения.
//
// Параметры:
//     ДополнениеВыгрузки           - Структура, ДанныеФормыСтруктура - настройки выгрузки.
//     Владелец, Уникальность, Окно - параметры открытия окна формы.
//
// Возвращаемое значение:
//     Открытая форма
//
Функция ОткрытьФормуДополненияВыгрузкиСохранениеНастроек(Знач ДополнениеВыгрузки, Знач Владелец=Неопределено, Знач Уникальность=Неопределено, Знач Окно=Неопределено) Экспорт
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, ДействиеВыбора", Истина, 3);
	
	// Компоновщик туда не передаем.
	ДополнениеВыгрузки.КомпоновщикОтбораВсехДокументов = Неопределено;
	
	ПараметрыФормы.Вставить("ПредставлениеТекущейНастройки", ДополнениеВыгрузки.ПредставлениеТекущейНастройки);
	ПараметрыФормы.Вставить("Объект", ДополнениеВыгрузки);
	
	Возврат ОткрытьФорму("Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.РедактированиеСоставаНастроек",
		ПараметрыФормы, Владелец, Уникальность, Окно);
КонецФункции

// Обработчик выбора для формы помощников дополнения выгрузки.
// Функция анализирует источник на предмет вызова из дополнения выгрузки и оперирует с данными ДополнениеВыгрузки.
//
// Параметры:
//     ВыбранноеЗначение  - Произвольный                    - результат выбора.
//     ИсточникВыбора     - УправляемаяФорма                - форма, которая произвела выбор.
//     ДополнениеВыгрузки - Структура, ДанныеФормыКоллекция - корректируемые настройки дополнения выбора.
//
// Возвращаемое значение:
//     Булево - Истина, если выбор вызван одной из форм дополнения выгрузки, Ложь - иначе.
//
Функция ОбработкаВыбораДополненияВыгрузки(Знач ВыбранноеЗначение, Знач ИсточникВыбора, ДополнениеВыгрузки) Экспорт
	
	Если ИсточникВыбора.ИмяФормы="Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.РедактированиеПериодаИОтбора" Тогда
		// Изменение предопределенного отбора "Все документы", действие определяется значением ВыбранноеЗначение.
		Возврат ДополнениеВыгрузкиОбработкаВыбораТиповогоВарианта(ВыбранноеЗначение, ДополнениеВыгрузки);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы="Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.Форма" Тогда
		// Изменение предопределенного отбора "Детально", действие определяется значением ВыбранноеЗначение.
		Возврат ДополнениеВыгрузкиОбработкаВыбораТиповогоВарианта(ВыбранноеЗначение, ДополнениеВыгрузки);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы="Обработка.ИнтерактивноеИзменениеВыгрузки.Форма.РедактированиеСоставаНастроек" Тогда
		// Настройки, действие определяется значением ВыбранноеЗначение.
		Возврат ДополнениеВыгрузкиОбработкаВыбораТиповогоВарианта(ВыбранноеЗначение, ДополнениеВыгрузки);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы=ДополнениеВыгрузки.ПараметрыСценарияДополнения.ВариантДополнительно.ИмяФормыОтбора Тогда
		// Изменение настроек по сценарию узла.
		Возврат ДополнениеВыгрузкиОбработкаВыбораСценарияУзла(ВыбранноеЗначение, ДополнениеВыгрузки);
		
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

Процедура ЗаполнитьДанныеСтруктуры(Форма)
	
	// Сохраним введенные значения этой программы.
	СтруктураНастроек = Форма.Контекст.НастройкаОтборовНаУзле;
	СоответствующиеРеквизиты = Форма.ИменаРеквизитов;
	
	Для Каждого ЭлементНастройки Из СтруктураНастроек Цикл
		
		Если СоответствующиеРеквизиты.Свойство(ЭлементНастройки.Ключ) Тогда
			
			ИмяРеквизита = СоответствующиеРеквизиты[ЭлементНастройки.Ключ];
			
		Иначе
			
			ИмяРеквизита = ЭлементНастройки.Ключ;
			
		КонецЕсли;
		
		РеквизитФормы = Форма[ИмяРеквизита];
		
		Если ТипЗнч(РеквизитФормы) = Тип("ДанныеФормыКоллекция") Тогда
			
			ИмяТаблицы = ЭлементНастройки.Ключ;
			
			Таблица = Новый Массив;
			
			Для Каждого Элемент Из Форма[ИмяРеквизита] Цикл
				
				СтрокаТаблицы = Новый Структура("Использовать, Представление, УникальныйИдентификаторСсылки");
				
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Элемент);
				
				Таблица.Добавить(СтрокаТаблицы);
				
			КонецЦикла;
			
			СтруктураНастроек.Вставить(ИмяТаблицы, Таблица);
			
		Иначе
			
			СтруктураНастроек.Вставить(ЭлементНастройки.Ключ, Форма[ИмяРеквизита]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.Контекст.НастройкаОтборовНаУзле = СтруктураНастроек;
	
	// Сохраним введенные значения другой программы.
	СтруктураНастроек = Форма.Контекст.НастройкаОтборовНаУзлеБазыКорреспондента;
	СоответствующиеРеквизиты = Форма.ИменаРеквизитовБазыКорреспондента;
	
	Для Каждого ЭлементНастройки Из СтруктураНастроек Цикл
		
		Если СоответствующиеРеквизиты.Свойство(ЭлементНастройки.Ключ) Тогда
			
			ИмяРеквизита = СоответствующиеРеквизиты[ЭлементНастройки.Ключ];
			
		Иначе
			
			ИмяРеквизита = ЭлементНастройки.Ключ;
			
		КонецЕсли;
		
		РеквизитФормы = Форма[ИмяРеквизита];
		
		Если ТипЗнч(РеквизитФормы) = Тип("ДанныеФормыКоллекция") Тогда
			
			ИмяТаблицы = ЭлементНастройки.Ключ;
			
			Таблица = Новый Массив;
			
			Для Каждого Элемент Из Форма[ИмяРеквизита] Цикл
				
				СтрокаТаблицы = Новый Структура("Использовать, Представление, УникальныйИдентификаторСсылки");
				
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Элемент);
				
				Таблица.Добавить(СтрокаТаблицы);
				
			КонецЦикла;
			
			СтруктураНастроек.Вставить(ИмяТаблицы, Таблица);
			
		Иначе
			
			СтруктураНастроек.Вставить(ЭлементНастройки.Ключ, Форма[ИмяРеквизита]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.Контекст.НастройкаОтборовНаУзлеБазыКорреспондента = СтруктураНастроек;
	
	Форма.Контекст.Вставить("ОписаниеКонтекста", Форма.ОписаниеКонтекста);
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////// 
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ИНТЕРАКТИВНОГО ДОПОЛНЕНИЯ ВЫГРУЗКИ
//

Функция ДополнениеВыгрузкиОбработкаВыбораТиповогоВарианта(Знач ВыбранноеЗначение, ДополнениеВыгрузки)
	
	Результат = Ложь;
	Если ТипЗнч(ВыбранноеЗначение)=Тип("Структура") Тогда 
		
		Если ВыбранноеЗначение.ДействиеВыбора=1 Тогда
			// Отбор и период всех документов.
			ДополнениеВыгрузки.КомпоновщикОтбораВсехДокументов = Неопределено;
			ДополнениеВыгрузки.АдресКомпоновщикаВсехДокументов = ВыбранноеЗначение.АдресКомпоновщикаНастроек;
			ДополнениеВыгрузки.ПериодОтбораВсехДокументов      = ВыбранноеЗначение.ПериодДанных;
			Результат = Истина;
			
		ИначеЕсли ВыбранноеЗначение.ДействиеВыбора=2 Тогда
			// Детальная настройка
			ОбъектВыбора = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресОбъекта);
			ЗаполнитьЗначенияСвойств(ДополнениеВыгрузки, ОбъектВыбора, , "ДополнительнаяРегистрация");
			ДополнениеВыгрузки.ДополнительнаяРегистрация.Очистить();
			Для Каждого Строка Из ОбъектВыбора.ДополнительнаяРегистрация Цикл
				ЗаполнитьЗначенияСвойств(ДополнениеВыгрузки.ДополнительнаяРегистрация.Добавить(), Строка);
			КонецЦикла;
			Результат = Истина;
			
		ИначеЕсли ВыбранноеЗначение.ДействиеВыбора=3 Тогда
			// Были сохранены настройки, надо запомнить текущее имя.
			ДополнениеВыгрузки.ПредставлениеТекущейНастройки = ВыбранноеЗначение.ПредставлениеНастройки;
			Результат = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ДополнениеВыгрузкиОбработкаВыбораСценарияУзла(Знач ВыбранноеЗначение, ДополнениеВыгрузки)
	Если ТипЗнч(ВыбранноеЗначение)<>Тип("Структура") Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	ДополнениеВыгрузки.ПериодОтбораСценарияУзла        = ВыбранноеЗначение.ПериодОтбора;
	ДополнениеВыгрузки.ПредставлениеОтбораСценарияУзла = ВыбранноеЗначение.ПредставлениеОтбора;
	
	ДополнениеВыгрузки.ДополнительнаяРегистрацияСценарияУзла.Очистить();
	Для Каждого СтрокаРегистрации Из ВыбранноеЗначение.Отбор Цикл
		ЗаполнитьЗначенияСвойств(ДополнениеВыгрузки.ДополнительнаяРегистрацияСценарияУзла.Добавить(), СтрокаРегистрации);
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

#КонецОбласти
