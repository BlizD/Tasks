////////////////////////////////////////////////////////////////////////////////
// Подсистема "Дополнительные отчеты и обработки", процедуры
//  и функции с повторным использованием возвращаемых значений.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Виды публикации дополнительных обработок, доступные в программе.
Функция ДоступныеВидыПубликации() Экспорт
	
	Результат = Новый Массив();
	
	Значения = Метаданные.Перечисления.ВариантыПубликацииДополнительныхОтчетовИОбработок.ЗначенияПеречисления;
	ИсключаемыеВидыПубликации = ДополнительныеОтчетыИОбработки.НедоступныеВидыПубликации();
	
	Для Каждого Значение Из Значения Цикл
		Если ИсключаемыеВидыПубликации.Найти(Значение.Имя) = Неопределено Тогда
			Результат.Добавить(Перечисления.ВариантыПубликацииДополнительныхОтчетовИОбработок[Значение.Имя]);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(Результат);
	
КонецФункции

// Вид публикации, который должен использоваться для конфликтующих дополнительных отчетов и обработок.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ВариантыПубликацииДополнительныхОтчетовИОбработок.
//
Функция ВидПубликацииДляКонфликтующихОбработок() Экспорт
	
	ВидОтключена = Перечисления.ВариантыПубликацииДополнительныхОтчетовИОбработок.Отключена;
	ВидРежимОтладки = Перечисления.ВариантыПубликацииДополнительныхОтчетовИОбработок.РежимОтладки;
	
	ДоступныеВиды = ДоступныеВидыПубликации();
	Если ДоступныеВиды.Найти(ВидРежимОтладки) Тогда
		Возврат ВидРежимОтладки;
	Иначе
		Возврат ВидОтключена;
	КонецЕсли;
	
КонецФункции

// Настройки для формы назначаемого объекта.
Функция ПараметрыФормыНазначаемогоОбъекта(ПолноеИмяФормы, ТипФормы = Неопределено) Экспорт
	Если НЕ ПравоДоступа("Чтение", Метаданные.Справочники.ДополнительныеОтчетыИОбработки) Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = Новый Структура("ЭтоФормаОбъекта, ТипФормы, СсылкаРодителя, ВыводитьПодменюЗаполнениеОбъекта");
	
	ФормаМетаданные = Метаданные.НайтиПоПолномуИмени(ПолноеИмяФормы);
	Если ФормаМетаданные = Неопределено Тогда
		ПозицияТочки = СтрДлина(ПолноеИмяФормы);
		Пока Сред(ПолноеИмяФормы, ПозицияТочки, 1) <> "." Цикл
			ПозицияТочки = ПозицияТочки - 1;
		КонецЦикла;
		ПолноеИмяРодителя = Лев(ПолноеИмяФормы, ПозицияТочки - 1);
		РодительМетаданные = Метаданные.НайтиПоПолномуИмени(ПолноеИмяРодителя);
	Иначе
		РодительМетаданные = ФормаМетаданные.Родитель();
	КонецЕсли;
	Если РодительМетаданные = Неопределено Или ТипЗнч(РодительМетаданные) = Тип("ОбъектМетаданныхКонфигурация") Тогда
		Возврат "";
	КонецЕсли;
	Результат.СсылкаРодителя = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(РодительМетаданные);
	
	Если ТипФормы <> Неопределено Тогда
		Если ВРег(ТипФормы) = ВРег(ДополнительныеОтчетыИОбработкиКлиентСервер.ТипФормыОбъекта()) Тогда
			Результат.ЭтоФормаОбъекта = Истина;
		ИначеЕсли ВРег(ТипФормы) = ВРег(ДополнительныеОтчетыИОбработкиКлиентСервер.ТипФормыСписка()) Тогда
			Результат.ЭтоФормаОбъекта = Ложь;
		Иначе
			Результат.ЭтоФормаОбъекта = (РодительМетаданные.ОсновнаяФормаОбъекта = ФормаМетаданные);
		КонецЕсли;
	Иначе
		Результат.ЭтоФормаОбъекта = (РодительМетаданные.ОсновнаяФормаОбъекта = ФормаМетаданные);
	КонецЕсли;
	
	Если Результат.ЭтоФормаОбъекта Тогда // Форма объекта
		Результат.ТипФормы = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипФормыОбъекта();
		РодительТип = Тип(СтрЗаменить(РодительМетаданные.ПолноеИмя(), ".", "Ссылка."));
		Результат.ВыводитьПодменюЗаполнениеОбъекта = Метаданные.ОбщиеКоманды.ЗаполнениеОбъекта.ТипПараметраКоманды.СодержитТип(РодительТип);
	Иначе // Форма списка
		Результат.ТипФормы = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипФормыСписка();
		Результат.ВыводитьПодменюЗаполнениеОбъекта = Ложь;
	КонецЕсли;
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
	
КонецФункции

#КонецОбласти
