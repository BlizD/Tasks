
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьЗаписи(Команда)
	
	ТекстВопроса = НСтр("ru = 'Удаление записей по версиям объектов может привести к невозможности 
		|выполнения анализа всей цепочки изменений этих объектов. Продолжить?'");
		
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьЗаписиЗавершение", ЭтотОбъект, Элементы.Список.ВыделенныеСтроки);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Предупреждение'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УдалитьЗаписиЗавершение(РезультатВопроса, СписокЗаписей) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьВерсииИзРегистра(СписокЗаписей);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УдалитьВерсииИзРегистра(Знач СписокЗаписей)
	
	Для Каждого КлючЗаписи Из СписокЗаписей Цикл
		НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Объект.Значение = КлючЗаписи.Объект;
		НаборЗаписей.Отбор.Объект.ВидСравнения = ВидСравнения.Равно;
		НаборЗаписей.Отбор.Объект.Использование = Истина;
		
		НаборЗаписей.Отбор.НомерВерсии.Значение = КлючЗаписи.НомерВерсии;
		НаборЗаписей.Отбор.НомерВерсии.ВидСравнения = ВидСравнения.Равно;
		НаборЗаписей.Отбор.НомерВерсии.Использование = Истина;
		
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
